#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// Обработчики событий бизнес-процесса

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = ТекущаяДатаСеанса();
	Автор = ПользователиКлиентСервер.ТекущийПользователь();
	ДатаНачала = '00010101';
	ДатаЗавершения = '00010101';
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт	
	
	Если ЭтоНовый() Тогда 
		Дата = ТекущаяДатаСеанса();
		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Проект) Тогда 
			Проект = РаботаСПроектами.ПолучитьПроектПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЗаполнения <> Неопределено И ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения, Ложь, Истина);
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		ЗадачаСсылка = ДанныеЗаполнения;
		ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ДанныеЗаполнения.Шаблон, ЭтотОбъект);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Предметы") Тогда
			Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ДанныеЗаполнения.Предметы, Ложь, Истина);
			Проект = МультипредметностьПереопределяемый.ПолучитьОсновнойПроектПоПредметам(ДанныеЗаполнения.Предметы);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("АвторСобытия") Тогда
			Автор = ДанныеЗаполнения.АвторСобытия;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Шаблон") Тогда
			ЗаполнитьПоШаблону(ДанныеЗаполнения.Шаблон);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ЗадачаИсполнителя") Тогда
			ЗадачаСсылка = ДанныеЗаполнения.ЗадачаИсполнителя;
			ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ПроектнаяЗадача") Тогда
			ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения.ПроектнаяЗадача);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Проект") Тогда
			Проект = ДанныеЗаполнения.Проект;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
		ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда 
	    МультипредметностьКлиентСервер.ЗаполнитьНаименованиеПроцесса(ЭтотОбъект, НСтр("ru = 'Обработка внутреннего'") + " ");
	КонецЕсли;
	
	БизнесПроцессыИЗадачиСервер.ЗаполнитьГлавнуюЗадачу(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Мультипредметность.ПроверитьКорректностьТиповОсновныхПредметов(ЭтотОбъект, Отказ);
	
	Если Не ЗначениеЗаполнено(ШаблонСогласования) 
	   И Не ЗначениеЗаполнено(ШаблонУтверждения) 
	   И Не ЗначениеЗаполнено(ШаблонРегистрации)
	   И Не ЗначениеЗаполнено(ШаблонРассмотрения)
	   И Не ЗначениеЗаполнено(ШаблонИсполненияОзнакомления)
	   И Не ЗначениеЗаполнено(ШаблонПоручения) Тогда
	  
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не указано ни одного шаблона подчиненных процессов!'"),,,, Отказ);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ШаблонСогласования) И ШаблонСогласования.Исполнители.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне согласования не указаны исполнители.'"), ЭтотОбъект, "ШаблонСогласования",,Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонУтверждения) И Не ЗначениеЗаполнено(ШаблонУтверждения.Исполнитель) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне утверждения не указан исполнитель.'"), ЭтотОбъект, "ШаблонУтверждения",,Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонРегистрации) И Не ЗначениеЗаполнено(ШаблонРегистрации.Исполнитель) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне регистрации не указан исполнитель.'"), ЭтотОбъект, "ШаблонРегистрации",,Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонРассмотрения) И Не ЗначениеЗаполнено(ШаблонРассмотрения.Исполнитель) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне рассмотрения не указан исполнитель.'"), ЭтотОбъект, "ШаблонРассмотрения",,Отказ);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ШаблонИсполненияОзнакомления) И ШаблонИсполненияОзнакомления.Исполнители.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне исполнения (ознакомления) не указаны исполнители.'"), ЭтотОбъект, "ШаблонИсполненияОзнакомления",,Отказ);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ШаблонПоручения) И Не ЗначениеЗаполнено(ШаблонПоручения.Исполнитель) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В шаблоне поручения не указан исполнитель.'"), ЭтотОбъект, "ШаблонПоручения",,Отказ);
	КонецЕсли;
		
	// Проверка прав на предметы
	Если МультипредметностьПереопределяемый.ПроверятьПраваУчастниковБизнесПроцессаНаПредметы(ЭтотОбъект) Тогда
		ПредметыДляПроверки = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(ЭтотОбъект);
		МультипредметностьПереопределяемый.ПроверитьПраваУчастниковБизнесПроцессаНаПредметы(ЭтотОбъект, ПредметыДляПроверки, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		РаботаСФайламиВызовСервера.ПометитьНаУдалениеПриложенныеФайлы(Ссылка, ПометкаУдаления);
	КонецЕсли;
	
	Если Не РаботаСБизнесПроцессами.ПроверитьПередЗаписью(ЭтотОбъект) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	// Обработка рабочей группы	
	РаботаСБизнесПроцессами.СформироватьРабочуюГруппу(ЭтотОбъект);
	
	Мультипредметность.ЗаполнитьПредметыТочекВложенныхПроцессов(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий элементов карты маршрута

Процедура СтартПередСтартом(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	ДатаНачала = ТекущаяДатаСеанса();
	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	ДатаЗавершения = ТекущаяДатаСеанса();
	
КонецПроцедуры


// согласование
Процедура НаСогласованиеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ЗначениеЗаполнено(ШаблонСогласования);
	
КонецПроцедуры

Процедура СогласованиеПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	СогласованиеОбъект = БизнесПроцессы.Согласование.СоздатьБизнесПроцесс();
	СогласованиеОбъект.Дата = ТекущаяДатаСеанса();
	СогласованиеОбъект.Автор = Автор;
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонСогласования, СогласованиеОбъект); 
	Мультипредметность.ПередатьПредметыПроцессу(СогласованиеОбъект, ЭтотОбъект, Ложь, Истина);
	
	СогласованиеОбъект.Проект = Проект;
	СогласованиеОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
	СогласованиеОбъект.ЗаполнитьПоШаблону(ШаблонСогласования);
	
	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, СогласованиеОбъект);
	
	ФормируемыеБизнесПроцессы.Добавить(СогласованиеОбъект);
	
КонецПроцедуры

Процедура СогласованиеПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ведущая задача - Согласовать %1'"),
			СтрокаПредметов);
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;	
		
КонецПроцедуры

Процедура СогласованПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Если Не ЗначениеЗаполнено(ШаблонСогласования) Тогда 
		Результат = Истина;
	Иначе	
		ВложенныйПроцесс = НайтиВложенныйПроцесс(БизнесПроцессы.ОбработкаВнутреннегоДокумента.ТочкиМаршрута.Согласование);
		Результат = БизнесПроцессы.Согласование.ПроцессЗавершилсяУдачно(ВложенныйПроцесс)
				Или БизнесПроцессы.Согласование.ПроцессЗавершилсяУдачноСЗамечаниями(ВложенныйПроцесс);
	КонецЕсли;
		
КонецПроцедуры


// утверждение
Процедура НаУтверждениеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат =	ЗначениеЗаполнено(ШаблонУтверждения);
	
КонецПроцедуры

Процедура УтверждениеПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	УтверждениеОбъект = БизнесПроцессы.Утверждение.СоздатьБизнесПроцесс();
	УтверждениеОбъект.Дата = ТекущаяДатаСеанса();
	УтверждениеОбъект.Автор = Автор;
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонУтверждения, УтверждениеОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(УтверждениеОбъект, ЭтотОбъект, Ложь, Истина);
	
	УтверждениеОбъект.Проект = Проект;
	УтверждениеОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
	УтверждениеОбъект.ЗаполнитьПоШаблону(ШаблонУтверждения);

	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, УтверждениеОбъект);
	
	ФормируемыеБизнесПроцессы.Добавить(УтверждениеОбъект);
	
КонецПроцедуры

Процедура УтверждениеПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ведущая задача - Утвердить %1'"),
			СтрокаПредметов);
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;
	
КонецПроцедуры

Процедура УтвержденПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Если Не ЗначениеЗаполнено(ШаблонУтверждения) Тогда 
		Результат = Истина;
	Иначе	
		ВложенныйПроцесс = НайтиВложенныйПроцесс(БизнесПроцессы.ОбработкаВнутреннегоДокумента.ТочкиМаршрута.Утверждение);
		Результат = БизнесПроцессы.Утверждение.ПроцессЗавершилсяУдачно(ВложенныйПроцесс);
	КонецЕсли;
	
КонецПроцедуры


// регистрация
Процедура НаРегистрациюПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат =	ЗначениеЗаполнено(ШаблонРегистрации);
	
КонецПроцедуры

Процедура РегистрацияПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	РегистрацияОбъект = БизнесПроцессы.Регистрация.СоздатьБизнесПроцесс();
	РегистрацияОбъект.Дата = ТекущаяДатаСеанса();
	РегистрацияОбъект.Автор = Автор;
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонРегистрации, РегистрацияОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(РегистрацияОбъект, ЭтотОбъект, Ложь, Истина);
	
	РегистрацияОбъект.Проект = Проект;
	РегистрацияОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
	РегистрацияОбъект.ЗаполнитьПоШаблону(ШаблонРегистрации);

	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, РегистрацияОбъект);
	
	ФормируемыеБизнесПроцессы.Добавить(РегистрацияОбъект);
	
КонецПроцедуры

Процедура РегистрацияПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ведущая задача - Зарегистрировать %1'"),
			СтрокаПредметов);
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;
	
КонецПроцедуры


// рассмотрение
Процедура НаРассмотрениеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ЗначениеЗаполнено(ШаблонРассмотрения);
	
КонецПроцедуры

Процедура РассмотрениеПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	РассмотрениеОбъект = БизнесПроцессы.Рассмотрение.СоздатьБизнесПроцесс();
	РассмотрениеОбъект.Дата = ТекущаяДатаСеанса();
	РассмотрениеОбъект.Автор = Автор;
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонРассмотрения, РассмотрениеОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(РассмотрениеОбъект, ЭтотОбъект, Ложь, Истина);
	
	РассмотрениеОбъект.Проект = Проект;
	РассмотрениеОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
	РассмотрениеОбъект.ЗаполнитьПоШаблону(ШаблонРассмотрения);

	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, РассмотрениеОбъект);	
	
	РассмотрениеСсылка = БизнесПроцессы.Рассмотрение.ПолучитьСсылку();
	РассмотрениеОбъект.УстановитьСсылкуНового(РассмотрениеСсылка);
	
	Рассмотрение = РассмотрениеСсылка;
	Записать();
	
	ФормируемыеБизнесПроцессы.Добавить(РассмотрениеОбъект);
	
КонецПроцедуры

Процедура РассмотрениеПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ведущая задача - Рассмотреть %1'"),
			СтрокаПредметов);
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;	
		
КонецПроцедуры

Процедура РассмотренПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = БизнесПроцессы.Рассмотрение.ПроцессЗавершилсяУдачно(Рассмотрение);
	
КонецПроцедуры

// исполнение
Процедура НаИсполнениеПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ЗначениеЗаполнено(ШаблонИсполненияОзнакомления) ИЛИ ЗначениеЗаполнено(ШаблонРассмотрения);
	
КонецПроцедуры

Процедура ИсполнениеОзнакомлениеПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Если ТипЗнч(ШаблонИсполненияОзнакомления) = Тип("СправочникСсылка.ШаблоныИсполнения") Тогда  
			Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ведущая задача - Исполнить %1'"),
				СтрокаПредметов);
		ИначеЕсли ТипЗнч(ШаблонИсполненияОзнакомления) = Тип("СправочникСсылка.ШаблоныОзнакомления") Тогда 
			Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ведущая задача - Ознакомиться %1'"),
				СтрокаПредметов);
		КонецЕсли;	
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИсполнениеОзнакомлениеПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	Если ЗначениеЗаполнено(ШаблонРассмотрения) Тогда  
		
		Если Рассмотрение.ВариантРассмотрения = Перечисления.ВариантыРассмотрения.НаправитьНаИсполнение Тогда 
			ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Исполнение.СоздатьБизнесПроцесс();
			
		ИначеЕсли Рассмотрение.ВариантРассмотрения = Перечисления.ВариантыРассмотрения.НаправитьНаОзнакомление Тогда 
			ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Ознакомление.СоздатьБизнесПроцесс();
			
		ИначеЕсли Рассмотрение.ВариантРассмотрения = Перечисления.ВариантыРассмотрения.ВвестиТекстРезолюции Тогда 
			
			Если Рассмотрение.ВариантОбработкиРезолюции = Перечисления.ВариантыОбработкиРезолюции.НаправитьНаИсполнение Тогда 
				ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Исполнение.СоздатьБизнесПроцесс();
			ИначеЕсли Рассмотрение.ВариантОбработкиРезолюции = Перечисления.ВариантыОбработкиРезолюции.НаправитьНаОзнакомление Тогда 
				ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Ознакомление.СоздатьБизнесПроцесс();
			КонецЕсли;	
				
		КонецЕсли;	
			
		ИсполнениеОзнакомлениеОбъект.Дата = ТекущаяДатаСеанса();
		ИсполнениеОзнакомлениеОбъект.ЗаполнитьПоРассмотрению(Рассмотрение);
		
		РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, ИсполнениеОзнакомлениеОбъект);
		
		ФормируемыеБизнесПроцессы.Добавить(ИсполнениеОзнакомлениеОбъект);
		
	ИначеЕсли ЗначениеЗаполнено(ШаблонИсполненияОзнакомления) Тогда 
		
		Если ТипЗнч(ШаблонИсполненияОзнакомления) = Тип("СправочникСсылка.ШаблоныИсполнения") Тогда 
			ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Исполнение.СоздатьБизнесПроцесс();
			НаименованиеПоУмолчанию = НСтр("ru = 'Исполнить'");
		ИначеЕсли ТипЗнч(ШаблонИсполненияОзнакомления) = Тип("СправочникСсылка.ШаблоныОзнакомления") Тогда 
			ИсполнениеОзнакомлениеОбъект = БизнесПроцессы.Ознакомление.СоздатьБизнесПроцесс();
			НаименованиеПоУмолчанию = НСтр("ru = 'Ознакомиться'");
		КонецЕсли;
		
		ИсполнениеОзнакомлениеОбъект.Дата = ТекущаяДатаСеанса();
		ИсполнениеОзнакомлениеОбъект.Автор = Автор;
		
		Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонИсполненияОзнакомления, ИсполнениеОзнакомлениеОбъект);
		Мультипредметность.ПередатьПредметыПроцессу(ИсполнениеОзнакомлениеОбъект, ЭтотОбъект, Ложь, Истина);
		
		ИсполнениеОзнакомлениеОбъект.Проект = Проект;
		ИсполнениеОзнакомлениеОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
		ИсполнениеОзнакомлениеОбъект.ЗаполнитьПоШаблону(ШаблонИсполненияОзнакомления);
		
		РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, ИсполнениеОзнакомлениеОбъект);
		
		ФормируемыеБизнесПроцессы.Добавить(ИсполнениеОзнакомлениеОбъект);
		
	КонецЕсли;
	
КонецПроцедуры


// в дело
Процедура ПоместитьВДелоПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	
	Результат = ЗначениеЗаполнено(ШаблонПоручения);
	
КонецПроцедуры

Процедура ВДелоПередСозданиемВложенныхБизнесПроцессов(ТочкаМаршрутаБизнесПроцесса, ФормируемыеБизнесПроцессы, Отказ)
	
	ПоручениеОбъект = БизнесПроцессы.Поручение.СоздатьБизнесПроцесс();
	ПоручениеОбъект.Дата = ТекущаяДатаСеанса();
	ПоручениеОбъект.Автор = Автор;
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонПоручения, ПоручениеОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(ПоручениеОбъект, ЭтотОбъект);
	
	ПоручениеОбъект.Проект = Проект;
	ПоручениеОбъект.ПроектнаяЗадача = ПроектнаяЗадача;
	ПоручениеОбъект.ЗаполнитьПоШаблону(ШаблонПоручения);
	
	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЭтотОбъект, ПоручениеОбъект);
	
	ФормируемыеБизнесПроцессы.Добавить(ПоручениеОбъект);
	
КонецПроцедуры

Процедура ВДелоПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	МассивПредметов = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(ЭтотОбъект,,Истина);
	СтрокаПредметов = МультипредметностьКлиентСервер.ПредметыСтрокой(МассивПредметов, Истина, Ложь);
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		Задача.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ведущая задача - Поместить в дело %1'"), 
			СтрокаПредметов);
		Задача.Автор = Автор;
		Задача.Проект = Проект;
		Задача.ПроектнаяЗадача = ПроектнаяЗадача;
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

Процедура ЗаполнитьБизнесПроцессПоЗадаче(ЗадачаСсылка)
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоЗадаче(ЭтотОбъект, ЗадачаСсылка);
	
	Проект = ЗадачаСсылка.Проект;
	ПроектнаяЗадача = ЗадачаСсылка.ПроектнаяЗадача;
	
	РаботаСБизнесПроцессами.СкопироватьЗначенияДопРеквизитов(ЗадачаСсылка, ЭтотОбъект);
		
КонецПроцедуры

// Заполняет бизнес-процесс на основании шаблона бизнес-процесса.
//
// Параметры
//  ШаблонБизнесПроцесса  - шаблон бизнес-процесса
//
Процедура ЗаполнитьПоШаблону(ШаблонБизнесПроцесса) Экспорт
	
	ШаблоныБизнесПроцессов.ЗаполнитьПоШаблонуСоставногоБизнесПроцесса(ШаблонБизнесПроцесса, ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ШаблонДляОтложенногоСтарта", ШаблонБизнесПроцесса);
	
КонецПроцедуры

// Заполняет бизнес-процесс на основании проектной задачи
//
Процедура ЗаполнитьПоПроектнойЗадаче(ДанныеЗаполнения) Экспорт 
	
	Проект = ДанныеЗаполнения.Владелец;
	ПроектнаяЗадача = ДанныеЗаполнения;
	
	НаименованиеПоУмолчанию = МультипредметностьКлиентСервер.ПолучитьНаименованиеСПредметами(
		НСтр("ru = 'Обработка внутреннего'") + " ", Предметы);
	
	Если Не ЗначениеЗаполнено(Наименование) Или Наименование = НаименованиеПоУмолчанию Тогда
		Наименование = ПроектнаяЗадача.Наименование;
	КонецЕсли;
	
	Если Предметы.Количество() = 0 Тогда 
		Предмет = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПроектнаяЗадача, "Предмет");
		
		Если Предмет <> Неопределено И Предметы.Найти(Предмет,"Предмет") = Неопределено Тогда
			СтрокаПредметов = Предметы.Добавить();
			СтрокаПредметов.РольПредмета = Перечисления.РолиПредметов.Основной;
			СтрокаПредметов.ИмяПредмета =  МультипредметностьВызовСервера.ПолучитьСсылкуНаИмяПредметаПоСсылкеНаПредмет(
				Предмет, Предметы.ВыгрузитьКолонку("ИмяПредмета"));
			СтрокаПредметов.Предмет = Предмет;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Заполняет бизнес-процесс на основании шаблона бизнес-процесса, предмета и автора.
//
// Параметры
//  ШаблонБизнесПроцесса  - шаблон бизнес-процесса
//  Предмет - предмет бизнес-процесса
//  Автор  - автор
//
Процедура ЗаполнитьПоШаблонуИПредмету(ШаблонБизнесПроцесса, ПредметСобытия, АвторСобытия) Экспорт
	
	Мультипредметность.ЗаполнитьПредметыПроцессаПоШаблону(ШаблонБизнесПроцесса, ЭтотОбъект);
	Мультипредметность.ПередатьПредметыПроцессу(ЭтотОбъект, ПредметСобытия, Ложь, Истина);
	ЗаполнитьПоШаблону(ШаблонБизнесПроцесса);
	Проект = МультипредметностьПереопределяемый.ПолучитьОсновнойПроектПоПредметам(ПредметСобытия);
	
	Дата = ТекущаяДатаСеанса();
	
	Автор = АвторСобытия;
	
КонецПроцедуры	

Функция НайтиВложенныйПроцесс(ТочкаМаршрута)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеБизнесПроцессов.БизнесПроцесс
	|ИЗ
	|	РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
	|ГДЕ
	|	ДанныеБизнесПроцессов.ВедущаяЗадача.БизнесПроцесс = &БизнесПроцесс
	|	И ДанныеБизнесПроцессов.ВедущаяЗадача.ТочкаМаршрута = &ТочкаМаршрута";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("БизнесПроцесс", Ссылка);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.БизнесПроцесс;
	
КонецФункции

// Возвращает описание задачи, специфичное для бизнес-процесса
Функция ПолучитьОписаниеУведомленияЗадачи(Задача) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для работы со стартом процесса

Процедура ОтложенныйСтарт() Экспорт
	
	ОтложенныйСтартБизнесПроцессовСервер.СтартоватьПроцессОтложенно(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОтключитьОтложенныйСтарт() Экспорт
	
	ОтложенныйСтартБизнесПроцессовСервер.ОтключитьОтложенныйСтарт(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для поддержки комплексных процессов

// Формирует шаблон по процессу
// Параметры:
//	ВладелецШаблона - ссылка на шаблон комплексного процесса или комплексный процесс, который будет владельцем
//		создаваемого шаблона процесса
// Возвращает:
//	Ссылка на созданный шаблон
Функция СоздатьШаблонПоПроцессу(ВладелецШаблона = Неопределено) Экспорт
	
	ШаблонОбъект = Шаблон.ПолучитьОбъект().Скопировать();
	ШаблонОбъект.ВладелецШаблона = ВладелецШаблона;
	ШаблонОбъект.Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	ШаблонОбъект.Записать();
	Возврат ШаблонОбъект.Ссылка;
			
КонецФункции

// Дополняет описание процесса общим описанием
Процедура ДополнитьОписание(ОбщееОписание) Экспорт	
КонецПроцедуры

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	Возврат МассивПолей;
	
КонецФункции	

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыПользователей.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;
	
	ОтложенныйСтартБизнесПроцессовСервер.ПроцессПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецЕсли
