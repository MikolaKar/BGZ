#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПолучитьЗначенияПоУмолчанию") Тогда
		ИмяПланаОбмена = "ОбменБухгалтерияПредприятияДокументооборот";
		НастройкаОтборовНаУзле = ОбменДаннымиСервер.НастройкаОтборовНаУзле(ИмяПланаОбмена, Параметры.ВерсияКорреспондента);
	Иначе
		НастройкаОтборовНаУзле = Параметры.Настройки.НастройкаОтборовНаУзле;
	КонецЕсли;
	
	// Прочтем в форму настройки отбора из полученной структуры.
	Для Каждого НастройкаОтбора ИЗ НастройкаОтборовНаУзле Цикл
		Ключ = НастройкаОтбора.Ключ;
		Если ТипЗнч(ЭтаФорма[Ключ]) = Тип("ДанныеФормыКоллекция") Тогда
			Таблица = Новый ТаблицаЗначений;
			СтруктураТабличнойЧасти = НастройкаОтбора.Значение;
			Для Каждого Элемент ИЗ СтруктураТабличнойЧасти Цикл
				УстановитьКоличествоСтрокТаблицы(Таблица, Элемент.Значение.Количество());
				Таблица.Колонки.Добавить(Элемент.Ключ);
				Таблица.ЗагрузитьКолонку(Элемент.Значение, Элемент.Ключ);
			КонецЦикла;
			ЭтаФорма[Ключ].Загрузить(Таблица);
		Иначе
			ЭтаФорма[Ключ] = НастройкаОтбора.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ПолучитьОписаниеКонтекста();
	
	Контекст = Новый Структура;
	Контекст.Вставить("НастройкаОтборовНаУзле", НастройкаОтборовНаУзле);
	Контекст.Вставить("НастройкаОтборовНаУзлеБазыКорреспондента", Новый Структура);
	Контекст.Вставить("ОписаниеКонтекста", ОписаниеКонтекста);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыгружатьДоговорыПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьЗаявкиНаОплатуПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничиватьВыгрузкуОрганизациямиПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗаполнитьКонтекстИзФормы();
	ПолучитьОписаниеКонтекста();
	Контекст.Вставить("ОписаниеКонтекста", ОписаниеКонтекста);
	Закрыть(Контекст);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	
	Элементы.ЗаявкиНаОплату.Доступность = ВыгружатьЗаявкиНаОплату;
	Элементы.Договоры.Доступность = ВыгружатьДоговоры;
	Элементы.Организации.Доступность = ОграничиватьВыгрузкуОрганизациями;
		
КонецПроцедуры

&НаСервере
Процедура ПолучитьОписаниеКонтекста()
	
	ОписаниеКонтекста = ПланыОбмена.ОбменБухгалтерияПредприятияДокументооборот.ОписаниеОграниченийПередачиДанных(
		НастройкаОтборовНаУзле, ВерсияКорреспондента);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКонтекстИзФормы()
	
	Для Каждого НастройкаОтбора Из НастройкаОтборовНаУзле Цикл
		Если ТипЗнч(ЭтаФорма[НастройкаОтбора.Ключ]) = Тип("ДанныеФормыКоллекция") Тогда
			СтруктураТабличнойЧасти = НастройкаОтборовНаУзле[НастройкаОтбора.Ключ];
			Для Каждого Элемент Из СтруктураТабличнойЧасти Цикл
				СтруктураТабличнойЧасти[Элемент.Ключ].Очистить();
				Для Каждого СтрокаКоллекции Из ЭтаФорма[НастройкаОтбора.Ключ] Цикл
					СтруктураТабличнойЧасти[Элемент.Ключ].Добавить(СтрокаКоллекции[Элемент.Ключ]);
				КонецЦикла;
			КонецЦикла;
		Иначе
			НастройкаОтборовНаУзле[НастройкаОтбора.Ключ] = ЭтаФорма[НастройкаОтбора.Ключ];
		КонецЕсли;
	КонецЦикла;
	Контекст.Вставить("НастройкаОтборовНаУзле", НастройкаОтборовНаУзле);
	Контекст.Вставить("НастройкаОтборовНаУзлеБазыКорреспондента", Новый Структура);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоСтрокТаблицы(Таблица, КоличествоСтрок) Экспорт
	
	Пока Таблица.Количество() < КоличествоСтрок Цикл
		
		Таблица.Добавить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти