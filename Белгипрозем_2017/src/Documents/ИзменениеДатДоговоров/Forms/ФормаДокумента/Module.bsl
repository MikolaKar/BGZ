
&НаКлиенте
Процедура ОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяСтрока", Объект.Основание);
	
	Если Не ЗначениеЗаполнено(Объект.Корреспондент) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнено поле ""Корреспондент""'"),,,"Корреспондент");
		Возврат;
	КонецЕсли;
	
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить("ВходящиеДокументы", "Входящие документы");
	СписокТипов.Добавить("ИсходящиеДокументы", "Исходящие документы");
	
	ВыбрЭлемент = СписокТипов.ВыбратьЭлемент();
	Если ВыбрЭлемент = Неопределено Тогда
	    Возврат;
	КонецЕсли; 
	
	Если ВыбрЭлемент.Значение = "ВходящиеДокументы" Тогда
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Отправитель", Объект.Корреспондент));
	Иначе
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Получатель", Объект.Корреспондент));
	КонецЕсли;
	
	ОткрытьФорму("Справочник."+ВыбрЭлемент.Значение+".ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДатыДоговораПриИзменении(Элемент)
	ЭтаФорма.Элементы.ЭтапыПлановыйСрок.Видимость = УстановитьВидимостьПлановогоСрока(Объект.ВидДатыДоговора);
КонецПроцедуры

&НаСервере
Функция УстановитьВидимостьПлановогоСрока(ВидДаты)
	Если ВидДаты = Справочники.мВидыДатДоговоров.ДатаПодписанияЗаказчиком ИЛИ
		ВидДаты = Справочники.мВидыДатДоговоров.ДатаВозвратаДоговора ИЛИ 
		ВидДаты = Справочники.мВидыДатДоговоров.ДатаВозобновленияРабот ИЛИ 
		ВидДаты = Справочники.мВидыДатДоговоров.ДатаПриостановленияРабот ИЛИ 
		ВидДаты = Справочники.мВидыДатДоговоров.ДатаПредоставленияДокументов Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции	

&НаКлиенте
Процедура ДатаУстановленияПриИзменении(Элемент)
	Если УстановитьВидимостьПлановогоСрока(Объект.ВидДатыДоговора) Тогда
	    ЗаполнитьПлановыйСрок();
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлановыйСрок()
	// для расчета срока - необходимо предварительно внести в регистр ДатыДоговоров данные этого документа
	
	Если Объект.Этапы.Количество() = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.Модифицированность Тогда
		ЭтаФорма.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	КонецЕсли; 
	
	Таблица = Документы.ИзменениеДатДоговоров.ИнициализироватьДанныеДокумента(Объект.Ссылка);
	мРаботаСДоговорами.ЗаписатьДатуДоговора(Таблица, Объект.Ссылка);
	
	ДатаСреза = КонецДня(Объект.Дата)+1;
	Для каждого СтрокаТаб Из Объект.Этапы Цикл
		
		СтрокаТаб.ПлановыйСрок = мРаботаСДоговорами.РассчитатьПлановыйСрокВыполненияДоговора(СтрокаТаб.ЭтапДоговора, ДатаСреза, Истина);
		
	КонецЦикла; 
	
	мРаботаСДоговорами.УдалитьДатуДоговора(Объект.Ссылка);
	
КонецПроцедуры // ЗаполнитьПлановыйСрок()
