&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТипПолучателя.СписокВыбора.Добавить( НСтр("ru='Кому:'"));
	Элементы.ТипПолучателя.СписокВыбора.Добавить(НСтр("ru='Копия:'"));
	Элементы.ТипПолучателя.СписокВыбора.Добавить(НСтр("ru='Скрытая копия:'"));
	
	ТипПолучателя = НСтр("ru='Кому:'");
	
	Если Параметры.Свойство("УникальныйИдентификаторРодительскойФормы") Тогда
		
		УникальныйИдентификаторРодительскойФормы = Параметры.УникальныйИдентификаторРодительскойФормы;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПолучателей(Команда)
	
	МассивАдресов = РаботаСоСтроками.РазложитьСтрокуПочтовыхАдресов(ТекстСАдресами);
	
	МассивВозврата = Новый Массив;
	
	Для каждого ПочтовыйАдресИнфо Из МассивАдресов Цикл
		
		Если ЗначениеЗаполнено(ПочтовыйАдресИнфо.Адрес) Тогда
			СтруктураПочтовогоАдреса = Новый Структура;
			СтруктураПочтовогоАдреса.Вставить("Адрес", ПочтовыйАдресИнфо.Адрес);
			СтруктураПочтовогоАдреса.Вставить("Представление", ПочтовыйАдресИнфо.ОтображаемоеИмя);
			СтруктураПочтовогоАдреса.Вставить("ТипПолучателя", ТипПолучателя);
			МассивВозврата.Добавить(СтруктураПочтовогоАдреса);
		КонецЕсли;
	
	КонецЦикла;
	
	Если МассивВозврата.Количество() <> 0 Тогда
		ЗаполнитьКонтакты(МассивВозврата);
	КонецЕсли;	
	
	Закрыть();
	Оповестить("ВставкаПолучателейПисьма_Добавить", МассивВозврата, УникальныйИдентификаторРодительскойФормы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКонтакты(МассивВозврата)
	
	Для каждого СтруктураПочтовогоАдреса Из МассивВозврата Цикл
		
		Адресат = ВстроеннаяПочтаСерверПовтИсп.ПолучитьПочтовогоАдресата(СтруктураПочтовогоАдреса.Адрес);
		ПредставлениеАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(Адресат);
		Контакт = ПредставлениеАдресата.Контакт;
		СтруктураПочтовогоАдреса.Вставить("Контакт", Контакт);
		СтруктураПочтовогоАдреса.Представление = ПредставлениеАдресата.Представление;
			
	КонецЦикла;

КонецПроцедуры
