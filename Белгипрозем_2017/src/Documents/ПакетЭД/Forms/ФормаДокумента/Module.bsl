////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура РаспаковатьПакетЭД(Команда)
	
	МассивПЭД = Новый Массив;
	МассивПЭД.Добавить(Объект.Ссылка);
	ЭлектронныеДокументыСлужебныйКлиент.РаспаковатьМассивПакетовЭД(МассивПЭД);
	ЭтаФорма.Прочитать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ЭлектронныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЭлектронныеДокументыОбъектВладелец" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.ОбъектВладелец);
		
	ИначеЕсли ТипЗнч(Элемент.ТекущиеДанные.ЭлектронныйДокумент) = Тип("СправочникСсылка.СообщенияЭДО") Тогда
		СтандартнаяОбработка = Ложь;
		ЭлектронныеДокументыСлужебныйКлиент.ОткрытьЭДДляПросмотра(Элемент.ТекущиеДанные.ЭлектронныйДокумент);
	
	Иначе
		ПоказатьЗначение(, Элемент.ТекущиеДанные.ЭлектронныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.КомандаРаспаковатьПакетЭД.Видимость = Объект.СтатусПакета = Перечисления.СтатусыПакетовЭД.КРаспаковке;
	Элементы.АдресРесурсаКонтрагента.Видимость   = Истина;
	Элементы.АдресРесурсаОрганизации.Видимость   = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторОрганизации = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.СоглашениеЭД, "ИдентификаторОрганизации");
	Если Объект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском
		И ЗначениеЗаполнено(ИдентификаторОрганизации) И Не Найти(ИдентификаторОрганизации, "2AL") > 0 Тогда
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

