Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	ПроведениеСервер.УстановитьРежимПроведения(Проведен, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.мПродлениеСроков.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ТаблицаСроки = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаСроки;
	
	ВидДатыДоговора = Справочники.мВидыДатДоговоров.ДатаПродления;
	ВидДатыДоговораПлановыйСрок = Справочники.мВидыДатДоговоров.ПлановыйСрок;
	
	НаборЗаписей = РегистрыСведений.мДатыДоговоров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Основание.Установить(Ссылка);
	
	Для каждого Стр Из ТаблицаСроки Цикл
		Если Стр.ЗапретИзмененияПлановогоСрока Тогда
			Продолжить;
		КонецЕсли; 	
		
		НовЗап = НаборЗаписей.Добавить();
		НовЗап.Период = Стр.Дата;
		НовЗап.ЭтапДоговора = Стр.ЭтапДоговора;
		НовЗап.Основание = Ссылка;
		НовЗап.ВидДатыДоговора = ВидДатыДоговора;
		НовЗап.Дата = Стр.ПродлитьДо;  
		НовЗап.Установил = Стр.Ответственный;
		
		Комментарий = СокрЛП(Стр.ВидДокумента)+" №"+СокрЛП(Стр.НомерДС)+
			" от "+Формат(Стр.ДатаДС, "ДЛФ=D");
		НовЗап.Комментарий = Комментарий;
		
		НовыйНабор = НаборЗаписей.Добавить();
		НовыйНабор.Период = Стр.Дата;
		НовыйНабор.ЭтапДоговора = Стр.ЭтапДоговора;
		НовыйНабор.Основание = Ссылка;
		НовыйНабор.ВидДатыДоговора = ВидДатыДоговораПлановыйСрок;
		НовыйНабор.Дата = Стр.ПродлитьДо;
		НовыйНабор.Комментарий = Комментарий;
		НовыйНабор.Установил = Стр.Ответственный;
		
	КонецЦикла; 
	
	НаборЗаписей.Записать();
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры  

Процедура ОбработкаУдаленияПроведения(Отказ)
	НаборЗаписей = РегистрыСведений.мДатыДоговоров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Основание.Установить(Ссылка);
	НаборЗаписей.Записать();
КонецПроцедуры
