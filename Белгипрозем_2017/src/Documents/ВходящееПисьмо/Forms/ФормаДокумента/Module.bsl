////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Запрет создания новых
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИспользоватьВнутреннююМаршрутизацию = Константы.ИспользоватьВнутреннююМаршрутизацию.Получить();
	Если Не ИспользоватьВнутреннююМаршрутизацию Тогда 
		Элементы.ДекорацияВидМаршрутизации.Видимость = Ложь;
	КонецЕсли;	
	
	КартинкаВнешнийАдресат = БиблиотекаКартинок.ВнешнийАдресат24;
	КартинкаВнутреннийАдресат = БиблиотекаКартинок.ВнутреннийАдресат24;	
	КартинкаВнутренняяМаршрутизация = БиблиотекаКартинок.ВнутренняяМаршрутизация24;
	КартинкаВнешняяМаршрутизация = БиблиотекаКартинок.ВнешняяМаршрутизация24;
	
	ОтображатьУдаленныеФайлы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ЭтаФорма.ИмяФормы,
		"ОтображатьУдаленныеФайлы",
		Ложь);
	
	Элементы.ОтображатьУдаленныеФайлы.Пометка = ОтображатьУдаленныеФайлы;
	
	ОтобразитьСвязиПисьма();
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
	
	// Установка заголовка формы
	Заголовок = ВстроеннаяПочтаСервер.ПолучитьПредставлениеПисьма(Объект);
	
	// Инициализация начальных значений реквизитов
	НачальноеЗначениеПредмет = Объект.Предмет;
	НачальноеЗначениеПроект = Объект.Проект;
	
	// Установка надписи для важности письма
	ВажностьНадпись = Объект.Важность;
	
	УчетВремени.ПроинициализироватьПараметрыУчетаВремени(
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ОпцияВестиУчетТрудозатрат,
		Объект.Ссылка,
		ВидыРабот,
		СпособУказанияВремени,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		ЭтаФорма.Элементы.УказатьТрудозатраты);
	
	Если Не РольДоступна("РаботаСПротоколомРаботыПользователей") И Не РольДоступна("ПолныеПрава") Тогда
		Элементы.ПротоколРаботы.Видимость = Ложь;
	КонецЕсли;
	
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	Если ПриложениеЯвляетсяВебКлиентом Тогда
		Элементы.ФормаСохранитьПисьмо.Видимость = Ложь;
		Элементы.СохранитьТекстПисьма.Видимость = Истина;
	Иначе
		Элементы.ФормаСохранитьПисьмо.Видимость = Истина;
		Элементы.СохранитьТекстПисьма.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьДоступность();
	
	
	Если ПриложениеЯвляетсяВебКлиентом Тогда 
		Элементы.ПолучателиВнешний.Видимость = Ложь;
		Элементы.ДекорацияВидМаршрутизации.Видимость = Ложь;
		Элементы.ДекорацияВнешний.Видимость = Ложь;
	КонецЕсли;
	
	// Определяем список выбора типа предмета
	СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.ВнутренниеДокументы.ПустаяСсылка"), НСтр("ru = 'Внутренний документ'"));
	СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.ВходящиеДокументы.ПустаяСсылка"), НСтр("ru = 'Входящий документ'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи") Тогда 
		СписокВыбора.Добавить(ПредопределенноеЗначение("Задача.ЗадачаИсполнителя.ПустаяСсылка"), НСтр("ru = 'Задача'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.ИсходящиеДокументы.ПустаяСсылка"), НСтр("ru = 'Исходящий документ'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") Тогда 
		СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.Мероприятия.ПустаяСсылка"), НСтр("ru = 'Мероприятие'"));
	КонецЕсли;	
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда 
		СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"), НСтр("ru = 'Проект'"));
		СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.ПроектныеЗадачи.ПустаяСсылка"), НСтр("ru = 'Проектная задача'"));
	КонецЕсли;
	
	СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.Файлы.ПустаяСсылка"), НСтр("ru = 'Файл'"));
	
	ВывестиВидМаршрутизации();
	ВывестиВидАдресатов();
	
	// Контроль писем
	Контроль.УстановитьСвойстваКнопкиКонтроляПисьма(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПраваПоОбъекту = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка);
		Если Не ПраваПоОбъекту.Изменение Тогда
			ЭтаФорма.ТолькоПросмотр = Истина;
		Иначе
			ЭтаФорма.ТолькоПросмотр = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСвязиПисьма()
	
	ПолучатьПомеченныеНаУдаление = Ложь; 
	ВсеСвязиДокумента = СвязиДокументов.ПолучитьВсеСвязиДокумента(Объект.Ссылка, ПолучатьПомеченныеНаУдаление);
	Если ВсеСвязиДокумента.Количество() > 0 Тогда
		
		// ПолученоВОтветНаДокумент
		НайденнаяСтрока = ВсеСвязиДокумента.Найти(
			Справочники.ТипыСвязей.ПолученоВОтветНаДокумент,
			"ТипСвязи");
		Если НайденнаяСтрока <> Неопределено Тогда
			ПолученоВОтветНаДокумент = НайденнаяСтрока.СвязанныйДокумент;			
			ПолученоВОтветНа = ПолученоВОтветНаДокумент;
		Иначе
			// ПолученоВОтветНаПисьмо
			НайденнаяСтрока = ВсеСвязиДокумента.Найти(
				Справочники.ТипыСвязей.ПолученоВОтветНаПисьмо,
				"ТипСвязи");
				
			Если НайденнаяСтрока <> Неопределено Тогда
				ПолученоВОтветНаПисьмо = НайденнаяСтрока.СвязанныйДокумент;
				ПолученоВОтветНа = ПолученоВОтветНаПисьмо;
			КонецЕсли;
			
		КонецЕсли;
		
		ОтправленОтвет.Очистить();
		СтруктураОтбора = Новый Структура("ТипСвязи", Справочники.ТипыСвязей.ОтправленоОтветноеПисьмо);
		НайденныеСтроки = ВсеСвязиДокумента.НайтиСтроки(СтруктураОтбора);
		Для каждого Строка Из НайденныеСтроки Цикл
			ОтправленОтвет.Добавить(Строка.СвязанныйДокумент);
		КонецЦикла;
		
		СтруктураОтбора = Новый Структура("ТипСвязи", Справочники.ТипыСвязей.ОтправленОтветныйДокумент);
		НайденныеСтроки = ВсеСвязиДокумента.НайтиСтроки(СтруктураОтбора);
		Для каждого Строка Из НайденныеСтроки Цикл
			ОтправленОтвет.Добавить(Строка.СвязанныйДокумент);
		КонецЦикла;
		
		СтруктураОтбора = Новый Структура("ТипСвязи", Справочники.ТипыСвязей.ПересланоПисьмом);
		НайденныеСтроки = ВсеСвязиДокумента.НайтиСтроки(СтруктураОтбора);
		Для каждого Строка Из НайденныеСтроки Цикл
			ПересланоПисьмом.Добавить(Строка.СвязанныйДокумент);
		КонецЦикла;
		
	КонецЕсли;
	
	// Обработка связи письма ПолученоВОтветНа
	Если ЗначениеЗаполнено(ПолученоВОтветНа) Тогда
		Если ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(ПолученоВОтветНа) Тогда
			ПолученоВОтветНаСтрока = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПолученоВОтветНа, "Тема");
			Элементы.ПолученоВОтветНаСтрока.Гиперссылка = Истина;
		ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоИсходящийДокумент(ПолученоВОтветНа) ТОгда
			ПолученоВОтветНаСтрока = Строка(ПолученоВОтветНа);
			Элементы.ПолученоВОтветНаСтрока.Гиперссылка = Истина;
		Иначе
			ПолученоВОтветНаСтрока = НСтр("ru = 'Нет'");
			Элементы.ПолученоВОтветНаСтрока.Гиперссылка = Ложь;
			Элементы.ПолученоВОтветНаСтрока.Доступность = Ложь;
		КонецЕсли;
	Иначе
		ПолученоВОтветНаСтрока = НСтр("ru = 'Нет'");
		Элементы.ПолученоВОтветНаСтрока.Гиперссылка = Ложь;
		Элементы.ПолученоВОтветНаСтрока.Доступность = Ложь;
	КонецЕсли;
	
	// Обработка связи письма ОтправленОтвет
	Если ОтправленОтвет.Количество() = 0 Тогда
		ОтправленОтветСтрока = НСтр("ru = 'Нет'");
		Элементы.ОтправленОтветСтрока.Гиперссылка = Ложь;
		Элементы.ОтправленОтветСтрока.Доступность = Ложь;
	Иначе
		ОтправленОтветСтрока = "";
		Для каждого Ответ Из ОтправленОтвет Цикл
			Если ДелопроизводствоКлиентСервер.ЭтоИсходящийДокумент(Ответ.Значение) Тогда
				ОтправленОтветСтрока = ОбщегоНазначения.ПолучитьЗначениеРеквизита(
					Ответ.Значение,
					"Заголовок");
				Если Не ЗначениеЗаполнено(ОтправленОтветСтрока) Тогда
					ОтправленОтветСтрока = НСтр("ru = 'Документ без заголовка'");
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ЗначениеЗаполнено(ОтправленОтветСтрока) Тогда
			Для каждого Ответ Из ОтправленОтвет Цикл
				Если ВстроеннаяПочтаКлиентСервер.ЭтоИсходящееПисьмо(Ответ.Значение) Тогда
					ОтправленОтветСтрока = ОбщегоНазначения.ПолучитьЗначениеРеквизита(
						Ответ.Значение,
						"Тема");
					Если Не ЗначениеЗаполнено(ОтправленОтветСтрока) Тогда
						ОтправленОтветСтрока = НСтр("ru = 'Письмо без темы'");
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Элементы.ОтправленОтветСтрока.Гиперссылка = Истина;
	КонецЕсли;
	
	// Обработка связи письма ПересланоПисьмом
	Если ПересланоПисьмом.Количество() = 0 Тогда
		ПересланоПисьмомСтрока = НСтр("ru = 'Нет'");
		Элементы.ПересланоПисьмомСтрока.Гиперссылка = Ложь;
		Элементы.ПересланоПисьмомСтрока.Доступность = Ложь;
	Иначе
		ПересланоПисьмомТема = ОбщегоНазначения.ПолучитьЗначениеРеквизита(
			ПересланоПисьмом[0].Значение,
			"Тема");
		ПересланоПисьмомСтрока = ПересланоПисьмомТема;
		Элементы.ПересланоПисьмомСтрока.Гиперссылка = Истина;
	КонецЕсли;
	
	ЧислоПисемВПереписке = ВстроеннаяПочтаСервер.ПолучитьКоличествоПисемВПереписке(Объект.Ссылка);
	Если ЧислоПисемВПереписке > 1 Тогда
		Элементы.ПисемВПереписке.Гиперссылка = Истина;
		ПисемВПереписке = Формат(ЧислоПисемВПереписке, "ЧН=0") +
			" " +
			ВстроеннаяПочтаКлиентСервер.ПодписьКЧислуПисемСтрокой(ЧислоПисемВПереписке);
	Иначе
		ПисемВПереписке = НСтр("ru = 'Писем нет'");
		Элементы.ПисемВПереписке.Гиперссылка = Ложь;
		Элементы.ПисемВПереписке.Доступность = Ложь;
	КонецЕсли;
	
	АктивныеЗадачиПоПисьму.ЗагрузитьЗначения(ПолучитьАктивныеЗадачиПоПисьму(Объект.Ссылка));
	КоличествоАктивныхЗадач = АктивныеЗадачиПоПисьму.Количество();
	Если КоличествоАктивныхЗадач > 1 Тогда
		Элементы.Задачи.Гиперссылка = Истина;
		Элементы.Задачи.Доступность = Истина;
		Задачи = Формат(КоличествоАктивныхЗадач, "ЧН=0") +
			" " +
			ВстроеннаяПочтаКлиентСервер.ПодписьКЧислуЗадачСтрокой(КоличествоАктивныхЗадач);
		
	ИначеЕсли КоличествоАктивныхЗадач = 1 Тогда
		Элементы.Задачи.Гиперссылка = Истина;
		Элементы.Задачи.Доступность = Истина;
		Задачи = АктивныеЗадачиПоПисьму[0];
		
	Иначе
		Задачи = НСтр("ru = 'Нет задач'");
		Элементы.Задачи.Гиперссылка = Ложь;
		Элементы.Задачи.Доступность = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАктивныеЗадачиПоПисьму(Письмо)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЗадачаИсполнителя.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.Предметы.Предмет = &Предмет
		|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
		|	И НЕ ЗадачаИсполнителя.Отменена
		|	И НЕ ЗадачаИсполнителя.Выполнена
		|	И ЗадачаИсполнителя.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)");
	Запрос.УстановитьПараметр("Предмет", Письмо);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПолучателейКомуКопия()
	
	ТаблицаЗначений = ВстроеннаяПочтаСервер.ПолучитьТаблицуПолучателейКомуКопияУВходящегоПисьма(Объект.Ссылка);
	ЗначениеВРеквизитФормы(ТаблицаЗначений, "Получатели");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Получатели.Очистить();
	
	ЗаполнитьПолучателейКомуКопия();
	
	ВставитьПолучателя(
		НСтр("ru = 'От:'"),
		Объект.ОтправительАдресат);
		
	Если Объект.ТипТекста <> Перечисления.ТипыТекстовПочтовыхСообщений.HTML Тогда
		Элементы.ФормаПоказатьИсходныйТекст.Видимость = Ложь;
	КонецЕсли;	
		
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиПрограммы",
			"ОтображатьФотографииПерсональнаяНастройка",
			Истина);
	
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	ПолучатьФотографии = Истина;
	
	Если Не ОтображатьФотографииОбщаяНастройка
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая
		Или ПриложениеЯвляетсяВебКлиентом Тогда
		ПолучатьФотографии = Ложь;
		Элементы.ГруппаСтраницыФотографии.Видимость = Ложь;
	КонецЕсли;
	
	ПредставлениеАдресата = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(
		Объект.ОтправительАдресат);
	
	ТекущийПолучатель = ПредставлениеАдресата.Контакт;
	
	ПоказатьФотоПользователя(ТекущийПолучатель, УникальныйИдентификатор, Фотография);
	
	// Формирование представления для поля От
	ОтправительСтрокой = ПредставлениеАдресата.Представление;
	
	// Формирование HTML представления тела письма
	ТекстПисьма = ВстроеннаяПочтаСервер.СформироватьHTMLПредставлениеПисьма(
		Объект.Ссылка,
		УникальныйИдентификатор,
		Ложь); // ВыводитьШапку
	
	// Вложения
	ОбновитьФайлыПисьма();
	
	РаботаСФлагамиОбъектовСервер.ОтобразитьФлагВФормеОбъекта(ЭтаФорма);
	
	// Инициализация начальных значений реквизитов
	НачальноеЗначениеПредмет = Объект.Предмет;
	НачальноеЗначениеПроект = Объект.Проект;
	
	ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную =
		ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную");
		
	Если Не ПомечатьВходящиеПисьмаПрочтеннымиТолькоВручную Тогда
		
		Если РаботаСПрочтениями.УстановитьСвойствоПрочтен(Объект.Ссылка) Тогда
			ОповеститьОПрочтении = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОповеститьОПрочтении Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Оповестить("ОбновитьСписокПоследних");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОчиститьСообщения();
	
	СписокОшибокПриЗаписи.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
	ТекущийОбъект.ЕстьВложения = Ложь;
	Для каждого ВложенияСтрока Из Вложения Цикл
		Если Не ВложенияСтрока.ПометкаУдаления Тогда
			ТекущийОбъект.ЕстьВложения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыЗаписи.Свойство("ПереместитьВКорзину")
		И ПараметрыЗаписи.ПереместитьВКорзину = Истина Тогда
		
		ТекущийОбъект.ПометкаУдаления = Ложь;
		
		Корзина = РегистрыСведений.ПапкиУчетныхЗаписей.ПолучитьПапку(
			ТекущийОбъект.УчетнаяЗапись, Перечисления.ВидыПапокПисем.Корзина);
		
		ТекущийОбъект.Папка = Корзина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьВложения(ТекущийОбъект);
	
	Если ПараметрыЗаписи.Свойство("ЗаменитьПредметПереписки")
		И ПараметрыЗаписи.ЗаменитьПредметПереписки = Истина
		И ЗначениеЗаполнено(ТекущийОбъект.Предмет) Тогда
		
		СообщенияОбОшибках = Новый Массив;
		Если Не ЗаменитьПредметПереписки(ТекущийОбъект, НачальноеЗначениеПредмет, СообщенияОбОшибках) Тогда
			СписокОшибокПриЗаписи.ЗагрузитьЗначения(СообщенияОбОшибках);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ЗаменитьПроектПереписки")
		И ПараметрыЗаписи.ЗаменитьПроектПереписки = Истина
		И ЗначениеЗаполнено(ТекущийОбъект.Проект) Тогда
		
		СообщенияОбОшибках = Новый Массив;
		Если Не ЗаменитьПроектПереписки(ТекущийОбъект, НачальноеЗначениеПроект, СообщенияОбОшибках) Тогда
			СписокОшибокПриЗаписи.ЗагрузитьЗначения(СообщенияОбОшибках);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ЭтоНовыйОбъект") И ПараметрыЗаписи.ЭтоНовыйОбъект = Истина Тогда
		РаботаСПоследнимиОбъектами.ЗаписатьОбращениеКОбъекту(Объект.Ссылка);
		РаботаСФлагамиОбъектовСервер.СохранитьФлагОбъектаИзФормы(ЭтаФорма);
	КонецЕсли;
	
	// Заголовок формы
	Автозаголовок = Ложь;
	Заголовок = ВстроеннаяПочтаСервер.ПолучитьПредставлениеПисьма(Объект);
	
	ОбновитьФайлыПисьма();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СозданВнутреннийДокументНаОснованииПисьма"
		И ЗначениеЗаполнено(Объект.Ссылка)
		И Параметр.ПараметрСобытия = Объект.Ссылка Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
	Если (ИмяСобытия = "БизнесПроцессИзменен"
		Или ИмяСобытия = "ЗадачаВыполнена")
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОтобразитьСвязиПисьма();
	КонецЕсли;
	
	Если (ИмяСобытия = "Запись_ЛичныйАдресат" 
		ИЛИ ИмяСобытия = "Запись_Корреспондент" 
		ИЛИ ИмяСобытия = "Запись_КонтактноеЛицо" 
		ИЛИ ИмяСобытия = "Запись_Пользователь"
		ИЛИ ИмяСобытия = "Запись_РолиИсполнителей" 
		ИЛИ ИмяСобытия = "ИзмененоФизическоеЛицо") Тогда
		
		Прочитать();
		
	КонецЕсли;
	
	Если ИмяСобытия = "ФайлИзменен"
		И ЗначениеЗаполнено(Объект.Ссылка)
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Владелец")
		И Параметр.Владелец = Объект.Ссылка Тогда
		ОбновитьФайлыПисьма();
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменилсяФлаг"
		И Источник <> ЭтаФорма
		И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		
		РаботаСФлагамиОбъектовКлиентСервер.ОтобразитьФлагВФормеОбъекта(ЭтаФорма);
		
	КонецЕсли;
	
	Если ИмяСобытия = "ЗаписьКонтроля" Тогда
		Если ЗначениеЗаполнено(Параметр.Предмет)
			И ТипЗнч(Параметр.Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
			УстановитьСвойстваКнопкиКонтроля();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваКнопкиКонтроля()
	
	Контроль.УстановитьСвойстваКнопкиКонтроляПисьма(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПоказатьПараметрыПисьма(Команда)
	
	ТекстЗаголовкиИнтернета = Новый ТекстовыйДокумент;
	ТекстЗаголовкиИнтернета.ДобавитьСтроку(Объект.ВнутреннийЗаголовок);
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Дата", Объект.Дата);
	СтруктураПараметры.Вставить("Получено", Объект.ДатаПолучения);
	СтруктураПараметры.Вставить("ЗаголовкиИнтернета", ТекстЗаголовкиИнтернета);
	СтруктураПараметры.Вставить("Письмо", Объект.Ссылка);
	СтруктураПараметры.Вставить("Кодировка", Объект.Кодировка);
	СтруктураПараметры.Вставить("ВнутреннийНомер", Объект.Номер);
	СтруктураПараметры.Вставить("УчетнаяЗапись", Объект.УчетнаяЗапись);
	
	ОткрытьФорму("ЖурналДокументов.ЭлектроннаяПочта.Форма.ПараметрыПисьма", СтруктураПараметры, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКодировку(Команда)
	
	ПараметрыФормы = Новый Структура("ТекущаяКодировка", Объект.Кодировка);
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьКодировкуЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Справочник.Файлы.Форма.ВыборКодировки", ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКодировкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ВыбраннаяКодировка = Результат.Значение;
		Если ВыбраннаяКодировка <> Неопределено Тогда
			ПреобразоватьКодировкуПисьма(ВыбраннаяКодировка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьКодировкуПисьма(ВыбраннаяКодировка)
	
	СтроковыеДанные = Новый Структура;
	СтроковыеДанные.Вставить("ТекстПисьма", ТекстПисьма);
	СтроковыеДанные.Вставить("Тема", Объект.Тема);
	Для каждого ПолучателиСтрока Из Получатели Цикл
		СтроковыеДанные.Вставить(
			"ПолучателиПредставление" + Формат(ПолучателиСтрока.ПолучитьИдентификатор(), "ЧГ=0"),
			ПолучателиСтрока.Представление);
	КонецЦикла;
	
	СтрокиВКодировке = РаботаСоСтроками.ПолучитьСтрокиВКодировке(СтроковыеДанные, ВыбраннаяКодировка);
	ТекстПисьма = СтрокиВКодировке.ТекстПисьма;
	Объект.Тема = СтрокиВКодировке.Тема;
	Для каждого ПолучателиСтрока Из Получатели Цикл
		ПолучателиСтрока.Представление = СтрокиВКодировке["ПолучателиПредставление" + Формат(ПолучателиСтрока.ПолучитьИдентификатор(), "ЧГ=0")];
	КонецЦикла;
	
	Объект.Кодировка = ВыбраннаяКодировка;
	
КонецПроцедуры

&НаКлиенте
Процедура Ответить(Команда)
	
	Если Модифицированность Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВстроеннаяПочтаКлиент.ОтветитьНаПисьмо(Объект.Ссылка);
	
	ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветитьВсем(Команда)
	
	Если Модифицированность Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВстроеннаяПочтаКлиент.ОтветитьВсемНаПисьмо(Объект.Ссылка);
	
	ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Переслать(Команда)
	
	Если Модифицированность Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВстроеннаяПочтаКлиент.ПереслатьПисьмо(Объект.Ссылка);
	
	ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьКомментарий(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", Объект.Комментарий);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактироватьКомментарийЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьКомментарийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Комментарий = Результат;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиПоПредмету(Команда)
	
	ОткрытьОбщуюФормуБизнесПроцессыИЗадачиПоПредмету();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбщуюФормуБизнесПроцессыИЗадачиПоПредмету()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Предмет", Объект.Ссылка);
	ПараметрыФормы.Вставить(
		"Заголовок",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процессы и задачи по предмету: ""%1""'"),
			Заголовок));
	ОткрытьФорму(
		"ОбщаяФорма.БизнесПроцессыИЗадачиПоПредмету",
		ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВКорзину(Команда)
	
	ПредыдущаяПапка = Объект.Папка;
	ПараметрыЗаписи = Новый Структура("ПереместитьВКорзину", Истина);
	Если Не Записать(ПараметрыЗаписи) Тогда
		Объект.Папка = ПредыдущаяПапка;
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекстПисьма(Команда)
	
	Если Модифицированность = Истина Или (Не ЗначениеЗаполнено(Объект.Ссылка)) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
	
		Записать();
	КонецЕсли;
	
	ВстроеннаяПочтаКлиент.СохранитьТекстПисьма(Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПисьмо(Команда)
	
	Если Модифицированность = Истина Или (Не ЗначениеЗаполнено(Объект.Ссылка)) Тогда
		Если Не ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
	
		Записать();
	КонецЕсли;
	
	ВстроеннаяПочтаКлиент.СохранитьПисьмо(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ПисемВПерепискеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВстроеннаяПочтаКлиент.ОткрытьДеревоПереписки(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если АктивныеЗадачиПоПисьму.Количество() = 1 Тогда
		ПоказатьЗначение(, АктивныеЗадачиПоПисьму[0].Значение);
	Иначе
		ОткрытьОбщуюФормуБизнесПроцессыИЗадачиПоПредмету();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Тип") Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбранныйПредмет = Неопределено;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредметОбработкаВыбораЗавершение", ЭтотОбъект);
		
		ПоказатьВводЗначения(
			ОписаниеОповещения,
			ВыбранныйПредмет,
			НСтр("ru = 'Выбор предмета'"),
			ВыбранноеЗначение);
		
		Возврат;
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда	
		
		ВыбранныйПредмет = ВыбранноеЗначение.ПроектнаяЗадача;	
		
	Иначе
		
		ВыбранныйПредмет = ВыбранноеЗначение;
		
	КонецЕсли;
	
	Если ВыбранныйПредмет = НачальноеЗначениеПредмет Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьСменуПредмета", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйПредмет = Результат;
	
	Если ВыбранныйПредмет = НачальноеЗначениеПредмет Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработатьСменуПредмета", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСменуПредмета()
	
	Объект.Предмет = ВыбранныйПредмет;
	
	Если ЗначениеЗаполнено(Объект.Предмет) Тогда
		
		ПараметрыЗаписи = Новый Структура("ЗаменитьПредметПереписки", Истина);
		Если Записать(ПараметрыЗаписи) Тогда
			
			НачальноеЗначениеПредмет = Объект.Предмет;
			Модифицированность = Ложь;
			
		Иначе
			
			Объект.Предмет = НачальноеЗначениеПредмет;
			
			Если СписокОшибокПриЗаписи.Количество() > 0 Тогда
				
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Предмет не установлен'"));
				
				ПараметрыОткрытия.Вставить("ПояснениеОшибки", 
					НСтр("ru = 'Не удалось установить предмет, т.к. некоторые письма в переписке заблокированы'"));
				
				ПараметрыОткрытия.Вставить("МассивОшибок", СписокОшибокПриЗаписи.ВыгрузитьЗначения());
				ОткрытьФорму("ОбщаяФорма.ПротоколОшибокОбработкиПисемПереписки", ПараметрыОткрытия, ЭтаФорма);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВыбранныйПредмет = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("Элемент", Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПредметНачалоВыбораПослеВыбораТипаПредмета",
		ЭтотОбъект,
		ПараметрыОбработчика);
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокВыбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНачалоВыбораПослеВыбораТипаПредмета(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат.Значение) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыбиратьТолькоПроектнуюЗадачу", Истина);
		
		Если ТипЗнч(НачальноеЗначениеПредмет) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда
			ПараметрыФормы.Вставить("ПроектнаяЗадача", НачальноеЗначениеПредмет);
		ИначеЕсли ТипЗнч(НачальноеЗначениеПредмет) = Тип("СправочникСсылка.Проекты") Тогда
			ПараметрыФормы.Вставить("Проект", НачальноеЗначениеПредмет);
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ВыборПроектаЗадачи", ПараметрыФормы, ДополнительныеПараметры.Элемент);
		
	Иначе
		
		ВыбранныйТипПредмета = ТипЗнч(Результат.Значение);
		ПодключитьОбработчикОжидания("ОбработатьВыборТипаПредмета", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборТипаПредмета()
	
	ВыбранныйПредмет = Неопределено;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредметНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПоказатьВводЗначения(
		ОписаниеОповещения,
		ВыбранныйПредмет,
		НСтр("ru = 'Выбор предмета'"),
		ВыбранныйТипПредмета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйПредмет = Результат;
	
	Если ВыбранныйПредмет = НачальноеЗначениеПредмет Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Предмет = ВыбранныйПредмет;
	
	Если ЗначениеЗаполнено(Объект.Предмет) Тогда
		
		ПараметрыЗаписи = Новый Структура("ЗаменитьПредметПереписки", Истина);
		Если Записать(ПараметрыЗаписи) Тогда
			
			НачальноеЗначениеПредмет = Объект.Предмет;
			
		Иначе
			
			Объект.Предмет = НачальноеЗначениеПредмет;
			
			Если СписокОшибокПриЗаписи.Количество() > 0 Тогда
				
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Предмет не установлен'"));
				
				ПараметрыОткрытия.Вставить("ПояснениеОшибки", 
					НСтр("ru ='Не удалось установить предмет, т.к. некоторые письма в переписке заблокированы'"));
				
				ПараметрыОткрытия.Вставить("МассивОшибок", СписокОшибокПриЗаписи.ВыгрузитьЗначения());
				ОткрытьФорму("ОбщаяФорма.ПротоколОшибокОбработкиПисемПереписки", ПараметрыОткрытия, ЭтаФорма);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Предмет = Неопределено;
	
	Если Не ЗначениеЗаполнено(НачальноеЗначениеПредмет) Тогда
		Возврат;
	КонецЕсли;
	
	Если Записать() Тогда
		
		НачальноеЗначениеПредмет = Объект.Предмет;
		
	Иначе
		
		Объект.Предмет = НачальноеЗначениеПредмет;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Проект = Неопределено;
	
	Если Не ЗначениеЗаполнено(НачальноеЗначениеПроект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Записать() Тогда
		
		НачальноеЗначениеПроект = Объект.Проект;
		
	Иначе
		
		Объект.Проект = НачальноеЗначениеПроект;
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ВЛОЖЕНИЯМИ

&НаСервере
Процедура ОбновитьСписокВложений()
	
	МассивФайлов = Новый Массив;
	Для каждого ВложенияСтрока Из Вложения Цикл
		Если ВложенияСтрока.Расположение = "Файл" Тогда
			МассивФайлов.Добавить(ВложенияСтрока.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивФайлов.Количество() > 0 Тогда
		ФайлыИнфо = ВстроеннаяПочтаСервер.ПолучитьИнформациюОВложениях(МассивФайлов);
		Для каждого ВложенияСтрока Из Вложения Цикл
			Для каждого ФайлИнфо Из ФайлыИнфо Цикл
				Если ВложенияСтрока.Расположение = "Файл" И ВложенияСтрока.Ссылка = ФайлИнфо.Ссылка Тогда
					ЗаполнитьЗначенияСвойств(ВложенияСтрока, ФайлИнфо);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВложения(ТекущийОбъект)
	
	Для каждого ВложениеСтрока Из Вложения Цикл
		Если ВложениеСтрока.Расположение = "СсылкаНаФайл" Тогда
			РаботаСФайламиВызовСервера.СкопироватьФайл(ВложениеСтрока.Ссылка, ТекущийОбъект.Ссылка);
		ИначеЕсли ВложениеСтрока.Расположение = "ВременноеХранилище" Тогда
			ВстроеннаяПочтаСервер.ДобавитьВложениеПисьмаИзВременногоХранилища(
				ТекущийОбъект.Ссылка, // Письмо
				ВложениеСтрока.Адрес, // АдресВременногоХранилища
				Неопределено, // АдресВременногоХранилищаТекста
				ВложениеСтрока.Размер,
				ВложениеСтрока.ИмяФайла,
				ТекущаяДата(), // ВремяИзменения
				Неопределено); // Идентификатор - идентификатор картинки
			ВложениеСтрока.Расположение = "Файл";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьВложение(Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьВложение(Команда)
	
	ТекущиеДанные = Элементы.Вложения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьВложение(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВложение(ФайлСсылка)
	
	ВстроеннаяПочтаКлиент.ОткрытьВложение(ФайлСсылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элементы.Вложения.ТекущиеДанные.Расположение <> "Файл" Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вложение еще не записано. Для редактирования вложения необходимо записать письмо.'"));
		Возврат;
	КонецЕсли;
	
	// Открытие карточки файла
	ПоказатьЗначение(, Элементы.Вложения.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВложение(Команда)
	
	Если Элементы.Вложения.ТекущиеДанные.Расположение <> "Файл" Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вложение еще не записано. Для редактирования вложения необходимо записать письмо.'"));
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьСписокВложенийИУстановитьДоступностьКоманд", ЭтотОбъект);
	
	РаботаСФайламиКлиент.РедактироватьСОповещением(Обработчик, 
		Элементы.Вложения.ТекущиеДанные.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандРаботыСВложением();
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Вложения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Расположение = "Файл" Тогда
		Если Не ТекущиеДанные.ПометкаУдаления Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометить ""%1"" на удаление?'"),
				ТекущиеДанные.Представление);
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Снять с ""%1"" пометку на удаление?'"),
				ТекущиеДанные.Представление);
		КонецЕсли;
	Иначе
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Удалить ""%1""?'"),
			ТекущиеДанные.Представление);
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ТекущиеДанные", ТекущиеДанные);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВложенияПередУдалениемЗавершение", ЭтотОбъект, ПараметрыОбработчика);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияПередУдалениемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ТекущиеДанные.Расположение = "Файл" Тогда
		ВложениеПометитьНаУдалениеСервер(ДополнительныеПараметры.ТекущиеДанные.Ссылка);
	Иначе
		Если ДополнительныеПараметры.ТекущиеДанные.Расположение = "ВременноеХранилище"
			И ЭтоАдресВременногоХранилища(ДополнительныеПараметры.ТекущиеДанные.Адрес) Тогда
			УдалитьИзВременногоХранилища(ДополнительныеПараметры.ТекущиеДанные.Адрес);
		КонецЕсли;
		ИндексЭлемента = Вложения.Индекс(ДополнительныеПараметры.ТекущиеДанные);
		Вложения.Удалить(ИндексЭлемента);
	КонецЕсли;
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("Владелец", Объект.Ссылка);
	ПараметрОповещения.Вставить("Файл", ДополнительныеПараметры.ТекущиеДанные.Ссылка);
	ПараметрОповещения.Вставить("ЕстьЗашифрованныеИлиЗанятыеФайлы", Ложь);
	ПараметрОповещения.Вставить("ИдентификаторРодительскойФормы", УникальныйИдентификатор);
	ПараметрОповещения.Вставить("Событие", "ДанныеФайлаИзменены");
	
	Оповестить("Запись_Файл", ПараметрОповещения, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ВложениеПометитьНаУдалениеСервер(ФайлСсылка)
	
	ФайлОбъект = ФайлСсылка.ПолучитьОбъект();
	ЗаблокироватьДанныеДляРедактирования(ФайлСсылка);
	ФайлОбъект.УстановитьПометкуУдаления(Не ФайлОбъект.ПометкаУдаления);
	ОбновитьФайлыПисьма();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФайлыПисьма()
	
	ТекПозиция = Неопределено;
	Если Элементы.Вложения.ТекущаяСтрока <> Неопределено Тогда
		ТекДанные = Вложения.НайтиПоИдентификатору(Элементы.Вложения.ТекущаяСтрока);
		Если ТекДанные <> Неопределено Тогда
			Если ТекДанные.Свойство("Ссылка")
				И ЗначениеЗаполнено(ТекДанные.Ссылка) Тогда
				ТекПозиция = Новый Структура("Ссылка", ТекДанные.Ссылка);
			ИначеЕсли ТекДанные.Свойство("Представление")
				И ТекДанные.Свойство("Размер") Тогда
				ТекПозиция = Новый Структура("Представление, Размер", ТекДанные.Представление, ТекДанные.Размер);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ФайлыПисьма = ВстроеннаяПочтаСервер.ПолучитьФайлыПисьма(
			Объект.Ссылка, // Письмо
			Истина, // ФормироватьРазмерПредставление
			ОтображатьУдаленныеФайлы, // ВключатьПомеченныеНаУдаление
			Ложь,    // ТолькоСИдентификаторами
			Истина); // ТолькоБезИдентификаторов  - чтобы картинки в HTML не показывать
		
		ВложенияВременнаяТаблица = Вложения.Выгрузить();
		Вложения.Очистить();
		Для каждого ФайлыПисьмаСтрока Из ФайлыПисьма Цикл
			ВложенияСтрока = Вложения.Добавить();
			ЗаполнитьЗначенияСвойств(ВложенияСтрока, ФайлыПисьмаСтрока);
			Если ВложенияСтрока.ПометкаУдаления Тогда
				ВложенияСтрока.ИндексКартинки = ВложенияСтрока.ИндексКартинки + 1;
			КонецЕсли;
			ВложенияСтрока.Расположение = "Файл";
		КонецЦикла;
		
		Для каждого ВложенияВременнаяТаблицаСтрока Из ВложенияВременнаяТаблица Цикл
			Если ВложенияВременнаяТаблицаСтрока.Расположение <> "Файл" Тогда
				ВложенияСтрока = Вложения.Добавить();
				ЗаполнитьЗначенияСвойств(ВложенияСтрока, ВложенияВременнаяТаблицаСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекПозиция) Тогда
		Если ТипЗнч(ТекПозиция) = Тип("Структура") Тогда
			Строки = Вложения.НайтиСтроки(ТекПозиция);
			Если Строки.Количество() > 0 Тогда
				Элементы.Вложения.ТекущаяСтрока = Строки[0].ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОтобразитьКоличествоФайловСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьКоличествоФайловСервер()
	
	Если Вложения.Количество() > 0 Тогда
		Элементы.ВложенияПредставление.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Файлы (%1)'"),
			Вложения.Количество());
	Иначе
		Элементы.ВложенияПредставление.Заголовок = НСтр("ru = 'Файлы'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВложенияНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если Элементы.Вложения.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивФайлов = Новый Массив;
	Для каждого ВыделеннаяСтрока Из Элементы.Вложения.ВыделенныеСтроки Цикл
		ДанныеСтроки = Вложения.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если ДанныеСтроки.Расположение <> "Файл" Тогда
			Продолжить;
		КонецЕсли;
		МассивФайлов.Добавить(ДанныеСтроки.Ссылка);
	КонецЦикла;
	Если МассивФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПеретаскивания.Значение = МассивФайлов;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеВложения(Команда)
	
	Если Элементы.Вложения.ТекущиеДанные.Расположение <> "Файл" Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вложение еще не записано. Для редактирования вложения необходимо записать письмо.'"));
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьСписокВложенийИУстановитьДоступностьКоманд", ЭтотОбъект);
	
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(
		Обработчик,
		Элементы.Вложения.ТекущиеДанные.Ссылка,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокВложенийИУстановитьДоступностьКоманд(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	ОбновитьСписокВложений();
	УстановитьДоступностьКомандРаботыСВложением();
	
КонецПроцедуры	

&НаКлиенте
Процедура СохранитьВложениеКак(Команда)
	
	Если Элементы.Вложения.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Вложения.ВыделенныеСтроки.Количество() > 1 Тогда
		СписокФайловДляВыгрузки = Новый СписокЗначений;
		Для каждого ВыбраннаяСтрока Из Элементы.Вложения.ВыделенныеСтроки Цикл
			ДанныеСтроки = Элементы.Вложения.ДанныеСтроки(ВыбраннаяСтрока);
			Если ЗначениеЗаполнено(ДанныеСтроки.Ссылка) Тогда
				СписокФайловДляВыгрузки.Добавить(ДанныеСтроки.Ссылка);
			КонецЕсли;
		КонецЦикла;
		Если СписокФайловДляВыгрузки.Количество() > 0 Тогда
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("СписокОбъектов", СписокФайловДляВыгрузки);
			РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ОткрытьФорму("Справочник.Файлы.Форма.ВыгрузкаФайловНаДиск", ПараметрыФормы, , , , , , РежимОткрытияОкна);
		КонецЕсли;
	Иначе
		ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайлаДляСохранения(
			Элементы.Вложения.ТекущиеДанные.Ссылка,
			Неопределено,
			ЭтаФорма.УникальныйИдентификатор);
		КомандыРаботыСФайламиКлиент.СохранитьКак(ДанныеФайла, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандРаботыСВложением()
	
	Если Элементы.Вложения.ТекущаяСтрока = Неопределено Тогда
		Элементы.ПросмотретьВложение.Доступность = Ложь;
		Элементы.РедактироватьВложение.Доступность = Ложь;
		Элементы.ЗакончитьРедактированиеВложения.Доступность = Ложь;
		Элементы.СохранитьВложениеКак.Доступность = Ложь;
	Иначе
		ТекущиеДанные = Элементы.Вложения.ТекущиеДанные;
		Элементы.ПросмотретьВложение.Доступность = (ТекущиеДанные.Расположение = "Файл");
		Элементы.РедактироватьВложение.Доступность = (ТекущиеДанные.Расположение = "Файл");
		Элементы.ЗакончитьРедактированиеВложения.Доступность = ТекущиеДанные.РедактируетТекущийПользователь;
		Элементы.СохранитьВложениеКак.Доступность = (ТекущиеДанные.Расположение = "Файл");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьУдаленныеФайлы(Команда)
	
	ОтображатьУдаленныеФайлыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОтображатьУдаленныеФайлыСервер()
	
	ОтображатьУдаленныеФайлы = Не ОтображатьУдаленныеФайлы;
	Элементы.ОтображатьУдаленныеФайлы.Пометка = ОтображатьУдаленныеФайлы;
	ОбновитьФайлыПисьма();
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ЭтаФорма.ИмяФормы,
		"ОтображатьУдаленныеФайлы",
		ОтображатьУдаленныеФайлы);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// УЧЕТ ВРЕМЕНИ

&НаСервере
Процедура ПереключитьХронометражСервер(ПараметрыОповещения)
	
	УчетВремени.ПереключитьХронометражСервер(
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		Объект.Ссылка,
		ВидыРабот,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОтчетИОбновитьФорму(ПараметрыОтчета, ПараметрыОповещения)
	
	УчетВремени.ДобавитьВОтчетИОбновитьФорму(
		ПараметрыОтчета, 
		ПараметрыОповещения,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВключенХронометраж,
		ЭтаФорма.Команды.ПереключитьХронометраж,
		ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьХронометражСервер()
	
	УчетВремени.ОтключитьХронометражСервер(
	ДатаНачалаХронометража,
	ДатаКонцаХронометража,
	ВключенХронометраж,
	Объект.Ссылка,
	ЭтаФорма.Команды.ПереключитьХронометраж,
	ЭтаФорма.Элементы.ПереключитьХронометраж);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометраж(Команда)
	
	ПараметрыОповещения = Неопределено;
	
	НуженДиалог = УчетВремениКлиент.НуженДиалогДляХронометража(
		ВключенХронометраж,
		ДатаНачалаХронометража,
		ВидыРабот);
	
	Если НуженДиалог = Ложь Тогда
		
		ПереключитьХронометражСервер(ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
		
	Иначе
		
		ДлительностьРаботы = УчетВремениКлиент.ПолучитьДлительностьРаботы(ДатаНачалаХронометража);
		
		ОписаниеРаботы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обработка письма ""%1""'"),
			Объект.Тема);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДатаОтчета", ТекущаяДата());
		ПараметрыФормы.Вставить("ВидыРабот", ВидыРабот);
		ПараметрыФормы.Вставить("ОписаниеРаботы", ОписаниеРаботы);
		ПараметрыФормы.Вставить("ДлительностьРаботы", ДлительностьРаботы);
		ПараметрыФормы.Вставить("НачалоРаботы", ДатаНачалаХронометража);
		ПараметрыФормы.Вставить("Объект", Объект.Ссылка);
		ПараметрыФормы.Вставить("СпособУказанияВремени", СпособУказанияВремени);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПереключитьХронометражЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаДобавленияРаботы",
			ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьХронометражЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ПараметрыОповещения = Неопределено;
		ДобавитьВОтчетИОбновитьФорму(Результат, ПараметрыОповещения);
		УчетВремениКлиент.ПоказатьОповещение(ПараметрыОповещения, ВключенХронометраж, Объект.Ссылка);
	Иначе
		ОтключитьХронометражСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьТрудозатраты(Команда)
	
	ДатаОтчета = ТекущаяДата();
	
	УчетВремениКлиент.ДобавитьВОтчетКлиент(
		ДатаОтчета,
		ВключенХронометраж,
		ДатаНачалаХронометража,
		ДатаКонцаХронометража,
		ВидыРабот,
		Объект.Ссылка,
		СпособУказанияВремени,
		ЭтаФорма.Элементы.ПереключитьХронометраж,
		Ложь,
		ЭтаФорма); // Выполнена
		
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРаботы(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектДанных", Объект.Ссылка);
		ОткрытьФорму("РегистрСведений.ПротоколРаботыПользователей.Форма.ПротоколРаботы", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвязиОбъекта(Команда)
	
	ПараметрыФормы = Новый Структура("Документ", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.СвязиДокументов.Форма.ФормаСвязейОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПисьмаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВстроеннаяПочтаКлиент.ОткрытьСсылку(ДанныеСобытия.Href, ДанныеСобытия.Element, Объект.Ссылка, Элемент.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты(Команда)
	
	ПараметрыФормы = Новый Структура("Источник", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.ФактическиеТрудозатраты.Форма.ФормаСпискаИсточника", ПараметрыФормы);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СВЯЗИ

&НаКлиенте
Процедура ПолученоВОтветНаСтрокаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, ПолученоВОтветНа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправленОтветСтрокаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОтправленОтвет.Количество() = 1 Тогда
		ПоказатьЗначение(, ОтправленОтвет[0].Значение);
	Иначе
		ВстроеннаяПочтаКлиент.ОткрытьДеревоПереписки(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересланоПисьмомСтрокаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ПересланоПисьмом.Количество() = 1 Тогда
		ПоказатьЗначение(, ПересланоПисьмом[0].Значение);
	Иначе
		ВстроеннаяПочтаКлиент.ОткрытьДеревоПереписки(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ПОЛУЧАТЕЛЯМИ

&НаСервере
Процедура ВставитьПолучателя(ТипАдреса, ПочтовыйАдресат)
	
	НоваяСтрока = Получатели.Вставить(0);
	ДанныеПолучателя = ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(ПочтовыйАдресат);
	НоваяСтрока.Представление = ДанныеПолучателя.Представление;
	НоваяСтрока.Контакт = ДанныеПолучателя.Контакт;
	НоваяСтрока.ПочтовыйАдресат = ПочтовыйАдресат;
 	НоваяСтрока.ТипАдреса = ТипАдреса;
	НоваяСтрока.Адрес = ПочтовыйАдресат.Адрес;
	НоваяСтрока.Внешний = ВстроеннаяПочтаСервер.ЭтоВнешнийАдресат(ПочтовыйАдресат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтрокаДанных = Получатели.НайтиПоИдентификатору(Элементы.Получатели.ТекущаяСтрока);
	Если ЗначениеЗаполнено(СтрокаДанных.Контакт) Тогда
		ПоказатьЗначение(, СтрокаДанных.Контакт);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиТипАдресаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЛичногоАдресата(Команда)
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено
		И Не ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Контакт) Тогда
		
		Представление = ВстроеннаяПочтаКлиентСервер.ВыделитьПредставление(Элементы.Получатели.ТекущиеДанные.Представление);
		Адрес = Элементы.Получатели.ТекущиеДанные.Адрес;
		
		ПараметрыФормы = Новый Структура("Адрес, Представление", 
			Адрес,
			Представление);
		
		АдресДляЗамены = Адрес;
		
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.ЛичныеАдресаты.ФормаОбъекта", ПараметрыФормы, , , , , , РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКорреспондента(Команда)
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено
		И Не ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Контакт) Тогда
		
		Представление = ВстроеннаяПочтаКлиентСервер.ВыделитьПредставление(Элементы.Получатели.ТекущиеДанные.Представление);
		Адрес = Элементы.Получатели.ТекущиеДанные.Адрес;
		
		ПараметрыФормы = Новый Структура("Адрес, Представление", 
			Адрес,
			Представление);
		
		АдресДляЗамены = Адрес;
		
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.Корреспонденты.ФормаОбъекта", ПараметрыФормы, , , , , , РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтактноеЛицо(Команда)
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено
		И Не ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Контакт) Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьКонтактноеЛицоЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Справочник.Корреспонденты.ФормаВыбора", , , , , , ОписаниеОповещения, РежимОткрытияОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтактноеЛицоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Представление = ВстроеннаяПочтаКлиентСервер.ВыделитьПредставление(Элементы.Получатели.ТекущиеДанные.Представление);
	Адрес = Элементы.Получатели.ТекущиеДанные.Адрес;
	
	ПараметрыФормы = Новый Структура("Адрес, Представление, Корреспондент", 
		Адрес,
		Представление,
		Результат);
	
	АдресДляЗамены = Адрес;
	
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Справочник.КонтактныеЛица.ФормаОбъекта", ПараметрыФормы, , , , , , РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено Тогда
		
		СохранениеДоступно = Не ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Контакт)
			И ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Представление);
		УстановитьДоступностьКомандСохраненияКонтакта(СохранениеДоступно);
		
		Элементы.ПолучателиПредставление.КнопкаОткрытия = ЗначениеЗаполнено(Элементы.Получатели.ТекущиеДанные.Контакт);
		
	Иначе
		УстановитьДоступностьКомандСохраненияКонтакта(Ложь);
	КонецЕсли;
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено Тогда
		
		Контакт = Элементы.Получатели.ТекущиеДанные.Контакт;
		
		Если ТекущийПолучатель <> Контакт Тогда
			
			ТекущийПолучатель = Контакт;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;
		
	Иначе
		
		Фотография = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандСохраненияКонтакта(Доступность)
	
	 Элементы.ПолучателиКонтекстноеМенюСоздатьЛичногоАдресата.Доступность = Доступность;
	 Элементы.ПолучателиКонтекстноеМенюСоздатьКорреспондента.Доступность = Доступность;
	 Элементы.ПолучателиКонтекстноеМенюСоздатьКонтактноеЛицо.Доступность = Доступность;
	 Элементы.ФормаСоздатьЛичногоАдресата.Доступность = Доступность;
	 Элементы.ФормаСоздатьКорреспондента.Доступность = Доступность;
	 Элементы.ФормаСоздатьКонтактноеЛицо.Доступность = Доступность;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ФЛАГОМ

&НаКлиенте
Процедура КрасныйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Красный"),
		БиблиотекаКартинок.КрасныйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура СинийФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Синий"),
		БиблиотекаКартинок.СинийФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖелтыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Желтый"),
		БиблиотекаКартинок.ЖелтыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗеленыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Зеленый"),
		БиблиотекаКартинок.ЗеленыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОранжевыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Оранжевый"),
		БиблиотекаКартинок.ОранжевыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛиловыйФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.Лиловый"),
		БиблиотекаКартинок.ЛиловыйФлаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФлаг(Команда)
	
	РаботаСФлагамиОбъектовКлиент.УстановитьФлагВФормеОбъекта(
		ЭтаФорма,
		ПредопределенноеЗначение("Перечисление.ФлагиОбъектов.ПустаяСсылка"),
		БиблиотекаКартинок.ПустойФлаг);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ФОТО ПОЛЬЗОВАТЕЛЯ

&НаКлиенте
Процедура ОбновитьФотоПользователя()
	
	Если Не ПолучатьФотографии Тогда
		Возврат;
	КонецЕсли;	
	
	ПоказатьФотоПользователя(ТекущийПолучатель, УникальныйИдентификатор, Фотография);	
		
КонецПроцедуры

&НаСервере
Процедура ПоказатьФотоПользователя(Контакт, УникальныйИдентификатор, Фотография)
	
	Если Не ПолучатьФотографии Тогда
		Возврат;
	КонецЕсли;	
	
	// фото пользователя
	Если ЭтоАдресВременногоХранилища(Фотография) Тогда
		УдалитьИзВременногоХранилища(Фотография);
	КонецЕсли;	
	
	Фотография = "";
	Если ЗначениеЗаполнено(Контакт) 
		И ТипЗнч(Контакт) <> Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		ЕстьКартинка = Ложь;
		Фотография = РаботаСФотографиями.ПолучитьАдресФото(Контакт, УникальныйИдентификатор, ЕстьКартинка);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Фотография) Тогда
		Фотография = "";
		ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаКартинкаПоУмолчанию;
	Иначе	
		ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаФотография;
	КонецЕсли;	
	// фото пользователя

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ДЕРЕВОМ ПЕРЕПИСКИ

&НаСервере
Функция ЗаменитьПредметПереписки(ТекущийОбъект, ЗаменяемыйПредмет, СообщенияОбОшибках)
	
	ПисьмаПереписки = ВстроеннаяПочтаСервер.ПолучитьПисьмаПереписки(ТекущийОбъект.Ссылка);
	ПисьмаДляЗаменыПредмета = Новый Массив;
	
	Для каждого Письмо Из ПисьмаПереписки Цикл
		
		Если Письмо <> ТекущийОбъект.Ссылка
			И ВстроеннаяПочтаКлиентСервер.ЭтоПисьмо(Письмо) Тогда
			
			Предмет = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Письмо, "Предмет");
			
			Если Предмет = ЗаменяемыйПредмет Или Не ЗначениеЗаполнено(Предмет) Тогда
				ПисьмаДляЗаменыПредмета.Добавить(Письмо);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПисьмаДляЗаменыПредмета.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ВстроеннаяПочтаСервер.ЗаменитьПредметВПисьмах(
		ПисьмаДляЗаменыПредмета,
		ТекущийОбъект.Предмет,
		СообщенияОбОшибках);
	
КонецФункции

&НаСервере
Функция ЗаменитьПроектПереписки(ТекущийОбъект, ЗаменяемыйПроект, СообщенияОбОшибках)
	
	ПисьмаПереписки = ВстроеннаяПочтаСервер.ПолучитьПисьмаПереписки(ТекущийОбъект.Ссылка);
	ПисьмаДляЗаменыПроекта = Новый Массив;
	
	Для каждого Письмо Из ПисьмаПереписки Цикл
		
		Если Письмо <> ТекущийОбъект.Ссылка Тогда
			
			Проект = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Письмо, "Проект");
			
			Если Проект = ЗаменяемыйПроект Или Не ЗначениеЗаполнено(Проект) Тогда
				ПисьмаДляЗаменыПроекта.Добавить(Письмо);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПисьмаДляЗаменыПроекта.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ВстроеннаяПочтаСервер.ЗаменитьПроектВПисьмах(
		ПисьмаДляЗаменыПроекта,
		ТекущийОбъект.Проект,
		СообщенияОбОшибках);
	
КонецФункции

&НаКлиенте
Процедура ФотографияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.Получатели.ТекущиеДанные <> Неопределено Тогда
		
		Контакт = Элементы.Получатели.ТекущиеДанные.Контакт;
		Если ЗначениеЗаполнено(Контакт) Тогда
			ПоказатьЗначение(, Контакт);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАБОТА С ОТОБРАЖЕНИЕМ ВНЕШНИХ АДРЕСАТОВ

&НаСервере
Процедура ВывестиВидМаршрутизации()
	
	ВидМаршрутизацииВнешняя = ПредопределенноеЗначение("Перечисление.ВидыМаршрутизацииПисем.Внешняя");
	ВидМаршрутизацииВнутренняя = ПредопределенноеЗначение("Перечисление.ВидыМаршрутизацииПисем.Внутренняя");
	
	Если Не ПриложениеЯвляетсяВебКлиентом Тогда
		
		Если Объект.ВидМаршрутизации = ВидМаршрутизацииВнутренняя Тогда 
			Элементы.ДекорацияВидМаршрутизации.Картинка = КартинкаВнутренняяМаршрутизация;
			Элементы.ДекорацияВидМаршрутизации.Подсказка = НСтр("ru = 'Доставка внутренняя'");
		Иначе
			Элементы.ДекорацияВидМаршрутизации.Картинка = КартинкаВнешняяМаршрутизация;
			Элементы.ДекорацияВидМаршрутизации.Подсказка = НСтр("ru = 'Доставка через почтовый сервер'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВидАдресатов()
	
	ВнешниеАдресаты = Новый Массив;
	Для Каждого Строка Из Получатели Цикл
		Если Строка.Внешний Тогда 
			ВнешниеАдресаты.Добавить(Строка);
		КонецЕсли;	
	КонецЦикла;	
	
	Если Не ПриложениеЯвляетсяВебКлиентом Тогда
		
		Если ВнешниеАдресаты.Количество() > 0 Тогда 
			Элементы.ДекорацияВнешний.Картинка = КартинкаВнешнийАдресат;
			Элементы.ДекорацияВнешний.Подсказка = НСтр("ru = 'Это внешнее письмо'");
		Иначе
			Элементы.ДекорацияВнешний.Картинка = КартинкаВнутреннийАдресат;
			Элементы.ДекорацияВнешний.Подсказка = НСтр("ru = 'Это внутреннее письмо'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВнешнийНажатие(Элемент)
	
	ПисьмоОтправлено = ЗначениеЗаполнено(Объект.ДатаОтправки);
	
	ВнешниеАдресаты = Новый Массив;
	Для Каждого Строка Из Получатели Цикл
		Если Строка.Внешний Тогда 
			ВнешниеАдресаты.Добавить(Строка);
		КонецЕсли;	
	КонецЦикла;	
		
	Если ВнешниеАдресаты.Количество() > 0 Тогда 
			
		ВнешниеАдресатыСтрокой = "";
		Для Каждого Строка Из ВнешниеАдресаты Цикл
			ВнешниеАдресатыСтрокой = ВнешниеАдресатыСтрокой + " - " + Строка(Строка.Представление) + Символы.ПС;
		КонецЦикла;
		
		ТекстСообщения = НСтр("ru = 'Это внешнее письмо, так как некоторые его получатели не являются пользователями 1С:Документооборота:'") +
			Символы.ПС +
			Символы.ПС +
			ВнешниеАдресатыСтрокой;
		
		ТекстЗаголовка = НСтр("ru = 'Внешнее письмо'");
			
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Это внутреннее письмо, так как все его получатели являются пользователями 1С:Документооборота.'");
		ТекстЗаголовка = НСтр("ru = 'Внутреннее письмо'");
			
	КонецЕсли;
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", ТекстЗаголовка);
	ПараметрыФормы.Вставить("ТекстСообщения", ТекстСообщения);
	
	ОткрытьФорму("ОбщаяФорма.ФорматированноеСообщение", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВидМаршрутизацииНажатие(Элемент)
	
	ВидМаршрутизацииВнешняя = ПредопределенноеЗначение("Перечисление.ВидыМаршрутизацииПисем.Внешняя");
	ВидМаршрутизацииВнутренняя = ПредопределенноеЗначение("Перечисление.ВидыМаршрутизацииПисем.Внутренняя");
	
	Если Объект.ВидМаршрутизации = ВидМаршрутизацииВнутренняя Тогда 
		ТекстСообщения = НСтр("ru = 'Это письмо было получено напрямую, а не через почтовый сервер, 
			|так как все получатели этого письма работают с почтой в 1С:Документообороте.'");
		ТекстЗаголовка = НСтр("ru = 'Внутренняя доставка'");	
	Иначе
		ТекстСообщения = НСтр("ru = 'Это письмо было получено через почтовый сервер.'");
		ТекстЗаголовка = НСтр("ru = 'Доставка через почтовый сервер'");
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", ТекстЗаголовка);
	ПараметрыФормы.Вставить("ТекстСообщения", ТекстСообщения);
	
	ОткрытьФорму("ОбщаяФорма.ФорматированноеСообщение", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСписок(Команда)
	
	Если Элементы.Получатели.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("УчетнаяЗапись", Объект.УчетнаяЗапись);
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВСписокЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("Справочник.СпискиАдресовЭлектроннойПочты.ФормаВыбора",
		ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВСписокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧислоДобавленных = 0;
	Причина = "";
	
	ДобавитьВСписокСервер(Результат, Элементы.Получатели.ВыделенныеСтроки, ЧислоДобавленных, Причина);
	
	Если ЧислоДобавленных <> 0 Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Адресаты (%1 шт) добавлены в список адресов ""%2""'"), ЧислоДобавленных, Результат);
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Результат);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Добавление в список адресов'"), НавигационнаяСсылка, Текст);
		
	ИначеЕсли Элементы.Получатели.ВыделенныеСтроки.Количество() = 1 Тогда
		ПоказатьПредупреждение(, Причина);
		
	ИначеЕсли Элементы.Получатели.ВыделенныеСтроки.Количество() > 1 Тогда
		
		Причина = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Все выбранные адресаты уже есть в списке ""%1"".'"),
			Результат);
		ПоказатьПредупреждение(, Причина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВСписокСервер(ЭлементСправочникаСпискиАдресов, Знач ВыделенныеСтроки, ЧислоДобавленных, Причина)
	
	Для Каждого ИдСтроки Из ВыделенныеСтроки Цикл
	
		СтрокаДанных = Получатели.НайтиПоИдентификатору(ИдСтроки);
		Адрес = СтрокаДанных.Адрес;
	
		Если Справочники.СпискиАдресовЭлектроннойПочты.ДобавитьВСписок(ЭлементСправочникаСпискиАдресов, Адрес) Тогда
			ЧислоДобавленных = ЧислоДобавленных + 1;
		Иначе
			Причина = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данный адресат уже есть в списке ""%1"".'"), 
				ЭлементСправочникаСпискиАдресов);
		КонецЕсли;	
		
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДоменВСписок(Команда)
	
	Если Элементы.Получатели.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыФормы = Новый Структура("УчетнаяЗапись", Объект.УчетнаяЗапись);
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьДоменВСписокЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("Справочник.СпискиАдресовЭлектроннойПочты.ФормаВыбора",
		ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДоменВСписокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧислоДобавленных = 0;
	Причина = "";
	
	ДобавитьДоменВСписокСервер(Результат, Элементы.Получатели.ВыделенныеСтроки, ЧислоДобавленных, Причина);
	
	Если ЧислоДобавленных <> 0 Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Адресаты (%1 шт) добавлены в список адресов ""%2""'"), ЧислоДобавленных, Результат);
		НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Результат);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Добавление в список адресов'"), НавигационнаяСсылка, Текст);
		
	ИначеЕсли Элементы.Получатели.ВыделенныеСтроки.Количество() = 1 Тогда
		ПоказатьПредупреждение(, Причина);
		
	ИначеЕсли Элементы.Получатели.ВыделенныеСтроки.Количество() > 1 Тогда
		
		Причина = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Все выбранные адресаты уже есть в списке ""%1"".'"),
			Результат);
		ПоказатьПредупреждение(, Причина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДоменВСписокСервер(ЭлементСправочникаСпискиАдресов, Знач ВыделенныеСтроки, ЧислоДобавленных, Причина)
	
	Для Каждого ИдСтроки Из ВыделенныеСтроки Цикл
	
		СтрокаДанных = Получатели.НайтиПоИдентификатору(ИдСтроки);
		Адрес = СтрокаДанных.Адрес;
		Домен = ВстроеннаяПочтаКлиентСервер.ПолучитьДомен(Адрес);
	
		Если Справочники.СпискиАдресовЭлектроннойПочты.ДобавитьВСписок(ЭлементСправочникаСпискиАдресов, Домен) Тогда
			ЧислоДобавленных = ЧислоДобавленных + 1;
		Иначе
			Причина = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данный адресат уже есть в списке ""%1"".'"), 
				ЭлементСправочникаСпискиАдресов);
		КонецЕсли;	
		
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура СпискиАдресов(Команда)
	
	ПараметрыОткрытия = Новый Структура("УчетнаяЗапись", Объект.УчетнаяЗапись);
	ОткрытьФорму("Справочник.СпискиАдресовЭлектроннойПочты.Форма.ФормаСпискаПоУчетнымЗаписям", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИсходныйТекст(Команда)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	ТекстHTML = ПолучитьHTMLТекст(Объект.Ссылка);
	ТекстовыйДокумент.УстановитьТекст(ТекстHTML);
	ТекстовыйДокумент.ТолькоПросмотр = Истина;
	ТекстовыйДокумент.Показать(НСтр("ru = 'HTML код письма'"));

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьHTMLТекст(Ссылка)
	
	РеквизитыПисьма = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
		"ТипТекста, Ссылка, ТекстПисьмаHTMLХранилище, ТекстПисьмаПростойТекстХранилище, Кодировка, ОтправительАдресат, ДатаОтправки, Тема, ПолучателиПисьма, ПолучателиКопий, ВнутреннийЗаголовок");
		
	ТекстHTML = Документы.ВходящееПисьмо.ПолучитьHTMLПредставлениеСодержанияПисьма(РеквизитыПисьма);
	
	Возврат ТекстHTML;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВМоиКонтакты(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВМоиКонтактыЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Справочник.ГруппыКонтактовПользователей.Форма.ВыборГруппыДляДобавления", , , , , , ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВМоиКонтактыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧислоДобавленных = 0;
	Причина = "";
	ДобавитьВИзбранноеПользователяСервер(Элементы.Получатели.ВыделенныеСтроки, Результат, ЧислоДобавленных, Причина);
	
	Если ЧислоДобавленных <> 0 Тогда
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Адресаты (%1 шт) добавлены в группу моих контактов ""%2""'"), ЧислоДобавленных, Результат);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Добавление в ""Мои контакты""'"),,Текст);
	ИначеЕсли Элементы.Получатели.ВыделенныеСтроки.Количество() = 1 Тогда
		ПоказатьПредупреждение(, Причина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВИзбранноеПользователяСервер(Знач ВыделенныеСтроки, ГруппаИзбранного, ЧислоДобавленных, Причина)
	
	Для Каждого ИдСтроки Из ВыделенныеСтроки Цикл
	
		СтрокаДанных = Получатели.НайтиПоИдентификатору(ИдСтроки);
		Контакт = СтрокаДанных.Контакт;
		Адрес = СтрокаДанных.Адрес;
		Если ДобавитьВМоиКонтактыСервер(Контакт, Адрес, ГруппаИзбранного) Тогда
			ЧислоДобавленных = ЧислоДобавленных + 1;
		Иначе	
			Причина = НСтр("ru = 'Данный адресат уже есть в группе моих контактов.'")
		КонецЕсли;
			
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ДобавитьВМоиКонтактыСервер(Контакт, Адрес, ГруппаИзбранного = Неопределено)
	
	Если Не Справочники.ГруппыКонтактовПользователей.ЗаписатьКонтакт(Контакт, Адрес, ГруппаИзбранного) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ДекорацияКонтрольНажатие(Элемент)
	
	Если Объект.Ссылка.Пустая() Или Модифицированность Тогда 
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	КонтрольКлиент.ОбработкаКомандыКонтроль(Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры
