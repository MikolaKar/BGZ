// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Запрос.Текст ="ВЫБРАТЬ
	              |	М_ВедомостьКомиссииАктыПроверки.ОценкаКомиссии КАК ОценкаИтоговая,
	              |	М_ВедомостьКомиссииАктыПроверки.КоэффициентПроверки КАК Оценка,
	              |	М_ВедомостьКомиссииАктыПроверки.АктПроверки КАК АктПроверки,
	              |	М_ВедомостьКомиссииАктыПроверки.ДатаАктирования КАК ДатаАктирования,
	              |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.Владелец.ЭтапДоговора КАК ЭтапДоговора
	              |ИЗ
	              |	Документ.М_ВедомостьКомиссии.АктыПроверки КАК М_ВедомостьКомиссииАктыПроверки
	              |ГДЕ
	              |	М_ВедомостьКомиссииАктыПроверки.Ссылка = &Ссылка
	              |	И М_ВедомостьКомиссииАктыПроверки.ОценкаКомиссии > 0
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Акты.НачальникОтдела КАК НачОтдела,
	              |	Акты.ОценкаНачОтделаИтоговая КАК ОценкаНачОтдела,
	              |	Акты.ГлавныйСпециалист КАК ЗамНачОтдела,
	              |	Акты.ОценкаГлавСпецаИтоговая КАК ОценкаЗамНачОтдела,
	              |	Акты.НачальникОтряда КАК НачСектора,
	              |	Акты.ОценкаНачОтрядаИтоговая КАК ОценкаНачСектора,
	              |	Акты.ДатаАктирования,
	              |	Акты.ВидПроверки,
	              |	Акты.Исполнитель,
	              |	Акты.ОценкаИсполнителю,
	              |	Акты.ОценкаКомиссии,
	              |	Акты.АктПроверки.Владелец.ЭтапДоговора КАК ЭтапДоговора,
	              |	ВЫБОР
	              |		КОГДА Акты.Договор.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	              |			ТОГДА ИСТИНА
	              |		ИНАЧЕ ЛОЖЬ
	              |	КОНЕЦ КАК ЭтоФизЛицо,
	              |	ВЫБОР
	              |		КОГДА Акты.ОценкаНачОтделаИтоговая > 0
	              |			ТОГДА ВЫБОР
	              |					КОГДА Размер.Должность = ЗНАЧЕНИЕ(Перечисление.мРольВПроверкеКачества.НачальникОтдела)
	              |							И Размер.ФизЛица = ВЫБОР
	              |								КОГДА Акты.Договор.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	              |									ТОГДА ИСТИНА
	              |								ИНАЧЕ ЛОЖЬ
	              |							КОНЕЦ
	              |							И Размер.Оценка = Акты.ОценкаНачОтделаИтоговая
	              |						ТОГДА Размер.Процент
	              |					ИНАЧЕ 0
	              |				КОНЕЦ
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПроцНачОтдела,
	              |	ВЫБОР
	              |		КОГДА Акты.ОценкаГлавСпецаИтоговая > 0
	              |			ТОГДА ВЫБОР
	              |					КОГДА Размер.Должность = ЗНАЧЕНИЕ(Перечисление.мРольВПроверкеКачества.ЗаместительНачальникаОтдела)
	              |							И Размер.ФизЛица = ВЫБОР
	              |								КОГДА Акты.Договор.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	              |									ТОГДА ИСТИНА
	              |								ИНАЧЕ ЛОЖЬ
	              |							КОНЕЦ
	              |							И Размер.Оценка = Акты.ОценкаГлавСпецаИтоговая
	              |						ТОГДА Размер.Процент
	              |					ИНАЧЕ 0
	              |				КОНЕЦ
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПроцЗамНачОтдела,
	              |	ВЫБОР
	              |		КОГДА Акты.ОценкаНачОтрядаИтоговая > 0
	              |			ТОГДА ВЫБОР
	              |					КОГДА Размер.Должность = ЗНАЧЕНИЕ(Перечисление.мРольВПроверкеКачества.НачальникСектора)
	              |							И Размер.ФизЛица = ВЫБОР
	              |								КОГДА Акты.Договор.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	              |									ТОГДА ИСТИНА
	              |								ИНАЧЕ ЛОЖЬ
	              |							КОНЕЦ
	              |							И Размер.Оценка = Акты.ОценкаНачОтрядаИтоговая
	              |						ТОГДА Размер.Процент
	              |					ИНАЧЕ 0
	              |				КОНЕЦ
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПроцНачСектора,
	              |	ВЫБОР
	              |		КОГДА Акты.ОценкаИсполнителю > 0
	              |			ТОГДА ВЫБОР
	              |					КОГДА Размер.Должность = ЗНАЧЕНИЕ(Перечисление.мРольВПроверкеКачества.Исполнитель)
	              |							И Размер.ФизЛица = ВЫБОР
	              |								КОГДА Акты.Договор.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	              |									ТОГДА ИСТИНА
	              |								ИНАЧЕ ЛОЖЬ
	              |							КОНЕЦ
	              |							И Размер.Оценка = Акты.ОценкаИсполнителю
	              |						ТОГДА Размер.Процент
	              |					ИНАЧЕ 0
	              |				КОНЕЦ
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ПроцИсполнителя
	              |ПОМЕСТИТЬ Оценки
	              |ИЗ
	              |	Документ.М_ВедомостьКомиссии.АктыПроверки КАК Акты,
	              |	РегистрСведений.мРазмерСниженияПремии.СрезПоследних КАК Размер
	              |ГДЕ
	              |	Акты.Ссылка = &Ссылка
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Оценки.ЭтапДоговора,
	              |	Оценки.ЭтапДоговора.Владелец,
	              |	Оценки.ВидПроверки,
	              |	-Оценки.ПроцИсполнителя / 100 КАК Коэффициент
	              |ИЗ
	              |	Оценки КАК Оценки
	              |ГДЕ
	              |	Оценки.ПроцИсполнителя > 0
	              |	И Оценки.ДатаАктирования > &ПустаяДата
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Оценки.НачОтдела,
	              |	СУММА(Оценки.ПроцНачОтдела) КАК ПроцНачОтдела
	              |ИЗ
	              |	Оценки КАК Оценки
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	Оценки.НачОтдела
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Оценки.ЗамНачОтдела,
	              |	СУММА(Оценки.ПроцЗамНачОтдела) КАК ПроцЗамНачОтдела
	              |ИЗ
	              |	Оценки КАК Оценки
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	Оценки.ЗамНачОтдела
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	Оценки.НачСектора,
	              |	СУММА(Оценки.ПроцНачСектора) КАК ПроцНачСектора
	              |ИЗ
	              |	Оценки КАК Оценки
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	Оценки.НачСектора";
				  
	Результат = Запрос.ВыполнитьПакет();
  
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаОценок", Результат[0].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаИсполнителей", Результат[2].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаНачОтделов", Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаЗамНачОтделов", Результат[4].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаНачСекторов", Результат[5].Выгрузить());
		
КонецПроцедуры 

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
  // ВедомостьКомиссииСОшибками
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.М_ВедомостьКомиссии";
  КомандаПечати.Идентификатор = "ПФ_MXL_ВедомостьКомиссииСОшибками";
  КомандаПечати.Представление = НСтр("ru = 'Печать ведомости с ошибками'");
  КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокумента";

  // ВедомостьКомиссииБезОшибок
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Документ.М_ВедомостьКомиссии";
  КомандаПечати.Идентификатор = "ПФ_MXL_ВедомостьКомиссииБезОшибок";
  КомандаПечати.Представление = НСтр("ru = 'Печать ведомости без ошибок'");
  КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокумента";
  
  // ВедомостьКомиссии по подразделениям
  КомандаПечати = КомандыПечати.Добавить();
  КомандаПечати.МенеджерПечати = "Обработка.мПечатьВедомостьКомиссииПоПодразделениям";
  КомандаПечати.Обработчик = "мУправлениеПечатьюКлиентДокументооборот.ПечатьВедомостиКомиссииПоПодразделениям";
  КомандаПечати.Представление = НСтр("ru = 'Печать ведомости по подразделениям'");
  КомандаПечати.СписокФорм    = "ФормаСписка,ФормаДокумента";

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
  // Устанавливаем признак доступности печати покомплектно.
  ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
  
  // Проверяем, нужно ли для макета формировать табличный документ.
  Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ВедомостьКомиссииСОшибками") Тогда
    // Формируем табличный документ и добавляем его в коллекцию печатных форм.
    УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
      "ПФ_MXL_ВедомостьКомиссииСОшибками", "Ведомость комиссии с ошибками", ПечатьВедомостьКомиссииСОшибками(МассивОбъектов, ОбъектыПечати));
  КонецЕсли;
  
  Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ВедомостьКомиссииБезОшибок") Тогда
    // Формируем табличный документ и добавляем его в коллекцию печатных форм.
    УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
      "ПФ_MXL_ВедомостьКомиссииБезОшибок", "Ведомость комиссии без ошибок", ПечатьВедомостьКомиссииБезОшибок(МассивОбъектов, ОбъектыПечати));
  КонецЕсли;
  
КонецПроцедуры

Функция ПечатьВедомостьКомиссииСОшибками(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ПФ_MXL_ВедомостьКомиссииСОшибками";
	// Запомним номер строки с которой начали выводить текущий документ
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	Макет = ПолучитьМакет("ПФ_MXL_ВедомостьКомиссииСОшибками");

    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка,
        |	М_ВедомостьКомиссииАктыПроверки.НомерСтроки,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки КАК АктПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.НомерАктаПроверки КАК НомерАктаПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.ДатаАктаПроверки КАК ДатаАктаПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.ВидПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.КоэффициентПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.Исполнитель.ПредставлениеВДокументах КАК Исполнитель,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаКомиссии,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаНачОтдела,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаГлавСпеца,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаНачОтряда,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаСТКК,
        |	М_ВедомостьКомиссииАктыПроверки.НачальникОтдела.ПредставлениеВДокументах КАК НачальникОтдела,
        |	М_ВедомостьКомиссииАктыПроверки.ГлавныйСпециалист.ПредставлениеВДокументах КАК ГлавныйСпециалист,
        |	М_ВедомостьКомиссииАктыПроверки.НачальникОтряда.ПредставлениеВДокументах КАК НачальникОтряда,
        |	М_ВедомостьКомиссииАктыПроверки.ПроверяющийСТКК.ПредставлениеВДокументах КАК ПроверяющийСТКК,
        |	М_ВедомостьКомиссииАктыПроверки.Корреспондент,
        |	М_ВедомостьКомиссииАктыПроверки.Договор.РегистрационныйНомер КАК НомерДоговора,
        |	М_ВедомостьКомиссииАктыПроверки.Договор.ДатаРегистрации КАК ДатаДоговора,
        |	М_ВедомостьКомиссииАктыПроверки.ВидРабот,
        |	М_ВедомостьКомиссииАктыПроверки.НаименованиеОбъекта,
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка.Номер КАК НомерВедомости,
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка.Дата КАК ДатаВедомости,
        |	М_ВедомостьКомиссииАктыПроверки.ДатаАктирования КАК ДатаАктирования,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.Замечания.(
        |		Замечание КАК Замечание,
        |		Проверяющий.ПредставлениеВДокументах КАК Проверяющий,
        |		Содержание КАК Содержание,
        |		Оценка КАК Оценка
        |	) КАК Замечания
        |ИЗ
        |	Документ.М_ВедомостьКомиссии.АктыПроверки КАК М_ВедомостьКомиссииАктыПроверки
        |ГДЕ
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка В(&МассивОбъектов)";

    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

    РезультатЗапроса = Запрос.Выполнить();

    Выборка = РезультатЗапроса.Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьЗамечания = Макет.ПолучитьОбласть("Замечания");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	НомерПП = 0;

	ПервыйДокумент = Истина;
    Пока Выборка.Следующий() Цикл
        
		Если ПервыйДокумент Тогда
			//ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			// Заголовок
			ОбластьЗаголовок.Параметры.НазваниеПредприятия = Константы.НаименованиеПредприятия.Получить();
			ОбластьЗаголовок.Параметры.Заголовок = "Ведомость комиссии № "+Число(Выборка.НомерВедомости) + 
			" от " + Формат(Выборка.ДатаВедомости, "ДЛФ=D");
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			// Шапка
			ТабличныйДокумент.Вывести(ОбластьШапка);
		КонецЕсли;
		ПервыйДокумент = Ложь;
        
         // Строки
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, Выборка);
		
		НомерПП = НомерПП+1;
		ОбластьСтрока.Параметры.НомерПП = НомерПП;
		ОбластьСтрока.Параметры.АктПроверки = Выборка.НомерАктаПроверки + " от " + Формат(Выборка.ДатаАктаПроверки, "ДЛФ=D");
		ОбластьСтрока.Параметры.Договор = Выборка.НомерДоговора + " от " + Формат(Выборка.ДатаДоговора, "ДЛФ=D");
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		 // Замечания
		 НомерЗамечания = 0;
		 ВыборкаЗамечания = Выборка.Замечания.Выбрать();
		 Пока ВыборкаЗамечания.Следующий() Цикл
		 	ЗаполнитьЗначенияСвойств(ОбластьЗамечания.Параметры, ВыборкаЗамечания);
			
			НомерЗамечания = НомерЗамечания + 1;
			ОбластьЗамечания.Параметры.НомерЗамечания = НомерЗамечания;

			ТабличныйДокумент.Вывести(ОбластьЗамечания);
		 КонецЦикла; 
		ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);
    КонецЦикла;

	Возврат ТабличныйДокумент;
КонецФункции 

Функция ПечатьВедомостьКомиссииБезОшибок(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_ВедомостьКомиссииБезОшибок";
	// Запомним номер строки с которой начали выводить текущий документ
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	Макет = ПолучитьМакет("ПФ_MXL_ВедомостьКомиссииБезОшибок");

    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка,
        |	М_ВедомостьКомиссииАктыПроверки.НомерСтроки,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.НомерАктаПроверки КАК НомерАктаПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.АктПроверки.ДатаАктаПроверки КАК ДатаАктаПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.ВидПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.КоэффициентПроверки,
        |	М_ВедомостьКомиссииАктыПроверки.Исполнитель.ПредставлениеВДокументах КАК Исполнитель,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаКомиссии,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаНачОтдела,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаГлавСпеца,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаНачОтряда,
        |	М_ВедомостьКомиссииАктыПроверки.ОценкаСТКК,
        |	М_ВедомостьКомиссииАктыПроверки.НачальникОтдела.ПредставлениеВДокументах КАК НачальникОтдела,
        |	М_ВедомостьКомиссииАктыПроверки.ГлавныйСпециалист.ПредставлениеВДокументах КАК ГлавныйСпециалист,
        |	М_ВедомостьКомиссииАктыПроверки.НачальникОтряда.ПредставлениеВДокументах КАК НачальникОтряда,
        |	М_ВедомостьКомиссииАктыПроверки.ПроверяющийСТКК.ПредставлениеВДокументах КАК ПроверяющийСТКК,
        |	М_ВедомостьКомиссииАктыПроверки.Корреспондент,
        |	М_ВедомостьКомиссииАктыПроверки.Договор.РегистрационныйНомер Как НомерДоговора,
        |	М_ВедомостьКомиссииАктыПроверки.Договор.ДатаРегистрации Как ДатаДоговора,
        |	М_ВедомостьКомиссииАктыПроверки.ВидРабот,
        |	М_ВедомостьКомиссииАктыПроверки.НаименованиеОбъекта,
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка.Номер КАК НомерВедомости,
        |	М_ВедомостьКомиссииАктыПроверки.ДатаАктирования КАК ДатаАктирования,
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка.Дата КАК ДатаВедомости
        |ИЗ
        |	Документ.М_ВедомостьКомиссии.АктыПроверки КАК М_ВедомостьКомиссииАктыПроверки
        |ГДЕ
        |	М_ВедомостьКомиссииАктыПроверки.Ссылка В(&МассивОбъектов)";

    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

    РезультатЗапроса = Запрос.Выполнить();

    Выборка = РезультатЗапроса.Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	НомерПП = 0;

	ПервыйДокумент = Истина;
    Пока Выборка.Следующий() Цикл
        
		Если ПервыйДокумент Тогда
			//ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			// Заголовок
			ОбластьЗаголовок.Параметры.НазваниеПредприятия = Константы.НаименованиеПредприятия.Получить();
			ОбластьЗаголовок.Параметры.Заголовок = "Ведомость комиссии № "+Число(Выборка.НомерВедомости) + 
			" от " + Формат(Выборка.ДатаВедомости, "ДЛФ=D");
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			// Шапка
			ТабличныйДокумент.Вывести(ОбластьШапка);
		КонецЕсли;
		ПервыйДокумент = Ложь;
        
         // Строки
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, Выборка);
		
		НомерПП = НомерПП+1;
		ОбластьСтрока.Параметры.НомерПП = НомерПП;
		ОбластьСтрока.Параметры.АктПроверки = Выборка.НомерАктаПроверки + " от " + Формат(Выборка.ДатаАктаПроверки, "ДЛФ=D");
		ОбластьСтрока.Параметры.Договор = Выборка.НомерДоговора + " от " + Формат(Выборка.ДатаДоговора, "ДЛФ=D");
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
        
    КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции 
