
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
    // Запись итоговой оценки в Акты проверок, в случае, когда они отличаются от 
    //  оценки, выставленной в акте
    
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
    
	Документы.М_ВедомостьКомиссии.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
    
    ЗаписатьИтоговыеОценкиАктовПроверки(ДополнительныеСвойства, Отказ);
    ПересчитатьНачислениеЗП(ДополнительныеСвойства, Отказ);
    
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
КонецПроцедуры

Процедура ПересчитатьНачислениеЗП(ДополнительныеСвойства, Отказ)
    
    Таб = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаИсполнителей;
	
	МассивЭтапов = Таб.ВыгрузитьКолонку("ЭтапДоговора");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗПОбороты.Подразделение,
		|	НачислениеЗПОбороты.Исполнитель,
		|	НачислениеЗПОбороты.ЭтапДоговора,
		|	НачислениеЗПОбороты.БазовоеНачислениеОборот КАК БазовоеНачисление,
		|	НачислениеЗПОбороты.Должность,
		|	НачислениеЗПОбороты.Оклад,
		|	НачислениеЗПОбороты.ОбъектРабот
		|ИЗ
		|	РегистрНакопления.НачислениеЗП.Обороты(&ДатаНач, &ДатаКон, Запись, ЭтапДоговора В (&МассивЭтапов)) КАК НачислениеЗПОбороты";
	
	Запрос.УстановитьПараметр("ДатаНач", НачалоМесяца(ЭтотОбъект.Дата));
	Запрос.УстановитьПараметр("ДатаКон", КонецМесяца(ЭтотОбъект.Дата));
	Запрос.УстановитьПараметр("МассивЭтапов", МассивЭтапов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Движения.НачислениеЗП.Записывать = Истина;
	Движения.НачислениеЗП.Очистить();
	
	Для каждого Стр Из Таб Цикл
		//Если Не ЗначениеЗаполнено(Стр.ДатаАктирования) Тогда
		//	Продолжить;
		//КонецЕсли; 
		
		ПараметрыОтбора = Новый Структура("ЭтапДоговора", Стр.ЭтапДоговора); 
		ИскСтроки = Результат.НайтиСтроки(ПараметрыОтбора);
		Если ИскСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		Для каждого СтрЭтап Из ИскСтроки Цикл
			
			Если СтрЭтап.Подразделение.Родитель.Наименование = "Отдел геоинформационных систем и технологий" Тогда
				Продолжить;
			КонецЕсли; 
		
			Движение = Движения.НачислениеЗП.Добавить();
			//Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = ЭтотОбъект.Дата;
			Движение.Подразделение = СтрЭтап.Подразделение;
			Движение.Исполнитель = СтрЭтап.Исполнитель;
			Движение.Должность = СтрЭтап.Должность;
			Движение.Оклад = СтрЭтап.Оклад;
			Движение.ОбъектРабот = СтрЭтап.ОбъектРабот;
			Движение.ЭтапДоговора = СтрЭтап.ЭтапДоговора;
			Движение.Сумма = СтрЭтап.БазовоеНачисление*Стр.Коэффициент;
			Движение.Коэффициент = Стр.Коэффициент;
			
		КонецЦикла; 
		
	КонецЦикла; 

КонецПроцедуры

Процедура ЗаписатьИтоговыеОценкиАктовПроверки(ДополнительныеСвойства, Отказ)
    
    СостояниеПроверен = Перечисления.М_СостоянияАктовПроверки.Проверен;
    
    Таб = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОценок;
    
    Для каждого Стр Из Таб Цикл
        Если Стр.АктПроверки.ОценкаИтоговая = Стр.ОценкаИтоговая Тогда
			Продолжить;
		КонецЕсли; 
		
		АктПроверки = Стр.АктПроверки.ПолучитьОбъект();
        АктПроверки.ОценкаИтоговая = Стр.ОценкаИтоговая;
        АктПроверки.Состояние = СостояниеПроверен;
    
 		Попытка
            АктПроверки.Записать();
		Исключение
			Сообщить("Не удалось записать Акт проверки качества "+АктПроверки.Ссылка);
            Отказ = Истина;
		КонецПопытки;
    КонецЦикла; 

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
    // Отмена Запись итоговой оценки в Акты проверок, в случае, когда они отличаются от 
    //  оценки, выставленной в акте
    
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
    
	Документы.М_ВедомостьКомиссии.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
    
    ОтменитьЗаписатьИтоговыеОценкиАктовПроверки(ДополнительныеСвойства, Отказ);
    
КонецПроцедуры

Процедура ОтменитьЗаписатьИтоговыеОценкиАктовПроверки(ДополнительныеСвойства, Отказ)
    
    СостояниеНаКомиссии = Перечисления.М_СостоянияАктовПроверки.НаКомиссии;
    
    Таб = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОценок;
    
    Для каждого Стр Из Таб Цикл
    
        Если Стр.АктПроверки.ОценкаИтоговая = Стр.Оценка Тогда
			Продолжить;
		КонецЕсли; 
		
		АктПроверки = Стр.АктПроверки.ПолучитьОбъект();
        АктПроверки.ОценкаИтоговая = Стр.Оценка;
        АктПроверки.Состояние = СостояниеНаКомиссии;
    
 		Попытка
            АктПроверки.Записать();
		Исключение
			Сообщить("Не удалось записать Акт проверки качества "+АктПроверки.Ссылка);
            Отказ = Истина;
		КонецПопытки;
    КонецЦикла; 

КонецПроцедуры

