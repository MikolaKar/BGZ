
&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мРеестрПочтовыхОтправленийПисьма.Письмо
		|ПОМЕСТИТЬ НаПочте
		|ИЗ
		|	Документ.мРеестрПочтовыхОтправлений.Письма КАК мРеестрПочтовыхОтправленийПисьма
		|ГДЕ
		|	мРеестрПочтовыхОтправленийПисьма.Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
		|	И мРеестрПочтовыхОтправленийПисьма.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходящиеДокументыПолучатели.Получатель,
		|	ИсходящиеДокументыПолучатели.Ссылка КАК Письмо,
		|	1 КАК Уведомление
		|ИЗ
		|	Справочник.ИсходящиеДокументы.Получатели КАК ИсходящиеДокументыПолучатели
		|ГДЕ
		|	НЕ ИсходящиеДокументыПолучатели.Ссылка В
		|				(ВЫБРАТЬ
		|					НаПочте.Письмо
		|				ИЗ
		|					НаПочте КАК НаПочте)
		|	И ИсходящиеДокументыПолучатели.СпособОтправки = &СпособОтправки1
		|	И ИсходящиеДокументыПолучатели.ДатаОтправки МЕЖДУ &Дата1 И &Дата2
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящиеДокументыПолучатели.Получатель,
		|	ИсходящиеДокументыПолучатели.Ссылка,
		|	0
		|ИЗ
		|	Справочник.ИсходящиеДокументы.Получатели КАК ИсходящиеДокументыПолучатели
		|ГДЕ
		|	ИсходящиеДокументыПолучатели.СпособОтправки = &СпособОтправки2
		|	И ИсходящиеДокументыПолучатели.ДатаОтправки МЕЖДУ &Дата1 И &Дата2
		|	И НЕ ИсходящиеДокументыПолучатели.Ссылка В
		|				(ВЫБРАТЬ
		|					НаПочте.Письмо
		|				ИЗ
		|					НаПочте КАК НаПочте)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Письмо";
	
	Запрос.УстановитьПараметр("Дата1", НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("СпособОтправки1", Справочники.СпособыДоставки.НайтиПоНаименованию("Почта с уведомлением"));
	Запрос.УстановитьПараметр("СпособОтправки2", Справочники.СпособыДоставки.НайтиПоНаименованию("Заказная почта"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Объект.Письма.Очистить();
	Объект.Отправления.Очистить();
	
	Пока Выборка.Следующий() Цикл
		Стр = Объект.Письма.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка);
		
		// Заполнение отправлений
		Отбор = Новый Структура("Получатель", Выборка.Получатель); 
		ИскСтроки = Объект.Отправления.НайтиСтроки(Отбор);
		Если ИскСтроки.Количество() = 0 Тогда
			Стр = Объект.Отправления.Добавить();
			ЗаполнитьЗначенияСвойств(Стр, Выборка);
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры
