&НаКлиенте
Процедура НайтиАкт(Команда)
	НомерАкта = "";
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", ЭтотОбъект, Параметры);
	ПоказатьВводЗначения(Оповещение,
	НомерАкта, "Укажите номер акта", Тип("Строка"));
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(НомерАкта, Параметры) Экспорт
    Если НомерАкта <> Неопределено Тогда
		МассивМест = НайтиАктирование(НомерАкта);
		
		НайденоАктов = МассивМест.Количество();
		
		Если НайденоАктов = 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не найдено ни одного акта с номером "+НомерАкта;
			Сообщение.Сообщить(); 
			Возврат;
			
		ИначеЕсли НайденоАктов = 1 Тогда
			Место = МассивМест[0];
		Иначе
			// уточнение номера акта
			СпАктов = Новый СписокЗначений;
			Для каждого Место Из МассивМест Цикл
				СпАктов.Добавить(Место, Место.НомерАкта);
			КонецЦикла;
			ИскМесто = СпАктов.ВыбратьЭлемент("Выберите номер акта");
			Если ИскМесто = Неопределено Тогда
				Возврат;
			КонецЕсли; 
			Место = ИскМесто.Значение;
		КонецЕсли; 
		
		ПараметрыФормы = Новый Структура("Ключ", Место.Документ);
		ПараметрыФормы.Вставить("НомерАкта", Место.НомерАкта);
		ПараметрыФормы.Вставить("НомерСтроки", Место.НомерСтроки);
		ОткрытьФорму("Документ.мАктирование.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, ЭтаФорма.УникальныйИдентификатор);
    КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НайтиАктирование(НомерАкта) 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мАктированиеАкты.НомерАкта КАК НомерАкта,
		|	мАктированиеАкты.Ссылка КАК Документ,
		|	мАктированиеАкты.НомерСтроки
		|ИЗ
		|	Документ.мАктирование.Акты КАК мАктированиеАкты
		|ГДЕ
		|	НЕ мАктированиеАкты.Ссылка.ПометкаУдаления
		|	И мАктированиеАкты.НомерАкта Подобно &НомерАкта";
	
	Запрос.УстановитьПараметр("НомерАкта", "%"+НомерАкта+"%");
	
	Результат = Запрос.Выполнить();
	
	МассивМест = Новый Массив;
	
	Если Результат.Пустой() Тогда
		Возврат МассивМест;
	КонецЕсли; 
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Место = Новый Структура("Документ, НомерСтроки, НомерАкта", Выборка.Документ, Выборка.НомерСтроки, Выборка.НомерАкта); 
		МассивМест.Добавить(Место);
	КонецЦикла;
	Возврат МассивМест;
КонецФункции

&НаКлиенте
Процедура СоздатьАктирование(Команда)
	ОткрытьФорму("Обработка.мСоздатьАктирование.Форма.Форма");
КонецПроцедуры

