&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНДС(ТекДанные)
	Если ЗначениеЗаполнено(ТекДанные.СтавкаНДС) Тогда
		РеквНДС = ПолучитьРеквизитыСтавкиНДС(ТекДанные.СтавкаНДС);
		
		Если РеквНДС.ЗначениеСтавкиНДС <> Неопределено Тогда
			ТекДанные.СуммаНДС = Окр(ТекДанные.Цена * РеквНДС.ЗначениеСтавкиНДС / 100, 2); 
		Иначе
			ТекДанные.СуммаНДС = 0; 
		КонецЕсли; 
		Если РеквНДС.БезНДС <> Неопределено И РеквНДС.БезНДС Тогда
			ТекДанные.ОсвобождениеОтНДС = "";
		КонецЕсли; 
	Иначе
		ТекДанные.СуммаНДС = 0; 
		ТекДанные.ОсвобождениеОтНДС = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтоимость(ТекДанные)
	ТекДанные.Стоимость = ТекДанные.Цена + ТекДанные.СуммаНДС;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыСтавкиНДС(СтавкаНДС)
	РеквНДС = Новый Структура("ЗначениеСтавкиНДС, БезНДС", Неопределено, Неопределено); 
	Если ЗначениеЗаполнено(СтавкаНДС) Тогда
		РеквНДС.Вставить("ЗначениеСтавкиНДС", СтавкаНДС.Ставка);
		РеквНДС.Вставить("БезНДС", СтавкаНДС.НеОблагается);
	КонецЕсли;
	Возврат РеквНДС;
КонецФункции  

&НаСервереБезКонтекста
Функция ЗнакОкр(Дата) Экспорт
	Если Дата < Дата(2016, 7, 1) Тогда
		Возврат 0;
	Иначе	
	    Возврат 2;
	КонецЕсли; 
КонецФункции // ЗнакОкр()

&НаСервереБезКонтекста
Функция ПолучитьЗначениеСтавкиНДС(СтавкаНДС)
	Возврат СтавкаНДС.Ставка;
КонецФункции // ПолучитьЗначениеСтавкиНДС(СтавкаНДС)

&НаКлиенте
Процедура ЦеныЦенаПриИзменении(Элемент)
	ТекДанные = Элементы.Цены.ТекущиеДанные;
	РассчитатьНДС(ТекДанные);
	РассчитатьСтоимость(ТекДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЦеныСтавкаНДСПриИзменении(Элемент)
	ТекДанные = Элементы.Цены.ТекущиеДанные;
	РассчитатьНДС(ТекДанные);
	РассчитатьСтоимость(ТекДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЦеныСуммаНДСПриИзменении(Элемент)
	ТекДанные = Элементы.Цены.ТекущиеДанные;
	РассчитатьСтоимость(ТекДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЦеныПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекДанные = Элементы.Цены.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекДанные.СтавкаНДС) Тогда
		ЗначениеСтавкиНДС = ПолучитьЗначениеСтавкиНДС(ТекДанные.СтавкаНДС);
		Если ЗначениеСтавкиНДС = 0 И НЕ ЗначениеЗаполнено(ТекДанные.ОсвобождениеОтНДС) Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В строке " + Элементы.Цены.ТекущиеДанные.НомерСтроки + " не заполнен пункт Освобождение от НДС!";
			ИндексСтроки = Элементы.Цены.ТекущиеДанные.НомерСтроки - 1; 
			Сообщение.Поле = "Цены["+ИндексСтроки+"].ОсвобождениеОтНДС";
			Сообщение.Сообщить();
		ИначеЕсли ЗначениеСтавкиНДС > 0 И ЗначениеЗаполнено(ТекДанные.ОсвобождениеОтНДС) Тогда
			ТекДанные.ОсвобождениеОтНДС = "";
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры
