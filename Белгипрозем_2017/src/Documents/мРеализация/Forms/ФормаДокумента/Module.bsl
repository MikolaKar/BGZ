#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Состояние = Перечисления.мСостоянияАктовВыполненныхРабот.Подготовлен;
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаСервере
Процедура ЭтапДоговораПриИзмененииНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мЭтапыДоговоровНоменклатура.Ссылка,
		|	мЭтапыДоговоровНоменклатура.НоменклатураПроизводства,
		|	мЭтапыДоговоровНоменклатура.Калькуляция,
		|	мЭтапыДоговоровНоменклатура.Цена,
		|	мЭтапыДоговоровНоменклатура.Количество,
		|	мЭтапыДоговоровНоменклатура.Сумма,
		|	мЭтапыДоговоровНоменклатура.СтавкаНДС,
		|	мЭтапыДоговоровНоменклатура.ОсвобождениеОтНДС,
		|	мЭтапыДоговоровНоменклатура.НДС,
		|	мЭтапыДоговоровНоменклатура.Всего,
		|	мЭтапыДоговоровНоменклатура.КоличествоКвартал
		|ИЗ
		|	Справочник.мЭтапыДоговоров.Номенклатура КАК мЭтапыДоговоровНоменклатура
		|ГДЕ
		|	мЭтапыДоговоровНоменклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.ЭтапДоговора);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Объект.Номенклатура.Очистить();
	
	Пока Выборка.Следующий() Цикл
		Стр = Объект.Номенклатура.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЭтапДоговораПриИзменении(Элемент)
	Если Объект.Номенклатура.Количество() > 0 Тогда
		Параметр = "";
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияВопроса", ЭтотОбъект, Параметр);

		ПоказатьВопрос(Оп, "Табличная часть будет заполнена номенклатурой из этапа. Продолжать?",
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ЭтапДоговораПриИзмененииНаСервере();		
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияВопроса(Результат, Параметр) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	ЭтапДоговораПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицы

&НаКлиенте
Процедура НоменклатураНоменклатураПроизводстваПриИзменении(Элемент)
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	ТекДанные.Цена = ПолучитьЦенуКалькуляции(ТекДанные.Калькуляция);
	ТекДанные.Количество = ?(ТекДанные.Количество=0, 1, ТекДанные.Количество);
	ПересчитатьСтроку();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКалькуляцияПриИзменении(Элемент)
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	ТекДанные.Цена = ПолучитьЦенуКалькуляции(ТекДанные.Калькуляция);
	ТекДанные.Количество = ?(ТекДанные.Количество=0, 1, ТекДанные.Количество);
	ПересчитатьСтроку();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураЦенаПриИзменении(Элемент)
	ПересчитатьСтроку();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКоличествоПриИзменении(Элемент)
	ПересчитатьСтроку();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСтавкаНДСПриИзменении(Элемент)
	РассчитатьНДС();
	РассчитатьВсего();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСуммаПриИзменении(Элемент)
	РассчитатьНДС();
	РассчитатьВсего();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНДСПриИзменении(Элемент)
	РассчитатьВсего();
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьЦенуКалькуляции(Калькуляция)
	Если ЗначениеЗаполнено(Калькуляция) Тогда
		Возврат Калькуляция.Цена;
	КонецЕсли; 
	Возврат 0;	
КонецФункции 

&НаКлиенте
Процедура ПересчитатьСтроку()
	РассчитатьСумму();
	РассчитатьНДС();
	РассчитатьВсего();
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьСумму()
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ТекДанные.Сумма = ТекДанные.Цена * ТекДанные.Количество;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНДС()
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ТекДанные.СтавкаНДС) Тогда
		РеквНДС = ПолучитьРеквизитыСтавкиНДС(ТекДанные.СтавкаНДС);
		
		Если РеквНДС.ЗначениеСтавкиНДС <> Неопределено Тогда
			ТекДанные.НДС = Окр(ТекДанные.Сумма * РеквНДС.ЗначениеСтавкиНДС / 100, 2); 
		Иначе
			ТекДанные.НДС = 0; 
		КонецЕсли; 
		Если РеквНДС.БезНДС <> Неопределено И РеквНДС.БезНДС Тогда
			ТекДанные.ОсвобождениеОтНДС = "";
		КонецЕсли; 
	Иначе
		ТекДанные.НДС = 0; 
		ТекДанные.ОсвобождениеОтНДС = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьВсего()
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ТекДанные.Всего = ТекДанные.Сумма + ТекДанные.НДС;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыСтавкиНДС(СтавкаНДС)
	РеквНДС = Новый Структура("ЗначениеСтавкиНДС, БезНДС", Неопределено, Неопределено); 
	Если ЗначениеЗаполнено(СтавкаНДС) Тогда
		РеквНДС.Вставить("ЗначениеСтавкиНДС", СтавкаНДС.Ставка);
		РеквНДС.Вставить("БезНДС", СтавкаНДС.НеОблагается);
	КонецЕсли;
	Возврат РеквНДС;
КонецФункции  




#КонецОбласти 


