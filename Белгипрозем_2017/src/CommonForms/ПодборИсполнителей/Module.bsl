
&НаКлиенте
Перем ТекущийЭлементФормы;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьВидПросмотра();
	
	Если Не Параметры.Свойство("Заголовок", Заголовок) Тогда
		Заголовок = НСтр("ru = 'Подбор исполнителей'");
	КонецЕсли;
	
	Если Не Параметры.Свойство("ВыбранныеЗаголовок", Элементы.Выбранные.Заголовок) Тогда
		Элементы.Выбранные.Заголовок = НСтр("ru = 'Выбранные пользователи и роли'");
	КонецЕсли;
	
	Если Не Параметры.Свойство("ПодбиратьГруппыПользователей", ПодбиратьГруппыПользователей) Тогда
		ПодбиратьГруппыПользователей = Ложь;
	КонецЕсли;
	
	ПоказыватьФункции = Истина;
	Если Не Параметры.Свойство("ПоказыватьФункции", ПоказыватьФункции) Тогда
		ПоказыватьФункции = Истина;
	КонецЕсли;
	Если ПоказыватьФункции Тогда
		Элементы.Функции.Видимость = Истина;
		Если Параметры.Свойство("ПоказыватьФункцииДокумента") И Параметры.ПоказыватьФункцииДокумента = Истина Тогда
			СписокФункций = ШаблоныДокументов.ПолучитьСписокДоступныхФункций();
		Иначе
			Если Параметры.Свойство("ИменаПредметовДляФункций") Тогда
				ИменаПредметовДляФункций = Параметры.ИменаПредметовДляФункций;
			Иначе
				ИменаПредметовДляФункций = Неопределено;
			КонецЕсли;
			СписокФункций = ШаблоныБизнесПроцессов.ПолучитьСписокДоступныхФункций(ИменаПредметовДляФункций);
		КонецЕсли;
	Иначе
		Элементы.Функции.Видимость = Ложь;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ПоказыватьРоли", ПоказыватьРоли) Тогда
		ПоказыватьРоли = Истина;
	КонецЕсли;
	Элементы.Роли.Видимость = ПоказыватьРоли;
	Если ПоказыватьРоли Тогда
		ТолькоПростыеРоли = Ложь;
		Если Параметры.Свойство("ТолькоПростыеРоли", ТолькоПростыеРоли) И ТолькоПростыеРоли = Истина Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРоли.Отбор, "ИспользуетсяБезОбъектовАдресации", Истина);
		КонецЕсли;
		БезВнешнихРолей = Ложь;
		Если Параметры.Свойство("БезВнешнихРолей", БезВнешнихРолей) И БезВнешнихРолей = Истина Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРоли.Отбор, "ВнешняяРоль", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	ПоказыватьКорреспондентов = Ложь;
	Если Не Параметры.Свойство("ПоказыватьКорреспондентов", ПоказыватьКорреспондентов) Тогда
		ПоказыватьКорреспондентов = Ложь;
	КонецЕсли;
	Если ПоказыватьКорреспондентов Тогда
		ЗаполнитьДеревоКорреспондентов();
		Элементы.Корреспонденты.Видимость = Истина;
	Иначе
		Элементы.Корреспонденты.Видимость = Ложь;
	КонецЕсли;
	
	ПоказыватьМоиКонтакты = Ложь;
	Если Не Параметры.Свойство("ПоказыватьМоиКонтакты", ПоказыватьМоиКонтакты) Тогда
		ПоказыватьМоиКонтакты = Ложь;
	КонецЕсли;
	Если ПоказыватьМоиКонтакты Тогда
		ЗаполнитьМоиКонтакты();
		Элементы.МоиКонтакты.Видимость = Истина;
	Иначе
		Элементы.МоиКонтакты.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Выбранные") Тогда
		Если ТипЗнч(Параметры.Выбранные) = Тип("Массив") Тогда
			Для каждого Элемент Из Параметры.Выбранные Цикл
				Если ТипЗнч(Элемент) = Тип("Структура") Тогда
					ЗаполнитьЗначенияСвойств(Выбранные.Добавить(), Элемент);
				Иначе
					Выбранные.Добавить().Исполнитель = Элемент;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресВременногоХранилища") И ЭтоАдресВременногоХранилища(Параметры.АдресВременногоХранилища) Тогда
		АдресВременногоХранилища = Параметры.АдресВременногоХранилища;
		Выбранные.Загрузить(ПолучитьИзВременногоХранилища(АдресВременногоХранилища));
	КонецЕсли;
	
	ВыбранныеКоличество = Выбранные.Количество();
	Для Инд = 1 По ВыбранныеКоличество Цикл
		СтрокаВыбранные = Выбранные[ВыбранныеКоличество - Инд];
		Если Не ЗначениеЗаполнено(СтрокаВыбранные.Исполнитель) И 
			 Не ЗначениеЗаполнено(СтрокаВыбранные.ОсновнойОбъектАдресации) И
			 Не ЗначениеЗаполнено(СтрокаВыбранные.ДополнительныйОбъектАдресации) Тогда 
		Выбранные.Удалить(СтрокаВыбранные);
		КонецЕсли;
	КонецЦикла;	
	
	Если Не ПоказыватьРоли И Не ПоказыватьФункции Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
	ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
	
	ОтобразитьИерархию();
	
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы",
		"ОтображатьФотографииПерсональнаяНастройка",
		Истина);
		
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");	
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	ПолучатьФотографии = Истина;
	
	Если Не ОтображатьФотографииОбщаяНастройка 
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая
		Или ПриложениеЯвляетсяВебКлиентом Тогда
		ПолучатьФотографии = Ложь;
		Элементы.ГруппаФото.Видимость = Ложь;
	КонецЕсли;	
	
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		Элементы.ГруппаКарточкаПользователя.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлементФормы = Неопределено;
	ПодключитьОбработчикОжидания("ЗапомнитьТекущийЭлемент", 1);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПользователейПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Элемент.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
		ВыборПользователя(Элемент.ТекущиеДанные.Ссылка);
	Иначе	
		ВыборРоли(Элемент.ТекущиеДанные.Ссылка, Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПользователейВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборГруппыПользователей(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПредприятияВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборПодразделения(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыборРоли(Значение);
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборФункции(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение)=Тип("ДанныеФормыЭлементКоллекции")
		И ПараметрыПеретаскивания.Значение.Свойство("Ссылка") Тогда
		Если ТипЗнч(ПараметрыПеретаскивания.Значение.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда 
			ВыборПользователя(ПараметрыПеретаскивания.Значение.Ссылка); 
		ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение.Ссылка) = Тип("СправочникСсылка.РолиИсполнителей") Тогда 
			ВыборРоли(ПараметрыПеретаскивания.Значение.Ссылка);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.ГруппыПользователей") Тогда 
		ВыборГруппыПользователей(ПараметрыПеретаскивания.Значение); 

	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.РолиИсполнителей") Тогда 
		ВыборРоли(ПараметрыПеретаскивания.Значение);
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Число") Тогда 
		ВыборФункции(ПараметрыПеретаскивания.Значение);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПередНачаломИзменения(Элемент, Отказ)
	ПоказатьЗначение(, Элементы.СписокПользователи.ТекущиеДанные.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ВидПросмотраПриИзменении(Элемент)
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ТекущаяГруппаПользователей = Неопределено;
		ТекущееПодразделение = Неопределено;
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ТекущаяГруппаПользователей = Неопределено;
		Если ЗначениеЗаполнено(Элементы.СписокПользователи.ТекущаяСтрока) Тогда
			ВрПодразделение = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Подразделение;
		Иначе
			ВрПодразделение = Неопределено;
		КонецЕсли;
		Элементы.СтруктураПредприятия.ТекущаяСтрока = ВрПодразделение;
		ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ТекущееПодразделение = Неопределено;
		Элементы.ГруппыПользователей.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи");
		ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	
	ОтобразитьИерархию();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ВыборКорреспондента(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЛистДереваМоиКонтакты(Выборка, КореньДерева, ВеткиДереваДляГрупп);
	
	СтрокаДереваГруппы = Неопределено;
	ЭлементыДляДобавленияПользователей = КореньДерева;
	
	Если ЗначениеЗаполнено(Выборка.ГруппаСсылка) Тогда
	
		СтрокаДереваГруппы = ВеткиДереваДляГрупп.Получить(Выборка.ГруппаСсылка);
		Если СтрокаДереваГруппы = Неопределено Тогда
			
			СтрокаДереваГруппы = КореньДерева.Добавить();
			ВеткиДереваДляГрупп.Вставить(Выборка.ГруппаСсылка, СтрокаДереваГруппы);
			
			СтрокаДереваГруппы.Наименование = Выборка.ГруппаНаименование;
			СтрокаДереваГруппы.Группа = Выборка.ГруппаСсылка;
			СтрокаДереваГруппы.Ссылка = Выборка.ГруппаСсылка;
			СтрокаДереваГруппы.НомерКартинки = ?(Выборка.ГруппаПометкаУдаления, 2, 3);
			СтрокаДереваГруппы.Представление = СтрокаДереваГруппы.Наименование;
			
		КонецЕсли;
		
		ЭлементыДляДобавленияПользователей = СтрокаДереваГруппы.ПолучитьЭлементы();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ЛичныеАдресатыСсылка) Тогда
		
		СтрокаДереваПользователь = ЭлементыДляДобавленияПользователей.Добавить();
		
		СтрокаДереваПользователь.Наименование = Выборка.ЛичныеАдресатыНаименование;
		СтрокаДереваПользователь.Группа = Выборка.ГруппаСсылка;
		СтрокаДереваПользователь.Ссылка = Выборка.ЛичныеАдресатыСсылка;
		СтрокаДереваПользователь.НомерКартинки = ?(Выборка.ЛичныеАдресатыПометкаУдаления = Истина, 0, 1);
		СтрокаДереваПользователь.ЭтоАдресат = Истина;
		
		Для Каждого СтрокаКонтактнойИнформации Из Выборка.ЛичныеАдресатыСсылка.КонтактнаяИнформация Цикл
			Если СтрокаКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
				СтрокаДереваПользователь.АдресEmail = СтрокаКонтактнойИнформации.АдресЭП;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(СтрокаДереваПользователь.АдресEmail) Тогда
			СтрокаДереваПользователь.НомерКартинки = 4;
		КонецЕсли;	
		
		СтрокаДереваПользователь.Представление = РаботаСоСтроками.ПолучитьПредставлениеАдресаЭлектроннойПочты(
				Строка(СтрокаДереваПользователь.Наименование),
				СтрокаДереваПользователь.АдресEmail);
		
	КонецЕсли;	
	
	Выборка.Строки.Сортировать("ГруппаНаименование, ЛичныеАдресатыНаименование");
	Для Каждого СтрокаВыборкиДерево Из Выборка.Строки Цикл
		
		ЗаполнитьЛистДереваМоиКонтакты(СтрокаВыборкиДерево, 
			СтрокаДереваГруппы.ПолучитьЭлементы(), ВеткиДереваДляГрупп);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ПоказатьЗначение(, Элементы.ДеревоКонтактов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ВыборЛичногоАдресата(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЛичногоАдресата(ВыбранноеЗначение)
	
	ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
	Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПриАктивизацииСтроки(Элемент)
	
	СписокПользователиПриАктивизацииСтрокиВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокПользователи.ТекущиеДанные <> Неопределено Тогда
		
		ТекущаяСтрока = Элементы.СписокПользователи.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ТекущийПользователь = ТекущаяСтрока;
			
			Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
				ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			Иначе
				Фотография = "";
			КонецЕсли;		
			
		КонецЕсли;
		
	Иначе
		
		Фотография = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьФотоПользователя(ТекущаяСтрока, УникальныйИдентификатор, Фотография)
	
	Если ПолучатьФотографии Тогда
	
		Если ЭтоАдресВременногоХранилища(Фотография) Тогда
			УдалитьИзВременногоХранилища(Фотография);
		КонецЕсли;	
		
		Фотография = "";
		Если ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.РолиИсполнителей")
			И ТипЗнч(ТекущаяСтрока) <> Тип("Строка") Тогда
			
			ЕстьКартинка = Ложь;
			Фотография = РаботаСФотографиями.ПолучитьАдресФото(ТекущаяСтрока, УникальныйИдентификатор, ЕстьКартинка);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Фотография) Тогда
			Фотография = "";
			
			Если ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница 
				<> ЭтаФорма.Элементы.СтраницаКартинкаПоУмолчанию Тогда
				ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаКартинкаПоУмолчанию;	
			КонецЕсли;	
		Иначе	
			Если ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница 
				<> ЭтаФорма.Элементы.СтраницаФотография Тогда
				ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаФотография;			
			КонецЕсли;			
		КонецЕсли;
		
	КонецЕсли;	

	// карточка пользователя
	ОписаниеПользователяИмя = Строка(ТекущаяСтрока);
	ОписаниеПользователяПодразделение = "";
	
	// Заполнение подразделения и должности
	Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Пользователи") Тогда
		СведенияПользователей = РегистрыСведений.СведенияОПользователях.Получить(Новый Структура("Пользователь", ТекущаяСтрока));
		ОписаниеПользователяПодразделение = Строка(СведенияПользователей.Подразделение);
	КонецЕсли;
	ОписаниеПользователяАдрес = "";
	// карточка пользователя
	
	Если ЗначениеЗаполнено(ТекущаяСтрока)
		И ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.РолиИсполнителей")
		И ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.СтруктураПредприятия")
		И ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.ГруппыПользователей")
		И ТипЗнч(ТекущаяСтрока) <> Тип("Строка") Тогда
		
		ТаблицаКонтактов = УправлениеКонтактнойИнформацией.ЗначенияКонтактнойИнформацииОбъекта(ТекущаяСтрока, 
			Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			
		Для Каждого Строка Из ТаблицаКонтактов Цикл
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(ОписаниеПользователяАдрес, ";", Строка.Значение);
				
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыПриАктивизацииСтроки(Элемент)
	СписокКорреспондентыПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПриАктивизацииСтроки(Элемент)
	ДеревоКонтактовПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокКорреспонденты.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.СписокКорреспонденты.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.ДеревоКонтактов.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.ДеревоКонтактов.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиПриАктивизацииСтроки(Элемент)
	СписокРолиПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокРоли.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.СписокРоли.ТекущиеДанные.Наименование;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийПриАктивизацииСтроки(Элемент)
	СписокФункцийПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Функции Тогда
	
		Если Элементы.СписокФункций.ТекущиеДанные <> Неопределено Тогда
		
			ТекущаяСтрока = Элементы.СписокФункций.ТекущиеДанные.Представление;
			
			Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
				
				ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
				ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
				
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	Если Выбранные.Количество() = 0 Тогда
		ВключитьНаКлиенте(Ложь, Отказ);      	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(АдресВременногоХранилища) Тогда
		Результат = Новый Массив;
		Для каждого ВыбранныеСтрока Из Выбранные Цикл
			Участник = Новый Структура("Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
			ЗаполнитьЗначенияСвойств(Участник, ВыбранныеСтрока);
			Результат.Добавить(Участник);
		КонецЦикла;
		ОповеститьОВыборе(Результат);
		Возврат;
	КонецЕсли;
	ПоместитьИсполнителейВоВременноеХранилище();
	ОповеститьОВыборе(АдресВременногоХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура Включить(Команда)
	
	ВключитьНаКлиенте();		
	
КонецПроцедуры

&НаКлиенте
Процедура Исключить(Команда)
	Если Элементы.Выбранные.ВыделенныеСтроки.Количество() > 0 Тогда
		Для каждого Строка Из Элементы.Выбранные.ВыделенныеСтроки Цикл								
			СтрокаПоИдентификатору = Выбранные.НайтиПоИдентификатору(Строка);
			Выбранные.Удалить(СтрокаПоИдентификатору);
		КонецЦикла; 			
	КонецЕсли;	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбновитьФотоПользователя()
	
	ПоказатьФотоПользователя(ТекущийКонтактДляКарточкиПользователя, УникальныйИдентификатор, Фотография);	
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМоиКонтакты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникГруппыЛичныхАдресатов.Ссылка КАК ГруппаСсылка,
		|	СправочникГруппыЛичныхАдресатов.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникГруппыЛичныхАдресатов.Предопределенный КАК ГруппаПредопределенный,
		|	СправочникГруппыЛичныхАдресатов.Наименование КАК ГруппаНаименование,
		|	ЛичныеАдресаты.Ссылка КАК ЛичныеАдресатыСсылка,
		|	ЛичныеАдресаты.ПометкаУдаления КАК ЛичныеАдресатыПометкаУдаления,
		|	ЛичныеАдресаты.Наименование КАК ЛичныеАдресатыНаименование
		|ИЗ
		|	Справочник.ГруппыЛичныхАдресатов КАК СправочникГруппыЛичныхАдресатов
		|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
		|		ПО СправочникГруппыЛичныхАдресатов.Ссылка = ЛичныеАдресаты.Группа
		|ГДЕ
		|	ЕСТЬNULL(СправочникГруппыЛичныхАдресатов.Пользователь, ЛичныеАдресаты.Пользователь) = &Пользователь
		|	И ЕСТЬNULL(ЛичныеАдресаты.Пользователь, СправочникГруппыЛичныхАдресатов.Пользователь) = &Пользователь
		|	И ЛичныеАдресаты.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГруппаСсылка ИЕРАРХИЯ,
		|	ЛичныеАдресатыНаименование";
		
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДеревоКонтактов.ПолучитьЭлементы().Очистить();
	КореньДерева = ДеревоКонтактов.ПолучитьЭлементы();
	ВеткиДереваДляГрупп = Новый Соответствие;
	
	ВыборкаДерево.Строки.Сортировать("ГруппаНаименование, ЛичныеАдресатыНаименование");
	Для Каждого СтрокаВыборкиДерево Из ВыборкаДерево.Строки Цикл
		
		ЗаполнитьЛистДереваМоиКонтакты(СтрокаВыборкиДерево, КореньДерева, ВеткиДереваДляГрупп);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьИсполнителейВоВременноеХранилище()
	ПоместитьВоВременноеХранилище(Выбранные.Выгрузить(), АдресВременногоХранилища);
КонецПроцедуры	

&НаСервере
Процедура НастроитьВидПросмотра()
	
	Элементы.ВидПросмотра.СписокВыбора.Очистить();
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'Списком'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По подразделениям'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По группам пользователей'"));
	
	ВидПросмотра = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы, "ВидПросмотра");
	Если Элементы.ВидПросмотра.СписокВыбора.НайтиПоЗначению(ВидПросмотра) = Неопределено Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьТекущийЭлемент()
	
	Если ТекущийЭлемент = Элементы.СтруктураПредприятия 
	 Или ТекущийЭлемент = Элементы.ГруппыПользователей
	 Или ТекущийЭлемент = Элементы.СписокПользователи Тогда
		ТекущийЭлементФормы = ТекущийЭлемент;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущееПодразделение <> Элементы.СтруктураПредприятия.ТекущаяСтрока Тогда
				ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Если Элементы.ГруппыПользователей.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущаяГруппаПользователей <> Элементы.ГруппыПользователей.ТекущаяСтрока Тогда
				ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНаКлиенте(КонтролироватьДубли = Истина, Отказ = Ложь)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи Тогда 	
		
		Если ТекущийЭлементФормы = Элементы.СтруктураПредприятия Тогда
			
			ЕстьПодчиненные = Ложь;
			Для каждого Строка Из Элементы.СтруктураПредприятия.ВыделенныеСтроки Цикл
				ДанныеСтроки = Элементы.СтруктураПредприятия.ДанныеСтроки(Строка);
				Если ДанныеСтроки.ЕстьПодчиненные Тогда 
					ЕстьПодчиненные = Истина;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ВыбратьПодчиненные", 
				ЭтотОбъект,
				Новый Структура("КонтролироватьДубли", КонтролироватьДубли));

			ВыбратьПодчиненных = Ложь;
			Если ЕстьПодчиненные Тогда 
				ТекстВопроса = НСтр("ru = 'Выбрать сотрудников подчиненных подразделений?'");
				
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, 
					РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
			Иначе 
				ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Нет);
			КонецЕсли;
			
		ИначеЕсли ТекущийЭлементФормы = Элементы.ГруппыПользователей Тогда
			
			Если ПодбиратьГруппыПользователей Тогда
				
				Для каждого Строка Из Элементы.ГруппыПользователей.ВыделенныеСтроки Цикл
					ВыборГруппыПользователей(Строка, КонтролироватьДубли);
				КонецЦикла;
				
			Иначе
			
				ЕстьПодчиненные = Ложь;
				Для каждого Строка Из Элементы.ГруппыПользователей.ВыделенныеСтроки Цикл
					ДанныеСтроки = Элементы.ГруппыПользователей.ДанныеСтроки(Строка);
					Если ДанныеСтроки.ЕстьПодчиненные Тогда 
						ЕстьПодчиненные = Истина;
						Прервать;
					КонецЕсли;	
				КонецЦикла;	
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ВыбратьПодчиненныеГруппыПользователей", 
					ЭтотОбъект,
					Новый Структура("КонтролироватьДубли", КонтролироватьДубли));
					
				Если ЕстьПодчиненные Тогда 
					ТекстВопроса = НСтр("ru = 'Выбрать пользователей подчиненных групп?'");
					ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, 
						РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
				Иначе 
					ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Нет);
				КонецЕсли;
			КонецЕсли;
			
		Иначе	
			
			Для каждого Строка Из Элементы.СписокПользователи.ВыделенныеСтроки Цикл					
				ВыборПользователя(СписокПользователи.НайтиПоИдентификатору(Строка).Ссылка, КонтролироватьДубли);					
			КонецЦикла;
		
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Роли Тогда 
		
		Для каждого Строка Из Элементы.СписокРоли.ВыделенныеСтроки Цикл					
			ВыборРоли(Строка,, КонтролироватьДубли);
		КонецЦикла; 			
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Функции Тогда 
		
		ВыборФункции(Элементы.СписокФункций.ТекущаяСтрока, КонтролироватьДубли);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Корреспонденты Тогда 	
		
		ВыборКорреспондента(Элементы.СписокКорреспонденты.ТекущиеДанные, КонтролироватьДубли);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодчиненныеГруппыПользователей(Результат, Параметры) Экспорт 
	
	ВыбратьПодчиненных = (Результат = КодВозвратаДиалога.Да);		
	Для каждого Строка Из Элементы.ГруппыПользователей.ВыделенныеСтроки Цикл
		ВыборГруппыПользователей(Строка, Параметры.КонтролироватьДубли, ВыбратьПодчиненных);
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыбратьПодчиненные(Результат, Параметры) Экспорт 
	
	ВыбратьПодчиненных = (Результат = КодВозвратаДиалога.Да);		
	Для каждого Строка Из Элементы.СтруктураПредприятия.ВыделенныеСтроки Цикл
		ВыборПодразделения(Строка, Параметры.КонтролироватьДубли, ВыбратьПодчиненных);
	КонецЦикла;
	
КонецПроцедуры	    

&НаКлиенте
Процедура ВыборПользователя(ВыбранноеЗначение, КонтролироватьДубли = Истина)
	
	ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
	
	Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;	
	
КонецПроцедуры

// Выполняет перенос в список выбранных группу пользователей, 
// либо пользователей, входящих в эту группу.
//
// Параметры:
//  ВыбранноеЗначение - СправочникСсылка.ГруппыПользователей - выбранная группа пользователей.
//  КонтролироватьДубли - Булево - если Истина, то при выборе группы пользователей,
//								   уже имеющейся в списке выбранных, будет выдано предупреждение.
//
&НаКлиенте
Процедура ВыборГруппыПользователей(ВыбранноеЗначение, КонтролироватьДубли = Истина, ВыбратьПодчиненных = Неопределено)
	
	Если ПодбиратьГруппыПользователей Тогда
		
		ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
		Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			НоваяСтрока = Выбранные.Добавить();
			НоваяСтрока.Исполнитель = ВыбранноеЗначение;
			Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;	

	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыборГруппыПользователейПродолжение", 
			ЭтотОбъект,
			Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));

		Если ВыбратьПодчиненных = Неопределено Тогда 
			ВыбратьПодчиненных = Ложь;
			ДанныеСтроки = Элементы.ГруппыПользователей.ДанныеСтроки(ВыбранноеЗначение);
			Если ДанныеСтроки.ЕстьПодчиненные Тогда 
				ТекстВопроса = НСтр("ru = 'Выбрать пользователей подчиненных групп?'");
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, 
					РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
				Возврат;	
			КонецЕсли;	
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, 
			?(ВыбратьПодчиненных = Истина, КодВозвратаДиалога.Да, ВыбратьПодчиненных));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборГруппыПользователейПродолжение(Результат, Параметры) Экспорт 
	
	ВыбратьПодчиненных = (Результат = КодВозвратаДиалога.Да);		
	ПользователиГруппы = ПолучитьПользователейГруппыПользователей(Параметры.ВыбранноеЗначение, ВыбратьПодчиненных);
	Для Каждого ПользовательГруппы Из ПользователиГруппы Цикл
		ВыборПользователя(ПользовательГруппы, Ложь);
	КонецЦикла;
	
КонецПроцедуры

// Выполняет перенос в список выбранных всех пользователей подразделения.
//
// Параметры:
//  ВыбранноеЗначение - СправочникСсылка.СтруктураПредприятия - выбранное подразделение.
//  КонтролироватьДубли - Булево - если Истина, то при выборе пользователя,
//								   уже имеющегося в списке выбранных, будет выдано предупреждение.
//
&НаКлиенте
Процедура ВыборПодразделения(ВыбранноеЗначение, КонтролироватьДубли = Истина, ВыбратьПодчиненных = Неопределено)
		
	Если ВыбратьПодчиненных = Неопределено Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВыборПодразделенияПродолжение", 
			ЭтотОбъект,
			Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));

		ДанныеСтроки = Элементы.СтруктураПредприятия.ДанныеСтроки(ВыбранноеЗначение);
		Если ДанныеСтроки.ЕстьПодчиненные Тогда 
			ТекстВопроса = НСтр("ru = 'Выбрать сотрудников подчиненных подразделений?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,
				РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
		Иначе 
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Нет);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПользователиПодразделения = 
		РаботаСПользователями.ПолучитьПользователейПодразделения(ВыбранноеЗначение, ВыбратьПодчиненных);
	Для Каждого ПользовательПодразделения Из ПользователиПодразделения Цикл
		ВыборПользователя(ПользовательПодразделения, Ложь);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ВыборПодразделенияПродолжение(Результат, Параметры) Экспорт 
	
	ВыбратьПодчиненных = (Результат = КодВозвратаДиалога.Да);		
	ПользователиПодразделения = 
		РаботаСПользователями.ПолучитьПользователейПодразделения(Параметры.ВыбранноеЗначение, ВыбратьПодчиненных);
	Для Каждого ПользовательПодразделения Из ПользователиПодразделения Цикл
		ВыборПользователя(ПользовательПодразделения, Ложь);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРоли(ВыбранноеЗначение, СтруктураПредприятияТекущаяСтрока = Неопределено, КонтролироватьДубли = Истина)
	
	ПараметрыРоли = РаботаСПользователями.ПолучитьПараметрыРоли(ВыбранноеЗначение, СтруктураПредприятияТекущаяСтрока);
	РезультатВыбора = Неопределено;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ВыборРолиПродолжение", 
		ЭтотОбъект,
		Новый Структура("КонтролироватьДубли", КонтролироватьДубли));

	Если Не ПараметрыРоли.ИспользуетсяСОбъектамиАдресации Тогда 
		РезультатВыбора = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено);
	Иначе	
		
		Если СтруктураПредприятияТекущаяСтрока <> Неопределено Тогда
			
			ЕстьНезаполненныйТип = Ложь;
			
			Если ПараметрыРоли.ТипыОсновногоОбъектаАдресации <> Неопределено
				И ПараметрыРоли.ТипыОсновногоОбъектаАдресации.Количество() > 0 Тогда
				
				Если ПараметрыРоли.ОсновнойОбъектАдресации = Неопределено Тогда
					ЕстьНезаполненныйТип = Истина;
				КонецЕсли;	
					
			КонецЕсли;	
			
			Если ПараметрыРоли.ТипыДополнительногоОбъектаАдресации <> Неопределено
				И ПараметрыРоли.ТипыДополнительногоОбъектаАдресации.Количество() > 0 Тогда
				
				Если ПараметрыРоли.ДополнительныйОбъектАдресации = Неопределено Тогда
					ЕстьНезаполненныйТип = Истина;
				КонецЕсли;	
				
			КонецЕсли;	
			
			Если Не ЕстьНезаполненныйТип Тогда
				РезультатВыбора = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
					ВыбранноеЗначение, ПараметрыРоли.ОсновнойОбъектАдресации, ПараметрыРоли.ДополнительныйОбъектАдресации);
			КонецЕсли;		
			
		КонецЕсли;	
		
		Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
			ОткрытьФорму("ОбщаяФорма.ВыборРолиИсполнителя", 
				Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
					ВыбранноеЗначение, ПараметрыРоли.ОсновнойОбъектАдресации, ПараметрыРоли.ДополнительныйОбъектАдресации),
				ЭтаФорма,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Возврат;
		КонецЕсли;
	
	КонецЕсли;	
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРолиПродолжение(РезультатВыбора, Параметры) Экспорт 
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		
		ПараметрыОтбора = Новый Структура("Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
			РезультатВыбора.РольИсполнителя, 
			РезультатВыбора.ОсновнойОбъектАдресации, 
			РезультатВыбора.ДополнительныйОбъектАдресации);
			
		Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			НоваяСтрока = Выбранные.Добавить();
			НоваяСтрока.Исполнитель = РезультатВыбора.РольИсполнителя;
			НоваяСтрока.ОсновнойОбъектАдресации = РезультатВыбора.ОсновнойОбъектАдресации;
			НоваяСтрока.ДополнительныйОбъектАдресации = РезультатВыбора.ДополнительныйОбъектАдресации;
			Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФункции(ВыбраннаяСтрока, КонтролироватьДубли = Истина)
	
	СтрокаСписка = СписокФункций.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ВыбранноеЗначение = СтрокаСписка.Представление;
	
	Если СтрокаСписка.Пометка Тогда
		Текст = НСтр("ru = 'Нельзя выбрать автоподстановку, которая зависит от предмета. 
			|Для выбора такой автоподставновки нужно добавить предмет в шаблон.'");
		ПоказатьПредупреждение(,Текст);
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
	
	Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыборКорреспондента(ТекущиеДанные, КонтролироватьДубли = Истина)
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;	
	
	ВыбранноеЗначение = ТекущиеДанные.Ссылка;
	ПараметрыОтбора = Новый Структура("Исполнитель", ВыбранноеЗначение);
	
	Если Выбранные.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
		
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Исполнитель = ВыбранноеЗначение;
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ОтобразитьИерархию()
	
	Если ПустаяСтрока(ВидПросмотра) Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Элементы.СтраницыГруппировкаПользователей.Видимость = Ложь;
	Иначе
		Элементы.СтраницыГруппировкаПользователей.Видимость = Истина;
		Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
			Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаСтруктураПредприятия;
		ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
			Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаГруппыПользователей;
		Иначе
			ВызватьИсключение НСтр("ru = 'Некорректный вид просмотра'");
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПользователей();
	ХранилищеСистемныхНастроек.Сохранить(ИмяФормы, "ВидПросмотра", ВидПросмотра);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователей()
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ЗаполнитьПользователейСписком();
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда	
		ЗаполнитьПользователейВГруппе();
	Иначе
		ЗаполнитьПользователейВОтделе();
	КонецЕсли;	
	
КонецПроцедуры	                              

&НаСервере
Процедура ЗаполнитьПользователейСписком()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ СведенияОПользователях.Пользователь ЕСТЬ NULL 
	|				И СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|ГДЕ
	|	СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
				   
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
	Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВГруппе()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ПО СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|ГДЕ
	|	(НЕ &ИспользоватьИерархию
	|			ИЛИ СведенияОПользователях.Подразделение = &Подразделение)
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ГруппыПользователейСостав.Ссылка = &ГруппаПользователей)
	|	И СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|	И (НЕ &ИспользоватьИерархию
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|						ПО
	|							СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|								И СведенияОПользователях.Подразделение = &Подразделение
	|				ГДЕ
	|					СправочникПользователи.Недействителен = ЛОЖЬ))
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|						ПО
	|							СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|								И ГруппыПользователейСостав.Ссылка = &ГруппаПользователей))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
				   
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("ГруппаПользователей", ТекущаяГруппаПользователей);
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Истина);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Истина);
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ПоказатьФотоПользователя(ТекущееПодразделение, УникальныйИдентификатор, Фотография);	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ПоказатьФотоПользователя(ТекущаяГруппаПользователей, УникальныйИдентификатор, Фотография);	
	КонецЕсли;
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВОтделе()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ПО СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|ГДЕ
	|	(НЕ &ИспользоватьИерархию
	|			ИЛИ СведенияОПользователях.Подразделение = &Подразделение)
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ГруппыПользователейСостав.Ссылка = &ГруппаПользователей)
	|	И СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|	И (НЕ &ИспользоватьИерархию
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|						ПО
	|							СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|								И СведенияОПользователях.Подразделение = &Подразделение
	|				ГДЕ
	|					СправочникПользователи.Недействителен = ЛОЖЬ))
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|						ПО
	|							СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|								И ГруппыПользователейСостав.Ссылка = &ГруппаПользователей))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
				   
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("ГруппаПользователей", ТекущаяГруппаПользователей);
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Истина);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Истина);
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ПоказатьФотоПользователя(ТекущееПодразделение, УникальныйИдентификатор, Фотография);	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ПоказатьФотоПользователя(ТекущаяГруппаПользователей, УникальныйИдентификатор, Фотография);	
	КонецЕсли;
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьДеревоКорреспондентов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ИСТИНА
	|			ТОГДА КонтактныеЛица.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Корреспонденты.ПустаяСсылка)
	|	КОНЕЦ КАК Ссылка,
	|	КонтактныеЛица.Наименование,
	|	КонтактныеЛица.Владелец КАК Владелец,
	|	КонтактныеЛица.Должность,
	|	ЛОЖЬ КАК ЭтоГруппа,
	|	КонтактныеЛица.Владелец.ПометкаУдаления КАК ВладелецПометкаУдаления,
	|	КонтактныеЛица.Владелец.ЭтоГруппа КАК ВладелецЭтоГруппа,
	|	ВЫБОР
	|		КОГДА КонтактныеЛица.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Владелец <> ЗНАЧЕНИЕ(Справочник.Корреспонденты.ПустаяСсылка)
	|ИТОГИ ПО
	|	Владелец ИЕРАРХИЯ";
	
	ДеревоКорреспонденты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Для Каждого Строка Из ДеревоКорреспонденты.Строки Цикл
		ЗаполнитьСтрокуДереваКорреспондентов(Строка);
	КонецЦикла;	
	
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.Владелец);
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.ВладелецПометкаУдаления);
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.ВладелецЭтоГруппа);
	
	ЗначениеВРеквизитФормы(ДеревоКорреспонденты, "СписокКорреспонденты");
	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСтрокуДереваКорреспондентов(СтрокаДерева)
	
	Если Не ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда 
		СтрокаДерева.Ссылка = СтрокаДерева.Владелец;
		СтрокаДерева.Наименование = Строка(СтрокаДерева.Владелец);
		СтрокаДерева.ЭтоГруппа = СтрокаДерева.ВладелецЭтоГруппа;
		Если СтрокаДерева.ВладелецЭтоГруппа Тогда 
			Если СтрокаДерева.ВладелецПометкаУдаления Тогда 
				СтрокаДерева.ИндексКартинки = 1;
			Иначе	
			    СтрокаДерева.ИндексКартинки = 0;
			КонецЕсли;	
		Иначе
			Если СтрокаДерева.ВладелецПометкаУдаления Тогда 
				СтрокаДерева.ИндексКартинки = 4;
			Иначе	
			    СтрокаДерева.ИндексКартинки = 3;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Для каждого Строка Из СтрокаДерева.Строки цикл
		ЗаполнитьСтрокуДереваКорреспондентов(Строка);
	КонецЦикла;	
	
КонецПроцедуры	

// Получает список пользователей, входящих в группу пользователей
// из табличной части Состав
Функция ПолучитьПользователейГруппыПользователей(ГруппаПользователей, СУчетомИерархии = Истина) 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ГруппаПользователей = Справочники.ГруппыПользователей.ВсеПользователи Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Ссылка
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	НЕ Пользователи.ПометкаУдаления
		|	И НЕ Пользователи.Недействителен";
		
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	Иначе	
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ГруппыПользователейСостав.Пользователь
		|ИЗ
		|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		|ГДЕ
		|	НЕ ГруппыПользователейСостав.Пользователь.ПометкаУдаления
		|	И НЕ ГруппыПользователейСостав.Пользователь.Недействителен";
		
		Если СУчетомИерархии Тогда
			Запрос.Текст = Запрос.Текст + " И ГруппыПользователейСостав.Ссылка В ИЕРАРХИИ (&Ссылка)";	
		Иначе
			Запрос.Текст = Запрос.Текст + " И ГруппыПользователейСостав.Ссылка = &Ссылка";
		КонецЕсли;
		Запрос.УстановитьПараметр("Ссылка", ГруппаПользователей);
		
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
		
	КонецЕсли;
	
КонецФункции
