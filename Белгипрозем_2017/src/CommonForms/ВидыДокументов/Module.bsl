
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Делопроизводство.ИспользоватьВидыВнутреннихДокументов() Тогда 
		Группы.Добавить("Внутренние", НСтр("ru = 'Внутренние документы'"));
	КонецЕсли;	
	
	Если Делопроизводство.ИспользоватьВидыВходящихДокументов() Тогда 
		Группы.Добавить("Входящие", НСтр("ru = 'Входящие документы'"));
	КонецЕсли;	
	
	Если Делопроизводство.ИспользоватьВидыИсходящихДокументов() Тогда 
		Группы.Добавить("Исходящие", НСтр("ru = 'Исходящие документы'"));
	КонецЕсли;	
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда 
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Элементы.ГруппаРежимВыбора.Видимость = Истина;
		
		Элементы.СписокВходящие.РежимВыбора = Истина;
		Элементы.СписокИсходящие.РежимВыбора = Истина;
		Элементы.СписокВнутренние.РежимВыбора = Истина;
		
		Если Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда 
			Если ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыВходящихДокументов") Тогда 
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВходящие;
				Элементы.Панели.ТекущаяСтраница = Элементы.ПанельВходящие;
				Элементы.СписокВходящие.ТекущаяСтрока = Параметры.ТекущаяСтрока;
				Элементы.Группы.ТекущаяСтрока = 0;
			ИначеЕсли ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыИсходящихДокументов") Тогда 
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокИсходящие;
				Элементы.Панели.ТекущаяСтраница = Элементы.ПанельИсходящие;
				Элементы.СписокИсходящие.ТекущаяСтрока = Параметры.ТекущаяСтрока;
				Элементы.Группы.ТекущаяСтрока = 1;
			ИначеЕсли ТипЗнч(Параметры.ТекущаяСтрока) = Тип("СправочникСсылка.ВидыВнутреннихДокументов") Тогда 	
				Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВнутренние;
				Элементы.Панели.ТекущаяСтраница = Элементы.ПанельВнутренние;
				Элементы.СписокВнутренние.ТекущаяСтрока = Параметры.ТекущаяСтрока;
				Элементы.Группы.ТекущаяСтрока = 2;
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Группы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда 
		Значение = ТекущиеДанные.Значение;
		Элементы.Списки.ТекущаяСтраница = Элементы["ГруппаСписок" + Значение];
		Элементы.Панели.ТекущаяСтраница = Элементы["Панель" + Значение];
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВходящие Тогда 
		ОповеститьОВыборе(Элементы.СписокВходящие.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокИсходящие Тогда 
		ОповеститьОВыборе(Элементы.СписокИсходящие.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Списки.ТекущаяСтраница = Элементы.ГруппаСписокВнутренние Тогда 
		ОповеститьОВыборе(Элементы.СписокВнутренние.ТекущаяСтрока);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВходящиеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсходящиеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВнутренниеВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповеститьОВыборе(Значение);
	
КонецПроцедуры
