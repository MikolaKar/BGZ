&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Объект") Тогда
		Если Параметры.Объект.ПодписанЭП Тогда
			ЗаполнитьСписокПодписейОдногоОбъекта(Параметры.Объект, Параметры.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Подписи") Тогда
		ЗаполнитьСписокПодписейИзМассива(Параметры.Подписи, Параметры.УникальныйИдентификатор);
	КонецЕсли;
	
	БольшеНеСпрашивать = Ложь;
	СохранятьВсеПодписи = Перечисления.ДействияПриСохраненииСЭП.СохранятьВсеПодписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	МассивВозврата = Новый Массив;
	
	Для Каждого Строка Из ТаблицаПодписей Цикл
		Если Строка.Пометка Тогда
			СтруктураВозврата = Новый Структура("АдресПодписи, КомуВыданСертификат, ИмяФайлаПодписи", 
												Строка.АдресПодписи,
												Строка.КомуВыданСертификат,
												Строка.ИмяФайлаПодписи);
			МассивВозврата.Добавить(СтруктураВозврата);
		КонецЕсли;
	КонецЦикла;
	
	Если БольшеНеСпрашивать Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ЭП", "ДействияПриСохраненииСЭП", СохранятьВсеПодписи);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;	
	
	Закрыть(МассивВозврата);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписейОдногоОбъекта(ОбъектСсылка, УникальныйИдентификаторВызывающейФормы)
	
	Выборка = РаботаСЭП.ПолучитьЭлектронныеПодписи(ОбъектСсылка);
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаПодписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		НоваяСтрока.Статус = РаботаСЭПКлиентСервер.ПолучитьОбщийСтатусПроверкиПодписи(
			Выборка.ПодписьВерна, Выборка.СертификатДействителен, Выборка.ДатаПроверкиПодписи);
		НоваяСтрока.ИндексКартинки = -1;
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(
			Выборка.Подпись.Получить(), УникальныйИдентификаторВызывающейФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписейИзМассива(Подписи, УникальныйИдентификаторВызывающейФормы)
	
	Для Каждого Подпись Из Подписи Цикл
		НоваяСтрока = ТаблицаПодписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Подпись);
		
		НоваяСтрока.ИндексКартинки = -1;
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(Подпись.Подпись, УникальныйИдентификаторВызывающейФормы);
	КонецЦикла;
	
КонецПроцедуры
