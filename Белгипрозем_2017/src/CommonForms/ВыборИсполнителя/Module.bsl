////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьВидПросмотра();
	
	Если Не Параметры.Свойство("Заголовок", Заголовок) Тогда
		Заголовок = НСтр("ru = 'Выбор исполнителя'");
	КонецЕсли;
	
	Если Параметры.Свойство("СписокОтбора") Тогда 
		СписокОтбора = Параметры.СписокОтбора;
	КонецЕсли;	
	
	Если Не Параметры.Свойство("ВыбиратьГруппуПользователей", ВыбиратьГруппуПользователей) Тогда
		ВыбиратьГруппуПользователей = Ложь;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ВыбиратьПодразделения", ВыбиратьПодразделения) Тогда
		ВыбиратьПодразделения = Ложь;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ПоказыватьРоли", ПоказыватьРоли) Тогда
		ПоказыватьРоли = Истина;
	КонецЕсли;
	Элементы.Роли.Видимость = ПоказыватьРоли;
	Если ПоказыватьРоли Тогда
		ТолькоПростыеРоли = Ложь;
		Если Параметры.Свойство("ТолькоПростыеРоли", ТолькоПростыеРоли) И ТолькоПростыеРоли = Истина Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРоли.Отбор, "ИспользуетсяБезОбъектовАдресации", Истина);
		КонецЕсли;
		БезВнешнихРолей = Ложь;
		Если Параметры.Свойство("БезВнешнихРолей", БезВнешнихРолей) И БезВнешнихРолей = Истина Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРоли.Отбор, "ВнешняяРоль", Ложь);
		КонецЕсли;
		Если СписокОтбора <> Неопределено Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокРоли.Отбор, 
				"Ссылка", СписокОтбора, ВидСравненияКомпоновкиДанных.ВСписке);
		КонецЕсли;
	КонецЕсли;
	
	ПоказыватьФункции = Истина;
	Если Не Параметры.Свойство("ПоказыватьФункции", ПоказыватьФункции) Тогда
		ПоказыватьФункции = Истина;
	КонецЕсли;
	Если ПоказыватьФункции Тогда
		Элементы.Функции.Видимость = Истина;
		Если Параметры.Свойство("ПоказыватьФункцииДокумента") И Параметры.ПоказыватьФункцииДокумента = Истина Тогда
			СписокФункций = ШаблоныДокументов.ПолучитьСписокДоступныхФункций();
		Иначе
			Если Параметры.Свойство("ИменаПредметовДляФункций") Тогда
				ИменаПредметовДляФункций = Параметры.ИменаПредметовДляФункций;
			Иначе
				ИменаПредметовДляФункций = Неопределено;
			КонецЕсли;
			СписокФункций = ШаблоныБизнесПроцессов.ПолучитьСписокДоступныхФункций(ИменаПредметовДляФункций);
		КонецЕсли;
	Иначе
		Элементы.Функции.Видимость = Ложь;
	КонецЕсли;
	
	ПоказыватьКорреспондентов = Ложь;
	Если Не Параметры.Свойство("ПоказыватьКорреспондентов", ПоказыватьКорреспондентов) Тогда
		ПоказыватьКорреспондентов = Ложь;
	КонецЕсли;
	Если ПоказыватьКорреспондентов Тогда
		ЗаполнитьДеревоКорреспондентов();
		Элементы.Корреспонденты.Видимость = Истина;
	Иначе
		Элементы.Корреспонденты.Видимость = Ложь;
	КонецЕсли;
	
	ПоказыватьМоиКонтакты = Ложь;
	Если Не Параметры.Свойство("ПоказыватьМоиКонтакты", ПоказыватьМоиКонтакты) Тогда
		ПоказыватьМоиКонтакты = Ложь;
	КонецЕсли;
	Если ПоказыватьМоиКонтакты Тогда
		ЗаполнитьМоиКонтакты();
		Элементы.МоиКонтакты.Видимость = Истина;
	Иначе
		Элементы.МоиКонтакты.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПоказыватьРоли И Не ПоказыватьФункции Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
	ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
	
	ОтобразитьИерархию();
	
	Если Параметры.Свойство("Исполнитель") И ЗначениеЗаполнено(Параметры.Исполнитель) Тогда
		УстановитьПозициюНаИсполнителя(Параметры.Исполнитель);
	КонецЕсли;
	
	ОтображатьФотографииПерсональнаяНастройка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиПрограммы",
		"ОтображатьФотографииПерсональнаяНастройка",
		Истина);
		
	ОтображатьФотографииОбщаяНастройка = ПолучитьФункциональнуюОпцию("ОтображатьФотографииОбщаяНастройка");	
	ПриложениеЯвляетсяВебКлиентом = ОбщегоНазначенияДокументооборот.ПриложениеЯвляетсяВебКлиентом();
	
	ПолучатьФотографии = Истина;
	
	Если Не ОтображатьФотографииОбщаяНастройка 
		Или Не ОтображатьФотографииПерсональнаяНастройка
		Или ПриложениеЯвляетсяВебКлиентом Тогда
		ПолучатьФотографии = Ложь;
		Элементы.ГруппаФото.Видимость = Ложь;
	КонецЕсли;	
	
	Если ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая Тогда
		Элементы.ГруппаКарточкаПользователя.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидПросмотра()
	
	Элементы.ВидПросмотра.СписокВыбора.Очистить();
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'Списком'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По подразделениям'"));
	Элементы.ВидПросмотра.СписокВыбора.Добавить(НСтр("ru = 'По группам пользователей'"));
	
	ВидПросмотра = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы, "ВидПросмотра");
	Если Элементы.ВидПросмотра.СписокВыбора.НайтиПоЗначению(ВидПросмотра) = Неопределено Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПозициюНаИсполнителя(Исполнитель)
	
	Если ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
		Подразделение = РаботаСПользователями.ПолучитьПодразделение(Исполнитель);
		Если Не Подразделение.Пустая() Тогда
			ВидПросмотра = НСтр("ru = 'По подразделениям'");
			Элементы.СтруктураПредприятия.ТекущаяСтрока = Подразделение;
			ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
			ОтобразитьИерархию();
			
			МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", Исполнитель));
			Если МассивСтрок.Количество() = 1 Тогда
				Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
		ВидПросмотра = НСтр("ru = 'По группам пользователей'");
		Элементы.ГруппыПользователей.ТекущаяСтрока = Исполнитель;
		ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
		ОтобразитьИерархию();
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		ОсновнойОбъектАдресации = Неопределено;
		Если Параметры.Свойство("ОсновнойОбъектАдресации") Тогда
			ОсновнойОбъектАдресации = Параметры.ОсновнойОбъектАдресации;
		КонецЕсли;
		
		ДополнительныйОбъектАдресации = Неопределено;
		Если Параметры.Свойство("ДополнительныйОбъектАдресации") Тогда
			ДополнительныйОбъектАдресации = Параметры.ДополнительныйОбъектАдресации;
		КонецЕсли;
		
		РольРазмещенаНаЗакладкеПользователи = Ложь;
		Если ОсновнойОбъектАдресации <> Неопределено Или ДополнительныйОбъектАдресации <> Неопределено Тогда
			ТаблицаПользователи = РегистрыСведений.ИсполнителиЗадач.ПолучитьИсполнителейРоли(
									Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации);
			Если ТаблицаПользователи.Количество() = 1 Тогда
				Пользователь = ТаблицаПользователи[0].Исполнитель;
				Подразделение = РаботаСПользователями.ПолучитьПодразделение(Пользователь);
				Если Не Подразделение.Пустая() Тогда
					Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи;
					ВидПросмотра = НСтр("ru = 'По подразделениям'");
					Элементы.СтруктураПредприятия.ТекущаяСтрока = Подразделение;
					ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
					ОтобразитьИерархию();
					
					МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", Исполнитель));
					Если МассивСтрок.Количество() = 1 Тогда
						Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
					КонецЕсли;		
					
					РольРазмещенаНаЗакладкеПользователи = Истина;
					
				КонецЕсли;		
			
			КонецЕсли;		
			
		КонецЕсли;	
		
		Если Не РольРазмещенаНаЗакладкеПользователи Тогда
			Элементы.СписокРоли.ТекущаяСтрока = Исполнитель;
			Элементы.Страницы.ТекущаяСтраница = Элементы.Роли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("Строка") Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.Функции;
		Для Инд = 0 По СписокФункций.Количество() - 1 Цикл
			Если СписокФункций[Инд].Представление = Исполнитель Тогда 
				Элементы.СписокФункций.ТекущаяСтрока = Инд;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
	ИначеЕсли ТипЗнч(Исполнитель) = Тип("СправочникСсылка.КонтактныеЛица")
		Или ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Корреспонденты") Тогда 
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.Корреспонденты;
		
	КонецЕсли;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбработкаОжидания()
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущееПодразделение <> Элементы.СтруктураПредприятия.ТекущаяСтрока Тогда
				ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Если Элементы.ГруппыПользователей.ТекущаяСтрока <> Неопределено Тогда 
			Если ТекущаяГруппаПользователей <> Элементы.ГруппыПользователей.ТекущаяСтрока Тогда
				ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
				ЗаполнитьПользователей();
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи Тогда
		Если ТекущийЭлемент.Имя = "ГруппыПользователей" И ВыбиратьГруппуПользователей Тогда
			Если Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаГруппыПользователей
				И Элементы.ГруппыПользователей.ТекущаяСтрока <> Неопределено Тогда
				
				ОповеститьОВыборе(Элементы.ГруппыПользователей.ТекущаяСтрока);
			КонецЕсли;
		Иначе
			Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
				Если ТипЗнч(Элементы.СписокПользователи.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
					ОповеститьОВыборе(Элементы.СписокПользователи.ТекущиеДанные.Ссылка);
				ИначеЕсли ТипЗнч(Элементы.СписокПользователи.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
					ВыборРоли(Элементы.СписокПользователи.ТекущиеДанные.Ссылка, Элементы.СтруктураПредприятия.ТекущаяСтрока);
				КонецЕсли;
			ИначеЕсли Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаГруппыПользователей Тогда
				Если Элементы.ГруппыПользователей.ТекущаяСтрока <> Неопределено
					И ВыбиратьГруппуПользователей Тогда
					
					ОповеститьОВыборе(Элементы.ГруппыПользователей.ТекущаяСтрока);
				КонецЕсли;
			ИначеЕсли Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаСтруктураПредприятия Тогда
				Если Элементы.СтруктураПредприятия.ТекущаяСтрока <> Неопределено
					И ВыбиратьПодразделения Тогда
					
					ВыборПодразделения(Элементы.СтруктураПредприятия.ТекущаяСтрока);
				КонецЕсли;
			ИначеЕсли СписокПользователи.Количество() = 1 Тогда
				Ссылка = СписокПользователи[0].Ссылка;
				Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
					ОповеститьОВыборе(Ссылка);
				ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
					ВыборРоли(Ссылка, Элементы.СтруктураПредприятия.ТекущаяСтрока);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Роли Тогда
		ВыборРоли(Элементы.СписокРоли.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Функции Тогда
		ВыбраннаяСтрока = Элементы.СписокФункций.ТекущаяСтрока;
		Если СписокФункций[ВыбраннаяСтрока].Пометка Тогда
			Текст = НСтр("ru = 'Нельзя выбрать автоподстановку, которая зависит от предмета. 
						|Для выбора такой автоподставновки нужно добавить предмет в шаблон.'");
			ПоказатьПредупреждение(, Текст);
		Иначе
			ФункцияИсполнитель = СписокФункций[ВыбраннаяСтрока].Представление;
			ОповеститьОВыборе(ФункцияИсполнитель);
		КонецЕсли;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппыПользователей Тогда
		ОповеститьОВыборе(Элементы.СписокГруппПользователей.ТекущаяСтрока);
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Корреспонденты Тогда 	
		
		ТекущиеДанные = Элементы.СписокКорреспонденты.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;	
		
		Если ТекущиеДанные.ЭтоГруппа Тогда 
			Возврат;
		КонецЕсли;	
	
		ОповеститьОВыборе(ТекущиеДанные.Ссылка);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ВыборРоли(ВыбранноеЗначение, СтруктураПредприятияТекущаяСтрока = Неопределено)
	
	ПараметрыРоли = РаботаСПользователями.ПолучитьПараметрыРоли(ВыбранноеЗначение, СтруктураПредприятияТекущаяСтрока);
	
	Результат = Неопределено;
	
	ОповещениеОЗакрытииФормыВыборРолиИсполнителя = Новый ОписаниеОповещения(
		"ВыборРолиЗавершение", ЭтотОбъект);

	Если Не ПараметрыРоли.ИспользуетсяСОбъектамиАдресации Тогда 
		Результат = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", ВыбранноеЗначение, Неопределено, Неопределено);
	Иначе	
		
		Если СтруктураПредприятияТекущаяСтрока <> Неопределено Тогда
			
			ЕстьНезаполненныйТип = Ложь;
			
			Если ПараметрыРоли.ТипыОсновногоОбъектаАдресации <> Неопределено
				И ПараметрыРоли.ТипыОсновногоОбъектаАдресации.Количество() > 0 Тогда
				
				Если ПараметрыРоли.ОсновнойОбъектАдресации = Неопределено Тогда
					ЕстьНезаполненныйТип = Истина;
				КонецЕсли;	
					
			КонецЕсли;	
			
			Если ПараметрыРоли.ТипыДополнительногоОбъектаАдресации <> Неопределено
				И ПараметрыРоли.ТипыДополнительногоОбъектаАдресации.Количество() > 0 Тогда
				
				Если ПараметрыРоли.ДополнительныйОбъектАдресации = Неопределено Тогда
					ЕстьНезаполненныйТип = Истина;
				КонецЕсли;	
				
			КонецЕсли;	
			
			Если Не ЕстьНезаполненныйТип Тогда
				Результат = Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
					ВыбранноеЗначение, ПараметрыРоли.ОсновнойОбъектАдресации, ПараметрыРоли.ДополнительныйОбъектАдресации);
			КонецЕсли;		
			
		КонецЕсли;	
		
		Если ТипЗнч(Результат) <> Тип("Структура") Тогда
			РежимОткрытияФормы = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
			
			ОткрытьФорму("ОбщаяФорма.ВыборРолиИсполнителя", 
				Новый Структура("РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации", 
				ВыбранноеЗначение, ПараметрыРоли.ОсновнойОбъектАдресации, ПараметрыРоли.ДополнительныйОбъектАдресации),
				ЭтаФорма,,,,
				ОповещениеОЗакрытииФормыВыборРолиИсполнителя,
				РежимОткрытияФормы);
				
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;	
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗакрытииФормыВыборРолиИсполнителя, Результат);
	
КонецФункции

&НаКлиенте
Процедура ВыборРолиЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ОповеститьОВыборе(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоКорреспондентов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ИСТИНА
	|			ТОГДА КонтактныеЛица.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Корреспонденты.ПустаяСсылка)
	|	КОНЕЦ КАК Ссылка,
	|	КонтактныеЛица.Наименование,
	|	КонтактныеЛица.Владелец КАК Владелец,
	|	КонтактныеЛица.Должность,
	|	ЛОЖЬ КАК ЭтоГруппа,
	|	КонтактныеЛица.Владелец.ПометкаУдаления КАК ВладелецПометкаУдаления,
	|	КонтактныеЛица.Владелец.ЭтоГруппа КАК ВладелецЭтоГруппа,
	|	ВЫБОР
	|		КОГДА КонтактныеЛица.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Владелец <> ЗНАЧЕНИЕ(Справочник.Корреспонденты.ПустаяСсылка)
	|ИТОГИ ПО
	|	Владелец ИЕРАРХИЯ";
	
	ДеревоКорреспонденты = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Для Каждого Строка Из ДеревоКорреспонденты.Строки Цикл
		ЗаполнитьСтрокуДереваКорреспондентов(Строка);
	КонецЦикла;	
	
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.Владелец);
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.ВладелецПометкаУдаления);
	ДеревоКорреспонденты.Колонки.Удалить(ДеревоКорреспонденты.Колонки.ВладелецЭтоГруппа);
	
	ЗначениеВРеквизитФормы(ДеревоКорреспонденты, "СписокКорреспонденты");
	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСтрокуДереваКорреспондентов(СтрокаДерева)
	
	Если Не ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда 
		СтрокаДерева.Ссылка = СтрокаДерева.Владелец;
		СтрокаДерева.Наименование = Строка(СтрокаДерева.Владелец);
		СтрокаДерева.ЭтоГруппа = СтрокаДерева.ВладелецЭтоГруппа;
		Если СтрокаДерева.ВладелецЭтоГруппа Тогда 
			Если СтрокаДерева.ВладелецПометкаУдаления Тогда 
				СтрокаДерева.ИндексКартинки = 1;
			Иначе	
			    СтрокаДерева.ИндексКартинки = 0;
			КонецЕсли;	
		Иначе
			Если СтрокаДерева.ВладелецПометкаУдаления Тогда 
				СтрокаДерева.ИндексКартинки = 4;
			Иначе	
			    СтрокаДерева.ИндексКартинки = 3;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Для каждого Строка Из СтрокаДерева.Строки цикл
		ЗаполнитьСтрокуДереваКорреспондентов(Строка);
	КонецЦикла;	
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СтруктураПредприятияПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Элемент.ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.Пользователи") Тогда
		ОповеститьОВыборе(Элемент.ТекущиеДанные.Ссылка);
	Иначе	
		ВыборРоли(Элемент.ТекущиеДанные.Ссылка, Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборРоли(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если СписокФункций[ВыбраннаяСтрока].Пометка Тогда
		СтандартнаяОбработка = Ложь;
		Текст = НСтр("ru = 'Нельзя выбрать автоподстановку, которая зависит от предмета. 
			|Для выбора такой автоподставновки нужно добавить предмет в шаблон.'");
		ПоказатьПредупреждение(, Текст);
	Иначе
		ФункцияИсполнитель = СписокФункций[ВыбраннаяСтрока].Представление;
		ОповеститьОВыборе(ФункцияИсполнитель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПередНачаломИзменения(Элемент, Отказ)
	
	ПоказатьЗначение(, Элементы.СписокПользователи.ТекущиеДанные.Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура ГруппыПользователейПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПользователейВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ВыбиратьГруппуПользователей Тогда
		ОповеститьОВыборе(Элементы.ГруппыПользователей.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИерархию()
	
	Если ПустаяСтрока(ВидПросмотра) Тогда
		ВидПросмотра = НСтр("ru = 'По подразделениям'");
	КонецЕсли;
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Элементы.СтраницыГруппировкаПользователей.Видимость = Ложь;
	Иначе
		Элементы.СтраницыГруппировкаПользователей.Видимость = Истина;
		Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
			Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаСтруктураПредприятия;
		ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
			Элементы.СтраницыГруппировкаПользователей.ТекущаяСтраница = Элементы.СтраницаГруппыПользователей;
		Иначе
			ВызватьИсключение НСтр("ru = 'Некорректный вид просмотра'");
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПользователей();
	ХранилищеСистемныхНастроек.Сохранить(ИмяФормы, "ВидПросмотра", ВидПросмотра);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователей()
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ЗаполнитьПользователейСписком();
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда	
		ЗаполнитьПользователейВГруппе();
	Иначе
		ЗаполнитьПользователейВОтделе();
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейСписком()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ СведенияОПользователях.Пользователь ЕСТЬ NULL 
	|				И СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|ГДЕ
	|	СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ СправочникПользователи.Ссылка В (&СписокОтбора))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ ИсполнителиЗадач.РольИсполнителя В (&СписокОтбора))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
	Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	
	Запрос.УстановитьПараметр("ИспользоватьСписокОтбора", СписокОтбора <> Неопределено);
	Запрос.УстановитьПараметр("СписокОтбора", СписокОтбора);
	
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВГруппе()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ПО СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|ГДЕ
	|	(НЕ &ИспользоватьИерархию
	|			ИЛИ СведенияОПользователях.Подразделение = &Подразделение)
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ГруппыПользователейСостав.Ссылка = &ГруппаПользователей)
	|	И СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ СправочникПользователи.Ссылка В (&СписокОтбора))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|	И (НЕ &ИспользоватьИерархию
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|						ПО
	|							СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|								И СведенияОПользователях.Подразделение = &Подразделение
	|				ГДЕ
	|					СправочникПользователи.Недействителен = ЛОЖЬ))
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|						ПО
	|							СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|								И ГруппыПользователейСостав.Ссылка = &ГруппаПользователей))
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ ИсполнителиЗадач.РольИсполнителя В (&СписокОтбора))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("ГруппаПользователей", ТекущаяГруппаПользователей);
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	
	Запрос.УстановитьПараметр("ИспользоватьСписокОтбора", СписокОтбора <> Неопределено);
	Запрос.УстановитьПараметр("СписокОтбора", СписокОтбора);
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Истина);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Истина);
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ПоказатьФотоПользователя(ТекущееПодразделение, УникальныйИдентификатор, Фотография);	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ПоказатьФотоПользователя(ТекущаяГруппаПользователей, УникальныйИдентификатор, Фотография);	
	КонецЕсли;
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьПользователейВОтделе()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СправочникПользователи.Ссылка,
	|	СправочникПользователи.ПометкаУдаления,
	|	СправочникПользователи.Предопределенный,
	|	СправочникПользователи.Наименование КАК Наименование,
	|	СведенияОПользователях.Должность,
	|	СведенияОПользователях.Подразделение,
	|	ВЫБОР
	|		КОГДА СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРуководитель,
	|	ЛОЖЬ КАК ЭтоРоль
	|ИЗ
	|	Справочник.Пользователи КАК СправочникПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|		ПО СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|ГДЕ
	|	(НЕ &ИспользоватьИерархию
	|			ИЛИ СведенияОПользователях.Подразделение = &Подразделение)
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ГруппыПользователейСостав.Ссылка = &ГруппаПользователей)
	|	И СправочникПользователи.Недействителен = ЛОЖЬ
	|	И СправочникПользователи.ПометкаУдаления = ЛОЖЬ
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ СправочникПользователи.Ссылка В (&СписокОтбора))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсполнителиЗадач.РольИсполнителя,
	|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
	|	ИсполнителиЗадач.РольИсполнителя.Предопределенный,
	|	ИсполнителиЗадач.РольИсполнителя.Наименование,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
	|ГДЕ
	|	&ПоказыватьРоли
	|	И (НЕ &ИспользоватьИерархию
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|						ПО
	|							СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь
	|								И СведенияОПользователях.Подразделение = &Подразделение
	|				ГДЕ
	|					СправочникПользователи.Недействителен = ЛОЖЬ))
	|	И (НЕ &ИспользоватьГруппыПользователей
	|			ИЛИ &ГруппаПользователей = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|			ИЛИ ИсполнителиЗадач.Исполнитель В
	|				(ВЫБРАТЬ
	|					СправочникПользователи.Ссылка
	|				ИЗ
	|					Справочник.Пользователи КАК СправочникПользователи ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|						ПО
	|							СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
	|								И ГруппыПользователейСостав.Ссылка = &ГруппаПользователей))
	|	И (НЕ &ИспользоватьСписокОтбора ИЛИ ИсполнителиЗадач.РольИсполнителя В (&СписокОтбора))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	Запрос.УстановитьПараметр("Подразделение", ТекущееПодразделение);
	Запрос.УстановитьПараметр("ГруппаПользователей", ТекущаяГруппаПользователей);
	Запрос.УстановитьПараметр("ПоказыватьРоли", ПоказыватьРоли);
	
	Запрос.УстановитьПараметр("ИспользоватьСписокОтбора", СписокОтбора <> Неопределено);
	Запрос.УстановитьПараметр("СписокОтбора", СписокОтбора);
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Истина);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Ложь);
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		Запрос.УстановитьПараметр("ИспользоватьИерархию", Ложь);
		Запрос.УстановитьПараметр("ИспользоватьГруппыПользователей", Истина);
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	Результат = Запрос.Выполнить();
	СписокВыгрузки = Результат.Выгрузить();
	
	Если ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ПоказатьФотоПользователя(ТекущееПодразделение, УникальныйИдентификатор, Фотография);	
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ПоказатьФотоПользователя(ТекущаяГруппаПользователей, УникальныйИдентификатор, Фотография);	
	КонецЕсли;
	
	ТекПользователь = Неопределено;
	Если Элементы.СписокПользователи.ТекущаяСтрока <> Неопределено Тогда
		ТекПользователь = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Ссылка;
	КонецЕсли;
	СписокПользователи.Очистить();
	СписокПользователи.Загрузить(СписокВыгрузки);
	Если ЗначениеЗаполнено(ТекПользователь) Тогда
		МассивСтрок = СписокПользователи.НайтиСтроки(Новый Структура("Ссылка", ТекПользователь));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.СписокПользователи.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
				   
КонецПроцедуры	

&НаКлиенте
Процедура ВидПросмотраПриИзменении(Элемент)
	
	Если ВидПросмотра = НСтр("ru = 'Списком'") Тогда
		ТекущаяГруппаПользователей = Неопределено;
		ТекущееПодразделение = Неопределено;
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По подразделениям'") Тогда
		ТекущаяГруппаПользователей = Неопределено;
		Если ЗначениеЗаполнено(Элементы.СписокПользователи.ТекущаяСтрока) Тогда
			ВрПодразделение = СписокПользователи.НайтиПоИдентификатору(Элементы.СписокПользователи.ТекущаяСтрока).Подразделение;
		Иначе
			ВрПодразделение = Неопределено;
		КонецЕсли;
		Элементы.СтруктураПредприятия.ТекущаяСтрока = ВрПодразделение;
		ТекущееПодразделение = Элементы.СтруктураПредприятия.ТекущаяСтрока;
	ИначеЕсли ВидПросмотра = НСтр("ru = 'По группам пользователей'") Тогда
		ТекущееПодразделение = Неопределено;
		Элементы.ГруппыПользователей.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи");
		ТекущаяГруппаПользователей = Элементы.ГруппыПользователей.ТекущаяСтрока;
	Иначе
		ВызватьИсключение "Некорректный вид просмотра";
	КонецЕсли;
	
	ОтобразитьИерархию();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;	
	
	ОповеститьОВыборе(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМоиКонтакты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникГруппыЛичныхАдресатов.Ссылка КАК ГруппаСсылка,
		|	СправочникГруппыЛичныхАдресатов.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникГруппыЛичныхАдресатов.Предопределенный КАК ГруппаПредопределенный,
		|	СправочникГруппыЛичныхАдресатов.Наименование КАК ГруппаНаименование,
		|	ЛичныеАдресаты.Ссылка КАК ЛичныеАдресатыСсылка,
		|	ЛичныеАдресаты.ПометкаУдаления КАК ЛичныеАдресатыПометкаУдаления,
		|	ЛичныеАдресаты.Наименование КАК ЛичныеАдресатыНаименование
		|ИЗ
		|	Справочник.ГруппыЛичныхАдресатов КАК СправочникГруппыЛичныхАдресатов
		|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ЛичныеАдресаты КАК ЛичныеАдресаты
		|		ПО СправочникГруппыЛичныхАдресатов.Ссылка = ЛичныеАдресаты.Группа
		|ГДЕ
		|	ЕСТЬNULL(СправочникГруппыЛичныхАдресатов.Пользователь, ЛичныеАдресаты.Пользователь) = &Пользователь
		|	И ЕСТЬNULL(ЛичныеАдресаты.Пользователь, СправочникГруппыЛичныхАдресатов.Пользователь) = &Пользователь
		|	И ЛичныеАдресаты.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГруппаСсылка ИЕРАРХИЯ,
		|	ЛичныеАдресатыНаименование";
		
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДеревоКонтактов.ПолучитьЭлементы().Очистить();
	КореньДерева = ДеревоКонтактов.ПолучитьЭлементы();
	ВеткиДереваДляГрупп = Новый Соответствие;
	
	ВыборкаДерево.Строки.Сортировать("ГруппаНаименование, ЛичныеАдресатыНаименование");
	Для Каждого СтрокаВыборкиДерево Из ВыборкаДерево.Строки Цикл
		
		ЗаполнитьЛистДереваМоиКонтакты(СтрокаВыборкиДерево, КореньДерева, ВеткиДереваДляГрупп);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЛистДереваМоиКонтакты(Выборка, КореньДерева, ВеткиДереваДляГрупп);
	
	СтрокаДереваГруппы = Неопределено;
	ЭлементыДляДобавленияПользователей = КореньДерева;
	
	Если ЗначениеЗаполнено(Выборка.ГруппаСсылка) Тогда
	
		СтрокаДереваГруппы = ВеткиДереваДляГрупп.Получить(Выборка.ГруппаСсылка);
		Если СтрокаДереваГруппы = Неопределено Тогда
			
			СтрокаДереваГруппы = КореньДерева.Добавить();
			ВеткиДереваДляГрупп.Вставить(Выборка.ГруппаСсылка, СтрокаДереваГруппы);
			
			СтрокаДереваГруппы.Наименование = Выборка.ГруппаНаименование;
			СтрокаДереваГруппы.Группа = Выборка.ГруппаСсылка;
			СтрокаДереваГруппы.Ссылка = Выборка.ГруппаСсылка;
			СтрокаДереваГруппы.НомерКартинки = ?(Выборка.ГруппаПометкаУдаления, 2, 3);
			СтрокаДереваГруппы.Представление = СтрокаДереваГруппы.Наименование;
			
		КонецЕсли;
		
		ЭлементыДляДобавленияПользователей = СтрокаДереваГруппы.ПолучитьЭлементы();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ЛичныеАдресатыСсылка) Тогда
		
		СтрокаДереваПользователь = ЭлементыДляДобавленияПользователей.Добавить();
		
		СтрокаДереваПользователь.Наименование = Выборка.ЛичныеАдресатыНаименование;
		СтрокаДереваПользователь.Группа = Выборка.ГруппаСсылка;
		СтрокаДереваПользователь.Ссылка = Выборка.ЛичныеАдресатыСсылка;
		СтрокаДереваПользователь.НомерКартинки = ?(Выборка.ЛичныеАдресатыПометкаУдаления = Истина, 0, 1);
		СтрокаДереваПользователь.ЭтоАдресат = Истина;
		
		Для Каждого СтрокаКонтактнойИнформации Из Выборка.ЛичныеАдресатыСсылка.КонтактнаяИнформация Цикл
			Если СтрокаКонтактнойИнформации.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
				СтрокаДереваПользователь.АдресEmail = СтрокаКонтактнойИнформации.АдресЭП;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(СтрокаДереваПользователь.АдресEmail) Тогда
			СтрокаДереваПользователь.НомерКартинки = 4;
		КонецЕсли;	
		
		СтрокаДереваПользователь.Представление = РаботаСоСтроками.ПолучитьПредставлениеАдресаЭлектроннойПочты(
				Строка(СтрокаДереваПользователь.Наименование),
				СтрокаДереваПользователь.АдресEmail);
		
	КонецЕсли;	
	
	Выборка.Строки.Сортировать("ГруппаНаименование, ЛичныеАдресатыНаименование");
	Для Каждого СтрокаВыборкиДерево Из Выборка.Строки Цикл
		
		ЗаполнитьЛистДереваМоиКонтакты(СтрокаВыборкиДерево, 
			СтрокаДереваГруппы.ПолучитьЭлементы(), ВеткиДереваДляГрупп);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ПоказатьЗначение(, Элементы.ДеревоКонтактов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не ТекущиеДанные.ЭтоАдресат Тогда 
		Возврат;
	КонецЕсли;	
	
	ОповеститьОВыборе(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПриАктивизацииСтроки(Элемент)
	
	СписокПользователиПриАктивизацииСтрокиВыполнить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокПользователи.ТекущиеДанные <> Неопределено Тогда
		
		ТекущаяСтрока = Элементы.СписокПользователи.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ТекущийПользователь = ТекущаяСтрока;
			
			Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
				ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			Иначе
				Фотография = "";
			КонецЕсли;		
			
		КонецЕсли;
		
	Иначе
		
		Фотография = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФотоПользователя()
	
	ПоказатьФотоПользователя(ТекущийКонтактДляКарточкиПользователя, УникальныйИдентификатор, Фотография);	
		
КонецПроцедуры

&НаСервере
Процедура ПоказатьФотоПользователя(ТекущаяСтрока, УникальныйИдентификатор, Фотография)
	
	Если ПолучатьФотографии Тогда
	
		Если ЭтоАдресВременногоХранилища(Фотография) Тогда
			УдалитьИзВременногоХранилища(Фотография);
		КонецЕсли;	
		
		Фотография = "";
		Если ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.РолиИсполнителей")
			И ТипЗнч(ТекущаяСтрока) <> Тип("Строка") Тогда
			
			ЕстьКартинка = Ложь;
			Фотография = РаботаСФотографиями.ПолучитьАдресФото(ТекущаяСтрока, УникальныйИдентификатор, ЕстьКартинка);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Фотография) Тогда
			Фотография = "";
			ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаКартинкаПоУмолчанию;
		Иначе	
			ЭтаФорма.Элементы.ГруппаСтраницыФотографии.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаФотография;
		КонецЕсли;	
		
	КонецЕсли;

	// карточка пользователя
	ОписаниеПользователяИмя = Строка(ТекущаяСтрока);
	ОписаниеПользователяПодразделение = "";
	
	// Заполнение подразделения и должности
	Если ТипЗнч(ТекущаяСтрока) = Тип("СправочникСсылка.Пользователи") Тогда
		СведенияПользователей = РегистрыСведений.СведенияОПользователях.Получить(Новый Структура("Пользователь", ТекущаяСтрока));
		ОписаниеПользователяПодразделение = Строка(СведенияПользователей.Подразделение);
	КонецЕсли;
	ОписаниеПользователяАдрес = "";
	// карточка пользователя
	
	Если ЗначениеЗаполнено(ТекущаяСтрока)
		И ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.СтруктураПредприятия")
		И ТипЗнч(ТекущаяСтрока) <> Тип("СправочникСсылка.ГруппыПользователей")
		И ТипЗнч(ТекущаяСтрока) <> Тип("Строка") Тогда
		
		ТаблицаКонтактов = УправлениеКонтактнойИнформацией.ЗначенияКонтактнойИнформацииОбъекта(ТекущаяСтрока, 
			Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			
		Для Каждого Строка Из ТаблицаКонтактов Цикл
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(ОписаниеПользователяАдрес, ";", Строка.Значение);
				
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Пользователи Тогда
		СписокПользователиПриАктивизацииСтрокиВыполнить();
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Роли Тогда
		СписокРолиПриАктивизацииСтрокиВыполнить();
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Функции Тогда
		СписокФункцийПриАктивизацииСтрокиВыполнить();
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Корреспонденты Тогда
		СписокКорреспондентыПриАктивизацииСтрокиВыполнить();
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.МоиКонтакты Тогда
		ДеревоКонтактовПриАктивизацииСтрокиВыполнить();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыПриАктивизацииСтроки(Элемент)
	СписокКорреспондентыПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПриАктивизацииСтроки(Элемент)
	ДеревоКонтактовПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокКорреспондентыПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокКорреспонденты.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.СписокКорреспонденты.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтактовПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.ДеревоКонтактов.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.ДеревоКонтактов.ТекущиеДанные.Ссылка;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиПриАктивизацииСтроки(Элемент)
	СписокРолиПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокРолиПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.СписокРоли.ТекущиеДанные <> Неопределено Тогда
	
		ТекущаяСтрока = Элементы.СписокРоли.ТекущиеДанные.Наименование;
		
		Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
			
			ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
			ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийПриАктивизацииСтроки(Элемент)
	СписокФункцийПриАктивизацииСтрокиВыполнить();
КонецПроцедуры

&НаКлиенте
Процедура СписокФункцийПриАктивизацииСтрокиВыполнить()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Функции Тогда
	
		Если Элементы.СписокФункций.ТекущиеДанные <> Неопределено Тогда
		
			ТекущаяСтрока = Элементы.СписокФункций.ТекущиеДанные.Представление;
			
			Если ТекущийКонтактДляКарточкиПользователя <> ТекущаяСтрока Тогда
				
				ТекущийКонтактДляКарточкиПользователя = ТекущаяСтрока;
				ПодключитьОбработчикОжидания("ОбновитьФотоПользователя", 0.2, Истина);
				
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыборПодразделения(ВыбранноеЗначение, ВыбратьПодчиненных = Неопределено)
	
	ОповещениеОЗакрытииВопроса = Новый ОписаниеОповещения(
		"ВыборПодразделенияЗавершение", ЭтотОбъект, ВыбранноеЗначение);

	Если ВыбратьПодчиненных = Неопределено Тогда 
		ВыбратьПодчиненных = Ложь;
		ДанныеСтроки = Элементы.СтруктураПредприятия.ДанныеСтроки(ВыбранноеЗначение);
		Если ДанныеСтроки.ЕстьПодчиненные Тогда 
			КнопкиВопроса = Новый СписокЗначений;
			КнопкиВопроса.Добавить(Истина, "Да");
			КнопкиВопроса.Добавить(Ложь, "Нет");
			
			ТекстВопроса = НСтр("ru = 'Выбрать сотрудников подчиненных подразделений?'");
			
			ПоказатьВопрос(ОповещениеОЗакрытииВопроса, ТекстВопроса, КнопкиВопроса,, Истина);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗакрытииВопроса, ВыбратьПодчиненных);

КонецПроцедуры

&НаКлиенте
Процедура ВыборПодразделенияЗавершение(ВыбратьПодчиненных, Подразделение) Экспорт
	
	ПользователиПодразделения = 
		РаботаСПользователями.ПолучитьПользователейПодразделения(Подразделение, ВыбратьПодчиненных);
		
	СписокВыбранных = Новый СписокЗначений();
	СписокВыбранных.ЗагрузитьЗначения(ПользователиПодразделения);
	ОповеститьОВыборе(СписокВыбранных);
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПредприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбиратьПодразделения Тогда
		ВыборПодразделения(Элементы.СтруктураПредприятия.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры
