
&НаКлиенте
Процедура ОбработкаНажатияКнопки(Команда)
	
	НажатаКнопка = Истина;
	Закрыть(Строка(КодВозвратаДиалога[СтрЗаменить(Команда.Имя, "Команда", "")]));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючНастройки = Параметры.КлючПерсональнойНастройки;
	ИмяНастройки = Параметры.ИмяПерсональнойНастройки;
	
	ТекстВопроса = Параметры.ТекстВопроса;
	ОбработатьЗакрытиеФормы = Истина;
	НажатаКнопка = Ложь;
	
	Заголовок = Параметры.Заголовок;
	
	Если Элементы.Найти("ФормаКоманда" + Параметры.ВариантОтветаПоУмолчанию) <> Неопределено Тогда
		Элементы["ФормаКоманда" + Параметры.ВариантОтветаПоУмолчанию].КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Для Каждого ВариантОтвета Из Параметры.СписокДоступныхВариантов Цикл
		Если Элементы.Найти("ФормаКоманда" + ВариантОтвета.Значение) <> Неопределено Тогда
			Элементы["ФормаКоманда" + ВариантОтвета.Значение].Видимость = Истина;
			Если ЗначениеЗаполнено(ВариантОтвета.Представление) Тогда
				Элементы["ФормаКоманда" + ВариантОтвета.Значение].Заголовок = ВариантОтвета.Представление;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПриЗакрытииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииСервер()
	
	ХранилищеОбщихНастроек.Сохранить(КлючНастройки, ИмяНастройки, НЕ БольшеНеПоказывать);		
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ОбработатьЗакрытиеФормы И НЕ НажатаКнопка Тогда
		ОбработатьЗакрытиеФормы = Ложь;
		СтандартнаяОбработка = Ложь;
		Закрыть(Ложь);
	КонецЕсли;	
	
КонецПроцедуры




