
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
	Элементы.Ранг.СписокВыбора.Добавить(0, НСтр("ru='Не задан'"));
	Элементы.Ранг.СписокВыбора.Добавить(1, "*");
	Элементы.Ранг.СписокВыбора.Добавить(2, "**");
	Элементы.Ранг.СписокВыбора.Добавить(3, "***");
	Элементы.Ранг.СписокВыбора.Добавить(4, "****");
	
	РангПриОткрытии = Запись.Ранг;
	
	НачальнаяЗапись = Новый Структура("Пользователь");
	ЗаполнитьЗначенияСвойств(НачальнаяЗапись, Запись);
	
	ЭтоНовый = Параметры.Ключ.Пустой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    
	Если РангПриОткрытии <> ТекущийОбъект.Ранг Тогда
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Запись.Пользователь);
			ПользовательОбъкт = Запись.Пользователь.ПолучитьОбъект();
			
			СтрокаРанга = ПользователиПереопределяемый.ПолучитьСтрокуРанга(Запись.Ранг);
			
			ПользовательОбъкт.ПредставлениеВПерепискеСРангом = ПользовательОбъкт.ПредставлениеВПереписке;
			Если Не ПустаяСтрока(СтрокаРанга) Тогда
				ПользовательОбъкт.ПредставлениеВПерепискеСРангом = ПользовательОбъкт.ПредставлениеВПерепискеСРангом + " " + СтрокаРанга;
			КонецЕсли;	
			 
			ПользовательОбъкт.Записать();
			РазблокироватьДанныеДляРедактирования(Запись.Пользователь);
			
		Исключение
		КонецПопытки;	
		
	КонецЕсли;	
	    
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
    	
	НачальнаяЗапись = Новый Структура("Пользователь");
	ЗаполнитьЗначенияСвойств(НачальнаяЗапись, Запись);
	
	ЭтоНовый = Ложь;
	
КонецПроцедуры
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// изменение подразделения
	Оповестить("ИзмененоПодразделениеПользователя", Запись.Пользователь, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ИзмеренияИзменились = Ложь;
	Для Каждого Эл Из НачальнаяЗапись Цикл
		Если Эл.Значение <> Запись[Эл.Ключ] Тогда 
			ИзмеренияИзменились = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ЭтоНовый Или ИзмеренияИзменились Тогда 
		СтруктураВозврата = Неопределено;
		Если ЕстьДубль(СтруктураВозврата) Тогда 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			    НСтр("ru = 'Пользователь ""%1"" уже входит в другое подразделение ""%2""'"),
				СтруктураВозврата.Пользователь, СтруктураВозврата.Подразделение);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Запись.Пользователь",, Отказ);
			Возврат;	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьДубль(СтруктураВозврата) 
	
	СтруктураВозврата = Новый Структура("Пользователь, Подразделение");
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.СведенияОПользователях.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда 
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, МенеджерЗаписи);
		Возврат Истина;	
	КонецЕсли;	
		
	Возврат Ложь;
		
КонецФункции


