
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектДоступа = Параметры.ОбъектДоступа;
	
	Элементы.ПраваДоступаУправлениеПравами.Видимость = 
		ДокументооборотПраваДоступа.ЭтоПапка(ТипЗнч(ОбъектДоступа));
	
	Дескриптор = РегистрыСведений.ДескрипторыДоступаДляОбъектов.НайтиДескрипторДляОбъекта(ОбъектДоступа);
	ОбновитьСведенияОбОчереди();
	
	УникальныйИдентификаторДескриптора = Строка(Дескриптор.УникальныйИдентификатор());
	
	ЗаполнитьТаблицуПрав();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбОчереди(ПраваОбновлены = Ложь)

	Если Дескриптор <> Неопределено Тогда
		Если РегистрыСведений.ОчередьОбновленияПравДоступа.ОбъектВОчереди(Дескриптор) Тогда
			Элементы.ГруппаСведенияОбОчереди.Видимость = Истина;
			Если ПраваОбновлены Тогда
				Элементы.ТекстСведенийОбОчереди.Заголовок = НСтр("ru = 'Объект находится в очереди.'");
			КонецЕсли;	
		Иначе	
			Элементы.ГруппаСведенияОбОчереди.Видимость = Ложь;
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСервер()
	
	// Определение дескриптора объекта заново
	Дескриптор = РегистрыСведений.ДескрипторыДоступаДляОбъектов.ОпределитьДескрипторДляОбъекта(ОбъектДоступа);
	УникальныйИдентификаторДескриптора = Строка(Дескриптор.УникальныйИдентификатор());
	
	// Расчет прав по новому определителю
	Если ЗначениеЗаполнено(Дескриптор) Тогда
		
		МенеджерДескриптора = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Дескриптор);
		Протокол = Новый Массив;
		МенеджерДескриптора.ОбновитьПрава(
			Дескриптор, 
			Протокол, 
			Истина); // Немедленно
		
		Элементы.СтраницаПротоколРасчетаПрав.Видимость = Истина;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		
		ПротоколРасчетаПрав.Очистить();
		
		Для каждого Эл Из Протокол Цикл
			
			Строка = ПротоколРасчетаПрав.Добавить();
			Если ТипЗнч(Эл) <> Тип("Строка") Тогда
				Строка.Описание = Строка(Эл) + " (" + Строка(ТипЗнч(Эл)) + ")";
				Строка.Ссылка = Эл;
			Иначе	
				Строка.Описание = Эл;
			КонецЕсли;	
			
		КонецЦикла;
		
		Строка = ПротоколРасчетаПрав.Добавить();
		Строка.Описание = НСтр("ru = 'Неограниченные права на таблицу'");
		
	КонецЕсли;
	
	ЗаполнитьТаблицуПрав();
	ОбновитьСведенияОбОчереди(Истина);
	
КонецПроцедуры

&НаСервере	
Процедура ЗаполнитьТаблицуПрав()
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ПолучитьТаблицуПравДляОтображенияВИнтерфейсе(ОбъектДоступа);
	ПраваДоступа.Загрузить(ТаблицаПрав);
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРасчетаПравВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПротоколРасчетаПрав.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПоказатьЗначение(,ТекущиеДанные.Ссылка);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПраваДоступа.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(,ТекущиеДанные.Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УникальныйИдентификаторДескриптораНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Дескриптор);
	
КонецПроцедуры
