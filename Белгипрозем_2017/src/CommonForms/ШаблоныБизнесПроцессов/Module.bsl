
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипыШаблонов.Добавить("Исполнение",   НСТр("ru = 'Исполнение'"));
	ТипыШаблонов.Добавить("Ознакомление", НСТр("ru = 'Ознакомление'"));
	ТипыШаблонов.Добавить("Поручение",    НСТр("ru = 'Поручение'"));
	ТипыШаблонов.Добавить("Рассмотрение", НСТр("ru = 'Рассмотрение'"));
	ТипыШаблонов.Добавить("Регистрация",  НСТр("ru = 'Регистрация'"));
	ТипыШаблонов.Добавить("Согласование", НСТр("ru = 'Согласование'"));
	ТипыШаблонов.Добавить("Утверждение",  НСТр("ru = 'Утверждение'"));
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПриглашениеНаМероприятие") Тогда 
		ТипыШаблонов.Добавить("Приглашение", НСТр("ru = 'Приглашение'"));
	КонецЕсли;	
	
	ТипыШаблонов.Добавить("Комплексный", НСТр("ru = 'Комплексный процесс'"));
	
	Для Каждого МДБизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		МДРеквизит = МДБизнесПроцесс.Реквизиты.Найти("Шаблон");
		Если МДРеквизит <> Неопределено Тогда
			Если МДРеквизит.Тип.СодержитТип(Тип("СправочникСсылка.ШаблоныСоставныхБизнесПроцессов")) Тогда 
				ТипыШаблонов.Добавить(МДБизнесПроцесс.Имя, МДБизнесПроцесс.Синоним);
				Продолжить;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ШаблоныСоставныхБизнесПроцессов.ТипШаблона
	|ИЗ
	|	Справочник.ШаблоныСоставныхБизнесПроцессов КАК ШаблоныСоставныхБизнесПроцессов";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ТипыШаблонов.НайтиПоЗначению(Выборка.ТипШаблона) = Неопределено Тогда 
			ТипыШаблонов.Добавить(Выборка.ТипШаблона);
		КонецЕсли;	
	КонецЦикла;
	
	ТекущийТипПроцесса = ХранилищеНастроекДанныхФорм.Загрузить("ШаблоныБизнесПроцессов", "ТекущийТипШаблона");
	Если ЗначениеЗаполнено(ТекущийТипПроцесса) Тогда
		ТекущийТип = ТипыШаблонов.НайтиПоЗначению(ТекущийТипПроцесса);
		Если ТекущийТип <> Неопределено Тогда
			Индекс = ТипыШаблонов.Индекс(ТекущийТип);
			Элементы.ТипыШаблонов.ТекущаяСтрока = Индекс;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыШаблонов

&НаКлиенте
Процедура ТипыШаблоновПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.ТипыШаблонов.ТекущаяСтрока;
	ЗначениеСписка = ТипыШаблонов.НайтиПоИдентификатору(ТекущаяСтрока).Значение;
	
	Если ЗначениеСписка = "Исполнение" Или 
		ЗначениеСписка = "Ознакомление" Или
		ЗначениеСписка = "Поручение" Или
		ЗначениеСписка = "Рассмотрение" Или
		ЗначениеСписка = "Регистрация" Или
		ЗначениеСписка = "Согласование" Или
		ЗначениеСписка = "Утверждение" Или
		ЗначениеСписка = "Приглашение" Или
		ЗначениеСписка = "Комплексный" Тогда
	
		Элементы.Панели.ТекущаяСтраница = Элементы["Панель" + ЗначениеСписка];
		Элементы.Списки.ТекущаяСтраница = Элементы["Список" + ЗначениеСписка];
	    Элементы["Список" + ЗначениеСписка].ПодчиненныеЭлементы[0].Обновить();
		
	Иначе
		
		Элементы.Панели.ТекущаяСтраница = Элементы["ПанельСоставные"];
		Элементы.Списки.ТекущаяСтраница = Элементы["СписокСоставные"];
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокСоставные.Отбор, 
			"ТипШаблона", 
			ЗначениеСписка);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ТекущийТипШаблона = ТипыШаблонов.НайтиПоИдентификатору(
		Элементы.ТипыШаблонов.ТекущаяСтрока);
		
	Если ТекущийТипШаблона <> Неопределено Тогда
		ХранилищеНастроекДанныхФорм.Сохранить(
			"ШаблоныБизнесПроцессов", "ТекущийТипШаблона", ТекущийТипШаблона.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

