#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если ЗначениеЗаполнено(Параметры.ПроектнаяЗадача) Тогда 
		Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ПроектнаяЗадача, "Владелец");
		ПроектнаяЗадача = Параметры.ПроектнаяЗадача;
	КонецЕсли;	
	Если ЗначениеЗаполнено(Параметры.Проект) Тогда 
		Проект = Параметры.Проект;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Проект) Тогда 
		Элементы.СписокПроекты.ТекущаяСтрока = Проект;
		ЗаполнитьДеревоЗадач(Проект);
		
		Если ЗначениеЗаполнено(ПроектнаяЗадача) Тогда 
			Идентификатор = Неопределено;
			НайтиСтрокуДерева(ПроектнаяЗадача, ДеревоЗадач.ПолучитьЭлементы(), Идентификатор);
			Если Идентификатор <> Неопределено Тогда 
				Элементы.ДеревоЗадач.ТекущаяСтрока = Идентификатор;
			КонецЕсли;	
		КонецЕсли;	
		
		ПерваяАктивизация = Истина;
	КонецЕсли;
	
	ВыбиратьТолькоПроектнуюЗадачу = Параметры.ВыбиратьТолькоПроектнуюЗадачу;
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		ЭтаФорма.Заголовок = НСтр("ru = 'Выбор проектной задачи'");
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПроекты

&НаКлиенте
Процедура СписокПроектыПриАктивизацииСтроки(Элемент)
	
	Если ПерваяАктивизация ИЛИ ПерваяАктивизация = Неопределено Тогда
		ПерваяАктивизация = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроектыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		Возврат;
	КонецЕсли;	
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите элемент, а не группу!'"));
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
		ТекущиеДанные.Ссылка,
		Неопределено,
		ТекущиеДанные.ЕстьПроектныеЗадачи);
		
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоЗадач

&НаКлиенте
Процедура ДеревоЗадачВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		ВыбранноеЗначение = ТекущиеДанные.Ссылка;
	Иначе	
		ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
			ТекущиеДанные.Владелец, 
			ТекущиеДанные.Ссылка,
			Истина);
	КонецЕсли;	
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ВыбиратьТолькоПроектнуюЗадачу Или Элементы.ДеревоЗадач.ТекущаяСтрока <> Неопределено Тогда
		ВыбратьЗадачу();
	ИначеЕсли Элементы.СписокПроекты.ТекущаяСтрока <> Неопределено Тогда  
		ВыбратьПроект();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НайтиСтрокуДерева(Ссылка, КоллекцияСтрок, Идентификатор)
	
	Для Каждого Строка Из КоллекцияСтрок Цикл
		Если Строка.Ссылка = Ссылка Тогда 
			Идентификатор = Строка.ПолучитьИдентификатор();	
		    Возврат;
		КонецЕсли;	
		
		НайтиСтрокуДерева(Ссылка, Строка.ПолучитьЭлементы(), Идентификатор);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	НовыйТекущийПроект = ТекущиеДанные.Ссылка;
	Если ТекущийПроект = НовыйТекущийПроект Тогда
		Возврат;
	КонецЕсли;
	ТекущийПроект = НовыйТекущийПроект;
	
	ЗаполнитьДеревоЗадач(ТекущийПроект);
	
	Для Каждого Строка Из ДеревоЗадач.ПолучитьЭлементы() Цикл 
		Элементы.ДеревоЗадач.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЗадач(Проект)
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗадач");
	Дерево.Строки.Очистить();
	
	Если ЗначениеЗаполнено(Проект) Тогда 
		
		Выборка = Справочники.ПроектныеЗадачи.ВыбратьИерархически(, Проект, , "НомерЗадачиВУровне Возр");
		Пока Выборка.Следующий() Цикл
			Если Выборка.ПометкаУдаления Тогда 
				Продолжить;
			КонецЕсли;	
			
			Родитель = Выборка.Родитель;
			Если Не ЗначениеЗаполнено(Родитель) Тогда 
				НоваяСтрока = Дерево.Строки.Добавить();
			Иначе	
				НайденнаяСтрока = Дерево.Строки.Найти(Родитель, "Ссылка", Истина);	
				Если НайденнаяСтрока = Неопределено Тогда 
					Продолжить;
				КонецЕсли;	
				НоваяСтрока = НайденнаяСтрока.Строки.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ИндексКартинки = ?(НоваяСтрока.ПометкаУдаления, 3, 2);
			НоваяСтрока.Представление = НоваяСтрока.КодСДР + " " + НоваяСтрока.Наименование;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоЗадач");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПроект()
	
	ТекущиеДанные = Элементы.СписокПроекты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ТекущиеДанные.ЭтоГруппа Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите элемент, а не группу!'"));
		Возврат;
	КонецЕсли;	
	
	ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
		ТекущиеДанные.Ссылка, 
		Неопределено,
		ТекущиеДанные.ЕстьПроектныеЗадачи);
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗадачу()
	
	ТекущиеДанные = Элементы.ДеревоЗадач.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если ВыбиратьТолькоПроектнуюЗадачу Тогда 
		ВыбранноеЗначение = ТекущиеДанные.Ссылка;
	Иначе	
		ВыбранноеЗначение = Новый Структура("Проект, ПроектнаяЗадача, ЕстьПроектныеЗадачи", 
			ТекущиеДанные.Владелец,
			ТекущиеДанные.Ссылка,
			Истина);
	КонецЕсли;		
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти
