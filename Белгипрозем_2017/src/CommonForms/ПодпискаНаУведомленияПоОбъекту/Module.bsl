#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОбъектПодписки") Тогда
		ОбъектПодписки = Параметры.ОбъектПодписки;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Пользователь = Пользователи.ТекущийПользователь();
	
	Если Не РольДоступна("ПолныеПрава") Тогда
		Элементы.ГруппаПользователь.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Подписки были изменены. Сохранить изменения?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьСервер();
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодпискиНаБизнесСобытияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПодпискиНаБизнесСобытияПодпискаАктивнаСтрокой" Тогда
		ИзменитьПодписку(Элемент.ТекущиеДанные.ПодпискаАктивна, Элемент.ТекущиеДанные.ВозможнаПодпискаНаСобытие, Элемент.ТекущиеДанные.ПодпискаАктивнаСтрокой);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	
	ОбновитьСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
			
    СохранитьСервер();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	СохранитьСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьСервер()
	
	Модифицированность = Ложь;
	РаботаСУведомлениями.ЗаполнитьПодпискиНаУведомления(ПодпискиНаУведомления, Пользователь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодпискиНаУведомления(СписокВидовБизнесСобытий, СписокСпособовУведомления)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Заполняем дерево
	Дерево = РеквизитФормыВЗначение("ПодпискиНаУведомления");
	
	Дерево.Очистить();
	
	// Получить данные из регистра сведений
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВидыБизнесСобытий.Ссылка КАК ВидСобытия,
		|	ЕСТЬNULL(ПодпискиНаУведомления.ПодпискаАктивна, 2) КАК ПодпискаАктивна
		|ИЗ
		|	Справочник.ВидыБизнесСобытий КАК ВидыБизнесСобытий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодпискиНаУведомления КАК ПодпискиНаУведомления
		|		ПО ВидыБизнесСобытий.Ссылка = ПодпискиНаУведомления.ВидСобытия
		|			И (ПодпискиНаУведомления.Пользователь = &Пользователь)
		|			И (ПодпискиНаУведомления.ОбъектПодписки = &ОбъектПодписки)
		|			И (ПодпискиНаУведомления.СпособУведомления В (&СписокСпособовУведомлений))
		|ГДЕ
		|	ВидыБизнесСобытий.Ссылка В(&СписокБизнесСобытий)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СобытияУведомлений.Ссылка,
		|	ЕСТЬNULL(ПодпискиНаУведомления.ПодпискаАктивна, 2)
		|ИЗ
		|	Перечисление.СобытияУведомлений КАК СобытияУведомлений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодпискиНаУведомления КАК ПодпискиНаУведомления
		|		ПО СобытияУведомлений.Ссылка = ПодпискиНаУведомления.ВидСобытия
		|			И (ПодпискиНаУведомления.Пользователь = &Пользователь)
		|			И (ПодпискиНаУведомления.ОбъектПодписки = &ОбъектПодписки)
		|			И (ПодпискиНаУведомления.СпособУведомления В (&СписокСпособовУведомлений))
		|ГДЕ
		|	СобытияУведомлений.Ссылка В(&СписокБизнесСобытий)";
	Запрос.УстановитьПараметр("СписокБизнесСобытий", СписокВидовБизнесСобытий);
	Запрос.УстановитьПараметр("СписокСпособовУведомлений", СписокСпособовУведомления);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ОбъектПодписки", ОбъектПодписки);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Дерево.Добавить();
		НоваяСтрока.ВидСобытия = Выборка.ВидСобытия;
		НоваяСтрока.ОбъектПодписки = ОбъектПодписки;
		НоваяСтрока.Представление = РаботаСУведомлениями.ПолучитьПредставлениеБизнесСобытия(Выборка.ВидСобытия, ОбъектПодписки);
		НоваяСтрока.СпособУведомления = Перечисления.СпособыУведомления.ПоПочте;
		НоваяСтрока.ПодпискаАктивна = Выборка.ПодпискаАктивна;
		НоваяСтрока.ВозможнаПодпискаНаСобытие = РаботаСУведомлениями.ПолучитьВозможностьПодпискиНаСобытие(Выборка.ВидСобытия, ОбъектПодписки);
		НоваяСтрока.ПодпискаАктивнаСтрокой = СтрокаПодписка(НоваяСтрока.ПодпискаАктивна, НоваяСтрока.ВозможнаПодпискаНаСобытие);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "ПодпискиНаУведомления");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПодписка(Подписка, ВозможнаПодпискаНаСобытие)
	
	Если ВозможнаПодпискаНаСобытие Тогда
		
		Если Подписка = 0 Тогда
			Возврат НСтр("ru = 'Нет'");
		ИначеЕсли Подписка = 1 Тогда
			Возврат НСтр("ru = 'Да'");
		ИначеЕсли Подписка = 2 Тогда
			Возврат НСтр("ru = 'Авто'");
		КонецЕсли;
		
	Иначе
		
		Если Подписка = 0 ИЛИ Подписка = 2 Тогда
			Возврат НСтр("ru = 'Нет'");
		ИначеЕсли Подписка = 1 Тогда
			Возврат НСтр("ru = 'Да'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьПодписку(Подписка, ВозможнаПодпискаНаСобытие, ПодпискаСтрокой)
	
	Модифицированность = Истина;
	
	Подписка = Подписка - 1;
	Если Подписка < 0 Тогда
		
		Если ВозможнаПодпискаНаСобытие Тогда
			Подписка = 2;
		Иначе
			Подписка = 1;
		КонецЕсли;
		
	КонецЕсли;
	
	ПодпискаСтрокой = СтрокаПодписка(Подписка, ВозможнаПодпискаНаСобытие);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСервер()
	
	// Получить список возможных событий
	СписокВидовБизнесСобытий = РаботаСУведомлениями.ПолучитьСписокВидовБизнесСобытий(ОбъектПодписки);
	
	// Получить список вариантов оповещения
	СписокСпособовУведомления = РаботаСУведомлениями.ПолучитьСписокСпособовУведомления();
	
	ОдинСпособУведомления = Истина;
	СпособУведомления = Перечисления.СпособыУведомления.ПоПочте;
	
	ЗаполнитьПодпискиНаУведомления(СписокВидовБизнесСобытий, СписокСпособовУведомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыОбработчикаОповещения = Новый Структура;
		ПараметрыОбработчикаОповещения.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПользовательОбработкаВыбораЗавершение", ЭтотОбъект, ПараметрыОбработчикаОповещения);
		ТекстВопроса = НСтр("ru = 'Подписки были изменены. Сохранить изменения?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьСервер();
		Пользователь = ДополнительныеПараметры.ВыбранноеЗначение;
		ОбновитьСервер();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Пользователь = ДополнительныеПараметры.ВыбранноеЗначение;
		ОбновитьСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти