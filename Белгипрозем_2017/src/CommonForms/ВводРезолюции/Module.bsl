
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Резолюция") Тогда 
		Резолюция = Параметры.Резолюция;
	КонецЕсли;	
	
	Если Параметры.Свойство("АвторРезолюции") Тогда 
		АвторРезолюции = Параметры.АвторРезолюции;
	Иначе
		АвторРезолюции = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаРезолюции") Тогда 
		ДатаРезолюции = Параметры.ДатаРезолюции;
	Иначе	
		ДатаРезолюции = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Резолюция", Резолюция);
	СтруктураВозврата.Вставить("АвторРезолюции", АвторРезолюции);
	СтруктураВозврата.Вставить("ДатаРезолюции", ДатаРезолюции);
	
	Если ТипЗнч(АвторРезолюции) = Тип("СправочникСсылка.КонтактныеЛица") Тогда 
		Корреспондент = ОбщегоНазначения.ПолучитьЗначениеРеквизита(АвторРезолюции, "Владелец");
	Иначе
		Корреспондент = Неопределено;
	КонецЕсли;	
	СтруктураВозврата.Вставить("Корреспондент", Корреспондент);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("АвторРезолюции");
	
КонецПроцедуры

&НаКлиенте
Процедура АвторРезолюцииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.ВыборПользователяКонтактноеЛицо", Новый Структура("ТекущаяСтрока", АвторРезолюции), Элемент);
	
КонецПроцедуры



&НаКлиенте
Процедура АвторРезолюцииАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораАвтораРезолюции(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвторРезолюцииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораАвтораРезолюции(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьДанныеВыбораАвтораРезолюции(Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Пользователи.Ссылка КАК Ссылка,
	|	СведенияОПользователях.Подразделение КАК Уточнение
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
	|		ПО Пользователи.Ссылка = СведенияОПользователях.Пользователь
	|ГДЕ
	|	Пользователи.Наименование ПОДОБНО &Текст
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка,
	|	КонтактныеЛица.Владелец
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Наименование ПОДОБНО &Текст";
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Уточнение) Тогда 
			ДанныеВыбора.Добавить(Выборка.Ссылка, Строка(Выборка.Ссылка) + " (" + Строка(Выборка.Уточнение) + ")");
		Иначе	
			ДанныеВыбора.Добавить(Выборка.Ссылка);
		КонецЕсли;	
	КонецЦикла;	
	
	Возврат ДанныеВыбора;
	
КонецФункции
