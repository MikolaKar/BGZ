
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    
    ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
    ДокументРезультат.АвтоМасштаб = Истина;
	ДокументРезультат.ПовторятьПриПечатиСтроки = ДокументРезультат.Область(5,,7,);
    
     // Вывод строки Организация
    мОтчетыВызовСервера.ВывестиНазваниеОрганизацииВОтчет(ДокументРезультат);
    
    мУправлениеКолонтитулами.УстановитьКолонтитулы(ДокументРезультат, "Ведомость6");
    
    Если Не ДокументРезультат.НижнийКолонтитул.Выводить Тогда
        НастройкиКолонтитулов = мУправлениеКолонтитулами.ПолучитьНастройкиКолонтитулов();
        
        НастройкиКолонтитулов.НижнийКолонтитул.ТекстСправа = "Стр. [&НомерСтраницы] из [&СтраницВсего]";
        НастройкиКолонтитулов.НижнийКолонтитул.НачальнаяСтраница = 2;
        мУправлениеКолонтитулами.СохранитьНастройкиКолонтитулов(НастройкиКолонтитулов);	
        мУправлениеКолонтитулами.УстановитьКолонтитулы(ДокументРезультат, "Ведомость6");
    
    КонецЕсли; 
    
    // Вывод Заголовка отчета
    мОтчетыВызовСервера.ВывестиЗаголовокВОтчет(КомпоновщикНастроек, ДокументРезультат);
        
    // Вывод Периода отчета
    мОтчетыВызовСервера.ВывестиПериодВОтчет(КомпоновщикНастроек, ДокументРезультат, Истина);
    
    //// Вставить содержимое обработчика.
    //КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("Заголовок").Значение = "Отчёт дебеторка по "+КомпоновщикНастроек.ПолучитьНастройки().ПараметрыДанных.Элементы.Получить(3).Значение;;
    
	
	//Макет компоновки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("НачПер", МакетКомпоновки.ЗначенияПараметров.НачПер.Значение);
	ПараметрыЗапроса.Вставить("КонПер", МакетКомпоновки.ЗначенияПараметров.КонПер.Значение);

	ТаблицаНабораДанных = ПолучитьТаблицаНабораДанных(ПараметрыЗапроса);
	
	//Связь между таблицей значений и именами в СКД 
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаНабораДанных", ТаблицаНабораДанных);
	
	//Компоновка данных 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	//Вывод результата ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
    
    // Вывод Подписи 
    мОтчетыВызовСервера.ВывестиПодписиВОтчет(КомпоновщикНастроек, ДокументРезультат);
    
КонецПроцедуры

Функция ПолучитьТаблицаНабораДанных(ПараметрыЗапроса)
    УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыСПокупателямиОбороты.Договор,
		|	РасчетыСПокупателямиОбороты.ЭтапДоговора,
		|	РасчетыСПокупателямиОбороты.Регистратор,
		|	РасчетыСПокупателямиОбороты.Период,
		|	0 КАК КредитН,
		|	0 КАК ДебетН,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОбороты.СуммаПриход > 0
		|				ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПокупателямиОбороты.Регистратор) = ТИП(Документ.мВзаимозачет)
		|					И РасчетыСПокупателямиОбороты.СуммаПриход < 0
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаПриход / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаПриход
		|					КОГДА РасчетыСПокупателямиОбороты.Период < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ РасчетыСПокупателямиОбороты.СуммаПриход
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Оплата,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОбороты.СуммаПриход < 0
		|				И ТИПЗНАЧЕНИЯ(РасчетыСПокупателямиОбороты.Регистратор) <> ТИП(Документ.мВзаимозачет)
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА -РасчетыСПокупателямиОбороты.СуммаПриход / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА -РасчетыСПокупателямиОбороты.СуммаПриход
		|					КОГДА РасчетыСПокупателямиОбороты.Период < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ -РасчетыСПокупателямиОбороты.СуммаПриход
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Возврат,
		|	0 КАК Заактировано,
		|	0 КАК СторноЗаактировано,
		|	0 КАК КредитК,
		|	0 КАК ДебетК,
		|	ГОД(РасчетыСПокупателямиОбороты.Договор.ДатаРегистрации) КАК Год,
		|	РасчетыСПокупателямиОбороты.ЭтапДоговора.ОбъектРабот.Представление КАК Объект,
		|	0 КАК СписаноНаУбытки,
		|	0 КАК СписаноНаДоходы
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.Обороты(&НачПер, &КонПер, Регистратор, ) КАК РасчетыСПокупателямиОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСПокупателями.Договор,
		|	РасчетыСПокупателями.ЭтапДоговора,
		|	РасчетыСПокупателями.Регистратор,
		|	РасчетыСПокупателями.Период,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ГОД(РасчетыСПокупателями.Договор.ДатаРегистрации),
		|	РасчетыСПокупателями.ЭтапДоговора.ОбъектРабот.Представление,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И РасчетыСПокупателями.ВидСписанияЗадолженности = ЗНАЧЕНИЕ(Перечисление.мВидыСписанияЗадолженности.НаУбытки)
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателями.Период < &ДатаДеноминации
		|						ТОГДА -РасчетыСПокупателями.Сумма / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателями.Период < &ДатаДеноминации
		|						ТОГДА -РасчетыСПокупателями.Сумма
		|					КОГДА РасчетыСПокупателями.Период < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ -РасчетыСПокупателями.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И РасчетыСПокупателями.ВидСписанияЗадолженности = ЗНАЧЕНИЕ(Перечисление.мВидыСписанияЗадолженности.НаДоходы)
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателями.Период < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателями.Сумма / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателями.Период < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателями.Сумма
		|					КОГДА РасчетыСПокупателями.Период < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ РасчетыСПокупателями.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями КАК РасчетыСПокупателями
		|ГДЕ
		|	РасчетыСПокупателями.Период МЕЖДУ &НачПер И &КонПер
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСПокупателямиОбороты.Договор,
		|	РасчетыСПокупателямиОбороты.ЭтапДоговора,
		|	РасчетыСПокупателямиОбороты.Регистратор,
		|	РасчетыСПокупателямиОбороты.Период,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОбороты.СуммаРасход > 0
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаРасход / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаРасход
		|					КОГДА РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ РасчетыСПокупателямиОбороты.СуммаРасход
		|				КОНЕЦ
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОбороты.СуммаРасход < 0
		|			ТОГДА ВЫБОР
		|					КОГДА НЕ &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаРасход / 10000
		|					КОГДА &ВСтарыхЦенах
		|							И РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаДеноминации
		|						ТОГДА РасчетыСПокупателямиОбороты.СуммаРасход
		|					КОГДА РасчетыСПокупателямиОбороты.Регистратор.Дата < &ДатаПослеДеноминации
		|						ТОГДА 0
		|					ИНАЧЕ РасчетыСПокупателямиОбороты.СуммаРасход
		|				КОНЕЦ
		|	КОНЕЦ,
		|	0,
		|	0,
		|	ГОД(РасчетыСПокупателямиОбороты.Договор.ДатаРегистрации),
		|	РасчетыСПокупателямиОбороты.ЭтапДоговора.ОбъектРабот.Представление,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.Обороты(&НачПер, &КонПер, Запись, ) КАК РасчетыСПокупателямиОбороты
		|ГДЕ
		|	НЕ РасчетыСПокупателямиОбороты.Регистратор ССЫЛКА Документ.мСписаниеЗадолженности
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСПокупателямиОстаткиИОбороты.Договор,
		|	РасчетыСПокупателямиОстаткиИОбороты.ЭтапДоговора,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаНачальныйОстаток > 0
		|			ТОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаНачальныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаНачальныйОстаток < 0
		|			ТОГДА -РасчетыСПокупателямиОстаткиИОбороты.СуммаНачальныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаКонечныйОстаток > 0
		|			ТОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаКонечныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиОстаткиИОбороты.СуммаКонечныйОстаток < 0
		|			ТОГДА -РасчетыСПокупателямиОстаткиИОбороты.СуммаКонечныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ГОД(РасчетыСПокупателямиОстаткиИОбороты.Договор.ДатаРегистрации),
		|	РасчетыСПокупателямиОстаткиИОбороты.ЭтапДоговора.ОбъектРабот.Представление,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.ОстаткиИОбороты(&НачПер, &КонПер, Период, , ) КАК РасчетыСПокупателямиОстаткиИОбороты";

		ДатаДеноминации = Дата(2016, 7, 1);
		ДатаПослеДеноминации = Дата(2016, 7, 2);
		Если ПараметрыЗапроса.НачПер = ДатаДеноминации Тогда
			Запрос.УстановитьПараметр("НачПер", ДатаПослеДеноминации);
		Иначе
			Запрос.УстановитьПараметр("НачПер", ПараметрыЗапроса.НачПер);
		КонецЕсли; 
		Запрос.УстановитьПараметр("КонПер", ПараметрыЗапроса.КонПер);
		Запрос.УстановитьПараметр("ВСтарыхЦенах", ПараметрыЗапроса.КонПер < ДатаДеноминации);
		Запрос.УстановитьПараметр("ДатаДеноминации", ДатаДеноминации);
		Запрос.УстановитьПараметр("ДатаПослеДеноминации", ДатаПослеДеноминации);
		
	Результат = Запрос.Выполнить().Выгрузить();
    УстановитьПривилегированныйРежим(Ложь);

    Возврат Результат;
КонецФункции