Процедура ЗаполнитьСрокДействия() Экспорт
	
	Сертификаты = Справочники.СертификатыЭП.Выбрать();
	
	Пока Сертификаты.Следующий() Цикл
		
		Попытка
			Сертификат = Сертификаты.ПолучитьОбъект();
			ДанныеСертификата = Сертификат.ФайлСертификата.Получить();
			Если ТипЗнч(ДанныеСертификата) = Тип("ДвоичныеДанные") Тогда
				СертификатКриптографии = Новый СертификатКриптографии(ДанныеСертификата);
				Сертификат.ДатаОкончания = СертификатКриптографии.ДатаОкончания;
				Сертификат.Записать();
			КонецЕсли;
		Исключение
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаВыбора" ИЛИ Не Параметры.Свойство("ТипОперации") Тогда
		Возврат;
	КонецЕсли;	
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.ТипОперации = "ВыборИзХранилища" Тогда
		
		ВыбраннаяФорма = "ВыборСертификатаИзХранилища";
		
	ИначеЕсли Параметры.ТипОперации = "Подписание" Тогда
		
		СертификатыПользователя = РаботаСЭП.ПолучитьСертификатыТекущегоПользователя();
		Если СертификатыПользователя.Количество() = 1 Тогда
			Параметры.Вставить("СертификатДляПодписания", СертификатыПользователя[0].Ссылка);
			ВыбраннаяФорма = "ВыборСертификатаДляПодписанияЕдинственныйСертификат";
		Иначе
			Параметры.Вставить("СертификатыПользователя", СертификатыПользователя);
			ВыбраннаяФорма = "ВыборСертификатаДляПодписания";
		КонецЕсли;
		
	ИначеЕсли Параметры.ТипОперации = "Шифрование" Тогда
		
		ВыбраннаяФорма = "ВыборСертификатовДляШифрования";
		
	ИначеЕсли Параметры.ТипОперации = "Расшифрование" Тогда
		
		// Отбор сертификатов, которыми зашифрован файл.
		ЗашифрованныйФайл = Параметры.ФайлСсылка;
		ОтпечаткиСертификатовШифрования = ЗашифрованныйФайл.СертификатыШифрования.ВыгрузитьКолонку("Отпечаток");
		ВсеСертификатыПользователя = РаботаСЭП.ПолучитьСертификатыТекущегоПользователя();
		СертификатыПользователя = ВсеСертификатыПользователя.СкопироватьКолонки();
		СертификатыПользователя.Очистить();
		Для Каждого Сертификат Из ВсеСертификатыПользователя Цикл
			Если ОтпечаткиСертификатовШифрования.Найти(Сертификат.Отпечаток) <> Неопределено Тогда
				НоваяСтрока = СертификатыПользователя.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Сертификат);
			КонецЕсли;
		КонецЦикла;
		
		Если СертификатыПользователя.Количество() = 1 Тогда
			Параметры.Вставить("СертификатДляРасшифрования", СертификатыПользователя[0].Ссылка);
			ВыбраннаяФорма = "ВыборСертификатаДляРасшифрованияЕдинственныйСертификат";
		Иначе
			Параметры.Вставить("СертификатыПользователя", СертификатыПользователя);
			ВыбраннаяФорма = "ВыборСертификатаДляРасшифрования";
		КонецЕсли;
	КонецЕсли;
	
	Параметры.Удалить("ТипОперации");
	
КонецПроцедуры
