
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);	
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьПолныйПуть();
	КонецЕсли;	
	
	УчитыватьПодпапки = Ложь;
	
	// Установка видимости команды "Права"
	Если Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей() Тогда
		Элементы.ФормаПрава.Видимость = Ложь;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
			Элементы.ФормаПрава.Видимость = Ложь;
		КонецЕсли;	
	Иначе
		Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
		Элементы.ОткрытьСписокПапокИДокументов.Доступность = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	ПапкаРодитель =Объект.Родитель;
	
	Если ЗначениеЗаполнено(ПапкаРодитель) Тогда
		
		ПолныйПуть = "";
		Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
			
			ПолныйПуть = Строка(ПапкаРодитель) + "\" + ПолныйПуть;
			
			ПапкаРодитель = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПапкаРодитель, "Родитель");
			Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
				Прервать;
			КонецЕсли;	
			
		КонецЦикла;
		
		ПолныйПуть = ПолныйПуть + Строка(Объект.Ссылка);
		
		Если Не ПустаяСтрока(ПолныйПуть) Тогда
			ПолныйПуть = """" + ПолныйПуть + """";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПапокИДокументов(Команда)
	
	ПараметрыФормы = Новый Структура("Папка", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.ВнутренниеДокументы.Форма.ФормаСпискаСПапками", ПараметрыФормы, ,Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьИнформациюОСодержимом();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюОСодержимом()
	
	ЧислоВерсий = 0;
	ЧислоДокументов = 0;
	ЧислоФайлов = 0;
	ЧислоПапок = 0;
	РазмерВерсий = 0;
	РазмерФайлов = 0;
	
	Запрос = Новый Запрос;
	
	Если УчитыватьПодпапки Тогда
	
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ПапкиВнутреннихДокументов.Ссылка КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |ИЗ
		               |	Справочник.ПапкиВнутреннихДокументов КАК ПапкиВнутреннихДокументов
		               |ГДЕ
		               |	ПапкиВнутреннихДокументов.Ссылка В ИЕРАРХИИ(&ПапкаКорневая)
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	КОЛИЧЕСТВО(ВременнаяТаблицаПапок.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВнутренниеДокументы.Ссылка) КАК ЧислоДокументов
		               |ИЗ
		               |	Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ПО Файлы.ВладелецФайла = ВнутренниеДокументы.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ПО Файлы.ВладелецФайла = ВнутренниеДокументы.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |			И (ВерсииФайлов.ФайлУдален = ЛОЖЬ)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)";
				   
	Иначе  // без подпапок
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	&ПапкаКорневая КАК Ссылка
		               |ПОМЕСТИТЬ ВременнаяТаблицаПапок
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ПапкиВнутреннихДокументов.Ссылка) КАК ЧислоПапок
		               |ИЗ
		               |	Справочник.ПапкиВнутреннихДокументов КАК ПапкиВнутреннихДокументов
		               |ГДЕ
		               |	ПапкиВнутреннихДокументов.Родитель = &ПапкаКорневая
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВнутренниеДокументы.Ссылка) КАК ЧислоДокументов
		               |ИЗ
		               |	Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(Файлы.Ссылка) КАК ЧислоФайлов,
		               |	ЕСТЬNULL(СУММА(Файлы.ТекущаяВерсияРазмер), 0) КАК РазмерФайлов
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ПО Файлы.ВладелецФайла = ВнутренниеДокументы.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	КОЛИЧЕСТВО(ВерсииФайлов.Ссылка) КАК ЧислоВерсий,
		               |	ЕСТЬNULL(СУММА(ВерсииФайлов.Размер), 0) КАК РазмерВерсий
		               |ИЗ
		               |	Справочник.Файлы КАК Файлы
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		               |		ПО Файлы.ВладелецФайла = ВнутренниеДокументы.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВерсииФайлов КАК ВерсииФайлов
		               |		ПО (ВерсииФайлов.Владелец = Файлы.Ссылка)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПапок КАК ВременнаяТаблицаПапок
		               |		ПО (ВременнаяТаблицаПапок.Ссылка = ВнутренниеДокументы.Папка)";
		
	КонецЕсли;		
				   
	Запрос.УстановитьПараметр("ПапкаКорневая", Объект.Ссылка);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[1].Выгрузить();
	ЧислоПапок = Выборка[0].ЧислоПапок;
	Если УчитыватьПодпапки Тогда
		ЧислоПапок = ЧислоПапок - 1;
	КонецЕсли;

	Выборка = МассивРезультатов[2].Выгрузить();
	ЧислоДокументов = Выборка[0].ЧислоДокументов;
	
	Выборка = МассивРезультатов[3].Выгрузить();
	ЧислоФайлов = Выборка[0].ЧислоФайлов;
	РазмерФайлов = Выборка[0].РазмерФайлов;
	
	Выборка = МассивРезультатов[4].Выгрузить();
	ЧислоВерсий = Выборка[0].ЧислоВерсий;
	РазмерВерсий = Выборка[0].РазмерВерсий;

	КоличествоВерсий = ?(ЧислоВерсий > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"), 
		ЧислоВерсий, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерВерсий)), "-");
	КоличествоПодпапок = ?(ЧислоПапок > 0, Строка(ЧислоПапок), "-");
	КоличествоДокументов = ?(ЧислоДокументов > 0, Строка(ЧислоДокументов), "-");
	КоличествоФайлов = ?(ЧислоФайлов > 0, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"), 
		ЧислоФайлов, ФайловыеФункцииКлиентСервер.ПолучитьСтрокуСРазмеромФайлаДляКарточкиПапки(РазмерФайлов)), "-");

КонецПроцедуры	

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапок(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодсчитатьСУчетомПодпапокПродолжение",
		ЭтотОбъект);

	ПоказатьВопрос(ОписаниеОповещения,
		НСтр("ru = 'Сейчас будет выполнен подсчет содержимого папки с учетом подпапок.
			|Эта операция может занять продолжительное время. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсчитатьСУчетомПодпапокПродолжение(Результат, Параметры) Экспорт 

	Если Результат = КодВозвратаДиалога.Да Тогда
		Состояние(НСтр("ru='Выполняется подсчет содержимого папки с учетом подпапок. Пожалуйста, подождите...'"));
		УчитыватьПодпапки = Истина;
		ЗаполнитьИнформациюОСодержимом();
		Элементы.ГруппаИнформация.Заголовок = НСтр("ru='Папка содержит (с учетом подпапок)'");
		Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Ложь;
		Состояние();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Права(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПраваПродолжение",
		ЭтотОбъект);

	// Предварительная запись новой папки
	Если Объект.Ссылка.Пустая() Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение команды ""Права"" возможно только после записи данных.
			|Данные будут записаны.'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПраваПродолжение(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Отмена Тогда 
		Возврат;	
	ИначеЕсли Результат = КодВозвратаДиалога.ОК Тогда 
					
		Если Не Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Изменение:'"), 
			ПолучитьНавигационнуюСсылку(Объект.Ссылка),
			Строка(Объект.Ссылка));
	КонецЕсли;
	
	// Проверка прав
	Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
		Элементы.ФормаПрава.Видимость = Ложь;
		ВызватьИсключение НСтр("ru = 'Доступ к настройке прав запрещен. 
			|Обратитесь к администратору.'");
	КонецЕсли;

	// Открытие формы настройки прав		
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Объект.Ссылка);
	ОткрытьФорму("Справочник.ПапкиВнутреннихДокументов.Форма.ПраваНаПапку", ПараметрыФормы, , Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура Подписаться(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("ОбъектПодписки", Объект.Ссылка);
		ОткрытьФорму("ОбщаяФорма.ПодпискаНаУведомленияПоОбъекту", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Элементы.ПодсчитатьСУчетомПодпапок.Доступность = Истина;
	Элементы.ОткрытьСписокПапокИДокументов.Доступность = Истина;
	
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));	
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	ОбновитьПолныйПуть();

КонецПроцедуры

