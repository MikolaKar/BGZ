#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	
	ВременноеДо10 = Перечисления.КатегорииДел.ВременноеДо10;
	ВременноеСвыше10 = Перечисления.КатегорииДел.ВременноеСвыше10;
	Постоянное = Перечисления.КатегорииДел.Постоянное;
	
	Если Объект.Ссылка.Пустая() Тогда
		Если Не ЗначениеЗаполнено(Объект.Год) Тогда
			Объект.Год = Год(ТекущаяДата());
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.СрокХранения) Тогда 
			Объект.СрокХранения = 0;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 
			Если ЗначениеЗаполнено(Объект.Раздел) Тогда 
				Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Раздел, "Организация");
			Иначе 	
				Объект.Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	СрокХраненияЗаполнитьСписокВыбора(
		Элементы.СрокХранения.СписокВыбора, Объект, ВременноеДо10, ВременноеСвыше10, Постоянное);
	
	// Инструкции
	ПоказыватьИнструкции = ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции");
	ПолучитьИнструкции();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененаНоменклатураДел", Объект.Ссылка, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если Настройки["ПоказыватьИнструкции"] <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьИнструкции") Тогда
		ПолучитьИнструкции();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СрокХраненияПриИзменении(Элемент)
	
	ЗаполнитьПодписьЛет();
	
КонецПроцедуры

&НаКлиенте
Процедура СрокХраненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "<Произвольный>" Тогда 
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = "";
		
	ИначеЕсли ВыбранноеЗначение = "1 год" Или ВыбранноеЗначение = "3 года" 
		Или ВыбранноеЗначение = "5 лет" Или ВыбранноеЗначение = "10 лет" 
		Или ВыбранноеЗначение = "15 лет" Или ВыбранноеЗначение = "50 лет" 
		Или ВыбранноеЗначение = "75 лет" Тогда 
		
		СтандартнаяОбработка = Ложь;
		Объект.СрокХранения = Число(Лев(ВыбранноеЗначение, 2));
		ЗаполнитьПодписьЛет();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияДелаПриИзменении(Элемент)
	
	Если Объект.КатегорияДела = ВременноеДо10 Тогда
		Объект.СрокХранения = "";
		
	ИначеЕсли Объект.КатегорияДела = ВременноеСвыше10 Тогда
		Объект.СрокХранения = "";	
		
	ИначеЕсли Объект.КатегорияДела = Постоянное Тогда
		Объект.СрокХранения = "Постоянно";
		
	КонецЕсли;	
	
	СрокХраненияЗаполнитьСписокВыбора(
		Элементы.СрокХранения.СписокВыбора, Объект, ВременноеДо10, ВременноеСвыше10, Постоянное);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказыватьДелаЗа", Объект.Год);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Объект.Раздел);
	
	ОткрытьФорму("Справочник.РазделыНоменклатурыДел.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСИнструкциямиКлиент.ОткрытьСсылку(ДанныеСобытия.Href, ДанныеСобытия.Element, Элемент.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("РежимВыбора, ТекущаяСтрока", Истина, Элементы.ВидыДокументов.ТекущиеДанные.ВидДокумента);
	ОткрытьФорму("ОбщаяФорма.ВидыДокументов", ПараметрыФормы, Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
 		Элементы.ВидыДокументов.ТекущиеДанные.ВидДокумента = 
			ПредопределенноеЗначение("Справочник.ВидыВнутреннихДокументов.ПустаяСсылка");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораВидаДокументов(Текст);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = СформироватьДанныеВыбораВидаДокументов(Текст);
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьИнструкции(Команда)
	
	ПоказыватьИнструкции = Не ПоказыватьИнструкции;
	ПолучитьИнструкции();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СрокХраненияЗаполнитьСписокВыбора(
	СписокВыбора, Объект, ВременноеДо10, ВременноеСвыше10, Постоянное)
	
	СписокВыбора.Очистить();
	
	Если Объект.КатегорияДела = ВременноеДо10 Тогда
		СписокВыбора.Добавить("1 год", НСтр("ru = '1 год'"));
		СписокВыбора.Добавить("3 года", НСтр("ru = '3 года'"));
		СписокВыбора.Добавить("5 лет", НСтр("ru = '5 лет'"));
		СписокВыбора.Добавить("10 лет", НСтр("ru = '10 лет'"));
		СписокВыбора.Добавить("До минования надобности", НСтр("ru = 'До минования надобности'"));
		СписокВыбора.Добавить("До замены новыми", НСтр("ru = 'До замены новыми'"));
		СписокВыбора.Добавить("<Произвольный>", НСтр("ru = '<Произвольный>'"));
		
	ИначеЕсли Объект.КатегорияДела = ВременноеСвыше10 Тогда
		СписокВыбора.Добавить("15 лет", НСтр("ru = '15 лет'"));
		СписокВыбора.Добавить("50 лет", НСтр("ru = '50 лет'"));
		СписокВыбора.Добавить("75 лет", НСтр("ru = '75 лет'"));
		СписокВыбора.Добавить("До замены новыми", НСтр("ru = 'До замены новыми'"));
		СписокВыбора.Добавить("<Произвольный>", НСтр("ru = '<Произвольный>'"));
		
	ИначеЕсли Объект.КатегорияДела = Постоянное Тогда
		СписокВыбора.Добавить("Постоянно", НСтр("ru = 'Постоянно'"));
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодписьЛет()
	
	Если ТипЗнч(Объект.СрокХранения) = Тип("Число") Тогда 
		Элементы.ДекорацияЛет.Заголовок = "(" + ДелопроизводствоКлиентСервер.ПодписьЛет(Объект.СрокХранения) + ")";
	Иначе
		Элементы.ДекорацияЛет.Заголовок = "";
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ПолучитьИнструкции()
	
	РаботаСИнструкциями.ПолучитьИнструкции(ЭтаФорма, 55, 85);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьДанныеВыбораВидаДокументов(Текст)
	
	ДанныеВыбора = Новый СписокЗначений;
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВидыВнутреннихДокументов.Ссылка
			|ИЗ
			|	Справочник.ВидыВнутреннихДокументов КАК ВидыВнутреннихДокументов
			|ГДЕ
			|	ВидыВнутреннихДокументов.Наименование ПОДОБНО &Текст
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВидыВходящихДокументов.Ссылка
			|ИЗ
			|	Справочник.ВидыВходящихДокументов КАК ВидыВходящихДокументов
			|ГДЕ
			|	ВидыВходящихДокументов.Наименование ПОДОБНО &Текст
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВидыИсходящихДокументов.Ссылка
			|ИЗ
			|	Справочник.ВидыИсходящихДокументов КАК ВидыИсходящихДокументов
			|ГДЕ
			|	ВидыИсходящихДокументов.Наименование ПОДОБНО &Текст"; 
				   
	Запрос.УстановитьПараметр("Текст", Текст + "%");

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2)'"), Выборка.Ссылка, ТипЗнч(Выборка.Ссылка)));
	КонецЦикла;
		
	Возврат ДанныеВыбора;

КонецФункции

#КонецОбласти
