
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает строку, содержащую перечисление полей доступа через запятую
// Это перечисление используется в дальнейшем для передачи в метод 
// ОбщегоНазначения.ПолучитьЗначенияРеквизитов()
Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Организация";
	
КонецФункции

// Заполняет переданный дескриптор доступа 
Процедура ЗаполнитьДескрипторДоступа(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДескрипторДоступа.Организация = ОбъектДоступа.Организация;
	
КонецПроцедуры

Процедура ПечатьНоменклатурыДел(ТабДок, ПараметрыПечати) Экспорт
	
	Если ПараметрыПечати.Свойство("Подразделение") И ЗначениеЗаполнено(ПараметрыПечати.Подразделение) Тогда
		Макет = Справочники.НоменклатураДел.ПолучитьМакет("НоменклатураДелПодразделения");
		Подразделение = ПараметрыПечати.Подразделение;
	Иначе 	
		Макет = Справочники.НоменклатураДел.ПолучитьМакет("НоменклатураДел");
		Подразделение = Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СправочникНоменклатураДел.Индекс КАК Индекс,
			|	СправочникНоменклатураДел.ПолноеНаименование КАК Наименование,
			|	СправочникНоменклатураДел.Раздел КАК Раздел,
			|	СправочникНоменклатураДел.ОтметкаЭПК КАК ОтметкаЭПК,
			|	СправочникНоменклатураДел.НомераСтатей КАК НомераСтатей,
			|	СправочникНоменклатураДел.СрокХранения КАК СрокХранения,
			|	СправочникНоменклатураДел.Примечание КАК Примечание,
			|	Дела.КоличествоТомов
			|ИЗ
			|	Справочник.НоменклатураДел КАК СправочникНоменклатураДел
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
			|			КОЛИЧЕСТВО(ДелаХраненияДокументов.Ссылка) КАК КоличествоТомов,
			|			ДелаХраненияДокументов.НоменклатураДел КАК НоменклатураДел
			|		ИЗ
			|			Справочник.ДелаХраненияДокументов КАК ДелаХраненияДокументов
			|		ГДЕ
			|			НЕ ДелаХраненияДокументов.ПометкаУдаления
			|		
			|		СГРУППИРОВАТЬ ПО
			|			ДелаХраненияДокументов.НоменклатураДел) КАК Дела
			|		ПО СправочникНоменклатураДел.Ссылка = Дела.НоменклатураДел
			|ГДЕ
			|	НЕ СправочникНоменклатураДел.ПометкаУдаления
			|	И СправочникНоменклатураДел.Год = &Год
			|	И (СправочникНоменклатураДел.Организация = &Организация
			|			ИЛИ &ИспользоватьУчетПоОрганизациям = ЛОЖЬ
			|			ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
			|	И (СправочникНоменклатураДел.Раздел.Подразделение = &Подразделение
			|			ИЛИ &Подразделение = НЕОПРЕДЕЛЕНО)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Индекс";
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", ПараметрыПечати.Подразделение);
	Иначе
		Запрос.УстановитьПараметр("Подразделение", Неопределено);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	ВыборкаСтроки = Запрос.Выполнить().Выбрать();
	
	ВыборкаРазделы = Справочники.РазделыНоменклатурыДел.ВыбратьИерархически(,, Новый Структура("Год", ПараметрыПечати.Год), "Индекс Возр");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьРаздел 	 = Макет.ПолучитьОбласть("Раздел");
	ОбластьПодвал    = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ОбластьЗаголовок.Параметры.НаименованиеПредприятия = РаботаСОрганизациями.ПолучитьНаименованиеОрганизации(ПараметрыПечати.Организация);
		
	ДатаПечати = Дата(ПараметрыПечати.Год, 1, 1);

	Если ЗначениеЗаполнено(Подразделение) Тогда
		ОбластьЗаголовок.Параметры.НаименованиеПодразделения = Подразделение.Наименование;
	Иначе 	
		Руководитель = РаботаСОрганизациями.ПолучитьОтветственноеЛицо("Руководитель", ПараметрыПечати.Организация, ДатаПечати);
		ОбластьЗаголовок.Параметры.Руководитель = Руководитель.ПредставлениеВДокументах;
	КонецЕсли;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// разделы
	Пока ВыборкаРазделы.Следующий() Цикл
		Если ВыборкаРазделы.ПометкаУдаления Тогда 
			Продолжить;
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") = Истина 
		   И ЗначениеЗаполнено(ПараметрыПечати.Организация) 
		   И ВыборкаРазделы.Организация <> ПараметрыПечати.Организация Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыПечати.Свойство("Подразделение") 
			И ЗначениеЗаполнено(ПараметрыПечати.Подразделение)
			И ВыборкаРазделы.Подразделение <> ПараметрыПечати.Подразделение Тогда
			Продолжить;
		КонецЕсли;	
		
		ОбластьРаздел.Параметры.Индекс = ВыборкаРазделы.Индекс;
		ОбластьРаздел.Параметры.Наименование = ВыборкаРазделы.Наименование;
		ТабДок.Вывести(ОбластьРаздел);
		
		// строки
		ВыборкаСтроки.Сбросить();
		Пока ВыборкаСтроки.НайтиСледующий(ВыборкаРазделы.Ссылка, "Раздел") Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			
			НомераСтатей = ВыборкаСтроки.НомераСтатей;
			СрокХранения = ВыборкаСтроки.СрокХранения;
			ОтметкаЭПК 	 = ВыборкаСтроки.ОтметкаЭПК;
			
			Если ТипЗнч(СрокХранения) = Тип("Число") Тогда 
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = Строка(СрокХранения) + " "
					+ ДелопроизводствоКлиентСервер.ПодписьЛет(СрокХранения) 
					+ ?(ОтметкаЭПК, " ЭПК", "") 
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			Иначе
				ОбластьСтрока.Параметры.СрокХраненияНомераСтатей = СрокХранения 
					+ ?(ОтметкаЭПК, " ЭПК", "")
					+ ?(ЗначениеЗаполнено(НомераСтатей), ", " + НомераСтатей, "");
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;	
	КонецЦикла;
	
	// Подвал	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		РуководительДОУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПечати.Подразделение, "Руководитель");	
	Иначе		
		РуководительДОУ = РаботаСОрганизациями.ПолучитьОтветственноеЛицо("РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПечати);
	КонецЕсли;

	ОбластьПодвал.Параметры.РуководительДОУ = РуководительДОУ.ПредставлениеВДокументах;
	ДолжностьРуководителяДОУ = РаботаСПользователями.ПолучитьДолжность(РуководительДОУ);
	
	Если Не ЗначениеЗаполнено(ДолжностьРуководителяДОУ) И ЗначениеЗаполнено(Подразделение) Тогда 
		ДолжностьРуководителяДОУ = НСтр("ru = 'Руководитель структурного подразделения'");
	ИначеЕсли Не ЗначениеЗаполнено(ДолжностьРуководителяДОУ) Тогда
		ДолжностьРуководителяДОУ = НСтр("ru = 'Руководитель службы ДОУ'");
	КонецЕсли;
	
	ОбластьПодвал.Параметры.ДолжностьРуководителяДОУ = ДолжностьРуководителяДОУ;
	ОбластьПодвал.Параметры.Дата = Формат(ТекущаяДата(), "ДФ=dd.MM.yyyy");
	
	РуководительАрхива = РаботаСОрганизациями.ПолучитьОтветственноеЛицо("РуководительАрхива", ПараметрыПечати.Организация, ДатаПечати);
	ОбластьПодвал.Параметры.РуководительАрхива = РуководительАрхива.ПредставлениеВДокументах;
	ДолжностьРуководителяАрхива = РаботаСПользователями.ПолучитьДолжность(РуководительАрхива);
	ОбластьПодвал.Параметры.ДолжностьРуководителяАрхива = ?(ЗначениеЗаполнено(ДолжностьРуководителяАрхива),
		ДолжностьРуководителяАрхива, НСтр("ru = 'Руководитель архива'"));
	
    ТабДок.Вывести(ОбластьПодвал);

КонецПроцедуры

Процедура ПечатьИтоговойЗаписи(ТабДок, ПараметрыПечати) Экспорт
	
	Макет = Справочники.НоменклатураДел.ПолучитьМакет("ИтоговаяЗапись");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Дела.НоменклатураДел.КатегорияДела КАК КатегорияДела,
	|	СУММА(1) КАК Всего,
	|	СУММА(ВЫБОР
	|			КОГДА Дела.НоменклатураДел.ОтметкаЭПК
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СОтметкойЭПК,
	|	СУММА(ВЫБОР
	|			КОГДА Дела.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|					И ГОД(Дела.ДатаОкончания) <> &Год
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Переходящих
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.НоменклатураДел.Год = &Год И (НоменклатураДел.Организация = &Организация Или &ИспользоватьУчетПоОрганизациям = ЛОЖЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Дела.НоменклатураДел.КатегорияДела";
	Запрос.УстановитьПараметр("Год", ПараметрыПечати.Год);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	Запрос.УстановитьПараметр("ИспользоватьУчетПоОрганизациям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям"));
	
	Выборка = Запрос.Выполнить().Выгрузить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 	 = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	 = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 	 = Макет.ПолучитьОбласть("Подвал");
	ТабДок.Очистить();

	// заголовок
	ОбластьЗаголовок.Параметры.Год = Формат(ПараметрыПечати.Год, "ЧГ=");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	// шапка
	ТабДок.Вывести(ОбластьШапка);
	
	// строки
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Постоянного";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.Постоянное, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);
	
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Временного (свыше 10 лет)";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.ВременноеСвыше10, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);
		
	ОбластьСтрока.Параметры.КатегорияДелаСтр = "Временного (до 10 лет включительно)";
	НайденнаяСтрока = Выборка.Найти(Перечисления.КатегорииДел.ВременноеДо10, "КатегорияДела");
	Если НайденнаяСтрока <> Неопределено Тогда 
		ОбластьСтрока.Параметры.Заполнить(НайденнаяСтрока);
	КонецЕсли;	
	ТабДок.Вывести(ОбластьСтрока);	
		
	// подвал
	ДатаПодписи = ТекущаяДата();
	РуководительСлужбыДОУ = РаботаСОрганизациями.ПолучитьОтветственноеЛицо("РуководительСлужбыДОУ", ПараметрыПечати.Организация, ДатаПодписи);
	
	ОбластьПодвал.Параметры.РуководительДОУ = РуководительСлужбыДОУ.ПредставлениеВДокументах;
	ОбластьПодвал.Параметры.ДолжностьРуководителяДОУ = РаботаСПользователями.ПолучитьДолжность(РуководительСлужбыДОУ);
	ОбластьПодвал.Параметры.ДатаПодписи = ДатаПодписи;
	ТабДок.Вывести(ОбластьПодвал);
	
КонецПроцедуры

Функция ПолучитьСтруктуруНоменклатурыДел() Экспорт 
	
	ПараметрыНоменклатурыДел = Новый Структура;
	ПараметрыНоменклатурыДел.Вставить("Год");
	ПараметрыНоменклатурыДел.Вставить("Раздел");
	ПараметрыНоменклатурыДел.Вставить("Организация");
	ПараметрыНоменклатурыДел.Вставить("Индекс");
	ПараметрыНоменклатурыДел.Вставить("ПолноеНаименование");
	ПараметрыНоменклатурыДел.Вставить("СрокХранения");
	ПараметрыНоменклатурыДел.Вставить("КатегорияДела");
	ПараметрыНоменклатурыДел.Вставить("НомераСтатей");
	ПараметрыНоменклатурыДел.Вставить("ОтметкаЭПК");
	ПараметрыНоменклатурыДел.Вставить("Примечание");
	
	Возврат ПараметрыНоменклатурыДел;
	
КонецФункции

Функция СоздатьНоменклатуруДел(СтруктураНоменклатурыДел) Экспорт
	
	НовыйЭлементНД = Справочники.НоменклатураДел.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлементНД, СтруктураНоменклатурыДел);
	НовыйЭлементНД.Записать();
	
	НовыйЭлементДело = Справочники.ДелаХраненияДокументов.СоздатьЭлемент();
	НовыйЭлементДело.НоменклатураДел = НовыйЭлементНД.Ссылка;
	НовыйЭлементДело.ДатаНачала = Дата(НовыйЭлементНД.Год, 1, 1);
	НовыйЭлементДело.НомерТома = 1;
	НовыйЭлементДело.Записать();
	
	Возврат НовыйЭлементНД.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли


