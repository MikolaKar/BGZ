
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Дела.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДелаХраненияДокументов КАК Дела
	|ГДЕ
	|	Дела.НомерТома = &НомерТома
	|	И Дела.НоменклатураДел = &НоменклатураДел
	|	И Дела.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("НомерТома", НомерТома);
	Запрос.УстановитьПараметр("НоменклатураДел", НоменклатураДел);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дело с указанным номером тома уже существует!'"), 
			ЭтотОбъект, 
			"НомерТома",, 
			Отказ);
		Возврат;	
	КонецЕсли;	
	
	Если ДелоЗакрыто Тогда 
		ПроверяемыеРеквизиты.Добавить("ДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДатаОкончания");
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) И ДатаНачала > ДатаОкончания Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата окончания дела меньше, чем дата начала.'"),
			ЭтотОбъект,
			"ДатаОкончания",, 
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыНоменклатурыДел = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураДел, "Наименование, Организация, Раздел");
	Наименование = РеквизитыНоменклатурыДел.Наименование
		+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' (том №%1)'"),
			Формат(НомерТома, "ЧГ="));
		
	Организация = РеквизитыНоменклатурыДел.Организация;
	Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыНоменклатурыДел.Раздел, "Подразделение");
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДелоЗакрыто = Ложь;
	КоличествоЛистов = 0;
	ДатаОкончания = '00010101';
	
КонецПроцедуры
