#Область ОбработчикиСобытийФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	МассивПеремещаемых = ПараметрыПеретаскивания.Значение;
	
	НеобходимоПодтверждениеПользователя = Ложь;
	ТекущиеРодители = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПеремещаемых, "Родитель");
	Для Каждого КлючИЗначение Из ТекущиеРодители Цикл
		ТекущийРодитель = КлючИЗначение.Значение.Родитель;
		Если ТекущийРодитель <> Строка
			И (ЗначениеЗаполнено(ТекущийРодитель) И ЗначениеЗаполнено(Строка)) Тогда
			НеобходимоПодтверждениеПользователя = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не НеобходимоПодтверждениеПользователя Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоПеремещаемых = МассивПеремещаемых.Количество();
	Если КоличествоПеремещаемых = 1 Тогда
		ЧтоПеремещать = """" + МассивПеремещаемых[0] + """";
	Иначе
		ЧтоПеремещать = НСтр("ru = 'выбранные подразделения'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Строка) Тогда
		КудаПеремещать = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'в ""%1""'"),
			Строка);
	Иначе
		КудаПеремещать = НСтр("ru = 'на верхний уровень'");
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Перенести %1 %2?'"),
		ЧтоПеремещать,
		КудаПеремещать);
		
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("МассивПеремещаемых", МассивПеремещаемых);
	ПараметрыОбработки.Вставить("НовыйРодитель", Строка);
	ОписаниеОповещения = Новый ОписаниеОповещения("СписокПеретаскиваниеЗавершение", ЭтотОбъект, ПараметрыОбработки);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокПеретаскиваниеЗавершение(Ответ, ПараметрыОбработки) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
	
		МассивПеремещаемых = ПараметрыОбработки.МассивПеремещаемых;
		НовыйРодитель = ПараметрыОбработки.НовыйРодитель;
		
		ОбработатьПеретаскиваниеНаСервере(МассивПеремещаемых, НовыйРодитель);
		
		ОбновитьОтображениеДанных(); 
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПеретаскиваниеНаСервере(МассивПеремещаемых, НовыйРодитель)
	
	НачатьТранзакцию();
	
	Попытка
	
		Для каждого ПеремещаемаяСсылка из МассивПеремещаемых Цикл
			Объект = ПеремещаемаяСсылка.ПолучитьОбъект();
			Объект.Родитель = НовыйРодитель;
			Объект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Структура предприятия.Ошибка перетаскивания элементов справочника'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка, 
			Метаданные.Справочники.СтруктураПредприятия, , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

