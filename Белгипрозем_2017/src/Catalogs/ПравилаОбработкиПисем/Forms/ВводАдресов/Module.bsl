
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Для Каждого Элемент Из Параметры.СписокЗначений Цикл
		НоваяСтрока = ТаблицаСтрок.Добавить();
		НоваяСтрока.Строка = Элемент.Значение;
		НоваяСтрока.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"""%1""",
			НоваяСтрока.Строка);
		
		Если ТаблицаСтрок.Количество() > 1 Тогда
			ДанныеПредыдущейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 2];
			ДанныеПредыдущейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1"" %2",
				ДанныеПредыдущейСтроки.Строка,
				НСтр("ru = 'или'"));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	Если ЗначениеЗаполнено(СтрокаДляДобавления) Тогда
		НоваяСтрока = ТаблицаСтрок.Добавить();
		НоваяСтрока.Строка = СтрокаДляДобавления;
		НоваяСтрока.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"""%1""",
			СтрокаДляДобавления);
		
		Если ТаблицаСтрок.Количество() > 1 Тогда
			ДанныеПредыдущейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 2];
			ДанныеПредыдущейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1"" %2",
				ДанныеПредыдущейСтроки.Строка,
				НСтр("ru = 'или'"));
		КонецЕсли;
	КонецЕсли;
	
	СтрокаДляДобавления = "";
	ТекущийЭлемент = Элементы.СтрокаДляДобавления;
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	МассивСтрокДляУдаления = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ТаблицаСтрок.ВыделенныеСтроки Цикл
		СтрокаДляУдаления = ТаблицаСтрок.НайтиПоИдентификатору(ВыделеннаяСтрока);
		МассивСтрокДляУдаления.Добавить(СтрокаДляУдаления);
	КонецЦикла;
	
	Для Каждого СтрокаДляУдаления Из МассивСтрокДляУдаления Цикл
		ТаблицаСтрок.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
	Если ТаблицаСтрок.Количество() > 0 Тогда
		ДанныеПоследнейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 1];
		ДанныеПоследнейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1""",
				ДанныеПоследнейСтроки.Строка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ВозвращаемыйСписок = Новый СписокЗначений();
	Для Каждого СтрокаТаблицы Из ТаблицаСтрок Цикл
		ВозвращаемыйСписок.Добавить(СтрокаТаблицы.Строка);
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Список", ВозвращаемыйСписок);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытия.Вставить("ЗаполненыПараметрыEmailУчетнойЗаписи", Истина);
	
	// Открытие формы для редактирования списка адресатов
	ИмяОбщейФормы = "ОбщаяФорма.АдреснаяКнига";
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СтрокаДляДобавленияНачалоВыбораПродолжение",
		ЭтотОбъект);
	ОткрытьФорму(ИмяОбщейФормы, ПараметрыОткрытия,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияНачалоВыбораПродолжение(Результат, Параметры) Экспорт
	
	Если (ТипЗнч(Результат) <> Тип("Массив")) И (ТипЗнч(Результат) <> Тип("Соответствие")) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() > 0 Тогда
		СтрокаДляДобавления = Результат[0].Адрес;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтрокаДляДобавления = ВыбранноеЗначение.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Или СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		Возврат;
	#КонецЕсли
	
	ЭтоВебКлиент = Ложь;
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#КонецЕсли
	
	ДанныеВыбора = ВстроеннаяПочтаСервер.ПолучитьДанныеВыбораДляЭлектронногоПисьма(Текст, 
		ТекущийПользователь, 
		ЭтоВебКлиент);

	Если ДанныеВыбора.Количество() <> 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Текст) Или СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	НовыйТекст = Текст;
	СтандартнаяОбработка = Ложь;
	ЭтоВебКлиент = Ложь;
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#КонецЕсли
	
	РезультатВыбора = Неопределено;
	ДанныеВыбора = ВстроеннаяПочтаСервер.ПолучитьДанныеВыбораДляЭлектронногоПисьма(
		Текст, 
		ТекущийПользователь, 
		ЭтоВебКлиент);
	Если ДанныеВыбора.Количество() = 1 Тогда
		РезультатВыбора = ДанныеВыбора[0];
		Если РезультатВыбора <> Неопределено Тогда
			СтрокаДляДобавления = РезультатВыбора.Значение.Представление;
		КонецЕсли;
	ИначеЕсли ДанныеВыбора.Количество() > 1 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"СтрокаДляДобавленияОкончаниеВводаТекстаПродолжение",
			ЭтотОбъект);
		ПоказатьВыборИзСписка(ОписаниеОповещения, ДанныеВыбора);
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияОкончаниеВводаТекстаПродолжение(РезультатВыбора, Параметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		СтрокаДляДобавления = РезультатВыбора.Значение.Представление;
	КонецЕсли;	
	
КонецПроцедуры