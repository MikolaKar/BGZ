
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипСправочника = Параметры.ТипСправочника;
	АвтоЗаголовок = Ложь;
	Заголовок = Параметры.ЗаголовокФормы;
	СтрокаДляДобавления = ПредопределенноеЗначение("Справочник." + ТипСправочника + ".ПустаяСсылка");
	Для Каждого Элемент Из Параметры.СписокЗначений Цикл
		НоваяСтрока = ТаблицаСтрок.Добавить();
		НоваяСтрока.Строка = Элемент.Значение;
		НоваяСтрока.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"""%1""",
			НоваяСтрока.Строка);
		
		Если ТаблицаСтрок.Количество() > 1 Тогда
			ДанныеПредыдущейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 2];
			ДанныеПредыдущейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1"" %2",
				ДанныеПредыдущейСтроки.Строка,
				НСтр("ru = 'или'"));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	Если ЗначениеЗаполнено(СтрокаДляДобавления) Тогда
		НоваяСтрока = ТаблицаСтрок.Добавить();
		НоваяСтрока.Строка = СтрокаДляДобавления;
		НоваяСтрока.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"""%1""",
			Строка(НоваяСтрока.Строка));
		
		Если ТаблицаСтрок.Количество() > 1 Тогда
			ДанныеПредыдущейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 2];
			ДанныеПредыдущейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1"" %2",
				Строка(ДанныеПредыдущейСтроки.Строка),
				НСтр("ru = 'или'"));
		КонецЕсли;
	КонецЕсли;
	
	СтрокаДляДобавления = ПредопределенноеЗначение("Справочник." + ТипСправочника + ".ПустаяСсылка");
	ТекущийЭлемент = Элементы.СтрокаДляДобавления;
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	МассивСтрокДляУдаления = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.ТаблицаСтрок.ВыделенныеСтроки Цикл
		СтрокаДляУдаления = ТаблицаСтрок.НайтиПоИдентификатору(ВыделеннаяСтрока);
		МассивСтрокДляУдаления.Добавить(СтрокаДляУдаления);
	КонецЦикла;
	
	Для Каждого СтрокаДляУдаления Из МассивСтрокДляУдаления Цикл
		ТаблицаСтрок.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
	Если ТаблицаСтрок.Количество() > 0 Тогда
		ДанныеПоследнейСтроки = ТаблицаСтрок[ТаблицаСтрок.Количество() - 1];
		ДанныеПоследнейСтроки.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"""%1""",
				Строка(ДанныеПоследнейСтроки.Строка));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	ВозвращаемыйСписок = Новый СписокЗначений();
	Для Каждого СтрокаТаблицы Из ТаблицаСтрок Цикл
		ВозвращаемыйСписок.Добавить(СтрокаТаблицы.Строка);
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Список", ВозвращаемыйСписок);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаДляДобавленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ОткрытьФорму(
		"Справочник." + ТипСправочника + ".ФормаВыбора", 
		ПараметрыФормы, 
		Элемент,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры
