
#Область ПрограммныйИнтерфейс
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда 
		ФизЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда 
		КПП = "";
		КодПоОКПО = "";
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда 
		КПП = "";
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда 
		ИНН = "";
		КПП = "";
		КодПоОКПО = "";
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
	СтароеНаименование = "";
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		СтароеНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование");
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("СтароеНаименование", СтароеНаименование);
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ПредыдущаяПометкаУдаления", ПредыдущаяПометкаУдаления);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
//1С-Минск
	УстановитьПривилегированныйРежим(Истина);
//1С-Минск
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	// Возможно, выполнена явная регистрация событий при загрузке объекта.
	Если Не ДополнительныеСвойства.Свойство("НеРегистрироватьБизнесСобытия") Тогда
		Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
			БизнесСобытияВызовСервера.ЗарегистрироватьСобытие(Ссылка, Справочники.ВидыБизнесСобытий.СозданиеКорреспондента);	
		КонецЕсли;
	КонецЕсли;
	
	СтароеНаименование = ДополнительныеСвойства.СтароеНаименование;
	Если ЗначениеЗаполнено(СтароеНаименование) И СтароеНаименование <> Наименование Тогда 
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсходящиеДокументы.Ссылка КАК Ссылка,
		|	ИсходящиеДокументы.Получатели.(
		|		НомерСтроки,
		|		Получатель КАК Корреспондент
		|	) КАК Корреспонденты
		|ИЗ
		|	Справочник.ИсходящиеДокументы КАК ИсходящиеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИсходящиеДокументы.Получатели КАК ИсходящиеДокументыПолучатели
		|		ПО ИсходящиеДокументы.Ссылка = ИсходящиеДокументыПолучатели.Ссылка
		|ГДЕ
		|	ИсходящиеДокументыПолучатели.Получатель = &Корреспондент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВнутренниеДокументы.Ссылка,
		|	ВнутренниеДокументы.Корреспонденты.(
		|		НомерСтроки,
		|		Корреспондент
		|	)
		|ИЗ
		|	Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы.Корреспонденты КАК ВнутренниеДокументыКорреспонденты
		|		ПО ВнутренниеДокументы.Ссылка = ВнутренниеДокументыКорреспонденты.Ссылка
		|ГДЕ
		|	ВнутренниеДокументыКорреспонденты.Корреспондент = &Корреспондент";
		
		Запрос.УстановитьПараметр("Корреспондент", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			КорреспондентыДляСписков = Делопроизводство.ПолучитьКорреспондентовДляСписков(
				Выборка.Корреспонденты.Выгрузить());
			РегистрыСведений.ОбщиеРеквизитыДокументов.ЗаписатьОбщийРеквизитДокумента(
				Выборка.Ссылка, "КорреспондентыДляСписков", КорреспондентыДляСписков);
		КонецЦикла;	
		
	КонецЕсли;	
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ДополнительныеСвойства.Свойство("ПредыдущаяПометкаУдаления") Тогда
		ПредыдущаяПометкаУдаления = ДополнительныеСвойства.ПредыдущаяПометкаУдаления;
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		ПротоколированиеРаботыПользователей.ЗаписатьПометкуУдаления(Ссылка, ПометкаУдаления);
	КонецЕсли;	
	
	//МиСофт+
	МенеджерЗаписи = РегистрыСведений.ИндексыНумерации.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = Ссылка;
	МенеджерЗаписи.Прочитать();
	
	Если Не ЗначениеЗаполнено(МенеджерЗаписи.Объект) Тогда
		МенеджерЗаписи.Объект = Ссылка;
		МенеджерЗаписи.Индекс = ВРег(Лев(Ссылка.Наименование, 1));
		МенеджерЗаписи.Записать();
	КонецЕсли;
	//МиСофт-
	
КонецПроцедуры

#КонецОбласти