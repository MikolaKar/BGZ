#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Создает новый дескриптор по указанной записи регистра, указанного через 
// идентификатор объекта метаданных
Функция СоздатьНовыйДескриптор(ИдентификаторОбъектаМетаданных, Запись) Экспорт

	Дескриптор = Справочники.ДескрипторыДоступаРегистров.СоздатьЭлемент();
	
	// Получение сведений о полях доступа
	Дескриптор.ОбъектМетаданных = ИдентификаторОбъектаМетаданных;
	Дескриптор.Регистр = ИдентификаторОбъектаМетаданных.ПолноеИмя;
	
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Дескриптор.Регистр);
	СведенияОПолях = Новый Структура(
		"ОбъектДоступа1, ОбъектДоступа2, ОбъектДоступа3", 
		Неопределено, Неопределено, Неопределено);
	МенеджерРегистра.ЗаполнитьСведенияОПоляхДоступа(СведенияОПолях);
	
	// Заполнение дескриптора
	Дескриптор.ОбъектДоступа1 = Запись[СведенияОПолях.ОбъектДоступа1];
	Если ЗначениеЗаполнено(СведенияОПолях.ОбъектДоступа2) Тогда
		Дескриптор.ОбъектДоступа2 = Запись[СведенияОПолях.ОбъектДоступа2];
	КонецЕсли;	
	Если ЗначениеЗаполнено(СведенияОПолях.ОбъектДоступа3) Тогда
		Дескриптор.ОбъектДоступа3 = Запись[СведенияОПолях.объектДоступа3];
	КонецЕсли;
	
	Возврат Дескриптор;

КонецФункции

// Находит дескриптор по переданному образцу
Функция НайтиДескрипторПоОбразцу(Дескриптор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Регистр = &Регистр
		|	И ДескрипторыДоступаРегистров.ОбъектДоступа1 = &ОбъектДоступа1
		|	И ДескрипторыДоступаРегистров.ОбъектДоступа2 = &ОбъектДоступа2
		|	И ДескрипторыДоступаРегистров.ОбъектДоступа3 = &ОбъектДоступа3
		|	И ДескрипторыДоступаРегистров.ОбъектМетаданных = &ОбъектМетаданных";
		
	Запрос.УстановитьПараметр("ОбъектДоступа1", Дескриптор.ОбъектДоступа1);
	Запрос.УстановитьПараметр("ОбъектДоступа2", Дескриптор.ОбъектДоступа2);
	Запрос.УстановитьПараметр("ОбъектДоступа3", Дескриптор.ОбъектДоступа3);
	Запрос.УстановитьПараметр("Регистр", Дескриптор.Регистр);
	Запрос.УстановитьПараметр("ОбъектМетаданных", Дескриптор.ОбъектМетаданных);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Обновляет права по указанному дескпритору доступа
Процедура ОбновитьПрава(Дескриптор, ПротоколРасчетаПрав = Неопределено, Немедленно = Неопределено) Экспорт
	
	// Проверка на отложенное обновление прав доступа
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(Дескриптор);
		Возврат;
		
	КонецЕсли;
	
	ПраваДоступа = Новый Соответствие;
	
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Дескриптор.Регистр);
	МенеджерРегистра.ЗаполнитьПраваДоступа(Дескриптор, ПраваДоступа, ПротоколРасчетаПрав);	
	
	НаборЗаписей = РегистрыСведений.ПраваПоДескрипторамДоступа.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.Дескриптор.Установить(Дескриптор.Ссылка);
	Для каждого Эл Из ПраваДоступа Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Дескриптор = Дескриптор.Ссылка;
		Запись.Пользователь = Эл.Ключ;
		ЗаполнитьЗначенияСвойств(Запись, Эл.Значение);
	КонецЦикла;
	
	НаборЗаписей.Записать();	
	
КонецПроцедуры

Процедура УдалитьНеиспользуемые() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаРегистров.Регистр
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров");
		
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ТекстЗапроса = "";
	
	Пока Выборка.Следующий() Цикл
		
		Если ТекстЗапроса <> "" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		
		МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Выборка.Регистр);
		СведенияОПолях = Новый Структура(
		"ОбъектДоступа1, ОбъектДоступа2, ОбъектДоступа3", 
		Неопределено, Неопределено, Неопределено);
		МенеджерРегистра.ЗаполнитьСведенияОПоляхДоступа(СведенияОПолях);
		
		ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ
			|	ДескрипторыДоступаРегистров.Ссылка КАК Дескриптор
			|ИЗ
			|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
			|		ЛЕВОЕ СОЕДИНЕНИЕ %Регистр КАК РегистрДанных
			|		ПО ДескрипторыДоступаРегистров.ОбъектДоступа1 = РегистрДанных.%ОбъектДоступа1";
			
		Если ЗначениеЗаполнено(СведенияОПолях.ОбъектДоступа2) Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|			И ДескрипторыДоступаРегистров.ОбъектДоступа2 = РегистрДанных.%ОбъектДоступа2";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СведенияОПолях.ОбъектДоступа3) Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|			И ДескрипторыДоступаРегистров.ОбъектДоступа3 = РегистрДанных.%ОбъектДоступа3";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			|	ДескрипторыДоступаРегистров.Регистр = %ИмяРегистра
			|	И РегистрДанных.%ОбъектДоступа1 ЕСТЬ NULL
			|	И НЕ ДескрипторыДоступаРегистров.ПометкаУдаления";
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Регистр", Выборка.Регистр);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяРегистра", """" + Выборка.Регистр + """");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОбъектДоступа1", СведенияОПолях.ОбъектДоступа1);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОбъектДоступа2", СведенияОПолях.ОбъектДоступа2);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОбъектДоступа3", СведенияОПолях.ОбъектДоступа3);
			
	КонецЦикла;
	
	Если ТекстЗапроса = "" Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			Объект = Выборка.Дескриптор.ПолучитьОбъект();
			Объект.Заблокировать();
			Объект.УстановитьПометкуУдаления(Истина);
		Исключение
			// Объект может быть уже заблокирован
		КонецПопытки;	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьВсеДескрипторыРегистра(Регистр) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаРегистров.Ссылка
		|ИЗ
		|	Справочник.ДескрипторыДоступаРегистров КАК ДескрипторыДоступаРегистров
		|ГДЕ
		|	ДескрипторыДоступаРегистров.Регистр = &Регистр";

	Запрос.УстановитьПараметр("Регистр", Регистр);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		Попытка
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.Заблокировать();
			Объект.Удалить();	
		Исключение
			// Объект может быть уже заблокирован
		КонецПопытки;	
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли