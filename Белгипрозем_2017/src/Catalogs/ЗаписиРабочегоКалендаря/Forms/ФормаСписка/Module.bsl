#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДатаНачала") И Параметры.ДатаНачала <> Неопределено Тогда
		
		ДатаНачала = Параметры.ДатаНачала;
		Список.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Параметры.ДатаНачала);
		
	Иначе
		
		ДатаНачала = Неопределено;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ДатаОкончания") И Параметры.ДатаОкончания <> Неопределено Тогда
		
		ДатаОкончания = Параметры.ДатаОкончания;
		Список.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Параметры.ДатаОкончания);
		
	Иначе
		
		ДатаОкончания = Неопределено;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВесьДень") И Параметры.ВесьДень <> Неопределено Тогда
		
		ВесьДень = Параметры.ВесьДень;
		Список.Параметры.УстановитьЗначениеПараметра("ВесьДень", Параметры.ВесьДень);
		
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") И Параметры.Заголовок <> Неопределено Тогда
		
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
		
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	ОтображатьПомеченныеНаУдаления = РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьПомеченныеНаУдаления");
	Список.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьПомеченныеНаУдаления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗаписьКалендаря"
		ИЛИ ИмяСобытия = "Запись_ЦветЗаписиКалендаря" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗаписьКалендаря(Команда)
	
	РаботаСРабочимКалендаремКлиент.СоздатьЗаписьКалендаря(
		, ДатаНачала, ДатаОкончания, ВесьДень);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаЖелтый(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Желтый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаЗеленый(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Зеленый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаКрасный(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Красный"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаНет(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Нет"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаОранжевый(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Оранжевый"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветСпискаСиний(Команда)
	
	УстановитьЦветСписка(ПредопределенноеЗначение("Перечисление.ЦветаРабочегоКалендаря.Синий"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЦветСписка(Цвет)
	
	Запись = Элементы.Список.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(Запись) Тогда
		РаботаСРабочимКалендаремКлиент.УстановитьЦветЗаписиКалендаря(Цвет, Запись);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти