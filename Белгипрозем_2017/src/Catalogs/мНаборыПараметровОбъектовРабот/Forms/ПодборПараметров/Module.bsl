////////////////////////////////////////////////////////////////////////////////
// Процедуры - обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбранных.ЗагрузитьЗначения(Параметры.МасВыбранных);
	
	Заголовок = НСтр("ru = 'Подбор параметров объектов'");
	
	ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("масВыбранных", Параметры.МасВыбранных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПараметрыОбъектов" Тогда
		
		Элементы.ДеревоСвойств.Обновить();
		
		ТекущиеДанные = Элементы.ДеревоСвойств.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.Ссылка = Источник Тогда
			Комментарий = Параметр.Комментарий;
		КонецЕсли;
		
		ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("масВыбранных", СписокВыбранных.ВыгрузитьЗначения());
		
	ИначеЕсли ИмяСобытия = "ИзменилсяНаборВыбранных" И Источник = ЭтаФорма.ВладелецФормы Тогда
		Для Каждого Элемент Из Параметр.МассивПараметров Цикл
			Найденное = СписокВыбранных.НайтиПоЗначению(Элемент);
			Если Параметр.Операция = "Добавление" И Найденное = Неопределено Тогда
				СписокВыбранных.Добавить(Элемент);
			ИначеЕсли Параметр.Операция = "Удаление" И Найденное <> Неопределено Тогда
				СписокВыбранных.Удалить(Найденное);
			КонецЕсли;
		КонецЦикла;
		
		ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("масВыбранных", СписокВыбранных.ВыгрузитьЗначения());
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры - обработчики событий табличной части формы

&НаКлиенте
Процедура ДеревоСвойствПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	// Для групп оставим стандартную обработку
	Если Группа = Истина Тогда
		Возврат;
	ИначеЕсли Копирование И (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("ОткрываетсяИзНабораПараметровОбъектовРабот", Истина);
	
	Если Копирование Тогда
		ПараметрыОткрытия.Вставить("Копируемый", Элементы.ДеревоСвойств.ТекущаяСтрока);
	Иначе
		ПараметрыОткрытия.Вставить("Родитель", Родитель);
	КонецЕсли;
	
	ОткрытьФорму("ПланВидовХарактеристик.ПараметрыОбъектов.Форма.ФормаЭлемента", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элементы.ДеревоСвойств.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		ИмяФормыПВХ = "ФормаГруппы";
	Иначе
		ИмяФормыПВХ = "ФормаЭлемента";
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Элементы.ДеревоСвойств.ТекущаяСтрока);
	ПараметрыОткрытия.Вставить("ОткрываетсяИзНабораПараметровОбъектовРабот", Истина);
	ОткрытьФорму("ПланВидовХарактеристик.ПараметрыОбъектов.Форма." + ИмяФормыПВХ, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПриАктивизацииСтроки(Элемент)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьПодборПараметров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрВНабор(Команда)
	
	ВыполнитьПодборПараметров();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодборПараметров()
	
	Если Элементы.ДеревоСвойств.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МасДобавляемыеПараметры = Новый Массив;
	
	Для Каждого ВыделенаяСтрока ИЗ Элементы.ДеревоСвойств.ВыделенныеСтроки Цикл
		ПараметрОбъекта = Элементы.ДеревоСвойств.ДанныеСтроки(ВыделенаяСтрока).Ссылка;
		МасДобавляемыеПараметры.Добавить(ПараметрОбъекта);
	КонецЦикла;
	
	Результат = Новый Структура("МассивДобавляемых", МасДобавляемыеПараметры);
	
	Оповестить("ПодборПараметровВНабор", Результат);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Служебные процедуры и функции

&НаКлиенте
Процедура УстановитьКомментарий()
	
	Если Элементы.ДеревоСвойств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Комментарий = Элементы.ДеревоСвойств.ТекущиеДанные.Комментарий;
	
КонецПроцедуры
