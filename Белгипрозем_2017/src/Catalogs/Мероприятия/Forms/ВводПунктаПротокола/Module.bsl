#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Реквизиты = 
		"Исполнитель, 
		|ОсновнойОбъектАдресации, 
		|ДополнительныйОбъектАдресации, 
		|Слушали,
		|Выступили,
		|Решили,
		|СрокИсполнения, 
		|НоваяСтрока,
		|НомерСтроки";
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, Реквизиты);
	НачальныйНомерПунктаПрограммы = Параметры.НомерПунктаПрограммы;
	
	Если Исполнитель = Неопределено Тогда 
		Исполнитель = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НомерПунктаПрограммы = НачальныйНомерПунктаПрограммы;
	
	Программа = ЭтаФорма.ВладелецФормы.Объект.Программа;
	Для Каждого Строка Из Программа Цикл
		Элементы.НомерПунктаПрограммы.СписокВыбора.Добавить(Строка.НомерПункта, "N " + Строка(Строка.НомерПункта) + " " + Строка(Строка.Содержание) );
	КонецЦикла;	
	
	Если НоваяСтрока Тогда 
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("НомерПункта", НомерПунктаПрограммы);
		НайденныеСтроки = Программа.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда 
			НайденнаяСтрока = НайденныеСтроки[0];
			Если ЗначениеЗаполнено(НайденнаяСтрока.Исполнитель) Тогда 
				Слушали = Слушали + Строка(НайденнаяСтрока.Исполнитель) + Символы.ПС;
			КонецЕсли;	
			Слушали = Слушали + НайденнаяСтрока.Содержание;
		КонецЕсли;
	КонецЕсли;	
	
	ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Пункт протокола №%1'"),
		ПолучитьНомерПунктаПротокола());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	РаботаСБизнесПроцессамиКлиент.ВыбратьОбъектыАдресацииРоли(
		ЭтаФорма,
		"Исполнитель",
		"ОсновнойОбъектАдресации",
		"ДополнительныйОбъектАдресации",
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСПользователямиКлиент.ВыбратьИсполнителя(Элемент, Исполнитель,,Истина,,,
		ОсновнойОбъектАдресации, 
		ДополнительныйОбъектАдресации, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		
		Исполнитель = ВыбранноеЗначение.РольИсполнителя;
		ОсновнойОбъектАдресации = ВыбранноеЗначение.ОсновнойОбъектАдресации;
		ДополнительныйОбъектАдресации = ВыбранноеЗначение.ДополнительныйОбъектАдресации;
		
		Модифицированность = Истина;
	Иначе  
		ОсновнойОбъектАдресации = Неопределено;
		ДополнительныйОбъектАдресации = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбораИсполнителя(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСПользователями.СформироватьДанныеВыбораИсполнителя(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПунктаПрограммыПриИзменении(Элемент)
	
	Программа = ЭтаФорма.ВладелецФормы.Объект.Программа;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("НомерПункта", НомерПунктаПрограммы);
	НайденныеСтроки = Программа.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() > 0 Тогда 
		НайденнаяСтрока = НайденныеСтроки[0];
		Если НомерПунктаПрограммы <> НачальныйНомерПунктаПрограммы Тогда 
			Слушали = "";
			Если ЗначениеЗаполнено(НайденнаяСтрока.Исполнитель) Тогда 
				Слушали = Слушали + Строка(НайденнаяСтрока.Исполнитель) + Символы.ПС;
			КонецЕсли;	
			Слушали = Слушали + НайденнаяСтрока.Содержание;
		КонецЕсли;	
	КонецЕсли;	
	
	НачальныйНомерПунктаПрограммы = НомерПунктаПрограммы;
	
	ЭтаФорма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Пункт протокола №%1'"),
		ПолучитьНомерПунктаПротокола());
	
КонецПроцедуры

&НаКлиенте
Процедура СрокВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;	
	
	Результат = Новый Структура("НомерПунктаПрограммы, 
		|Исполнитель, 
		|ОсновнойОбъектАдресации, 
		|ДополнительныйОбъектАдресации, 
		|Слушали,
		|Выступили,
		|Решили,
		|СрокИсполнения");
		
	ЗаполнитьЗначенияСвойств(Результат, ЭтаФорма);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьНомерПунктаПротокола()
	
	Если Не ЗначениеЗаполнено(НомерПунктаПрограммы) Тогда 
		Возврат "";
	КонецЕсли;	
	
	Протокол = ЭтаФорма.ВладелецФормы.Объект.Протокол;
	
	Строки = Новый Массив;
	Для Каждого Строка Из Протокол Цикл
		Если НомерПунктаПрограммы = Строка.НомерПунктаПрограммы Тогда 
			Строки.Добавить(Строка.НомерСтроки);
		КонецЕсли;
	КонецЦикла;	
	
	Если НоваяСтрока Тогда 
		
		Если Строки.Количество() = 0 Тогда 
			Возврат НомерПунктаПрограммы;
		Иначе
			Возврат Строка(НомерПунктаПрограммы) + "." + Строка(Строки.Количество()+1);
		КонецЕсли;
		
	Иначе
		
		Если Строки.Количество() = 1 Тогда 
			Возврат НомерПунктаПрограммы;
		Иначе
			Инд = Строки.Найти(НомерСтроки);
			Если Инд = Неопределено Тогда 
				Инд = 0;
			КонецЕсли;	
			Возврат Строка(НомерПунктаПрограммы) + "." + Строка(Инд+1);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецФункции

#КонецОбласти