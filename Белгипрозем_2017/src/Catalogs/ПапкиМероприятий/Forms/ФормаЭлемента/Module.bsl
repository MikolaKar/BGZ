#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьПолныйПуть();
	
	// Установка видимости команды "Права"
	Если Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей() Тогда
		Элементы.ФормаПрава.Видимость = Ложь;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
			Элементы.ФормаПрава.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОбновитьПолныйПуть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Права(Команда)
	
	// Предварительная запись новой папки
	Если Объект.Ссылка.Пустая() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПраваЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение команды ""Права"" возможно только после записи данных.
			|Данные будут записаны.'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	// Проверка прав
	Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
		Элементы.ФормаПрава.Видимость = Ложь;
		ВызватьИсключение НСтр("ru = 'Доступ к настройке прав запрещен. 
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	// Открытие формы настройки прав
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Объект.Ссылка);
	ОткрытьФорму("Справочник.ПапкиМероприятий.Форма.ПраваНаПапку", ПараметрыФормы, , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Изменение:'"),
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка));
	
	// Проверка прав
	Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
		Элементы.ФормаПрава.Видимость = Ложь;
		ВызватьИсключение НСтр("ru = 'Доступ к настройке прав запрещен. 
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	// Открытие формы настройки прав
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Объект.Ссылка);
	ОткрытьФорму("Справочник.ПапкиМероприятий.Форма.ПраваНаПапку", ПараметрыФормы, , Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПолныйПуть()
	
	ПолныйПуть = "";
	
	ПапкаРодитель = Объект.Родитель;
	Пока ЗначениеЗаполнено(ПапкаРодитель) Цикл
		
		ПолныйПуть = Строка(ПапкаРодитель) + "\" + ПолныйПуть;
		
		ПапкаРодитель = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПапкаРодитель, "Родитель");
		Если Не ЗначениеЗаполнено(ПапкаРодитель) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ПолныйПуть = ПолныйПуть + Объект.Наименование;
	
	Если Не ПустаяСтрока(ПолныйПуть) Тогда
		ПолныйПуть = """" + ПолныйПуть + """";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти