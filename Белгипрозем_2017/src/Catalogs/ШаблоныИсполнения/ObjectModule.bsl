
#Область ПрограммныйИнтерфейс

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	
	Если Исполнители.Количество() = 0 Тогда
		МассивПолей.Добавить("Исполнители");
	КонецЕсли;	
	
	Возврат МассивПолей;
	
КонецФункции	

//Формирует текстовое представление бизнес-процесса, создаваемого по шаблону
Функция СформироватьСводкуПоШаблону() Экспорт
	
	Результат = ШаблоныБизнесПроцессов.ПолучитьОбщуюЧастьОписанияШаблона(Ссылка);
	
	Если ЗначениеЗаполнено(НаименованиеБизнесПроцесса) Тогда
		Результат = Результат + НСтр("ru = 'Заголовок'") + ": " + НаименованиеБизнесПроцесса + Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Описание) Тогда
		Результат = Результат + НСтр("ru = 'Описание'") + ": " + Описание + Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокИсполнения) 
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач")
		И ЗначениеЗаполнено(СрокИсполненияЧас) Тогда
		Результат = Результат 
			+ НСтр("ru = 'Срок исполнения (общий)'") + ": "
			+ ?(ЗначениеЗаполнено(СрокИсполнения), Строка(СрокИсполнения) + НСтр("ru = ' дней'"),"")
			+ ?(ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач") И ЗначениеЗаполнено(СрокИсполненияЧас), Строка(СрокИсполненияЧас) + НСтр("ru = ' часов'"),"") 
			+ Символы.ПС;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Важность) Тогда
		Результат = Результат + НСтр("ru = 'Важность'") + ": " + Строка(Важность) + Символы.ПС;
	КонецЕсли;
	
	Если Исполнители.Количество() > 0 Тогда
		Результат = Результат + НСтр("ru = 'Исполнители'") + ": ";
		Для Каждого Исполнитель Из Исполнители Цикл
			Результат = Результат + Исполнитель.Исполнитель
				+ ?(ЗначениеЗаполнено(Исполнитель.ОсновнойОбъектАдресации),
					", " + Исполнитель.ОсновнойОбъектАдресации, "")
				+ ?(ЗначениеЗаполнено(Исполнитель.ДополнительныйОбъектАдресации),
					", " + Исполнитель.ДополнительныйОбъектАдресации, "")
				+ ";" + Символы.ПС;
		КонецЦикла;
		Результат = Сред(Результат, 1, СтрДлина(Результат) - 2);
		Результат = Результат + Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВариантИсполнения) Тогда
		Результат = Результат + Нстр("ru = 'Порядок исполнения'") + ": " + Строка(ВариантИсполнения) + Символы.ПС;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Проверяющий) Тогда
		Результат = Результат + Нстр("ru = 'Проверяющий'") + ": "
			+ Строка(Проверяющий)
			+ ?(ЗначениеЗаполнено(ОсновнойОбъектАдресацииПроверяющего),
				", " + ОсновнойОбъектАдресацииПроверяющего, "")
			+ ?(ЗначениеЗаполнено(ДополнительныйОбъектАдресацииПроверяющего),
				", " + ДополнительныйОбъектАдресацииПроверяющего, "")
			+ Символы.ПС;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контролер) Тогда
		Результат = Результат + Нстр("ru = 'Контролер'") + ": "
			+ ?(ЗначениеЗаполнено(ОсновнойОбъектАдресацииКонтролера),
				", " + ОсновнойОбъектАдресацииКонтролера, "")
			+ ?(ЗначениеЗаполнено(ДополнительныйОбъектАдресацииКонтролера),
				", " + ДополнительныйОбъектАдресацииКонтролера, "")
			+ Строка(Контролер) + Символы.ПС;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда 
		ШаблоныБизнесПроцессов.НачальноеЗаполнениеШаблона(ЭтотОбъект, ДанныеЗаполнения);
		ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Параллельно;
	КонецЕсли;
			
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// Обработка рабочей группы
	СсылкаОбъекта = Ссылка;
	// Установка ссылки нового
	Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
		СсылкаОбъекта = ПолучитьСсылкуНового();
		Если Не ЗначениеЗаполнено(СсылкаОбъекта) Тогда
			СсылкаНового = Справочники.ШаблоныИсполнения.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
			СсылкаОбъекта = СсылкаНового;
		КонецЕсли;
	КонецЕсли;
	
	// Подготовка рабочей группы
	РабочаяГруппа = РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту(СсылкаОбъекта);
		
	// Добавление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаДобавить") Тогда
		
		Для каждого Эл Из ДополнительныеСвойства.РабочаяГруппаДобавить Цикл
			
			// Добавление участника в итоговую рабочую группу
			Строка = РабочаяГруппа.Добавить();
			Строка.Участник = Эл.Участник;
			Строка.ОсновнойОбъектАдресации = Эл.ОсновнойОбъектАдресации;
			Строка.ДополнительныйОбъектАдресации = Эл.ДополнительныйОбъектАдресации;
			
		КонецЦикла;	
			
	КонецЕсли;		
	
	// Удаление участников, переданных "снаружи", например из формы объекта
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаУдалить") Тогда
		
		Для каждого Эл Из ДополнительныеСвойства.РабочаяГруппаУдалить Цикл
			
			// Поиск удаляемого участника в итоговой рабочей группе
			Для каждого Эл2 Из РабочаяГруппа Цикл
				
				Если Эл2.Участник = Эл.Участник 
					И Эл2.ОсновнойОбъектАдресации = Эл.ОсновнойОбъектАдресации
					И Эл2.ДополнительныйОбъектАдресации = Эл.ДополнительныйОбъектАдресации Тогда
					
					// Удаление участника из итоговой рабочей группы
					РабочаяГруппа.Удалить(Эл2);
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;	
				
		КонецЦикла;	
			
	КонецЕсли;			
	
	// Запись итоговой рабочей группы
	РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(
		СсылкаОбъекта,
		РабочаяГруппа,
		Ложь); //ОбновитьПраваДоступа
	
	// Установка необходимости обновления прав доступа
	ДополнительныеСвойства.Вставить("ДополнительныеПравообразующиеЗначенияИзменены");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(Проверяющий) = Тип("СправочникСсылка.РолиИсполнителей") И Проверяющий.ВнешняяРоль Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Внешняя роль не может быть использована в данном поле!'"),
			ЭтотОбъект,
			"Проверяющий",, 
			Отказ);
	КонецЕсли;
	
	Если БизнесСобытияВызовСервера.ЕстьЗаписиАвтоСтартаБизнесПроцессовДляШаблонаБизнесПроцессов(Ссылка) Тогда
		
		МассивПолей = ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта();
		Если МассивПолей.Количество() <> 0 Тогда
			
			СтрокаПолей = БизнесСобытияВызовСервера.МассивПолейВСтроку(МассивПолей);
			
			Текст = 
				НСтр("ru = 'Этот шаблон используется для автоматического запуска процессов. Должны быть заполнены поля: '")
				+ СтрокаПолей;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,,
				"Объект.Ссылка",, 
				Отказ);
			
		КонецЕсли;
		
		ПроверитьЭтапыЗаполняемыхПредметов(Отказ);
		
	КонецЕсли;
	
	// Проверка исполнителей на дубли при смешанном и параллельном варианте исполнения
	Если ВариантИсполнения = Перечисления.ВариантыМаршрутизацииЗадач.Последовательно Тогда
		КоличествоИсполнителей = 0; // В таком варианте проверку делать не нужно
	Иначе
		КоличествоИсполнителей = Исполнители.Количество();
		ТаблицаИсполнителейДляПроверки = Исполнители.Выгрузить();
		ТаблицаИсполнителейДляПроверки.Колонки.Добавить("Шаг");
		РаботаСБизнесПроцессамиКлиентСервер.ЗаполнитьШаг(ТаблицаИсполнителейДляПроверки);
	КонецЕсли;	
	
	Для Инд1 = 0 По КоличествоИсполнителей - 2 Цикл
		Строка1 = ТаблицаИсполнителейДляПроверки[Инд1];
		Если Не ЗначениеЗаполнено(Строка1.Исполнитель) Тогда 
			Продолжить;
		КонецЕсли;

		Для Инд2 = Инд1 + 1 По КоличествоИсполнителей - 1 Цикл
			Строка2 = ТаблицаИсполнителейДляПроверки[Инд2];
	    	Шаг1 = Строка1.Шаг;
			Шаг2 = Строка2.Шаг;

			Если Строка1.Исполнитель = Строка2.Исполнитель
				И Шаг1 = Шаг2
				И ТипЗнч(Строка1.Исполнитель) = Тип("СправочникСсылка.Пользователи") Тогда 
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Исполнитель ""%1"" указан дважды в списке исполнителей!'"), 
					Строка(Строка2.Исполнитель));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"Объект.Исполнители[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
					
			ИначеЕсли Строка1.Исполнитель = Строка2.Исполнитель
				И Шаг1 = Шаг2
				И ТипЗнч(Строка1.Исполнитель) = Тип("Строка")Тогда 
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Автоподстановка ""%1"" указана дважды в списке исполнителей!'"), 
					Строка(Строка2.Исполнитель));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"Объект.Исполнители[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
		
			ИначеЕсли (Строка1.Исполнитель = Строка2.Исполнитель 
				И Шаг1 = Шаг2
				И ТипЗнч(Строка1.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей")
				И Строка1.ОсновнойОбъектАдресации = Строка2.ОсновнойОбъектАдресации
				И Строка1.ДополнительныйОбъектАдресации = Строка2.ДополнительныйОбъектАдресации) Тогда 
				Если ЗначениеЗаполнено(Строка2.ОсновнойОбъектАдресации) И ЗначениеЗаполнено(Строка2.ДополнительныйОбъектАдресации) Тогда 	
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Исполнитель ""%1 (%2, %3)"" указан дважды в списке исполнителей!'"),
						Строка(Строка2.Исполнитель),
						Строка(Строка2.ОсновнойОбъектАдресации),
						Строка(Строка2.ДополнительныйОбъектАдресации));
						
				ИначеЕсли ЗначениеЗаполнено(Строка2.ОсновнойОбъектАдресации) Тогда 
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Исполнитель ""%1 (%2)"" указан дважды в списке исполнителей!'"),
						Строка(Строка2.Исполнитель),
						Строка(Строка2.ОсновнойОбъектАдресации));
						
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Исполнитель ""%1"" указан дважды в списке исполнителей!'"), 
						Строка(Строка2.Исполнитель));
						
				КонецЕсли;	
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"Объект.Исполнители[" + Формат(Строка2.НомерСтроки-1, "ЧГ=0") + "].Исполнитель",, 
					Отказ);
				
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЭтапыЗаполняемыхПредметов(Отказ)
	
	ЗаполняемыеПредметы = Предметы.НайтиСтроки(Новый Структура("РольПредмета", Перечисления.РолиПредметов.Заполняемый));
	
	Для Каждого СтрокаПредмета Из ЗаполняемыеПредметы Цикл
		Отбор = Новый Структура("ИмяПредмета, ОбязательноеЗаполнение",СтрокаПредмета.ИмяПредмета, Истина);
		ТочкиЗаполнения = ПредметыЗадач.НайтиСтроки(Отбор);
		Для Каждого СтрокаТочки Из ТочкиЗаполнения Цикл
			Если СтрокаТочки.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.Контролировать 
				И Не ЗначениеЗаполнено(Контролер) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан Контролер для заполнения предмета ""%1"" на этапе процесса ""%2""!'"),
						СтрокаТочки.ИмяПредмета,
						СтрокаТочки.ТочкаМаршрута),
					ЭтотОбъект,
					"Контролер",, 
					Отказ);
			ИначеЕсли СтрокаТочки.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.Проверить 
				И Не ЗначениеЗаполнено(Проверяющий) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан Проверяющий для заполнения предмета ""%1"" на этапе процесса ""%2""!'"),
						СтрокаТочки.ИмяПредмета,
						СтрокаТочки.ТочкаМаршрута),
					ЭтотОбъект,
					"Проверяющий",, 
					Отказ);
			ИначеЕсли СтрокаТочки.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.ОтветственноеИсполнение 
				И Исполнители.Найти(Истина, "Ответственный") = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан Ответственный исполнитель для заполнения предмета ""%1"" на этапе процесса ""%2""!'"),
						СтрокаТочки.ИмяПредмета,
						СтрокаТочки.ТочкаМаршрута),
					ЭтотОбъект,
					"Исполнители",, 
					Отказ);
			ИначеЕсли СтрокаТочки.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.Исполнить 
				И Исполнители.Найти(Ложь, "Ответственный") = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан Исполнитель для заполнения предмета ""%1"" на этапе процесса ""%2""!'"),
						СтрокаТочки.ИмяПредмета,
						СтрокаТочки.ТочкаМаршрута),
					ЭтотОбъект,
					"Исполнители",, 
					Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Поддержка комплексных процессов

// Получает строковое представление срока выполнения
Функция ПолучитьСтроковоеПредставлениеСрокаВыполнения() Экспорт
	
	Результат = "";
	
	ОбщийСрокИсполнения = 
		СрокИсполнения * 86400
		+ СрокИсполненияЧас * 3600
		+ СрокОтложенногоСтарта;
		
	ОбщийСрокИсполненияДни = Цел(ОбщийСрокИсполнения / 86400);
	ОбщийСрокИсполненияЧасы = 
		(ОбщийСрокИсполнения - ОбщийСрокИсполненияДни * 86400) / 3600;
	
	РезультатИсполнение = "";
	БизнесПроцессыИЗадачиКлиентСервер.ПолучитьСрокИсполненияПрописью(
		ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач"),
		РезультатИсполнение,
		ОбщийСрокИсполненияДни,
		ОбщийСрокИсполненияЧасы);

	Возврат РезультатИсполнение;
			
КонецФункции

// Получает строковое представление срока выполнения
Функция ПолучитьСтроковоеПредставлениеИсполнителей() Экспорт
	
	Результат = "";
	
	Для Счетчик = 0 по Исполнители.Количество() - 1 Цикл
		Исполнитель = Исполнители[Счетчик];
		Результат = Результат 
			+ Строка(Исполнитель.Исполнитель)
			+ ?(Счетчик = Исполнители.Количество() - 1, "", ", ");
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Проверяющий) Тогда
		Результат = Результат + 
			?(ЗначениеЗаполнено(Результат), ", ", "")
			+ Строка(Проверяющий)
			+ НСтр("ru = '(Проверяющий)'");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Контролер) Тогда
		Результат = Результат + 
			?(ЗначениеЗаполнено(Результат), ", ", "")
			+ Строка(Контролер)
			+ НСтр("ru = '(Контролер)'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает длительность в часах
Функция ПолучитьДлительностьПроцесса() Экспорт
	
	Результат = СрокИсполнения * 24 + СрокИсполненияЧас + СрокОтложенногоСтарта / 3600;
	Возврат Результат;
	
КонецФункции

#КонецОбласти