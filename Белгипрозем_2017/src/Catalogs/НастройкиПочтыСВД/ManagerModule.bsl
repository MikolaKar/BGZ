
/////////////////////////////////
//	Получение сообщений
/////////////////////////////////

Функция ПолучитьСообщения(НастройкаПочты, УдалятьПолученные = Истина) Экспорт
	
	МассивСообщенийСВД = Новый Массив;
	УчетнаяЗапись = ОбщегоНазначения.ПолучитьЗначениеРеквизита(НастройкаПочты, "УчетнаяЗапись");
	СообщениеОбОшибке = "";
	Соединение = Почта.ИнтернетПочтаУстановитьСоединение(УчетнаяЗапись, , СообщениеОбОшибке);
	Если Соединение = Неопределено Тогда
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'СерверСВД. Получение почты'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			НастройкаПочты.Метаданные(),
			НастройкаПочты,
			НСтр("ru = 'Невозможно подключиться к почтовому серверу.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат МассивСообщенийСВД;
		
	КонецЕсли;

	ПараметрыЗагрузки = Почта.СформироватьСтруктуруПараметровЗагрузки();
	СообщениеОбОшибке = "";
	Сообщения = Почта.ПолучитьВходящиеСообщения(Соединение, ПараметрыЗагрузки, СообщениеОбОшибке);
	
	Если ЗначениеЗаполнено(Сообщения) Тогда
		
		ИдентификаторыЗагруженныхСообщений = Новый Массив;
		Для каждого Сообщение Из Сообщения Цикл
			СообщениеСВД = Новый Структура();
			СообщениеСВД.Вставить("ОтправительПочта", Сообщение.Отправитель.Адрес);
			СообщениеСВД.Вставить("Наименование", Сообщение.Тема);
			МассивФайловВСообщенииСВД = Новый Массив;
			Для Каждого Вложение Из Сообщение.Вложения Цикл
				ДанныеФайлаВСообщении = Новый Структура;
				ДанныеФайлаВСообщении.Вставить("Имя", Вложение.Имя);
				ДанныеФайлаВСообщении.Вставить("Тип", Вложение.ТипСодержимого);
				ДанныеФайлаВСообщении.Вставить("Данные", Вложение.Данные);
				
				МассивФайловВСообщенииСВД.Добавить(ДанныеФайлаВСообщении);
				
			КонецЦикла;
			СообщениеСВД.Вставить("Файлы", МассивФайловВСообщенииСВД);
			МассивСообщенийСВД.Добавить(СообщениеСВД);
			ИдентификаторыЗагруженныхСообщений.Добавить(Сообщение.Идентификатор[0]);
		КонецЦикла;
		
		СообщениеОбОшибке = "";
		Если Не Почта.УдалитьСообщенияВПочтовомЯщике(
			Соединение,
			ИдентификаторыЗагруженныхСообщений,
			СообщениеОбОшибке) Тогда
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'СерверСВД. Получение почты'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				НастройкаПочты.Метаданные(),
				НастройкаПочты,
				НСтр("ru = 'Невозможно удалить полученные сообщения. '", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
					+ СообщениеОбОшибке);
			
		КонецЕсли;
		
	Иначе
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'СерверСВД. Получение почты'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			НастройкаПочты.Метаданные(),
			НастройкаПочты,
			НСтр("ru = 'Невозможно загрузить сообщения. '", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
				+ СообщениеОбОшибке);
		
	КонецЕсли;
	
	Соединение.Отключиться();
	Возврат МассивСообщенийСВД;
	
КонецФункции	

/////////////////////////////////
//	Отправка сообщений
/////////////////////////////////
Процедура ОтправитьСообщение(НастройкаПочты, Сообщение) Экспорт
	//МиСофт+
	Попытка 
		УчетнаяЗапись = ОбщегоНазначения.ПолучитьЗначениеРеквизита(НастройкаПочты, "УчетнаяЗапись");
		ПараметрыОтправки = Новый Структура();
		
		//Тема
		ПараметрыОтправки.Вставить("Тема", Сообщение.ИдентификаторСообщения);
		
		//Текст
		ПараметрыОтправки.Вставить("Текст", Строка(ТекущаяДатаСеанса()));
		
		// Кому отправлять
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КорреспондентыКонтактнаяИнформация.АдресЭП
			|ИЗ
			|	Справочник.Корреспонденты.КонтактнаяИнформация КАК КорреспондентыКонтактнаяИнформация
			|ГДЕ
			|	КорреспондентыКонтактнаяИнформация.Ссылка = &Ссылка
			|	И КорреспондентыКонтактнаяИнформация.Тип = &Тип";
		Запрос.УстановитьПараметр("Ссылка", Сообщение.Кому);
		Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Не Выборка.Следующий() Тогда
			Возврат;
		КонецЕсли;
		ПараметрыОтправки.Вставить("Кому", Выборка.АдресЭП);
		
		// Вставка вложений
		МассивВложений = Новый Массив;
		МассивФайлов = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(Сообщение);
		Для каждого ФайлСсылка Из МассивФайлов Цикл
			
			ДвоичныеДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДвоичныеДанныеФайла(ФайлСсылка);
				
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла);
			
			СтруктураВложения = Новый Структура();
			СтруктураВложения.Вставить("Адрес", АдресВоВременномХранилище);
			СтруктураВложения.Вставить("ИмяФайла", ФайлСсылка.Наименование + "." + ФайлСсылка.ТекущаяВерсияРасширение);
			МассивВложений.Добавить(СтруктураВложения);
				
		КонецЦикла;
		ПараметрыОтправки.Вставить("Вложения", МассивВложений);
			
		ЛегкаяПочтаСервер.ОтправитьИнтернетПочта(ПараметрыОтправки, УчетнаяЗапись);
	Исключение
		Информация = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'СерверСВД. Отправка почты'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			НастройкаПочты.Метаданные(),
			НастройкаПочты,
			НСтр("ru = 'Невозможно отправить сообщение. '", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
				+ ПодробноеПредставлениеОшибки(Информация));
	КонецПопытки;
	//МиСофт-
КонецПроцедуры