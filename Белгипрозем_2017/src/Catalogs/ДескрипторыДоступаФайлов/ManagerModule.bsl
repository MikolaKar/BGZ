#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Выполняет расчет прав по указанному дескриптору
Процедура ОбновитьПрава(Дескриптор, ПротоколРасчетаПрав = Неопределено, Немедленно = Неопределено) Экспорт
	
	// Проверка на отложенное обновление прав доступа
	Если ДокументооборотПраваДоступаПовтИсп.ОтложенноеОбновлениеПравДоступа() 
		И Немедленно <> Истина Тогда
		
		// Добавление в очередь
		РегистрыСведений.ОчередьОбновленияПравДоступа.Добавить(Дескриптор);
		Возврат;
		
	КонецЕсли;
	
	// Нахождение менеджера владельца файла
	ВладелецФайла = Дескриптор.ВладелецФайла;
	ДескрипторСвязанСВладельцем = ЗначениеЗаполнено(ВладелецФайла);
	Если ДескрипторСвязанСВладельцем Тогда
		
		МенеджерВладельцаФайла = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ВладелецФайла);
		
	Иначе
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ИдентификаторыОбъектовМетаданных.ПолноеИмя
		|ИЗ
		|	Справочник.ДескрипторыДоступаФайлов КАК ДескрипторыДоступаФайлов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|			ПО ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных = ИдентификаторыОбъектовМетаданных.Ссылка
		|		ПО ДескрипторыДоступаФайлов.ДескрипторВладельца = ДескрипторыДоступаОбъектов.Ссылка
		|ГДЕ
		|	ДескрипторыДоступаФайлов.Ссылка = &Дескриптор");
		
		Запрос.УстановитьПараметр("Дескриптор", Дескриптор);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() и ЗначениеЗаполнено(Выборка.ПолноеИмя) Тогда
			ПолноеИмяМетаданныхВладельца = Выборка.ПолноеИмя;
		Иначе
			ВызватьИсключение "Ошибка при обновлении прав файла: неверно указан владелец. Обратитесь к администратору.";
		КонецЕсли;
		
		МенеджерВладельцаФайла = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяМетаданныхВладельца);
		
	КонецЕсли;
	
	// Заполнение прав доступа
	ПраваДоступа = Новый Соответствие;
	
	ЕстьМетодЗаполнитьПраваДоступаДляФайлов = Ложь;
	Попытка 
		ЕстьМетодЗаполнитьПраваДоступаДляФайлов = МенеджерВладельцаФайла.ЕстьМетодЗаполнитьПраваДоступаДляФайлов();
	Исключение	
		// У объекта может и не быть метода "ЕстьМетодЗаполнитьПраваДоступаДляФайлов"
	КонецПопытки;	
	
	Если ЕстьМетодЗаполнитьПраваДоступаДляФайлов Тогда
		
		// Права доступа к файлам заполняет сам владелец файла
		Если ДескрипторСвязанСВладельцем Тогда
			
			ДескрипторВладельца = 
				РегистрыСведений.ДескрипторыДоступаДляОбъектов.НайтиДескрипторДляОбъекта(ВладелецФайла);
			
			Если ЗначениеЗаполнено(ДескрипторВладельца) Тогда
				// Это устаревший дескриптор файла из ранних версий
				МенеджерВладельцаФайла.ЗаполнитьПраваДоступаДляФайлов(ДескрипторВладельца, ПраваДоступа);
			Иначе
				// Это дескриптор файла, привязанного к процессу
				МенеджерВладельцаФайла.ЗаполнитьПраваДоступаДляФайлов(ВладелецФайла, ПраваДоступа);
			КонецЕсли;
			
		Иначе
			МенеджерВладельцаФайла.ЗаполнитьПраваДоступаДляФайлов(Дескриптор.ДескрипторВладельца, ПраваДоступа);
		КонецЕсли;
		
	Иначе
		
		// Стандартное заполнение прав - файл копирует все права владельца
		Если ДескрипторСвязанСВладельцем Тогда
			ДокументооборотПраваДоступа.СкопироватьПраваОбъекта(ВладелецФайла, ПраваДоступа);
		Иначе
			ДокументооборотПраваДоступа.СкопироватьПраваДескриптора(Дескриптор.ДескрипторВладельца, ПраваДоступа);
		КонецЕсли;

	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ПраваПоДескрипторамДоступа.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.Дескриптор.Установить(Дескриптор.Ссылка);
	
	Для каждого Эл Из ПраваДоступа Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Дескриптор = Дескриптор.Ссылка;
		Запись.Пользователь = Эл.Ключ;
		ЗаполнитьЗначенияСвойств(Запись, Эл.Значение);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Обновляет права доступа по дескпритору, который связан с указанным владельцем файла
Процедура ОбновитьПраваПоВладельцуФайла(ВладелецФайла) Экспорт
	
	ДескрипторФайла = ДокументооборотПраваДоступа.ОпределитьДескрипторДоступаФайлаПоВладельцу(ВладелецФайла);
	
	Если ДескрипторФайла <> Неопределено Тогда
		ОбновитьПрава(ДескрипторФайла);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет неиспользуемые дескрипторы доступа
Процедура УдалитьНеиспользуемые() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДескрипторыДоступаФайлов.Ссылка КАК Дескриптор
		|ИЗ
		|	Справочник.ДескрипторыДоступаФайлов КАК ДескрипторыДоступаФайлов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДескрипторыДоступаДляФайлов КАК ДескрипторыДоступаДляФайлов
		|		ПО (ДескрипторыДоступаДляФайлов.Дескриптор = ДескрипторыДоступаФайлов.Ссылка)
		|			И (НЕ ДескрипторыДоступаДляФайлов.ВладелецФайла.ПометкаУдаления)
		|ГДЕ
		|	ДескрипторыДоступаДляФайлов.ВладелецФайла ЕСТЬ NULL 
		|	И НЕ ДескрипторыДоступаФайлов.ПометкаУдаления";

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Попытка
			Объект = ВыборкаДетальныеЗаписи.Дескриптор.ПолучитьОбъект();
			Объект.Заблокировать();
			Объект.УстановитьПометкуУдаления(Истина);
		Исключение
			// Объект может быть уже заблокирован
		КонецПопытки;	
		
	КонецЦикла;

КонецПроцедуры

#КонецЕсли