#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей() Тогда
		Элементы.ФормаНастроитьПрава.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
			Элементы.ФормаНастроитьПрава.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьПрава(Команда)
	
	// Предварительная запись новой папки
	Если Объект.Ссылка.Пустая() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьПраваЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Выполнение команды ""Права"" возможно только после записи данных.
			|Данные будут записаны.'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	// Проверка прав
	Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
		Элементы.ФормаНастроитьПрава.Видимость = Ложь;
		ВызватьИсключение НСтр("ru = 'Доступ к настройке прав запрещен. 
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	// Открытие формы настройки прав
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Объект.Ссылка);
	ОткрытьФорму("Справочник.ПапкиФорума.Форма.ПраваНаПапку", ПараметрыФормы, , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПраваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Записать() Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Изменение:'"), 
		ПолучитьНавигационнуюСсылку(Объект.Ссылка),
		Строка(Объект.Ссылка));
	
	// Проверка прав
	Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Объект.Ссылка).УправлениеПравами Тогда
		Элементы.ФормаНастроитьПрава.Видимость = Ложь;
		ВызватьИсключение НСтр("ru = 'Доступ к настройке прав запрещен. 
			|Обратитесь к администратору.'");
	КонецЕсли;
	
	// Открытие формы настройки прав
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект", Объект.Ссылка);
	ОткрытьФорму("Справочник.ПапкиФорума.Форма.ПраваНаПапку", ПараметрыФормы, , Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти