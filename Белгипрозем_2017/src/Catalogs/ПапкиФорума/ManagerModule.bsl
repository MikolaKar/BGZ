#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает структуру полей папки форума
//
// Возвращаемое значение:
//   Структура
//     Наименование
//     Родитель
//     Описание
//     Ответственный
//     ДатаСоздания
//
Функция ПолучитьСтруктуруПапки() Экспорт
	
	СтруктураПапкиФорума = Новый Структура;
	СтруктураПапкиФорума.Вставить("Наименование");
	СтруктураПапкиФорума.Вставить("Родитель");
	СтруктураПапкиФорума.Вставить("Описание");
	СтруктураПапкиФорума.Вставить("Ответственный");
	СтруктураПапкиФорума.Вставить("ДатаСоздания");
	
	Возврат СтруктураПапкиФорума;
	
КонецФункции

// Создает и записывает в БД папку форума
//
// Параметры:
//   СтруктураПапкиФорума - Структура - структура полей папки форума.
//
// Возвращаемое значение:
//   СправочникСсылка.ПапкиФорума
//
Функция СоздатьПапку(СтруктураПапкиФорума) Экспорт
	
	НоваяПапкаФорума = СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяПапкаФорума, СтруктураПапкиФорума);
	НоваяПапкаФорума.Записать();
	
	Возврат НоваяПапкаФорума.Ссылка;
	
КонецФункции

#Область ПраваДоступа

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

// Заполняет переданный дескриптор доступа
Процедура ЗаполнитьДескрипторДоступа(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДокументооборотПраваДоступа.ЗаполнитьНастройкиДескриптора(ДескрипторДоступа, ОбъектДоступа);
	
КонецПроцедуры

Функция ЕстьМетодЗаполнитьПраваДоступа() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Заполняет права по переданному дескриптору доступа 
Процедура ЗаполнитьПраваДоступа(ДескрипторДоступа, ПраваДоступа, ПротоколРасчетаПрав) Экспорт
	
	// Заполнение правил обработки настроек папки
	ПравилаОбработкиНастроек = Новый Соответствие;
	
	Настройка = "ЧтениеПапокИТем";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Чтение");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	Настройка = "ИзменениеПапок";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Добавление");
	РазрешенныеДействия.Добавить("Изменение");
	РазрешенныеДействия.Добавить("Удаление");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	Настройка = "УправлениеПравами";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("УправлениеПравами");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	// Получение прав по настройкам
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПраваПоНастройкамДескриптора(
		ДескрипторДоступа, ПраваДоступа, ПравилаОбработкиНастроек);
	
	// Добавление папки в протокол прав
	Если ПротоколРасчетаПрав <> Неопределено Тогда
		ПротоколРасчетаПрав.Добавить(НСтр("ru = 'Права на папку (см. Настройка прав папки)'"));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет массив разрешенных прав на объекты, которые 
// хранятся в указанной папке.
Процедура ПолучитьПраваОтПапки(Папка, РазрешенныеПрава) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Поиск дескриптора папки
	ДескрипторДоступаПапки = РегистрыСведений.ДескрипторыДоступаДляОбъектов.НайтиДескрипторДляОбъекта(Папка);
	
	Если ДескрипторДоступаПапки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьРазрешенныеПраваОтВладельцаПоДескриптору(ДескрипторДоступаПапки, РазрешенныеПрава);
	
КонецПроцедуры

// Заполняет массив разрешенных прав на объекты, которые 
// хранятся папке с указанным дескриптором
Процедура ПолучитьРазрешенныеПраваОтВладельцаПоДескриптору(ДескрипторДоступаПапки, РазрешенныеПрава) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ДескрипторДоступаПапки) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение правил обработки настроек папки
	ПравилаОбработкиНастроек = Новый Соответствие;
	
	Настройка = "ЧтениеПапокИТем";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Чтение");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	Настройка = "ДобавлениеТемИСообщений";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Добавление");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	Настройка = "ИзменениеТемИСообщений";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Изменение");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	Настройка = "ПометкаУдаленияТемИСообщений";
	РазрешенныеДействия = Новый Массив;
	РазрешенныеДействия.Добавить("Удаление");
	ПравилаОбработкиНастроек.Вставить(Настройка, РазрешенныеДействия);
	
	// Получение прав по настройкам
	Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПраваПоНастройкамДескриптора(
		ДескрипторДоступаПапки,
		РазрешенныеПрава,
		ПравилаОбработкиНастроек);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
