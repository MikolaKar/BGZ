#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ЕстьТолькоПравоЧтенияИнструкции = РаботаСИнструкциями.ПравоТолькоЧтения();
	
	Если Не ЕстьТолькоПравоЧтенияИнструкции Тогда
		Если ВидФормы = "ФормаОбъекта" Тогда
			ВыбраннаяФорма = "ФормаЭлемента";
			СтандартнаяОбработка = Ложь;
		ИначеЕсли ВидФормы = "ФормаСписка" Тогда
			ВыбраннаяФорма = "ФормаДерева";
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеДоступом

Функция ПолучитьПоляДоступа() Экспорт
	
	Возврат "Ссылка";
	
КонецФункции

Процедура ЗаполнитьДескрипторДоступа(ОбъектДоступа, ДескрипторДоступа) Экспорт
	
	ДоступНаЧтение = ОбъектДоступа.Ссылка.ДоступНаЧтение;
	Если ДоступНаЧтение.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из ДоступНаЧтение Цикл
			Строка = ДескрипторДоступа.Пользователи.Добавить();
			Строка.Пользователь = ТекущаяСтрока.Пользователь;
			Строка.ОсновнойОбъектАдресации = ТекущаяСтрока.ОсновнойОбъектАдресации;
			Строка.ДополнительныйОбъектАдресации = ТекущаяСтрока.ДополнительныйОбъектАдресации;
		КонецЦикла;	
	Иначе
		Строка = ДескрипторДоступа.Пользователи.Добавить();
		Строка.Пользователь = Справочники.ГруппыПользователей.ВсеПользователи;
	КонецЕсли;
	
КонецПроцедуры	

Функция ЕстьМетодЗаполнитьПраваДоступа() Экспорт
	
	Возврат Истина;
	
КонецФункции	

Процедура ЗаполнитьПраваДоступа(ДескрипторДоступа, ПраваДоступа, ПротоколРасчетаПрав) Экспорт

	Для Каждого Эл Из ДескрипторДоступа.Пользователи Цикл
		
		ПраваПользователя = Новый Структура("Чтение, Добавление, Изменение, Удаление, УправлениеПравами",
										Истина, Ложь, Ложь, Ложь, Ложь);
		
		ДокументооборотПраваДоступа.ДобавитьПользователяВСоответствиеПрав(
			ПраваДоступа,
			Эл.Пользователь,
			Эл.ОсновнойОбъектАдресации,
			Эл.ДополнительныйОбъектАдресации,
			ПраваПользователя);
		
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти

#КонецОбласти
