
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ПараметрКоманды", ПараметрКоманды);
	ПараметрыОповещения.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	
	РежимОткрытияФормы = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКомандыПродолжение",
		ЭтотОбъект,
		ПараметрыОповещения);

	ДанныеПроектнойЗадачи = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(ПараметрКоманды);
	ОкончаниеФакт = ДанныеПроектнойЗадачи.ОкончаниеФакт;
	Если ОкончаниеФакт > Дата(1, 1, 1) Тогда
		
		ТекстВопроса = НСтр("ru = 'Проектная задача завершена. При старте процесса задача вновь перейдет в стадию выполнения. Стартовать процесс?'");
		Режим = Новый СписокЗначений;
		Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Стартовать'"));
		Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не стартовать'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Режим, , КодВозвратаДиалога.Нет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);  
	КонецЕсли;
		
КонецПроцедуры
	
&НаКлиенте
Процедура ОбработкаКомандыПродолжение(Результат, Параметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
	    Возврат;
	КонецЕсли;

	ПараметрыВыполненияКоманды = Параметры.ПараметрыВыполненияКоманды;
	ПараметрыФормы = Новый Структура("ПроектнаяЗадача", Параметры.ПараметрКоманды);
	ОткрытьФорму("ОбщаяФорма.СозданиеБизнесПроцесса",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);

КонецПроцедуры



