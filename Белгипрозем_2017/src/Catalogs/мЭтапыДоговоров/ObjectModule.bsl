
Процедура ПриЗаписи(Отказ)
    Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
        Возврат;
	КонецЕсли;
	
	мРаботаСДоговорами.ЗаписатьСметнуюСтоимостьЭтапаДоговора(ЭтотОбъект.Ссылка, Отказ);
    
    мРаботаСДоговорами.ЗаписатьУсловияДоговораДляЭтапа(ЭтотОбъект.Ссылка, Отказ, ЭтотОбъект.ДополнительныеСвойства);
	
	ЗаписьВЭтап = Ложь;
	мРаботаСДоговорами.УстановитьСостояниеЭтапаДоговора(ЭтотОбъект.Ссылка, ЭтотОбъект.Состояние, ЗаписьВЭтап);
	
	//Если есть Дело по этому этапу, то надо зафиксировать план расхода материалов
	мРаботаСоСметами.ОпределитьПланРасходМатериалов(ЭтотОбъект.Ссылка);
    
	// ПлановыйСрок
	ПлановыйСрок = Неопределено;
	ЭтотОбъект.ДополнительныеСвойства.Свойство("ПлановыйСрокДляЗаписи", ПлановыйСрок);
	Если ПлановыйСрок <> Неопределено Тогда
	    мРаботаСДоговорами.ЗаписатьПлановыйСрок(ЭтотОбъект.Ссылка, ПлановыйСрок, ТекущаяДата(), ЭтотОбъект.Ссылка);
	КонецЕсли; 
    
    мРаботаСДоговорами.СинхронизироватьИсточникФинансированияЭтапаДоговора(ЭтотОбъект.Ссылка);
    
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтрихкоды")
		И ДополнительныеСвойства.Свойство("ЭтоНовый") 
		И ДополнительныеСвойства.ЭтоНовый Тогда
		
		Штрихкод = ШтрихкодированиеСервер.СформироватьШтрихКод();
		ШтрихкодированиеСервер.ПрисвоитьШтрихКод(Ссылка, Штрихкод);
		
	КонецЕсли;
	
	// Основной этап договора
	ОсновнойЭтапДоговора = Неопределено;
	ЭтотОбъект.ДополнительныеСвойства.Свойство("ОсновнойЭтапДоговора", ОсновнойЭтапДоговора);
	Если ОсновнойЭтапДоговора <> Неопределено Тогда
	    мРаботаСДоговорами.ЗаписатьСвязьЭтаповДоговора(ЭтотОбъект.Ссылка, ОсновнойЭтапДоговора);
	КонецЕсли; 
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
    Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;
    
    БылаЗаписьНовогоУчетДоговоров = Ложь;
	// Обработка изменения ПометкаУдаления
    Если ЭтотОбъект.Ссылка.ПометкаУдаления <> ЭтотОбъект.ПометкаУдаления Тогда
        // смена пометки удаления
        Если ЭтотОбъект.ПометкаУдаления Тогда
            ЭтотОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаления", Истина);
			
			// Пометка на удаление всех УчетДоговоров
			Параметры = Новый Структура("НеИзменятьЭтап", Истина); 
			мРаботаСДоговорами.ПометитьНаУдалениеУчетДоговоровПоЭтапу(ЭтотОбъект.Ссылка, Отказ, Параметры);
        Иначе	
            ЭтотОбъект.ДополнительныеСвойства.Вставить("ПометкаУдаления", Ложь);
			
			// Запись нового УчетДоговоров
			Параметры = мРаботаСДоговорами.ПодготовитьПараметрыУчетаДоговоров();
			мРаботаСДоговорами.ЗаполнитьПараметрыУчетаДоговоровИзЭтапа(Параметры, ЭтотОбъект);
			Параметры.Вставить("НеИзменятьЭтап", Истина);
			мРаботаСДоговорами.ЗаписатьНовыйУчетДоговоров(ЭтотОбъект.Ссылка, ЭтотОбъект.ДатаПоследнейЗаписи, Отказ, Параметры);
		КонецЕсли; 
		БылаЗаписьНовогоУчетДоговоров = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ЭтотОбъект.Стоимость = 0;
	ЭтотОбъект.ДатаПоследнейЗаписи = '00010101000000';
	ЭтотОбъект.СуммаНДС = 0;
	ЭтотОбъект.СтоимостьСНДС = 0;
	ЭтотОбъект.Цена = 0;
	ЭтотОбъект.КоличествоУчастков = 0;
	ЭтотОбъект.Смета = Справочники.мСметы.ПустаяСсылка();
	ЭтотОбъект.Сметы.Очистить();
	ЭтотОбъект.Номенклатура.Очистить();
	//ЭтотОбъект.Геопортал.Очистить();
КонецПроцедуры



