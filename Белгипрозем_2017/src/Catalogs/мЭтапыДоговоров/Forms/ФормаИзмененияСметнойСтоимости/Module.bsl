
&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	СтрРекв = ПолучитьСтруктуруИзменяемыхРеквизитов();
	Для каждого Эл Из СтрРекв Цикл
		СтрРекв.Вставить(Эл.Ключ, ЭтаФорма[Эл.Ключ]);
	КонецЦикла;
	Закрыть(СтрРекв);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть("");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруИзменяемыхРеквизитов()
	Возврат Новый Структура("ДатаПоследнейЗаписи, Цена, КоличествоУчастков, Стоимость, СтавкаНДС, ОсвобождениеОтНДС, СуммаНДС, СтоимостьСНДС, Комментарий"); 
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтрРекв = Новый Структура("ДатаПоследнейЗаписи, Цена, КоличествоУчастков, Стоимость, СтавкаНДС, ОсвобождениеОтНДС, СуммаНДС, СтоимостьСНДС, Комментарий"); 
	Для каждого Эл Из СтрРекв Цикл
		ЭтаФорма[Эл.Ключ] = Параметры[Эл.Ключ];
	КонецЦикла;
	Параметры.Свойство("ЭтоФизЛицо", ЭтоФизЛицо);
	Параметры.Свойство("ДатаПоследнейЗаписи", ПриОткрытииДатаПоследнейЗаписи);
	
	Элементы.Цена.Видимость = ЭтоФизЛицо;
	Элементы.КоличествоУчастков.Видимость = ЭтоФизЛицо;
	
	Если ЗначениеЗаполнено(СтавкаНДС) Тогда
		РеквСтавкиНДС = мРаботаСоСметами.ПолучитьРеквСтавкиНДС(СтавкаНДС);
		ЗначениеСтавкиНДС = РеквСтавкиНДС.Ставка;
		БезНДС = РеквСтавкиНДС.БезНДС;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	РассчитатьСтоимость();
КонецПроцедуры

&НаКлиенте
Процедура КоличествоУчастковПриИзменении(Элемент)
	РассчитатьСтоимость();
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
    РеквСтавкиНДС = мРаботаСоСметами.ПолучитьРеквСтавкиНДС(СтавкаНДС);
    ЗначениеСтавкиНДС = РеквСтавкиНДС.Ставка;
    БезНДС = РеквСтавкиНДС.БезНДС;
    
	УстановитьВидимостьОсвобождениеОтНДС();
    
	РассчитатьНДС();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьОсвобождениеОтНДС()
	Элементы.ОсвобождениеОтНДС.Видимость = БезНДС;
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	РассчитатьНДС();
КонецПроцедуры

&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	РассчитатьСтоимостьСНДС();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтоимость()
	КоличествоУчастков = ?(КоличествоУчастков = 0, 1, КоличествоУчастков);
	Стоимость = Цена * КоличествоУчастков;
	
	РассчитатьНДС();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНДС()
    СуммаНДС = Окр(Стоимость * ЗначениеСтавкиНДС / 100, 2);
	РассчитатьСтоимостьСНДС();
КонецПроцедуры // РассчитатьНДС()

&НаКлиенте
Процедура РассчитатьСтоимостьСНДС()
    СтоимостьСНДС = Стоимость + СуммаНДС;
КонецПроцедуры // РассчитатьСтоимостьСНДС()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьОсвобождениеОтНДС();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПоследнейЗаписиПриИзменении(Элемент)
	Если ДатаПоследнейЗаписи < ПриОткрытииДатаПоследнейЗаписи Тогда
		Оп = Новый ОписаниеОповещения("ЗавершениеВопроса", ЭтаФорма);
		ПоказатьВопрос(Оп, "Все записи сметной стоимости после "+Формат(ДатаПоследнейЗаписи, "ДФ=dd.MM.yyyy")+" будут удалены!
		| Продолжать?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопроса(Результат, ПараметрыВопроса) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ДатаПоследнейЗаписи = ПриОткрытииДатаПоследнейЗаписи;
	КонецЕсли; 	
КонецПроцедуры
 

