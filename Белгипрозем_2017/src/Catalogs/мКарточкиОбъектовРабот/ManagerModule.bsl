////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
    // Печать Карточки объекта
    КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Справочник.мКарточкиОбъектовРабот";
	КомандаПечати.Идентификатор = "КарточкаПИР_Площадно_Линейный";
	КомандаПечати.Представление = НСтр("ru = 'Карточка объекта работ'");
    КомандаПечати.Картинка = БиблиотекаКартинок.Печать;
	//КомандаПечати.Обработчик = "Справочник.мКарточкиОбъектовРабот";
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
    НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КарточкаПИР_Площадно_Линейный");
    Если НужноПечататьМакет Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
        КоллекцияПечатныхФорм,
        "КарточкаПИР_Площадно_Линейный",
        НСтр("ru = 'Печать карточки'"),
        ПечатьКарточкиОбъекта(МассивОбъектов, ОбъектыПечати),
        ,
        "Справочник.мКарточкиОбъектовРабот.КарточкаПИР_Площадно_Линейный");
    КонецЕсли;
    
КонецПроцедуры

Функция ПечатьКарточкиОбъекта(МассивОбъектов, ОбъектыПечати) Экспорт
    // Создаем табличный документ и устанавливаем имя параметров печати.
    ТабличныйДокумент = Новый ТабличныйДокумент;
    ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_КарточкаОбъектаРабот1";
    ТабличныйДокумент.АвтоМасштаб = Истина;
    // Получаем запросом необходимые данные.
    Запрос = Новый Запрос();
    Запрос.Текст = "ВЫБРАТЬ
                   |    мКарточкиОбъектовРабот.Ссылка,
                   |    мКарточкиОбъектовРабот.ВидРабот,
                   |    мКарточкиОбъектовРабот.ДатаСоставления,
                   |    мКарточкиОбъектовРабот.Составил.ПредставлениеВДокументах КАК Сотрудник,
                   |    мКарточкиОбъектовРабот.ЦельПредоставления КАК Цель,
                   |    мКарточкиОбъектовРабот.ИсточникФинансирования,
                   |    мКарточкиОбъектовРабот.КонтактноеЛицо,
                   |    мКарточкиОбъектовРабот.РасчетныйСчет,
                   |    мКарточкиОбъектовРабот.БезНДС КАК СБезНДС,
                   |    мКарточкиОбъектовРабот.ПараметрыОбъекта.(
                   |        ПараметрОбъекта,
                   |        Значение,
                   |        ПараметрОбъекта.ИмяПредопределенныхДанных КАК ИмяПараметра
                   |    ),
                   |    мКарточкиОбъектовРабот.Владелец.Владелец.Ссылка КАК Заказчик,
                   |    мКарточкиОбъектовРабот.ВидКарточки,
                   |    мКарточкиОбъектовРабот.Владелец.ПолноеНаименование КАК НаименованиеОбъекта,
                   |    мКарточкиОбъектовРабот.Владелец.ВидОбъекта КАК ВидОбъекта,
                   |    мКарточкиОбъектовРабот.Владелец.Ссылка КАК ОбъектРабот
                   |ИЗ
                   |    Справочник.мКарточкиОбъектовРабот КАК мКарточкиОбъектовРабот
                   |ГДЕ
                   |    мКарточкиОбъектовРабот.Ссылка В(&МассивОбъектов)";
    
    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
    
    Шапка = Запрос.Выполнить().Выбрать();
    ПервыйДокумент = Истина;
    Пока Шапка.Следующий() Цикл
        Если Не ПервыйДокумент Тогда
            // Все документы нужно выводить на разных страницах.
            ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
        КонецЕсли;
        ПервыйДокумент = Ложь;
        // Запомним номер строки, с которой начали выводить текущий документ.
        НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати покомплектно 
        //УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
        //НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
        
        ИмяМакета = РаботаСКарточкойОбъекта.ПолучитьИмяМакета(Шапка.ВидКарточки);
        Макет = УправлениеПечатью.ПолучитьМакет("Справочник.мКарточкиОбъектовРабот." + ИмяМакета);
        
        // Заголовок карточки
        ОбластьПараметры = Макет.ПолучитьОбласть("Заголовок");  
        
        КонтактноеЛицо = Шапка.КонтактноеЛицо;
        Должность_ФИО = "";
        Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
            Должность_ФИО = ""+КонтактноеЛицо.Должность+" "+КонтактноеЛицо.Наименование;
        КонецЕсли;
        
        ЗаполнитьЗначенияСвойств(ОбластьПараметры.Параметры, Шапка);
        
        ОбластьПараметры.Параметры.Должность_ФИО = Должность_ФИО;
        ОбластьПараметры.Параметры.Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Шапка.Заказчик, 
            Справочники.ВидыКонтактнойИнформации.ЮридическийАдресКорреспондента);
        ОбластьПараметры.Параметры.Месторасположение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
        Шапка.ОбъектРабот, Справочники.ВидыКонтактнойИнформации.АдресЗемельногоУчасткаОбъектаРабот);
        
        ТабличныйДокумент.Вывести(ОбластьПараметры);
        
        // Параметры объекта
        ОбластьПараметры = Макет.ПолучитьОбласть("Параметры");
        ПараметрыОбъекта = Шапка.ПараметрыОбъекта.Выбрать();
        СтруктураПараметровОбъекта = Новый Структура;
        Пока ПараметрыОбъекта.Следующий() Цикл
            Если ЗначениеЗаполнено(ПараметрыОбъекта.ИмяПараметра) Тогда
                СтруктураПараметровОбъекта.Вставить(ПараметрыОбъекта.ИмяПараметра, ПараметрыОбъекта.Значение);
            КонецЕсли; 
        КонецЦикла; 
        СтруктураПараметровОбъекта.Вставить("ИсточникФинансирования", Шапка.ИсточникФинансирования);
        
        ЗаполнитьЗначенияСвойств(ОбластьПараметры.Параметры, СтруктураПараметровОбъекта);
        ТабличныйДокумент.Вывести(ОбластьПараметры);
        
        // Подвал карточки
        ОбластьПараметры = Макет.ПолучитьОбласть("Подвал");
        ЗаполнитьЗначенияСвойств(ОбластьПараметры.Параметры, Шапка);
        ТабличныйДокумент.Вывести(ОбластьПараметры);
        
    КонецЦикла;
    Возврат ТабличныйДокумент;
КонецФункции

