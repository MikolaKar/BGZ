
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
	ПоПользователю = Пользователи.ТекущийПользователь();
	
	Если НЕ ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
		
		Элементы.ПоПользователю.Видимость = Ложь;
		Элементы.Пользователь.Видимость = Ложь;
		
	КонецЕсли;
	
	ОтобразитьУведомленияПоПользователю();
	
	НастройкиФормы = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы + "/ТекущиеДанные");
	Если ЗначениеЗаполнено(НастройкиФормы) Тогда
		ОтображатьУдаленныеУведомления = НастройкиФормы.Получить("ОтображатьУдаленныеУведомления");
	КонецЕсли;
	
	ОтобразитьУдаленныеУведомления();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьУдаленныеУведомленияПриИзменении(Элемент)
	
	ОтобразитьУдаленныеУведомления();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.Объект Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Элементы.Список.ТекущиеДанные.Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоПользователюПриИзменении(Элемент)
	
	ОтобразитьУведомленияПоПользователю();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет параметр ОтображатьУдаленные для реквизита Список (динамический список).
// Параметр заполняет в зависимости от значения реквизита ОтображатьУдаленныеУведомления.
//
&НаСервере
Процедура ОтобразитьУдаленныеУведомления()
	
	Если ОтображатьУдаленныеУведомления Тогда
		ПараметрОтображатьУдаленные = 
			Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтображатьУдаленные"));
		ПараметрОтображатьУдаленные.Использование = Ложь;
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ОтображатьУдаленные", ОтображатьУдаленныеУведомления);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметр Пользователь для реквизита Список (динамический список).
// Параметр заполняет в зависимости от значения реквизита ПоПользователю.
//
&НаСервере
Процедура ОтобразитьУведомленияПоПользователю()
	
	Если ЗначениеЗаполнено(ПоПользователю) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ПоПользователю);
	Иначе
		ПараметрПользователь = 
			Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Пользователь"));
		ПараметрПользователь.Использование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
