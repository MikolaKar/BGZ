
#Область ПрограммныйИнтерфейс
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		
		Автор = ПользователиКлиентСервер.ТекущийПользователь();
		
		ПоставленНаКонтроль = Истина;
		ДатаПостановкиНаКонтроль = ТекущаяДатаСеанса();
		
		Если Не ЗначениеЗаполнено(Контролер) Тогда 
			Контролер = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Файлы") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = ДанныеЗаполнения.Автор;
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, "Подготовил, Ответственный, СрокИсполнения, ДатаОкончанияДействия");
			Если ЗначениеЗаполнено(ДанныеПредмета.Ответственный) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Ответственный;
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.Подготовил) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Подготовил;
			КонецЕсли;	
			Если ЗначениеЗаполнено(ДанныеПредмета.СрокИсполнения) Тогда 
				СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.ДатаОкончанияДействия) Тогда 
				СрокИсполнения = ДанныеПредмета.ДатаОкончанияДействия;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, "Подготовил, Ответственный, СрокИсполнения");
			Если ЗначениеЗаполнено(ДанныеПредмета.Ответственный) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Ответственный;
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.Подготовил) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Подготовил;
			КонецЕсли;	
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, "Адресат, Ответственный, СрокИсполнения");
			Если ЗначениеЗаполнено(ДанныеПредмета.Ответственный) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Ответственный;
			ИначеЕсли ЗначениеЗаполнено(ДанныеПредмета.Адресат) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Адресат;
			КонецЕсли;	
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
				
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Исполнение") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения,
				|Проверяющий, ОсновнойОбъектАдресацииПроверяющего, ДополнительныйОбъектАдресацииПроверяющего,
				|Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
			Для Каждого Исполнитель Из ДанныеЗаполнения.Исполнители Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = Исполнитель.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = Исполнитель.ДополнительныйОбъектАдресации;
				НовыйИсполнитель.Ответственный = Исполнитель.Ответственный;
			КонецЦикла;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Проверяющий) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Проверяющий;
				НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресацииПроверяющего;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресацииПроверяющего;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Ознакомление") Тогда 
			
			Предмет = ДанныеЗаполнения;
			
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
			Для Каждого Исполнитель Из ДанныеЗаполнения.Исполнители Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = Исполнитель.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = Исполнитель.ДополнительныйОбъектАдресации;
			КонецЦикла;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Утверждение")
			Или  ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Рассмотрение")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Регистрация") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения, Автор,
				|Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;

			Если ЗначениеЗаполнено(ДанныеПредмета.Исполнитель) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресации;
				
				Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда 
					НовыйИсполнитель.Ответственный = Истина;
				КонецЕсли;	
			КонецЕсли;	

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Поручение") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения,
				|Исполнитель, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации,
				|Проверяющий, ОсновнойОбъектАдресацииПроверяющего, ДополнительныйОбъектАдресацииПроверяющего,
				|Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Исполнитель) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресации;
				
				Если ЗначениеЗаполнено(ДанныеПредмета.Проверяющий) Тогда
					НовыйИсполнитель.Ответственный = Истина;
				КонецЕсли;	
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Проверяющий) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Проверяющий;
				НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресацииПроверяющего;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресацииПроверяющего;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Приглашение") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения, Автор");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = Исполнитель.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = Исполнитель.ДополнительныйОбъектАдресации;
			КонецЦикла;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.Согласование") Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполненияДатой, Автор");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполненияДатой;
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = Исполнитель.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = Исполнитель.ДополнительныйОбъектАдресации;
			КонецЦикла;

			Если ЗначениеЗаполнено(ДанныеПредмета.Автор) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Автор;
			КонецЕсли;	
				
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЗадачаИсполнителя.Ссылка,
			|	ЗадачаИсполнителя.ТочкаМаршрута,
			|	ЗадачаИсполнителя.БизнесПроцесс,
			|	ЗадачаИсполнителя.Исполнитель,
			|	ЗадачаИсполнителя.РольИсполнителя,
			|	ЗадачаИсполнителя.ОсновнойОбъектАдресации,
			|	ЗадачаИсполнителя.ДополнительныйОбъектАдресации,
			|	ЗадачаИсполнителя.ДатаИсполнения,
			|	ЗадачаИсполнителя.Выполнена
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|ГДЕ
			|	ЗадачаИсполнителя.БизнесПроцесс.ВедущаяЗадача.БизнесПроцесс = &БизнесПроцесс
			|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЗадачаИсполнителя.Дата";
			
			Запрос.УстановитьПараметр("БизнесПроцесс", Предмет);
			ЗадачиПроцесса = Запрос.Выполнить().Выгрузить();
			
			Для Каждого СтрокаЗадача Из ЗадачиПроцесса Цикл
				БизнесПроцесс = СтрокаЗадача.БизнесПроцесс;
				
				Если СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Согласование.ТочкиМаршрута.Согласовать Тогда 
					
					Отбор = Новый Структура("НомерИтерации, ЗадачаИсполнителя", БизнесПроцесс.НомерИтерации, СтрокаЗадача.Ссылка);
					Если БизнесПроцесс.РезультатыСогласования.НайтиСтроки(Отбор).Количество() = 0 Тогда 
						Продолжить;
					КонецЕсли;
					
				ИначеЕсли СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.Исполнить
					  Или СтрокаЗадача.ТочкаМаршрута = БизнесПроцессы.Исполнение.ТочкиМаршрута.ОтветственноеИсполнение Тогда 
					
					Отбор = Новый Структура("НомерИтерации, ЗадачаИсполнителя", БизнесПроцесс.НомерИтерации, СтрокаЗадача.Ссылка);
					Если БизнесПроцесс.РезультатыИсполнения.НайтиСтроки(Отбор).Количество() = 0 Тогда 
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;	
				
				НоваяСтрока = Исполнители.Добавить();
				НоваяСтрока.Источник = СтрокаЗадача.Ссылка;
				НоваяСтрока.Исполнено = СтрокаЗадача.Выполнена;
				НоваяСтрока.ДатаИсполнения = СтрокаЗадача.ДатаИсполнения;
				Если ЗначениеЗаполнено(СтрокаЗадача.Исполнитель) Тогда 
					НоваяСтрока.Исполнитель = СтрокаЗадача.Исполнитель;
				Иначе
					НоваяСтрока.Исполнитель = СтрокаЗадача.РольИсполнителя;
					НоваяСтрока.ОсновнойОбъектАдресации = СтрокаЗадача.ОсновнойОбъектАдресации;
					НоваяСтрока.ДополнительныйОбъектАдресации = СтрокаЗадача.ДополнительныйОбъектАдресации;
				КонецЕсли;	
			КонецЦикла;		
				
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
			
			Предмет = ДанныеЗаполнения;
			РеквизитыПисьма = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,
			 	"КорневоеПисьмо.Тема, КорневоеПисьмо.Ссылка");
			 
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(РеквизитыПисьма.КорневоеПисьмоТема));
				
			КорневоеПисьмо = РеквизитыПисьма.КорневоеПисьмоСсылка;
			Если ТипЗнч(КорневоеПисьмо) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда
				Для Каждого Строка Из КорневоеПисьмо.ПолучателиПисьма Цикл 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = Строка.Адресат;
					НовыйИсполнитель.Источник = КорневоеПисьмо;
				КонецЦикла;
			Иначе 
				КорневоеПисьмоОтправительАдресат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения,
					"КорневоеПисьмо.ОтправительАдресат");
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = КорневоеПисьмоОтправительАдресат;
				НовыйИсполнитель.Источник = КорневоеПисьмо;
			КонецЕсли; 
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВходящееПисьмо") Тогда 
			
			ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(ДанныеЗаполнения);
			Предмет = ВеткаПереписки;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения.Тема));
				
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = ДанныеЗаполнения.ОтправительАдресат;	
			НовыйИсполнитель.Источник = ДанныеЗаполнения.Ссылка;
				
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИсходящееПисьмо") Тогда 	
			
			ВеткаПереписки = РегистрыСведений.ПисьмаВеток.ПолучитьВетку(ДанныеЗаполнения);
			Предмет = ВеткаПереписки;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения.Тема));
				
			Для Каждого Строка Из ДанныеЗаполнения.ПолучателиПисьма Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Строка.Адресат;
				НовыйИсполнитель.Источник = ДанныеЗаполнения.Ссылка;
			КонецЦикла;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПроектныеЗадачи") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = РаботаСПроектами.ПолучитьДанныеПроектнойЗадачи(ДанныеЗаполнения);
			СрокИсполнения = ДанныеПредмета.ТекущийПланОкончание;
			
			ИсполнителиПредмета = ДанныеЗаполнения.Исполнители;
			Для Каждого Исполнитель Из ИсполнителиПредмета Цикл 
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = Исполнитель.Исполнитель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = Исполнитель.ОсновнойОбъектАдресации;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = Исполнитель.ДополнительныйОбъектАдресации;
			КонецЦикла;

		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Проекты") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"ТекущийПланОкончание, Руководитель");

			СрокИсполнения = ДанныеПредмета.ТекущийПланОкончание;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Руководитель) Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Руководитель;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Мероприятия") Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
				
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"ДатаОкончания, Организатор,
				|Председатель, ОсновнойОбъектАдресацииПредседателя, ДополнительныйОбъектАдресацииПредседателя,
				|Секретарь, ОсновнойОбъектАдресацииСекретаря, ДополнительныйОбъектАдресацииСекретаря");

			СрокИсполнения = ДанныеПредмета.ДатаОкончания;
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Председатель)
				И (ТипЗнч(ДанныеПредмета.Председатель) = Тип("СправочникСсылка.Пользователи")
				Или ТипЗнч(ДанныеПредмета.Председатель) = Тип("СправочникСсылка.РолиИсполнителей"))Тогда
				НовыйИсполнитель = Исполнители.Добавить();
				НовыйИсполнитель.Исполнитель = ДанныеПредмета.Председатель;
				НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресацииПредседателя;
				НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресацииПредседателя;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Секретарь)
				И (ТипЗнч(ДанныеПредмета.Секретарь) = Тип("СправочникСсылка.Пользователи")
				Или ТипЗнч(ДанныеПредмета.Секретарь) = Тип("СправочникСсылка.РолиИсполнителей"))Тогда
				НайденныеИсполнители = Исполнители.НайтиСтроки(
					Новый Структура("Исполнитель", ДанныеПредмета.Секретарь));
				
				Если НайденныеИсполнители.Количество() = 0 Тогда 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = ДанныеПредмета.Секретарь;
					НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресацииСекретаря;
					НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресацииСекретаря;
				КонецЕсли;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ДанныеПредмета.Организатор) Тогда
				НайденныеИсполнители = Исполнители.НайтиСтроки(
					Новый Структура("Исполнитель", ДанныеПредмета.Организатор));
				
				Если НайденныеИсполнители.Количество() = 0 Тогда 
					НовыйИсполнитель = Исполнители.Добавить();
					НовыйИсполнитель.Исполнитель = ДанныеПредмета.Организатор;
				КонецЕсли;
			КонецЕсли;	
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") И ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
			
			Предмет = ДанныеЗаполнения;
			ДанныеПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДанныеЗаполнения, 
				"Наименование, Описание, СрокИсполнения,
				|Исполнитель, РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
			
			Описание = СформироватьОписание(ДанныеЗаполнения, ДанныеПредмета.Наименование, ДанныеПредмета.Описание);
			СрокИсполнения = ДанныеПредмета.СрокИсполнения;
			
			НовыйИсполнитель = Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = ?(ЗначениеЗаполнено(ДанныеПредмета.Исполнитель), ДанныеПредмета.Исполнитель, ДанныеПредмета.РольИсполнителя);
			НовыйИсполнитель.ОсновнойОбъектАдресации = ДанныеПредмета.ОсновнойОбъектАдресации;
			НовыйИсполнитель.ДополнительныйОбъектАдресации = ДанныеПредмета.ДополнительныйОбъектАдресации;
			НовыйИсполнитель.Источник = Предмет;
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") И ЗначениеЗаполнено(ДанныеЗаполнения) Тогда 
			
			Предмет = ДанныеЗаполнения;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать ""%1""'"),  
				Строка(ДанныеЗаполнения));
			
		КонецЕсли;
		
		Если Исполнители.Количество() > 1 И ЗначениеЗаполнено(Контролер) Тогда 
			КоличествоИсполнителей = Исполнители.Количество();
			Для Инд = 1 По КоличествоИсполнителей Цикл
				Строка = Исполнители[КоличествоИсполнителей - Инд];
				Если Строка.Исполнитель = Контролер
					И Строка.ОсновнойОбъектАдресации = ОсновнойОбъектАдресацииКонтролера
					И Строка.ДополнительныйОбъектАдресации = ДополнительныйОбъектАдресацииКонтролера Тогда 
					Исполнители.Удалить(Строка);
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;	
		
		Если Исполнители.Количество() > 1 Тогда 
			Если Не ЗначениеЗаполнено(Предмет) Или Не ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
				Если Исполнители.Найти(Истина, "Ответственный") = Неопределено Тогда 
					Исполнители[0].Ответственный = Истина;
				КонецЕсли;	
			КонецЕсли;
		Иначе
			Если Исполнители.Количество() = 1 И Исполнители[0].Ответственный Тогда
				Исполнители[0].Ответственный = Ложь;
			КонецЕсли;	
		КонецЕсли;	
		
		УстановитьПривилегированныйРежим(Истина);
		
		// постановка на контроль уже стартованного процесса
		Если ЗначениеЗаполнено(Предмет)
			И ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные())
			И Предмет.Стартован Тогда 
			
			Если ТипЗнч(Предмет) <> Тип("БизнесПроцессСсылка.КомплексныйПроцесс")
				И ТипЗнч(Предмет) <> Тип("БизнесПроцессСсылка.ОбработкаВходящегоДокумента")
				И ТипЗнч(Предмет) <> Тип("БизнесПроцессСсылка.ОбработкаВнутреннегоДокумента")
				И ТипЗнч(Предмет) <> Тип("БизнесПроцессСсылка.ОбработкаИсходящегоДокумента") Тогда 
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗадачаИсполнителя.Ссылка,
				|	ЗадачаИсполнителя.Исполнитель,
				|	ЗадачаИсполнителя.РольИсполнителя,
				|	ЗадачаИсполнителя.ОсновнойОбъектАдресации,
				|	ЗадачаИсполнителя.ДополнительныйОбъектАдресации,
				|	ЗадачаИсполнителя.Выполнена
				|ИЗ
				|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
				|ГДЕ
				|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
				|	И НЕ ЗадачаИсполнителя.ПометкаУдаления
				|
				|УПОРЯДОЧИТЬ ПО
				|	ЗадачаИсполнителя.Дата УБЫВ";
				
				Запрос.УстановитьПараметр("БизнесПроцесс", Предмет);
				ЗадачиПроцесса = Запрос.Выполнить().Выгрузить();
				
				КоличествоИсполнителей = Исполнители.Количество();
				Для Инд = 1 По КоличествоИсполнителей Цикл
					Строка = Исполнители[КоличествоИсполнителей - Инд];
					Если ЗначениеЗаполнено(Строка.Источник) Тогда 
						Продолжить;
					КонецЕсли;	
					
					НайденнаяСтрока = Неопределено;
					Для Каждого СтрокаЗадача Из ЗадачиПроцесса Цикл
						Если ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.РолиИсполнителей")
							И Строка.Исполнитель = СтрокаЗадача.РольИсполнителя
							И Строка.ОсновнойОбъектАдресации = СтрокаЗадача.ОсновнойОбъектАдресации
							И Строка.ДополнительныйОбъектАдресации = СтрокаЗадача.ДополнительныйОбъектАдресации Тогда 
							НайденнаяСтрока = СтрокаЗадача;
							Прервать;
						ИначеЕсли ТипЗнч(Строка.Исполнитель) = Тип("СправочникСсылка.Пользователи")	
							И Строка.Исполнитель = СтрокаЗадача.Исполнитель Тогда 
							НайденнаяСтрока = СтрокаЗадача;
							Прервать;
						КонецЕсли;	
					КонецЦикла;	
					
					Если НайденнаяСтрока <> Неопределено Тогда 
						Строка.Источник = НайденнаяСтрока.Ссылка;
						Строка.Исполнено = НайденнаяСтрока.Выполнена;
						ЗадачиПроцесса.Удалить(НайденнаяСтрока);
					КонецЕсли;	
				КонецЦикла;	
				
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Предмет) Тогда 
		
		// Проверка прав доступа контролера на предмет
		ВнешняяТранзакция = ТранзакцияАктивна();
		
		Если Не ВнешняяТранзакция Тогда
			НачатьТранзакцию();
		КонецЕсли;
		
		ПерезаписатьРабочуюГруппуПредмета(Предмет);
		
		// Проверка прав на предмет
		ПроверитьПраваКонтролераНаПредмет(Отказ);
		
		// Отмена транзакции, в рамках которой была создана рабочая группа для 
		// проверки прав
		Если Не ВнешняяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИсполнителиСтрокой = ПолучитьИсполнителейСтрокой();
	Наименование = СвернутьТекстВСтроку(Описание, 150);
	
	// проверка возможности пометки удаления 
	СтараяПометкаУдаления = Ложь;
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		СтараяПометкаУдаления = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("СтараяПометкаУдаления", СтараяПометкаУдаления);
	
	Если ПометкаУдаления И Не СтараяПометкаУдаления Тогда 
		Если Контроль.ЭтоПроцессСКонтролером(Предмет) Тогда 
			РеквизитыПредмета = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Предмет,
				"Стартован, ПометкаУдаления, Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера");
			Если Контролер = РеквизитыПредмета.Контролер
				И ОсновнойОбъектАдресацииКонтролера = РеквизитыПредмета.ОсновнойОбъектАдресацииКонтролера
				И ДополнительныйОбъектАдресацииКонтролера = РеквизитыПредмета.ДополнительныйОбъектАдресацииКонтролера 
				И РеквизитыПредмета.Стартован 
				И Не РеквизитыПредмета.ПометкаУдаления Тогда 
					ТекстСообщения = НСтр("ru = 'Нельзя пометить на удаление контрольную карточку, автоматически созданную процессом.'");
					ВызватьИсключение ТекстСообщения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// обновление рабочей группы предмета
	Если ЗначениеЗаполнено(Предмет) Тогда 
		ПерезаписатьРабочуюГруппуПредмета(Предмет);
	КонецЕсли;
	
	СтарыйКонтролер = Справочники.Пользователи.ПустаяСсылка();
	Если ЗначениеЗаполнено(Ссылка) Тогда 
		СтарыйКонтролер = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "Контролер");
	КонецЕсли;
	ДополнительныеСвойства.Вставить("СтарыйКонтролер", СтарыйКонтролер);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// установка контролера в процессе
	СтарыйКонтролер = ДополнительныеСвойства.СтарыйКонтролер;
	СтараяПометкаУдаления = ДополнительныеСвойства.СтараяПометкаУдаления;
	УстановкаПометки = Не СтараяПометкаУдаления И ПометкаУдаления;
	
	Если Не УстановкаПометки Тогда 
		Если Контроль.ЭтоПроцессСКонтролером(Предмет) Тогда 
			Если Не ЗначениеЗаполнено(Предмет.Контролер) Или 
				(СтарыйКонтролер = Предмет.Контролер 
				И СтарыйКонтролер <> Контролер) Тогда 
				ПраваПоОбъекту = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Предмет); 
				Если ПраваПоОбъекту.Изменение Тогда 
					ПредметОбъект = Предмет.ПолучитьОбъект();
					ПредметОбъект.Контролер = Контролер;
					ПредметОбъект.ОсновнойОбъектАдресацииКонтролера = ОсновнойОбъектАдресацииКонтролера;
					ПредметОбъект.ДополнительныйОбъектАдресацииКонтролера = ДополнительныйОбъектАдресацииКонтролера;
					ПредметОбъект.Записать();
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	// установка состояния контроля для списков
	УстановитьСостояниеКонтроляВКеше(Предмет);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Для Каждого Исполнитель Из Исполнители Цикл 
		Исполнитель.Исполнено = Ложь;
	КонецЦикла;	
	
	ПоставленНаКонтроль = Истина;
	ДатаПостановкиНаКонтроль = ТекущаяДатаСеанса();
	
	СнятСКонтроля = Ложь;
	ДатаСнятияСКонтроля = '00010101';
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьОписание(Предмет, Наименование, Описание)
	
	КонтрольноеСлово = "";
		
	Если ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Исполнение") Тогда 
		КонтрольноеСлово = НСтр("ru = 'Исполнить'");
		СловоЗамены = НСтр("ru = 'Контролировать исполнение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Ознакомление") Тогда
		КонтрольноеСлово = НСтр("ru = 'Ознакомиться'");
		СловоЗамены = НСтр("ru = 'Контролировать ознакомление'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Утверждение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Утвердить'");
		СловоЗамены = НСтр("ru = 'Контролировать утверждение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Рассмотрение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Рассмотреть'");
		СловоЗамены = НСтр("ru = 'Контролировать рассмотрение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Регистрация") Тогда
		КонтрольноеСлово = НСтр("ru = 'Зарегистрировать'");
		СловоЗамены = НСтр("ru = 'Контролировать регистрацию'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Поручение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Выполнить'");
		СловоЗамены = НСтр("ru = 'Контролировать выполнение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Приглашение") Тогда
		КонтрольноеСлово = НСтр("ru = 'Принять участие в'");
		СловоЗамены = НСтр("ru = 'Контролировать приглашение'");
		
	ИначеЕсли ТипЗнч(Предмет) = Тип("БизнесПроцессСсылка.Согласование") Тогда
		КонтрольноеСлово = НСтр("ru = 'Согласовать'");
		СловоЗамены = НСтр("ru = 'Контролировать согласование'");
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(КонтрольноеСлово) Тогда 
		
		ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Контролировать: %1'"), 
			СокрЛП(Наименование));
		
	Иначе	
		Если Лев(СокрЛП(Наименование), СтрДлина(КонтрольноеСлово)) = КонтрольноеСлово Тогда
			
			ТекстОписания = СловоЗамены + " " + СокрЛП(Сред(СокрЛП(Наименование), СтрДлина(КонтрольноеСлово)+1));
			
		Иначе	
			
			ТекстОписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контролировать: %1'"), 
				СокрЛП(Наименование));
			
		КонецЕсли;
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Описание) Тогда 
		ТекстОписания = ТекстОписания + Символы.ПС + Символы.ПС + Описание;
	КонецЕсли;
			
	Возврат ТекстОписания;
	
КонецФункции

Функция СвернутьТекстВСтроку(Текст, ДлинаСтроки)
	
	СтрокаВозврата = Текст;
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.ВК, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.ПС, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, Символы.Таб, " ");
	СтрокаВозврата = СтрЗаменить(СтрокаВозврата, "  ", " ");
	СтрокаВозврата = Лев(СтрокаВозврата, ДлинаСтроки);
	
	Возврат СтрокаВозврата;
	
КонецФункции

Процедура УстановитьСостояниеКонтроляВКеше(Предмет)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда 
		Возврат;
	КонецЕсли;	
	
	ТекстЗапроса = "";
	Для Каждого БизнесПроцесс Из Метаданные.БизнесПроцессы Цикл
		Если Не БизнесПроцесс.ТабличныеЧасти.Предметы.Реквизиты.Предмет.Тип.СодержитТип(ТипЗнч(Предмет)) Тогда 
			Продолжить;
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	Ссылка
		|ИЗ
		|	БизнесПроцесс."+БизнесПроцесс.Имя+".Предметы
		|ГДЕ 
		|	Предмет = &Предмет И РольПредмета = ЗНАЧЕНИЕ(Перечисление.РолиПредметов.Основной)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	КонецЦикла;	
	МассивПредметов = Новый Массив;
	Если ТекстЗапроса <> "" Тогда 
		ТекстЗапроса = Лев(ТекстЗапроса, СтрДлина(ТекстЗапроса)-16);
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Предмет", Предмет);
		МассивПредметов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	МассивПредметов.Добавить(Предмет);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контроль.Ссылка,
	|	Контроль.СнятСКонтроля,
	|	Контроль.ПоставленНаКонтроль,
	|	Контроль.СрокИсполнения
	|ИЗ
	|	Справочник.Контроль КАК Контроль
	|ГДЕ
	|	НЕ Контроль.ПометкаУдаления
	|	И Контроль.Предмет В(&МассивПредметов)";
	Запрос.УстановитьПараметр("МассивПредметов", МассивПредметов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 0 Тогда 
		
		СрокИсполненияКеш = '00010101';
		СрокИсполненияОбщийКеш = '00010101';
		СостояниеКонтроля = Перечисления.СостоянияКонтроля.ПустаяСсылка();
		
	ИначеЕсли Результат.Количество() = 1 Тогда 
		
		СрокИсполненияКеш = Результат[0].СрокИсполнения;
		СрокИсполненияОбщийКеш = СрокИсполнения;
		Если Результат[0].СнятСКонтроля Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроля;
		Иначе
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтроле;
		КонецЕсли;	
		
	Иначе
		
		Если Результат.Найти(Истина, "СнятСКонтроля") <> Неопределено
			И Результат.Найти(Ложь, "СнятСКонтроля") <> Неопределено Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СмешанноНесколько;
		ИначеЕсли Результат.Найти(Истина, "СнятСКонтроля") <> Неопределено Тогда 
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроляНесколько;
		Иначе	
			СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтролеНесколько;
		КонецЕсли;	
		
		СрокИсполненияКеш = '39990101';
		СрокИсполненияОбщийКеш = '00010101';
		ЕстьПустойСрок = Ложь;
		Для Каждого Строка Из Результат Цикл 
			Если Строка.СнятСКонтроля Тогда 
				Продолжить;
			КонецЕсли;	
			Если ЗначениеЗаполнено(Строка.СрокИсполнения) И Строка.СрокИсполнения < СрокИсполненияКеш Тогда 
				СрокИсполненияКеш = Строка.СрокИсполнения;
			КонецЕсли;
			Если Строка.СрокИсполнения > СрокИсполненияОбщийКеш Тогда 
				СрокИсполненияОбщийКеш = Строка.СрокИсполнения;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.СрокИсполнения) Тогда 
				ЕстьПустойСрок = Истина;
			КонецЕсли;	
		КонецЦикла;	
		Если СрокИсполненияКеш = '39990101' Тогда 
			СрокИсполненияКеш = '00010101';
		КонецЕсли;	
		Если ЕстьПустойСрок Тогда 
			СрокИсполненияОбщийКеш = '00010101';
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда 
		ПредметКонтроля = Предмет.КорневоеПисьмо;
	Иначе	
		ПредметКонтроля = Предмет;
	КонецЕсли;	
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СостояниеКонтроля",
		СостояниеКонтроля);	
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СрокИсполнения",
		СрокИсполненияКеш);
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПредметКонтроля,
		"СрокИсполненияОбщий",
		СрокИсполненияОбщийКеш);
	
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		ОсновныеПредметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Предмет, , Истина);
		Для Каждого ОсновнойПредмет Из ОсновныеПредметы Цикл
			УстановитьСостояниеКонтроляВКеше(ОсновнойПредмет);
		КонецЦикла;	
	КонецЕсли;		
	
	// письма веток
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВеткиПереписки") Тогда
		ВеткаПереписки = Предмет;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПисьмаВеток.Письмо КАК Письмо
		|ПОМЕСТИТЬ ПисьмаВеток
		|ИЗ
		|	РегистрСведений.ПисьмаВеток КАК ПисьмаВеток
		|ГДЕ
		|	ПисьмаВеток.ВеткаПереписки = &ВеткаПереписки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПисьмаВеток.Письмо
		|ИЗ
		|	ПисьмаВеток КАК ПисьмаВеток
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ИсходящееПисьмо.Ссылка
		|ИЗ
		|	Документ.ИсходящееПисьмо КАК ИсходящееПисьмо
		|ГДЕ
		|	ИсходящееПисьмо.ДатаОтправки = ДАТАВРЕМЯ(1, 1, 1)
		|	И ИсходящееПисьмо.ПисьмоОснование В
		|			(ВЫБРАТЬ
		|				ПисьмаВеток.Письмо
		|			ИЗ
		|				ПисьмаВеток)
		|	И (ИсходящееПисьмо.ТипОтвета = ЗНАЧЕНИЕ(Перечисление.ТипыОтвета.ОтветНаПисьмо)
		|			ИЛИ ИсходящееПисьмо.ТипОтвета = ЗНАЧЕНИЕ(Перечисление.ТипыОтвета.ПересылкаПисьма))";
		
		Запрос.УстановитьПараметр("ВеткаПереписки", ВеткаПереписки);
		
		ПисьмаВетки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Письмо");
		Для Каждого ПисьмоВетки Из ПисьмаВетки Цикл
			
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СостояниеКонтроля",
				СостояниеКонтроля);
			
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СрокИсполнения",
				СрокИсполненияКеш);
	
			РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(ПисьмоВетки,
				"СрокИсполненияОбщий",
				СрокИсполненияОбщийКеш);
				
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПерезаписатьРабочуюГруппуПредмета(Предмет)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Добавление контролера в рабочую группу предмета
	Если ТипЗнч(Предмет) = Тип("СправочникСсылка.ВходящиеДокументы")
	 Или ТипЗнч(Предмет) = Тип("СправочникСсылка.ИсходящиеДокументы")
	 Или ТипЗнч(Предмет) = Тип("СправочникСсылка.ВнутренниеДокументы") 
	 Или ТипЗнч(Предмет) = Тип("СправочникСсылка.Мероприятия")
	 Или ТипЗнч(Предмет) = Тип("СправочникСсылка.Проекты") Тогда 
	 
		Если РаботаСРабочимиГруппами.ПоОбъектуВедетсяАвтоматическоеЗаполнениеРабочейГруппы(Предмет) Тогда 
			ТаблицаУчастников = РаботаСРабочимиГруппами.ПолучитьРабочуюГруппуДокумента(Предмет);
			
			РаботаСРабочимиГруппами.ДобавитьУчастникаВТаблицуНабора(ТаблицаУчастников, 
				Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера);
			
			РаботаСРабочимиГруппами.ПерезаписатьРабочуюГруппуОбъекта(Предмет, ТаблицаУчастников, Истина);
		КонецЕсли;
		
	// Добавление контролера процесса в рабочую группу предмета	
	ИначеЕсли ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		
		Если Предмет.Стартован Тогда 
			ПредметыПроцесса = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(Предмет);
			Для Каждого ПредметПроцесса Из ПредметыПроцесса Цикл
				ПерезаписатьРабочуюГруппуПредмета(ПредметПроцесса);
			КонецЦикла;	
		КонецЕсли;
			
	КонецЕсли;	
	
	// добавление контролера процесса\задачи в участники процесса
	Если ОбщегоНазначения.ЭтоБизнесПроцесс(Предмет.Метаданные()) Тогда 
		БизнесПроцесс = Предмет;
	ИначеЕсли ТипЗнч(Предмет) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда 	
		БизнесПроцесс = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Предмет, "БизнесПроцесс");
	Иначе	
		БизнесПроцесс = Неопределено;
	КонецЕсли;	
	
	Если БизнесПроцесс <> Неопределено Тогда 
		СтруктураУчастник = Новый Структура("Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации",
			Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера);
		НаборУчастниковПроцесса = РегистрыСведений.УчастникиПроцессов.ПолучитьУчастников(БизнесПроцесс);
		ЗаполнитьЗначенияСвойств(НаборУчастниковПроцесса.Добавить(), СтруктураУчастник);
		НаборУчастниковПроцесса.Свернуть("Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
		РегистрыСведений.УчастникиПроцессов.ЗаписатьНаборПоПроцессу(БизнесПроцесс, НаборУчастниковПроцесса);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПроверитьПраваКонтролераНаПредмет(Отказ)
	
	Если ТипЗнч(Контролер) = Тип("СправочникСсылка.Пользователи") Тогда
	
		ПраваПоОбъекту = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Предмет, Контролер);
		Если Не ПраваПоОбъекту.Чтение Тогда 
			ПредметСтрокой = Контроль.СформироватьПредставлениеПредмета(Предмет);
		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У контролера ""%1"" нет прав на предмет ""%2"".'"),
				Контролер, 
				ПредметСтрокой);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( 
				ТекстСообщения,
				ЭтотОбъект, 
				"Контролер", 
				, 
				Отказ);	
		КонецЕсли;
	
	ИначеЕсли ТипЗнч(Контролер) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		МассивПользователей = РегистрыСведений.ИсполнителиЗадач.ПолучитьИсполнителейРоли(
			Контролер, ОсновнойОбъектАдресацииКонтролера, ДополнительныйОбъектАдресацииКонтролера, Истина);
		
		КодВозврата = Истина;
		Для каждого Строка Из МассивПользователей Цикл
			ПраваПоОбъекту = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Предмет, Строка.Исполнитель);
			Если Не ПраваПоОбъекту.Чтение Тогда 
				КодВозврата = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если КодВозврата = Ложь Тогда
			ПредметСтрокой = Контроль.СформироватьПредставлениеПредмета(Предмет);
		
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не все пользователи  роли ""%1"" имеют права на предмет ""%2"".'"),
				Контролер, 
				ПредметСтрокой);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю( 
				ТекстСообщения,
				ЭтотОбъект, 
				"Контролер", 
				, 
				Отказ);	
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИсполнителейСтрокой()
	
	Перем ИсполнителиСтрокой;
	
	ИсполнителиСтрокой = "";
	Для Каждого Строка Из Исполнители Цикл
		ИсполнителиСтрокой = ИсполнителиСтрокой + Строка(Строка.Исполнитель) + "; ";
	КонецЦикла;	
	Если ИсполнителиСтрокой	<> "" Тогда 
		ИсполнителиСтрокой = Лев(ИсполнителиСтрокой, СтрДлина(ИсполнителиСтрокой) - 2);
	КонецЕсли;	
	
	Возврат ИсполнителиСтрокой;
	
КонецФункции	

#КонецОбласти
