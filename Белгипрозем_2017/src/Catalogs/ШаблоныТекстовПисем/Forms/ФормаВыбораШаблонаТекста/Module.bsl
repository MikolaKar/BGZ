////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьСписокШаблонов(ТекущийШаблон = Неопределено)
	
	Пользователь = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШаблоныТекстовПисем.Ссылка КАК Ссылка,
		|	ШаблоныТекстовПисем.Шаблон КАК Шаблон
		|ИЗ
		|	Справочник.ШаблоныТекстовПисем КАК ШаблоныТекстовПисем
		|ГДЕ
		|	ШаблоныТекстовПисем.Владелец = &Владелец");
		
	Запрос.УстановитьПараметр("Владелец", Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ШаблоныТекстов.Добавить();
		НоваяСтрока.Ссылка = Выборка.Ссылка;
		НоваяСтрока.ТекстШаблона = ПолучитьТекстШаблона(Выборка.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТекстШаблона(Шаблон)
	
	Возврат Справочники.ШаблоныТекстовПисем.ПолучитьТекстШаблона(Шаблон);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Владелец", Пользователи.ТекущийПользователь());
	
	ОбновитьСписокШаблонов();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ОбновитьШаблоныТекстов Тогда
		
		МассивСтрок = ШаблоныТекстов.НайтиСтроки(Новый Структура("Ссылка", Значение));
		
		Если МассивСтрок.Количество() > 0 Тогда
			ТекстШаблона = МассивСтрок[0].ТекстШаблона;
		Иначе
			ТекстШаблона = ПолучитьТекстШаблона(Значение);
		КонецЕсли;
		
	Иначе
		
		ТекстШаблона = ПолучитьТекстШаблона(Значение);
		
	КонецЕсли;
	
	Закрыть(ТекстШаблона);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ОбновитьШаблоныТекстов = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ОбновитьШаблоныТекстов = Истина;
	
КонецПроцедуры
