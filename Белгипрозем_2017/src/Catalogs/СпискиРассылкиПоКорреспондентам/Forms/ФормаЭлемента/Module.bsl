

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПротоколированиеРаботыПользователей.ЗаписатьОткрытие(Объект.Ссылка);
	ВидКорреспондентовФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	ЮрФизЛицо = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекущиеДанные.Получатель, "ЮрФизЛицо");
	
	Если ЮрФизЛицо = ВидКорреспондентовФизЛицо Тогда
		Элементы.Адресат.ТолькоПросмотр = Истина;
	Иначе	
		Элементы.Адресат.ТолькоПросмотр = Ложь;
	КонецЕсли;	
	
	ТекущийПолучатель = ТекущиеДанные.Получатель;
	Если ЗначениеЗаполнено(ТекущийПолучатель) И ЮрФизЛицо = ВидКорреспондентовФизЛицо Тогда
		
		Для Каждого Строка Из Объект.Получатели Цикл
			Если (Строка.Получатель = ТекущийПолучатель) И (Строка.НомерСтроки <> ТекущиеДанные.НомерСтроки) Тогда 
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Получатель ""%1"" уже выбран.'"),
					ТекущийПолучатель);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				ТекущиеДанные.Получатель = Неопределено;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура АдресатПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	
	ТекущийПолучатель = ТекущиеДанные.Получатель;
	ТекущийАдресат = ТекущиеДанные.Адресат;
	Если ЗначениеЗаполнено(ТекущийАдресат) И ТипЗнч(ТекущийПолучатель) = Тип("СправочникСсылка.Корреспонденты") Тогда
		
		Для Каждого Строка Из Объект.Получатели Цикл
			Если (Строка.Получатель = ТекущийПолучатель) И (Строка.Адресат = ТекущийАдресат) И (Строка.НомерСтроки <> ТекущиеДанные.НомерСтроки) Тогда 
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Адресат ""%1"" получателя ""%2"" уже выбран.'"),
					ТекущийАдресат,
					ТекущийПолучатель);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				ТекущиеДанные.Адресат = Неопределено;
				Прервать;
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Получатели.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	ЮрФизЛицо = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекущиеДанные.Получатель, "ЮрФизЛицо");
	
	Если ЮрФизЛицо = ВидКорреспондентовФизЛицо Тогда
		Элементы.Адресат.ТолькоПросмотр = Истина;
	Иначе	
		Элементы.Адресат.ТолькоПросмотр = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Корреспонденты.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	НоваяСтрока = Объект.Получатели.Добавить();
	НоваяСтрока.Получатель = ВыбранноеЗначение;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", НЕ ЗначениеЗаполнено(ТекущийОбъект.Ссылка));
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПротоколированиеРаботыПользователей.ЗаписатьСоздание(Объект.Ссылка, ПараметрыЗаписи.ЭтоНовыйОбъект);
	ПротоколированиеРаботыПользователей.ЗаписатьИзменение(Объект.Ссылка);
КонецПроцедуры



