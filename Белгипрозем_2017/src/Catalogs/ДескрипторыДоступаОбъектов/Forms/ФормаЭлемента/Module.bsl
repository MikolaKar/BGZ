
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектыИспользующиеДескриптор.Параметры.УстановитьЗначениеПараметра("Дескриптор", Объект.Ссылка);
	УникальныйИдентификаторДескриптора = Объект.Ссылка.УникальныйИдентификатор();
	ЗаполнитьТаблицуПрав();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбновитьПрава(Команда)
	
	ОбновитьПраваНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПраваНаСервере()
	
	Протокол = Новый Массив;
	Справочники.ДескрипторыДоступаОбъектов.ОбновитьПрава(Объект.Ссылка, Протокол, Истина);
	
	Элементы.ПраваДоступа.Обновить();
	Элементы.ОбъектыИспользующиеДескриптор.Обновить();
	Элементы.СтраницаПротоколРасчетаПрав.Видимость = Истина;
	Элементы.ГруппаПраваДоступаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	
	ПротоколРасчетаПрав.Очистить();
	
	Для каждого Эл Из Протокол Цикл
		
		Строка = ПротоколРасчетаПрав.Добавить();
		Если ТипЗнч(Эл) <> Тип("Строка") Тогда
			Строка.Описание = Строка(Эл) + " (" + Строка(ТипЗнч(Эл)) + ")";
			Строка.Ссылка = Эл;
		Иначе	
			Строка.Описание = Эл;
		КонецЕсли;	
		
	КонецЦикла;
	
	Строка = ПротоколРасчетаПрав.Добавить();
	Строка.Описание = НСтр("ru = 'Неограниченные права на таблицу'");
	
	ЗаполнитьТаблицуПрав();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦ ФОРМЫ

&НаКлиенте
Процедура ПраваДоступаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Пользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколРасчетаПравВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПротоколРасчетаПрав.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыИспользующиеДескрипторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Объект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере	
Процедура ЗаполнитьТаблицуПрав()
	
	ТаблицаПрав = ДокументооборотПраваДоступа.ПолучитьТаблицуПравДляОтображенияВИнтерфейсе(Объект.Ссылка);
	ПраваДоступа.Загрузить(ТаблицаПрав);
	
КонецПроцедуры
