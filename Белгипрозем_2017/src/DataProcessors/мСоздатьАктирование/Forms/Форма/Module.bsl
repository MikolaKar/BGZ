
&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетДоговоров.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ Заактировано
		|ИЗ
		|	РегистрНакопления.УчетДоговоров КАК УчетДоговоров
		|ГДЕ
		|	УчетДоговоров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И УчетДоговоров.Период МЕЖДУ &Дата1 И &Дата2
		|	И УчетДоговоров.ЭтапДоговора.Подразделение В ИЕРАРХИИ(&Подразделения)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мРеквизитыАктовВыполненныхРабот.Владелец КАК Акт,
		|	мРеквизитыАктовВыполненныхРабот.ЭтапДоговора КАК ЭтапДоговора,
		|	мРеквизитыАктовВыполненныхРабот.ЭтапДоговора.Смета КАК Смета,
		|	мРеквизитыАктовВыполненныхРабот.ЭтапДоговора.Владелец КАК Договор,
		|	мРеквизитыАктовВыполненныхРабот.ЭтапДоговора.Владелец.Корреспондент КАК Корреспондент
		|ПОМЕСТИТЬ Акты
		|ИЗ
		|	Справочник.мРеквизитыАктовВыполненныхРабот КАК мРеквизитыАктовВыполненныхРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ Заактировано КАК Заактировано
		|		ПО мРеквизитыАктовВыполненныхРабот.ЭтапДоговора = Заактировано.ЭтапДоговора
		|ГДЕ
		|	НЕ мРеквизитыАктовВыполненныхРабот.ЭтапДоговора В
		|				(ВЫБРАТЬ
		|					Заактировано.ЭтапДоговора
		|				ИЗ
		|					Заактировано КАК Заактировано)
		|	И (мРеквизитыАктовВыполненныхРабот.СостояниеАкта = ЗНАЧЕНИЕ(Перечисление.мСостоянияАктовВыполненныхРабот.НаправленЗаказчику)
		|			ИЛИ мРеквизитыАктовВыполненныхРабот.СостояниеАкта = ЗНАЧЕНИЕ(Перечисление.мСостоянияАктовВыполненныхРабот.ВрученЗаказчику)
		|			ИЛИ мРеквизитыАктовВыполненныхРабот.СостояниеАкта = ЗНАЧЕНИЕ(Перечисление.мСостоянияАктовВыполненныхРабот.ПодписанЗаказчиком))
		|	И НЕ мРеквизитыАктовВыполненныхРабот.Владелец.ПометкаУдаления
		|	И мРеквизитыАктовВыполненныхРабот.Владелец.ДатаРегистрации МЕЖДУ &Дата1 И &Дата2
		|	И мРеквизитыАктовВыполненныхРабот.ЭтапДоговора.Подразделение В ИЕРАРХИИ(&Подразделения)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(мДатыДоговоров.Период) КАК Период,
		|	мДатыДоговоров.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ МаксДаты
		|ИЗ
		|	РегистрСведений.мДатыДоговоров КАК мДатыДоговоров
		|ГДЕ
		|	мДатыДоговоров.ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ПлановыйСрок)
		|	И мДатыДоговоров.ЭтапДоговора В
		|			(ВЫБРАТЬ
		|				Акты.ЭтапДоговора
		|			ИЗ
		|				Акты КАК Акты)
		|
		|СГРУППИРОВАТЬ ПО
		|	мДатыДоговоров.ЭтапДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мДатыДоговоров.Дата КАК ПлановыйСрок,
		|	мДатыДоговоров.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ ПлановыеСроки
		|ИЗ
		|	РегистрСведений.мДатыДоговоров КАК мДатыДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ МаксДаты КАК МаксДаты
		|		ПО (МаксДаты.ЭтапДоговора = мДатыДоговоров.ЭтапДоговора)
		|			И (МаксДаты.Период = мДатыДоговоров.Период)
		|ГДЕ
		|	мДатыДоговоров.ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ПлановыйСрок)
		|	И мДатыДоговоров.ЭтапДоговора В
		|			(ВЫБРАТЬ
		|				Акты.ЭтапДоговора
		|			ИЗ
		|				Акты КАК Акты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетыСПокупателямиОстатки.СуммаОстаток КАК Оплачено,
		|	РасчетыСПокупателямиОстатки.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ Оплаты
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.Остатки(
		|			&Дата2,
		|			ЭтапДоговора В
		|					(ВЫБРАТЬ
		|						Акты.ЭтапДоговора
		|					ИЗ
		|						Акты КАК Акты)
		|				И Договор В
		|					(ВЫБРАТЬ
		|						Акты.Договор
		|					ИЗ
		|						Акты КАК Акты)
		|				И Корреспондент В
		|					(ВЫБРАТЬ
		|						Акты.Корреспондент
		|					ИЗ
		|						Акты КАК Акты)) КАК РасчетыСПокупателямиОстатки
		|ГДЕ
		|	РасчетыСПокупателямиОстатки.СуммаОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(мСметыОбъемРабот.Стоимость) КАК СуммаПрочих,
		|	мСметыОбъемРабот.Ссылка.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ СуммыПрочих
		|ИЗ
		|	Справочник.мСметы.ОбъемРабот КАК мСметыОбъемРабот
		|ГДЕ
		|	мСметыОбъемРабот.СтадияРабот.НеВходитВОбъемДляНачисленияЗП
		|	И мСметыОбъемРабот.Ссылка В
		|			(ВЫБРАТЬ
		|				Акты.Смета
		|			ИЗ
		|				Акты КАК Акты)
		|
		|СГРУППИРОВАТЬ ПО
		|	мСметыОбъемРабот.Ссылка.ЭтапДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(мСметыОбъемРабот.Стоимость) КАК СуммаЗИС,
		|	мСметыОбъемРабот.Ссылка.ЭтапДоговора КАК ЭтапДоговора
		|ПОМЕСТИТЬ СуммыЗИС
		|ИЗ
		|	Справочник.мСметы.ОбъемРабот КАК мСметыОбъемРабот
		|ГДЕ
		|	мСметыОбъемРабот.СтадияРабот.Наименование = ""Внесение изменений в ЗИС""
		|	И мСметыОбъемРабот.Ссылка В
		|			(ВЫБРАТЬ
		|				Акты.Смета
		|			ИЗ
		|				Акты КАК Акты)
		|
		|СГРУППИРОВАТЬ ПО
		|	мСметыОбъемРабот.Ссылка.ЭтапДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Акты.ЭтапДоговора,
		|	мРайоныПоСотрудникам.Сотрудник,
		|	СведенияОПользователях.Подразделение,
		|	СведенияОПользователях.Должность.Квалификация
		|ПОМЕСТИТЬ НеОсновные
		|ИЗ
		|	Акты КАК Акты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мРайоныПоСотрудникам КАК мРайоныПоСотрудникам
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|			ПО мРайоныПоСотрудникам.Сотрудник = СведенияОПользователях.Пользователь
		|		ПО (Акты.ЭтапДоговора.Месторасположение ПОДОБНО ""%"" + мРайоныПоСотрудникам.Месторасположение + ""%"")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Акты.Акт КАК Акт,
		|	Акты.Акт.РегистрационныйНомер КАК НомерАкта,
		|	Акты.Акт.ДатаРегистрации КАК ДатаАкта,
		|	Акты.ЭтапДоговора.ШаблонУсловийДоговора КАК УсловиеОплаты,
		|	Акты.ЭтапДоговора КАК ЭтапДоговора,
		|	Акты.ЭтапДоговора.Владелец КАК Договор,
		|	Акты.ЭтапДоговора.Владелец.Корреспондент КАК Корреспондент,
		|	Акты.ЭтапДоговора.Подразделение КАК Подразделение,
		|	ПлановыеСроки.ПлановыйСрок КАК ПлановыйСрок,
		|	ЕСТЬNULL(Оплаты.Оплачено, 0) КАК Оплачено,
		|	ЕСТЬNULL(Оплаты.Оплачено, 0) / Акты.ЭтапДоговора.СтоимостьСНДС * 100 КАК ПроцентОплаты,
		|	ВнутренниеДокументы.Ссылка КАК Дело,
		|	Акты.ЭтапДоговора.ШаблонУсловийДоговора.УчетДатыПредоплаты КАК УчетПредоплаты,
		|	Акты.ЭтапДоговора.ШаблонУсловийДоговора.ПроцентПредоплаты КАК ТребуемыйПроцент,
		|	ЕСТЬNULL(СуммыПрочих.СуммаПрочих, 0) КАК СуммаПрочих,
		|	ЕСТЬNULL(СуммыЗИС.СуммаЗИС, 0) КАК СуммаЗИС,
		|	Акты.ЭтапДоговора.СтоимостьСНДС КАК Сумма,
		|	Акты.ЭтапДоговора.СуммаНДС КАК НДС,
		|	НеОсновные.Сотрудник КАК НеОсновнойИсполнитель,
		|	НеОсновные.Подразделение КАК ПодразделениеНеОсновного,
		|	НеОсновные.ДолжностьКвалификация КАК КвалификацияНеОсновного
		|ИЗ
		|	Акты КАК Акты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПлановыеСроки КАК ПлановыеСроки
		|		ПО Акты.ЭтапДоговора = ПлановыеСроки.ЭтапДоговора
		|		ЛЕВОЕ СОЕДИНЕНИЕ Оплаты КАК Оплаты
		|		ПО Акты.ЭтапДоговора = Оплаты.ЭтапДоговора
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		|		ПО Акты.ЭтапДоговора = ВнутренниеДокументы.ЭтапДоговора
		|			И (НЕ ВнутренниеДокументы.ПометкаУдаления)
		|			И (ВнутренниеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыВнутреннихДокументов.Дело))
		|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыПрочих КАК СуммыПрочих
		|		ПО Акты.ЭтапДоговора = СуммыПрочих.ЭтапДоговора
		|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыЗИС КАК СуммыЗИС
		|		ПО Акты.ЭтапДоговора = СуммыЗИС.ЭтапДоговора
		|		ЛЕВОЕ СОЕДИНЕНИЕ НеОсновные КАК НеОсновные
		|		ПО Акты.ЭтапДоговора = НеОсновные.ЭтапДоговора";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ДатаАктирования));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ДатаАктирования));
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Акты.Очистить();
	
	Пока Выборка.Следующий() Цикл
		НовСтрока = Акты.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
		
		// Пометка
		Пометка = Истина;
		Если Не ЗначениеЗаполнено(НовСтрока.Дело) Тогда
			Пометка = Ложь;
		КонецЕсли; 
		Если Не ЗначениеЗаполнено(НовСтрока.ПлановыйСрок) Тогда
			Пометка = Ложь;
		КонецЕсли; 
		Если Выборка.УчетПредоплаты Тогда
			Если Выборка.ТребуемыйПроцент > Выборка.ПроцентОплаты Тогда
				Пометка = Ложь;
			КонецЕсли; 
		КонецЕсли; 
		Если Выборка.СуммаЗИС > 0 Тогда
			Если Не ЗначениеЗаполнено(Выборка.НеОсновнойИсполнитель) Тогда
				Пометка = Ложь;
			КонецЕсли; 
		КонецЕсли; 
		НовСтрока.Пометка = Пометка;		
	КонецЦикла;
	Акты.Сортировать("Пометка, Акт");
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Если Подразделения.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Подразделения не указаны!";
		Сообщение.Поле = "Подразделения";
		Сообщение.Сообщить(); 
	КонецЕсли; 
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Функция ДанныеДляЗарплаты(ТзАктов) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТзАктов.ЭтапДоговора КАК ЭтапДоговора,
		|	ТзАктов.Договор КАК Договор,
		|	ТзАктов.Корреспондент КАК Корреспондент,
		|	ТзАктов.НомерАкта КАК НомерАкта
		|ПОМЕСТИТЬ Этапы
		|ИЗ
		|	&ТзАктов КАК ТзАктов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КорреспондентыДополнительныеРеквизиты.Значение КАК Значение,
		|	КорреспондентыДополнительныеРеквизиты.Свойство КАК Свойство,
		|	Этапы.ЭтапДоговора
		|ПОМЕСТИТЬ ВидыЗаказчиков
		|ИЗ
		|	Справочник.Корреспонденты.ДополнительныеРеквизиты КАК КорреспондентыДополнительныеРеквизиты,
		|	Этапы КАК Этапы
		|ГДЕ
		|	КорреспондентыДополнительныеРеквизиты.Ссылка В
		|			(ВЫБРАТЬ
		|				Этапы.Корреспондент
		|			ИЗ
		|				Этапы КАК Этапы)
		|	И КорреспондентыДополнительныеРеквизиты.Свойство.Наименование = ВЫБОР
		|			КОГДА КорреспондентыДополнительныеРеквизиты.Ссылка.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|				ТОГДА ""Вид заказчика физ. лицо""
		|			ИНАЧЕ ""Вид заказчика юр. лицо""
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
        //|ВЫБРАТЬ
        //|	ВнутренниеДокументыДополнительныеРеквизиты.Значение КАК ЦельПредоставления,
        //|	ВнутренниеДокументыДополнительныеРеквизиты.Ссылка КАК Договор
        //|ПОМЕСТИТЬ ЦелиФизЛиц
        //|ИЗ
        //|	Справочник.ВнутренниеДокументы.ДополнительныеРеквизиты КАК ВнутренниеДокументыДополнительныеРеквизиты
        //|ГДЕ
        //|	ВнутренниеДокументыДополнительныеРеквизиты.Ссылка В
        //|			(ВЫБРАТЬ
        //|				Этапы.Договор
        //|			ИЗ
        //|				Этапы КАК Этапы)
        //|	И ВнутренниеДокументыДополнительныеРеквизиты.Ссылка.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
        //|	И ВнутренниеДокументыДополнительныеРеквизиты.Свойство.Наименование = ""Цель предоставления зем. участка физлицам""
        //|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ Различные
		|	мЭтапыДоговоровИсполнители.Исполнитель КАК Исполнитель,
		|	мЭтапыДоговоровИсполнители.Ссылка.ВидРабот КАК ВидРабот,
		|	мЭтапыДоговоровИсполнители.Ссылка.ДопУсловиеНормативаЗП КАК ДопУсловиеНормативаЗП,
		|	мЭтапыДоговоровИсполнители.Ссылка.Владелец.Корреспондент КАК Корреспондент,
		|	ВЫБОР
		|		КОГДА мЭтапыДоговоровИсполнители.КТУ = 0
		|			ТОГДА 1
		|		ИНАЧЕ мЭтапыДоговоровИсполнители.КТУ
		|	КОНЕЦ КАК КТУ,
		|	мЭтапыДоговоровИсполнители.Ссылка КАК ЭтапДоговора,
		|	СведенияОПользователях.Подразделение КАК Подразделение,
		|	ВидыЗаказчиков.Значение КАК ВидЗаказчика,
		|	ВЫБОР
		|		КОГДА мЭтапыДоговоровИсполнители.Ссылка.Владелец.Корреспондент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|			ТОГДА """"
        //|			ТОГДА ЦелиФизЛиц.ЦельПредоставления
		|		ИНАЧЕ мЭтапыДоговоровИсполнители.Ссылка.КарточкаОбъектаРабот.ЦельПредоставления
		|	КОНЕЦ КАК ЦельПредоставления,
		|	ВЫБОР
		|		КОГДА СведенияОПользователях.Подразделение = мЭтапыДоговоровИсполнители.Ссылка.Подразделение
		|				ИЛИ СведенияОПользователях.Подразделение.Родитель = мЭтапыДоговоровИсполнители.Ссылка.Подразделение
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК НеОсновноеПодразделение,
		|	СведенияОПользователях.Должность.Квалификация КАК Квалификация
		|ИЗ
		|	Справочник.мЭтапыДоговоров.Исполнители КАК мЭтапыДоговоровИсполнители
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|		ПО мЭтапыДоговоровИсполнители.Исполнитель = СведенияОПользователях.Пользователь
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗаказчиков КАК ВидыЗаказчиков
		|		ПО мЭтапыДоговоровИсполнители.Ссылка = ВидыЗаказчиков.ЭтапДоговора
        //|		ЛЕВОЕ СОЕДИНЕНИЕ ЦелиФизЛиц КАК ЦелиФизЛиц
        //|		ПО мЭтапыДоговоровИсполнители.Ссылка.Владелец = ЦелиФизЛиц.Договор
		|ГДЕ
		|	мЭтапыДоговоровИсполнители.Ссылка В
		|			(ВЫБРАТЬ
		|				Этапы.ЭтапДоговора
		|			ИЗ
		|				Этапы КАК Этапы)";
	
	Запрос.УстановитьПараметр("ТзАктов", ТзАктов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;	
КонецФункции
 
&НаСервере
Процедура ЗагрузитьНаСервере()
	//// Список подразделений для актирования
	//Отбор = Новый Структура("Пометка", Истина); 
	//ТзПодразделений = Акты.Выгрузить(Отбор , "Подразделение");
	//ТзПодразделений.Свернуть("Подразделение");
	
	//Для каждого СтрокаТаб  Из ТзПодразделений Цикл
		//Отбор = Новый Структура("Пометка, Подразделение", Истина, СтрокаТаб.Подразделение); 
		Отбор = Новый Структура("Пометка", Истина); 
	    ТзАктов = Акты.Выгрузить(Отбор);
		ТзДанныеДляЗарплаты = ДанныеДляЗарплаты(ТзАктов);
		Если ТзАктов.Количество() > 0 Тогда
			Док = Документы.мАктирование.СоздатьДокумент();
			Док.Дата = ДатаАктирования;
			//Док.Подразделение = СтрокаТаб.Подразделение;
			Док.Ответственный = Пользователи.ТекущийПользователь();
			
			ТзЗарплата = Док.Зарплата.Выгрузить();
			ТзЗарплата.Очистить();
			
			Для каждого СтрАкта Из ТзАктов Цикл
				НовСтр = Док.Акты.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, СтрАкта);
                
                ТзЗарплата.Очистить();
				
				ОбъемРаботКРаспределению = СтрАкта.Сумма - СтрАкта.НДС - СтрАкта.СуммаПрочих;
				
				// общие параметры для расчета норматива ЗП
				ВидЗаказчика = "";
				ЦельПредоставления = "";
				ДопУсловиеНормативаЗП = "";
				ВидРабот = "";
				
				// Предварительное заполнение ТзЗарплата исполнителями из этапа
				Отбор = Новый Структура("ЭтапДоговора", СтрАкта.ЭтапДоговора); 
				СтрокиЗП = ТзДанныеДляЗарплаты.НайтиСтроки(Отбор);
				Для каждого Стр Из СтрокиЗП Цикл
					СтрЗП = ТзЗарплата.Добавить();
					ЗаполнитьЗначенияСвойств(СтрЗП, Стр);
				    СтрЗП.НомерАкта = СтрАкта.НомерАкта;
					СтрЗП.Коэффициент = 1;
					Если СтрЗП.НеОсновноеПодразделение Тогда
						// Объем работ
						СтрЗП.ОбъемРабот = СтрАкта.СуммаЗИС;
					КонецЕсли; 
					
					// Получение норматива
					ДанныеДляНорматива = Новый Структура(); 
					ДанныеДляНорматива.Вставить("ВидЗаказчика", Стр.ВидЗаказчика);
					ДанныеДляНорматива.Вставить("ЦельПредоставления", Стр.ЦельПредоставления);
					ДанныеДляНорматива.Вставить("ДопУсловие", Стр.ДопУсловиеНормативаЗП);
					ДанныеДляНорматива.Вставить("Исполнитель", Стр.Исполнитель);
					ДанныеДляНорматива.Вставить("ВидРабот", Стр.ВидРабот);
					ДанныеДляНорматива.Вставить("Дата", ДатаАктирования);
					
					СтрЗП.Норматив = мРаботаСДоговорами.ПолучитьНормативЗП(ДанныеДляНорматива); 
					
					ВидЗаказчика = Стр.ВидЗаказчика;
					ЦельПредоставления = Стр.ЦельПредоставления;
					ДопУсловиеНормативаЗП = Стр.ДопУсловиеНормативаЗП;
					ВидРабот = Стр.ВидРабот;
				КонецЦикла;
				
				// Добавление неосновного исполнителя
				Если СтрАкта.СуммаЗИС > 0 Тогда
					// Возможно он уже есть в табице
					ОтборПоИсполнителю = Новый Структура("Исполнитель", СтрАкта.НеОсновнойИсполнитель);
					ИскСтроки = ТзЗарплата.НайтиСтроки(ОтборПоИсполнителю);
					Если ИскСтроки.Количество() = 0 Тогда
						СтрЗП = ТзЗарплата.Добавить();
						ЗаполнитьЗначенияСвойств(СтрЗП, СтрАкта);
						СтрЗП.НомерАкта = СтрАкта.НомерАкта;
						СтрЗП.Коэффициент = 1;
						СтрЗП.Исполнитель = СтрАкта.НеОсновнойИсполнитель;
						СтрЗП.ОбъемРабот = СтрАкта.СуммаЗИС;
						СтрЗП.Подразделение = СтрАкта.ПодразделениеНеОсновного;
						СтрЗП.НеОсновноеПодразделение = Истина;
						СтрЗП.КТУ = 1; // Всегда
						
						// Получение норматива
						ДанныеДляНорматива = Новый Структура(); 
						ДанныеДляНорматива.Вставить("ВидЗаказчика", ВидЗаказчика);
						ДанныеДляНорматива.Вставить("ЦельПредоставления", ЦельПредоставления);
						ДанныеДляНорматива.Вставить("ДопУсловие", ДопУсловиеНормативаЗП);
						ДанныеДляНорматива.Вставить("Исполнитель", СтрЗП.Исполнитель);
						ДанныеДляНорматива.Вставить("ВидРабот", ВидРабот);
						ДанныеДляНорматива.Вставить("Дата", ДатаАктирования);
						
						СтрЗП.Норматив = мРаботаСДоговорами.ПолучитьНормативЗП(ДанныеДляНорматива); 
					КонецЕсли; 
				КонецЕсли; 
				
				// ПосчитатьИсполнителейИСуммуДляРаспределения()
				КолвоИсполнителейДляРаспределения = 0;
				СуммаДляРаспределения = ОбъемРаботКРаспределению;
				Для каждого ТекСтрока Из ТзЗарплата Цикл
					Если ТекСтрока.НеОсновноеПодразделение Тогда
						// уменьшаем сумму к распределению на объем, отданный другим подразделениям
						СуммаДляРаспределения = СуммаДляРаспределения - ТекСтрока.ОбъемРабот;
						
						// Расчет по не основному подразделению
						ТекСтрока.Начислено = Окр(ТекСтрока.ОбъемРабот * ТекСтрока.Норматив * ТекСтрока.Коэффициент, мРазное.ЗнакОкр(Объект.Дата));
						ТекСтрока.БазовоеНачисление = Окр(ТекСтрока.ОбъемРабот * ТекСтрока.Норматив, мРазное.ЗнакОкр(Объект.Дата));
					Иначе
						КолвоИсполнителейДляРаспределения = КолвоИсполнителейДляРаспределения + 1;
					КонецЕсли; 
				КонецЦикла;
				
				// Расчет ЗП в ТзЗарплата по основному подразделению
				// расчет объема пропорционально, на последнюю строку - остаток
				ОсталосьРаспределить = СуммаДляРаспределения;
				НомерПП = 0;
				ОсталосьРаспределитьКТУ = 1;
				флПоровну = Истина;
				Для каждого ТекСтрока Из ТзЗарплата Цикл
					
					// неосновные подразделения пропускаем
					Если ТекСтрока.НеОсновноеПодразделение Тогда
						Продолжить;
					КонецЕсли;
					
					НомерПП = НомерПП + 1;
					
					Если НомерПП = КолвоИсполнителейДляРаспределения Тогда
						ТекСтрока.ОбъемРабот = ОсталосьРаспределить;
						Если флПоровну Тогда
							ТекСТрока.КТУ = ОсталосьРаспределитьКТУ;
						КонецЕсли; 
					Иначе
						Если флПоровну Тогда
							Если КолвоИсполнителейДляРаспределения > 0 Тогда
								ТекСтрока.ОбъемРабот = Окр(СуммаДляРаспределения / КолвоИсполнителейДляРаспределения, мРазное.ЗнакОкр(Объект.Дата));
								ТекСТрока.КТУ = 1 / КолвоИсполнителейДляРаспределения;
							КонецЕсли; 
						КонецЕсли; 
					КонецЕсли;
					
					ТекСтрока.Начислено = Окр(ТекСтрока.ОбъемРабот * ТекСтрока.Норматив * ТекСтрока.Коэффициент, мРазное.ЗнакОкр(Объект.Дата));
					ТекСтрока.БазовоеНачисление = Окр(ТекСтрока.ОбъемРабот * ТекСтрока.Норматив, мРазное.ЗнакОкр(Объект.Дата));
					
					ОсталосьРаспределить = ОсталосьРаспределить - ТекСтрока.ОбъемРабот;
					ОсталосьРаспределитьКТУ = ОсталосьРаспределитьКТУ - ТекСТрока.КТУ;
				КонецЦикла;
				
				// Перенос в док
				Для каждого ТекСтрока Из ТзЗарплата Цикл
					НовСтрЗП = Док.Зарплата.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрЗП, ТекСтрока);
				КонецЦикла;
			КонецЦикла; 
			Док.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли; 
	//КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	ЗагрузитьНаСервере();
КонецПроцедуры
