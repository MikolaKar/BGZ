
// Хранение контекста взаимодействия с сервисом
&НаКлиенте
Перем КонтекстВзаимодействия Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МестоЗапуска = Параметры.МестоЗапуска;
	
	// Сформировать шаблон письма в зависимости от переданных праметров
	СформироватьПисьмо(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнтернетПоддержкаПользователейКлиент.ОбработатьОткрытиеФормы(КонтекстВзаимодействия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	// Проверка заполнения полей
	Если ПустаяСтрока(ЭлектроннаяПочта) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Поле ""E-mail для обратной связи"" не заполнено.'");
		СообщениеПользователю.Поле  = "ЭлектроннаяПочта";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Тема) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Поле ""Тема сообщения"" не заполнено.'");
		СообщениеПользователю.Поле  = "Тема";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Сообщение) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Не заполнен тест письма.'");
		СообщениеПользователю.Поле  = "Сообщение";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = Новый Структура("ОтКого, Тема, Сообщение", ЭлектроннаяПочта, Тема, Сообщение);
	Если НЕ ПустаяСтрока(УсловноеИмяПолучателя) Тогда
		ПараметрыПисьма.Вставить("УсловноеИмяПолучателя", УсловноеИмяПолучателя);
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Отправка'")
		,
		,
		НСтр("ru = 'Выполняется отправка электронного письма в службу тех. поддержки.'"),
		БиблиотекаКартинок.ИнтернетПоддержкаПользователейОтправкаПисьма);
	
	РезультатОтправки = ИнтернетПоддержкаПользователейКлиент.ОтправитьЭлектронноеСообщениеСлужбеТехПоддержки(
		ПараметрыПисьма,
		КонтекстВзаимодействия);
	
	Состояние();
	
	Если НЕ РезультатОтправки Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'При отправке письма произошла ошибка.
			|Подробнее см. в журнале регистрации.'"));
	Иначе
		Закрыть();
		ПоказатьПредупреждение(, НСтр("ru = 'Сообщение успешно отправлено.'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет заполнение шаблона письма в зависимости от значения параметров.
//
// Параметры:
// - Параметры (ДанныеФормыСтруктура, Структура) - параметры формирования шаблона.
//
&НаСервере
Процедура СформироватьПисьмо(Параметры)
	
	ШаблонЗаголовка = НСтр("ru = 'Письмо будет отправлено в техподдержку пользователей на адрес %1'");
	EMailДляОтправки = "webits-info@1c.ru";
	
	ТекстТехПараметров = ТекстТехническихПараметров();
	
	ТекстСообщения = "";
	
	ЛогинПользователя = "";
	Если НЕ Параметры.Свойство("Логин", ЛогинПользователя) Тогда
		Параметры.Свойство("login", ЛогинПользователя);
	КонецЕсли;
	
	// Формирование письма по шаблону в зависимотси от типа сообщения
	Если Параметры.ТипСообщения = 1 Тогда
		
		Тема           = НСтр("ru = 'Интернет-поддержка. Авторизация.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается пройти авторизацию и подключить интернет-поддержку.
			|Логин и пароль мной введены правильно. Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|
			|%2
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 2 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Ввод регистрационного номера.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается ввести регистрационный номер программного продукта
			|для подключения интернет-поддержки.
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|Регистрационный номер: %2.
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		Если Параметры.Свойство("regnumber") Тогда
			РегНомер = Параметры.regnumber;
		Иначе
			РегНомер = "";
		КонецЕсли;
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			РегНомер,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 3 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Регистрация продукта.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается зарегистрировать программный продукт
			|для подключения интернет-поддержки. Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|Регистрационный номер: %2.
			|Пинкод: %3.
			|
			|%4
			|-----------------------------------------------
			|С уважением, .'");
		
		Если Параметры.Свойство("regnumber") Тогда
			РегНомер = Параметры.regnumber;
		Иначе
			РегНомер = "";
		КонецЕсли;
		
		Если Параметры.Свойство("pincode") Тогда
			ПинКод = Параметры.pincode;
		Иначе
			ПинКод = "";
		КонецЕсли;
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			РегНомер,
			Пинкод,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 4 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Ввод дополнительной информации о пользователе.'");
		ТекстСообщения = Нстр("ru = 'Здравствуйте!
			|У меня не получается ввести дополнительную информацию о моей организации
			|при подключении интернет-поддержки. Прошу помочь разобраться с проблемой.
			|
			|Логин: %1
			|Название организации: %2
			|Тип деятельности: %3
			|ИНН: %4
			|КПП: %5
			|Руководитель: %6
			|Телефон: %7
			|Адрес электронной почты: %8
			|Факс: %9'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			Параметры.НазваниеОрганизации,
			Параметры.ТипОсновнойДеятельности,
			Параметры.ИНН,
			Параметры.КПП,
			Параметры.РуководительОрганизации,
			Параметры.Телефон,
			Параметры.АдресЭлектроннойПочты,
			Параметры.Факс);
		
		ТекстСообщенияПродолжение = НСтр("ru = 'Адрес: %1
			|Место покупки: %2
			|Дата покупки: %3
			|Число рабочих мест: %4
			|Ответственный сотрудник: %5
			|
			|%6
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщенияПродолжение = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщенияПродолжение,
			Параметры.ПредставлениеАдреса,
			Параметры.ГдеПриобретенаПрограмма,
			Формат(Параметры.ДатаПриобретения, "Л=ru_RU; ДФ=dd.MM.yyyy; ДЛФ=D"),
			Параметры.ЧислоРабочихМест,
			Параметры.ОтветственныйЗаРаботуСПакетом,
			ТекстТехПараметров);
		
		ТекстСообщения = ТекстСообщения + Символы.ПС + ТекстСообщенияПродолжение;
		
	ИначеЕсли Параметры.ТипСообщения = 5 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Интернет-поддержка продукта не оказывается.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|При подключении интернет-поддержки этого программного продукта
			|мне выдается сообщение ""Интернет-поддержка продукта не оказывается"".
			|Прошу помочь разобраться с проблемой.
			|
			|%1
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 6 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Восстановление пароля.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается восстановить свой пароль для подключения
			|интернет-поддержки.
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|E-mail: %2.
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			Параметры.ЭлектроннаяПочта,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 7 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Регистрация программного продукта.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается зарегистрировать программный продукт
			|для подключения интернет-поддержки. Прошу помочь разобраться с проблемой.
			|
			|Регистрационный номер: %1
			|Пинкод: %2.
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			Параметры.РегистрационныйНомер,
			Параметры.ПинКод,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 8 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Регистрация нового пользователя.'");
		ТекстСообщения = Нстр("ru = 'Здравствуйте!
			|У меня не получается зарегистрировать нового пользователя
			|для подключения интернет-поддержки. Прошу помочь разобраться с проблемой.
			|
			|Логин: %1
			|E-mail: %2
			|Фамилия: %3
			|Имя: %4
			|Отчество: %5
			|Город: %6
			|Телефон: %7
			|Место работы: %8.
			|
			|%9
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			Параметры.Email,
			Параметры.Фамилия,
			Параметры.Имя,
			Параметры.Отчетсво,
			Параметры.Город,
			Параметры.Телефон,
			Параметры.МестоРаботы,
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 9 Тогда
		
		EMailДляОтправки = "1c-taxcom@1c.ru";
		УсловноеИмяПолучателя = МестоЗапуска;
		
		Тема = НСтр("ru = '1С-Такском. Идентификатор участника обмена ЭД'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается зарегистрировать уникальный идентификатор участника ЭД.
			|
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|
			|%2
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			ТекстТехническихПараметровЭДО(),
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 10 Тогда
		
		EMailДляОтправки = "1c-taxcom@1c.ru";
		УсловноеИмяПолучателя = МестоЗапуска;
		
		Тема = НСтр("ru = '1С-Такском. Ввод уникального идентификатора участника обмена ЭД вручную.'");
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У меня не получается ввести уникальный идентификатор участника ЭД вручную.
			|
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|
			|%2
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			ТекстТехническихПараметровЭДО(),
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 11 Тогда
		
		EMailДляОтправки = "1c-taxcom@1c.ru";
		УсловноеИмяПолучателя = МестоЗапуска;
		
		Тема = НСтр("ru = '1С-Такском. Заявка на регистрацию участника обмена ЭД'");
		
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|%1.
			|
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %2.
			|
			|%3
			|
			|%4
			|-----------------------------------------------
			|С уважением, .'");
		
		Если Параметры.СтатусФормы = "new" ИЛИ Параметры.СтатусФормы = Неопределено Тогда
			ЧтоНеПолучилось = НСтр("ru = 'У меня не получается отправить заявку на регистрацию участника обмена ЭД'");
		ИначеЕсли Параметры.СтатусФормы = "change" Тогда
			ЧтоНеПолучилось = НСтр("ru = 'У меня не получается отправить заявку на изменение данных участника обмена ЭД'");
		ИначеЕсли Параметры.СтатусФормы = "show" Тогда
			ЧтоНеПолучилось = НСтр("ru = 'У меня возникли проблемы с заявкой участника обмена ЭД'");
		КонецЕсли;
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЧтоНеПолучилось,
			ЛогинПользователя,
			ТекстТехническихПараметровЭДО(),
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 12 Тогда
		
		EMailДляОтправки = "1c-taxcom@1c.ru";
		УсловноеИмяПолучателя = МестоЗапуска;
		
		Тема = НСтр("ru = '1С-Такском. Изменение тарифа.'");
		
		ТекстСообщения = НСтр("ru = 'Здравствуйте!
			|У   меня   не   получается   изменить   тариф
			|работы с оператором обмена ЭД.
			|Прошу помочь разобраться с проблемой.
			|
			|Логин: %1.
			|
			|%2
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			ТекстТехническихПараметровЭДО(),
			ТекстТехПараметров);
		
	ИначеЕсли Параметры.ТипСообщения = 13 Тогда
		
		Тема = НСтр("ru = 'Интернет-поддержка. Проблемы с обновлением конфигурации.'");
		ТекстСообщения = Нстр("ru = 'Здравствуйте.
			|При обновлении конфигурации на новый релиз возникли следующие проблемы:
			|
			|
			|Логин: %1
			|Регистрационный номер: %2
			|
			|%3
			|-----------------------------------------------
			|С уважением, .'");
		
		Если Параметры.Свойство("regnumber") Тогда
			РегНомер = Параметры.regnumber;
		Иначе
			РегНомер = "";
		КонецЕсли;
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			РегНомер,
			ТекстТехПараметров);
		
	Иначе
		
		Тема = НСтр("ru = '<Укажите тему сообщения>'");
		ТекстСообщения = НСтр("ru = '<Заполните содержимое письма>,
			|
			|Логин: %1
			|С уважением, .'");
		
		ТекстСообщения = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ЛогинПользователя,
			РегНомер);
		
	КонецЕсли;
	
	Элементы.ПояснениеКЗаголовку.Заголовок = СтрЗаменить(ШаблонЗаголовка, "%1", EMailДляОтправки);
	
	Сообщение = ТекстСообщения;
	
КонецПроцедуры

// Формирует шаблон текста технических параметров.
//
// Возвращаемое значение - Строка - шаблон технических параметров.
//
&НаСервере
Функция ТекстТехническихПараметров()
	
	СисИнфо = Новый СистемнаяИнформация;
	
	Если МестоЗапуска = "systemStart" ИЛИ МестоЗапуска = "systemStartNew" Тогда
		МестоВызоваСервиса = НСтр("ru = 'автоматический'");
	Иначе
		МестоВызоваСервиса = НСтр("ru = 'ручной'");
	КонецЕсли;
	
	ТехническиеПараметры = НСтр("ru = 'Технические параметры подключения:
		|(нужны для воспроизведения описанной проблемы)
		|
		|- имя конфигурации: %1,
		|- номер версии конфигурации: %2,
		|- номер версии платформы: %3,
		|- версия библиотеки интернет-поддержки: %4,
		|- язык пользователя: %5,
		|- вид приложения: управляемый,
		|- вызов сервиса: %6.'")
		+ Символы.ПС;
	
	ТехническиеПараметры = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
		ТехническиеПараметры,
		Строка(Метаданные.Имя),
		Строка(Метаданные.Версия),
		Строка(СисИнфо.ВерсияПриложения),
		ИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки(),
		ТекущийКодЛокализации(),
		МестоВызоваСервиса);
	
	Возврат ТехническиеПараметры;
	
КонецФункции

// Формирует шаблон текста технических параметров для ЭДО.
//
// Параметры
// СОписаниемОшибки - говорит о том, что нужно выводить при
// необходимости секцию описания ошибки
// Возвращаемое значение - Строка - шаблон технических параметров ЭДО.
//
&НаСервере
Функция ТекстТехническихПараметровЭДО() Экспорт
	
	ТехническиеПараметры = НСтр("ru = 'Параметры участника обмена ЭД:
		|
		|- отпечаток сертификата: %1'");
	
	ТехническиеПараметры = ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
		ТехническиеПараметры,
		ЗначениеОтпечаткаСертификата());
	
	КодОшибки   = Параметры.codeErrorED;
	ТекстОшибки = Параметры.textErrorED;
	
	Если НЕ ПустаяСтрока(КодОшибки) Тогда
		
		СтрДанныхОшибки = Символы.ПС + НСтр("ru = '- код ошибки: %1,
			|- описание ошибки: %2'");
		
		ТехническиеПараметры = ТехническиеПараметры
			+ ИнтернетПоддержкаПользователейКлиентСервер.ПодставитьПараметрыВСтроку(
				СтрДанныхОшибки,
				КодОшибки,
				ТекстОшибки);
		
	КонецЕсли;
	
	Возврат ТехническиеПараметры;
	
КонецФункции

// Функция получения отпечатка сертификата, который
// был записан в хранимые параметры при старте механизма
// Параметры:
// - РеквизитыОбработки (Структура) - определяет текущий контекст исполнения бизнес-процесса
//		(адрес веб-сервиса и др. (см. функцию ОпределитьСтруктуруРеквизитовОбработки()));
// Возвращаемое значение - строка - отпечаток сертификата
//
&НаСервере
Функция ЗначениеОтпечаткаСертификата()
	
	СтрокаВнутр = Параметры.IDCertificateED;
	СертификатЭЦП = ЗначениеИзСтрокиВнутр(СтрокаВнутр);
	
	Отпечаток = "";
	Попытка
		ИнтернетПоддержкаПользователейСерверПереопределяемый.ПолучитьОтпечатокСертификата(СертификатЭЦП, Отпечаток);
	Исключение
	КонецПопытки;
	
	Возврат Отпечаток;
	
КонецФункции

#КонецОбласти
