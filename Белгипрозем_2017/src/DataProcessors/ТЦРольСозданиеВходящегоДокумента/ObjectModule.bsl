
Функция СоздатьВходящийДокумент(Адресат) Экспорт 
	
	СозданныеОбъекты = Новый Массив;
	
	// формирование документа
	ВходящийДокумент = Справочники.ВходящиеДокументы.СоздатьЭлемент();
	ВходящийДокумент.Заголовок = НСтр("ru = 'Новый входящий документ'");
	ВходящийДокумент.Комментарий = НСтр("ru = 'Тестовый входящий документ'");
	
	ВходящийДокумент.Отправитель  		= Корреспондент;
	ВходящийДокумент.Подписал			= Делопроизводство.КонтактноеЛицоКорреспондента(ВходящийДокумент.Отправитель);
	ВходящийДокумент.ВидДокумента 		= ВидДокумента;
	ВходящийДокумент.ГрифДоступа  		= ГрифДоступа;
	ВходящийДокумент.Организация  		= Организация;
	ВходящийДокумент.СпособПолучения  	= СпособДоставки;
	ВходящийДокумент.ВопросДеятельности = ВопросыДеятельности;
	ВходящийДокумент.Адресат 			= Адресат;
	ВходящийДокумент.Подразделение 		= РаботаСПользователями.ПолучитьПодразделение(ВходящийДокумент.Адресат);
	ВходящийДокумент.ДатаСоздания		= ТекущаяДатаСеанса();
	
	// регистрация
	ВходящийДокумент.ДатаРегистрации = ТекущаяДатаСеанса();
	Нумератор = Нумерация.ПолучитьНумераторДокумента(ВходящийДокумент);
	Если ЗначениеЗаполнено(Нумератор) Тогда 
		СтруктураПараметров = НумерацияКлиентСервер.ПолучитьПараметрыНумерации(ВходящийДокумент);
		Нумерация.СформироватьЧисловойНомерДокумента(СтруктураПараметров, ВходящийДокумент.ЧисловойНомер);
		
		ОписанияОшибок = Новый СписокЗначений;
		СтруктураПараметров = НумерацияКлиентСервер.ПолучитьПараметрыНумерации(ВходящийДокумент);
		Нумерация.СформироватьСтроковыйНомерДокумента(СтруктураПараметров, ВходящийДокумент.РегистрационныйНомер, ОписанияОшибок);
	Иначе
		ВходящийДокумент.РегистрационныйНомер = "12345";
	КонецЕсли;
	ВходящийДокумент.Зарегистрировал = Зарегистрировал;
	ВходящийДокумент.Записать();
	
	Состояние = Перечисления.СостоянияДокументов.Зарегистрирован;
	Делопроизводство.ЗаписатьСостояниеДокумента(
			ВходящийДокумент.Ссылка, 
			ТекущаяДата(), 
			Состояние, 
			Зарегистрировал);
			
			
	// на рассмотрение
	Рассмотрение = БизнесПроцессы.Рассмотрение.СоздатьБизнесПроцесс();
	Рассмотрение.Заполнить(ВходящийДокумент.Ссылка);
	Рассмотрение.Записать();
	Рассмотрение.Старт();
	
	//на исполнение
	Исполнение = БизнесПроцессы.Исполнение.СоздатьБизнесПроцесс();
	Исполнение.Заполнить(ВходящийДокумент.Ссылка);
	СтрокаТЧ = Исполнение.Исполнители.Добавить();
	СтрокаТЧ.Исполнитель = Адресат;
	Исполнение.Записать();
	Исполнение.Старт();
	
	СозданныеОбъекты.Добавить(ВходящийДокумент.Ссылка);
	СозданныеОбъекты.Добавить(Рассмотрение.Ссылка);
	СозданныеОбъекты.Добавить(Исполнение.Ссылка);
	
	
	Возврат СозданныеОбъекты;
	
КонецФункции
