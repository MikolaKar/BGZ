
&НаСервере
Процедура ПоискНаСервере()
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |	мЭтапыДоговоров.Ссылка КАК ЭтапДоговора,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора,
        |	мЭтапыДоговоров.Владелец КАК Договор,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.ПроцентПредоплаты КАК ПроцентПредоплаты,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыПодписания КАК УчетДатыПодписания,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыПредоставленияДокументов КАК УчетДатыПредоставленияДокументов,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыВозвратаДоговора КАК УчетДатыВозвратаДоговора,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыПредоплаты КАК УчетДатыПредоплаты,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыВыполненияПредыдущегоЭтапа КАК УчетДатыВыполненияПредыдущегоЭтапа,
        |	мЭтапыДоговоров.ШаблонУсловийДоговора.УчетДатыПредоставленияРешения КАК УчетДатыПредоставленияРешения
        |ПОМЕСТИТЬ ЭтапыШаблоны
        |ИЗ
        |	Справочник.мЭтапыДоговоров КАК мЭтапыДоговоров
        |ГДЕ
        |	мЭтапыДоговоров.Владелец.ДатаРегистрации МЕЖДУ &Дата1 И &Дата2
        |	И НЕ мЭтапыДоговоров.ПометкаУдаления
        |	И мЭтапыДоговоров.ДатаИсключенИзДоговора=ДатаВремя(1,1,1,0,0,0)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	мЭтапыДоговоров.Ссылка КАК ЭтапДоговора,
        |	мЭтапыДоговоров.ДатаПредоставленияРешения
        |ПОМЕСТИТЬ ДатыРешений
        |ИЗ
        |	Справочник.мЭтапыДоговоров КАК мЭтапыДоговоров
        |ГДЕ
        |	мЭтапыДоговоров.Ссылка В
        |			(ВЫБРАТЬ
        |				ЭтапыШаблоны.ЭтапДоговора
        |			ИЗ
        |				ЭтапыШаблоны КАК ЭтапыШаблоны)
        |	И мЭтапыДоговоров.ДатаПредоставленияРешения <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	ЭтапыШаблоны.Договор.ДатаНачалаДействия КАК ДатаПодписания
        |ПОМЕСТИТЬ ДатыПодписания
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |ГДЕ
        |	ЭтапыШаблоны.Договор.ДатаНачалаДействия <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	мДатыДоговоровСрезПоследних.Дата КАК ДатаСтоп,
        |	МАКСИМУМ(мДатыДоговоровСрезПоследних.Период) КАК Период,
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора
        |ПОМЕСТИТЬ Приостановлены
        |ИЗ
        |	РегистрСведений.мДатыДоговоров.СрезПоследних(, ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаПриостановленияРабот)) КАК мДатыДоговоровСрезПоследних
        |
        |СГРУППИРОВАТЬ ПО
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора,
        |	мДатыДоговоровСрезПоследних.Дата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	МАКСИМУМ(мДатыДоговоровСрезПоследних.Период) КАК Период,
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора,
        |	ЕСТЬNULL(мДатыДоговоровСрезПоследних.Дата, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаПуск
        |ПОМЕСТИТЬ Возобновлены
        |ИЗ
        |	РегистрСведений.мДатыДоговоров.СрезПоследних(
        |			,
        |			ЭтапДоговора В
        |					(ВЫБРАТЬ
        |						Приостановлены.ЭтапДоговора
        |					ИЗ
        |						Приостановлены КАК Приостановлены)
        |				И ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаВозобновленияРабот)) КАК мДатыДоговоровСрезПоследних
        |
        |СГРУППИРОВАТЬ ПО
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора,
        |	ЕСТЬNULL(мДатыДоговоровСрезПоследних.Дата, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	Приостановлены.ЭтапДоговора,
        |	Приостановлены.ДатаСтоп,
        |	ЕСТЬNULL(Возобновлены.ДатаПуск, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаПуск
        |ПОМЕСТИТЬ Остановлены
        |ИЗ
        |	Приостановлены КАК Приостановлены
        |		ЛЕВОЕ СОЕДИНЕНИЕ Возобновлены КАК Возобновлены
        |		ПО Приостановлены.ЭтапДоговора = Возобновлены.ЭтапДоговора
        |ГДЕ
        |	Приостановлены.ДатаСтоп > ЕСТЬNULL(Возобновлены.ДатаПуск, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ Возобновлены
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ Приостановлены
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	мДатыДоговоровСрезПоследних.Дата КАК ДатаВыполненияПредЭтапа,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	ЭтапыШаблоны.Договор
        |ПОМЕСТИТЬ ДатыВыполненияПредЭтапа
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мДатыДоговоров.СрезПоследних(
        |				,
        |				ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаВыполнения)
        |					И Договор В
        |						(ВЫБРАТЬ
        |							ЭтапыШаблоны.Договор
        |						ИЗ
        |							ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК мДатыДоговоровСрезПоследних
        |		ПО ЭтапыШаблоны.Договор = мДатыДоговоровСрезПоследних.Договор
        |			И (мДатыДоговоровСрезПоследних.ЭтапДоговора.НомерЭтапа = ЭтапыШаблоны.ЭтапДоговора.НомерЭтапа - 1)
        |ГДЕ
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора.НомерЭтапа = ЭтапыШаблоны.ЭтапДоговора.НомерЭтапа - 1
        |	И мДатыДоговоровСрезПоследних.Дата <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	СУММА(УчетДоговоровОбороты.СуммаПриход) КАК Всего,
        |	УчетДоговоровОбороты.ЭтапДоговора КАК ЭтапДоговора
        |ПОМЕСТИТЬ Сметная
        |ИЗ
        |	РегистрНакопления.УчетДоговоров.Обороты(
        |			,
        |			,
        |			Период,
        |			ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ЭтапыШаблоны.ЭтапДоговора
        |				ИЗ
        |					ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК УчетДоговоровОбороты
        |
        |СГРУППИРОВАТЬ ПО
        |	УчетДоговоровОбороты.ЭтапДоговора
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	РасчетыСПокупателямиОбороты.ЭтапДоговора,
        |	РасчетыСПокупателямиОбороты.СуммаПриход КАК Оплачено
        |ПОМЕСТИТЬ Оплаты
        |ИЗ
        |	РегистрНакопления.РасчетыСПокупателями.Обороты(
        |			,
        |			,
        |			,
        |			ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ЭтапыШаблоны.ЭтапДоговора
        |				ИЗ
        |					ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК РасчетыСПокупателямиОбороты
        |ГДЕ
        |	РасчетыСПокупателямиОбороты.СуммаПриход > 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.Договор,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	ЭтапыШаблоны.ПроцентПредоплаты,
        |	ЕСТЬNULL(Сметная.Всего, 0) КАК СметнаяСтоимость,
        |	ЕСТЬNULL(Оплаты.Оплачено, 0) КАК Оплачено,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(Сметная.Всего, 0) > 0
        |			ТОГДА ЕСТЬNULL(Оплаты.Оплачено, 0) / ЕСТЬNULL(Сметная.Всего, 0) * 100
        |		ИНАЧЕ 0
        |	КОНЕЦ КАК ПроцентПредоплатыФакт
        |ПОМЕСТИТЬ Оплачены
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ Сметная КАК Сметная
        |		ПО ЭтапыШаблоны.ЭтапДоговора = Сметная.ЭтапДоговора
        |		ЛЕВОЕ СОЕДИНЕНИЕ Оплаты КАК Оплаты
        |		ПО ЭтапыШаблоны.ЭтапДоговора = Оплаты.ЭтапДоговора
        |ГДЕ
        |	ВЫБОР
        |			КОГДА ЕСТЬNULL(Сметная.Всего, 0) > 0
        |				ТОГДА ЕСТЬNULL(Оплаты.Оплачено, 0) / ЕСТЬNULL(Сметная.Всего, 0) * 100
        |			ИНАЧЕ 0
        |		КОНЕЦ >= ЭтапыШаблоны.ПроцентПредоплаты
        |	И ЕСТЬNULL(Оплаты.Оплачено, 0) > 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ Оплаты
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ Сметная
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.Договор,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	мДатыДоговоровСрезПоследних.Дата КАК ДатаПредоставленияДокументов
        |ПОМЕСТИТЬ ДатыПредоставленияДокументов
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мДатыДоговоров.СрезПоследних(
        |				,
        |				ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаПредоставленияДокументов)
        |					И ЭтапДоговора В
        |						(ВЫБРАТЬ
        |							ЭтапыШаблоны.ЭтапДоговора
        |						ИЗ
        |							ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК мДатыДоговоровСрезПоследних
        |		ПО ЭтапыШаблоны.ЭтапДоговора = мДатыДоговоровСрезПоследних.ЭтапДоговора
        |ГДЕ
        |	мДатыДоговоровСрезПоследних.Дата <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.Договор,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	мДатыДоговоровСрезПоследних.Дата КАК ДатаВозвратаДоговора
        |ПОМЕСТИТЬ ДатыВозвратаДоговора
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мДатыДоговоров.СрезПоследних(
        |				,
        |				ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаВозвратаДоговора)
        |					И ЭтапДоговора В
        |						(ВЫБРАТЬ
        |							ЭтапыШаблоны.ЭтапДоговора
        |						ИЗ
        |							ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК мДатыДоговоровСрезПоследних
        |		ПО ЭтапыШаблоны.ЭтапДоговора = мДатыДоговоровСрезПоследних.ЭтапДоговора
        |ГДЕ
        |	мДатыДоговоровСрезПоследних.Дата <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.Договор,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	мДатыДоговоровСрезПоследних.Дата КАК ДатаПродления
        |ПОМЕСТИТЬ ДатыПродления
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мДатыДоговоров.СрезПоследних(
        |				,
        |				ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ДатаПродления)
        |					И ЭтапДоговора В
        |						(ВЫБРАТЬ
        |							ЭтапыШаблоны.ЭтапДоговора
        |						ИЗ
        |							ЭтапыШаблоны КАК ЭтапыШаблоны)) КАК мДатыДоговоровСрезПоследних
        |		ПО ЭтапыШаблоны.ЭтапДоговора = мДатыДоговоровСрезПоследних.ЭтапДоговора
        |ГДЕ
        |	мДатыДоговоровСрезПоследних.Дата <> &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	МАКСИМУМ(мДатыДоговоровСрезПоследних.Период) КАК Период,
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора КАК ЭтапДоговора
        |ПОМЕСТИТЬ МаксДатыЗаписиПлановогоСрока
        |ИЗ
        |	РегистрСведений.мДатыДоговоров.СрезПоследних(
        |			,
        |			ЭтапДоговора В
        |					(ВЫБРАТЬ
        |						ЭтапыШаблоны.ЭтапДоговора
        |					ИЗ
        |						ЭтапыШаблоны КАК ЭтапыШаблоны)
        |				И ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ПлановыйСрок)) КАК мДатыДоговоровСрезПоследних
        |
        |СГРУППИРОВАТЬ ПО
        |	мДатыДоговоровСрезПоследних.ЭтапДоговора
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	мДатыДоговоровСрезПоследних.Дата КАК ПлановыйСрок,
        |	МаксДатыЗаписиПлановогоСрока.ЭтапДоговора
        |ПОМЕСТИТЬ ПлановыеСроки
        |ИЗ
        |	МаксДатыЗаписиПлановогоСрока КАК МаксДатыЗаписиПлановогоСрока
        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мДатыДоговоров.СрезПоследних(
        |				,
        |				ВидДатыДоговора = ЗНАЧЕНИЕ(Справочник.мВидыДатДоговоров.ПлановыйСрок)
        |					И ЭтапДоговора В
        |						(ВЫБРАТЬ
        |							МаксДатыЗаписиПлановогоСрока.ЭтапДоговора
        |						ИЗ
        |							МаксДатыЗаписиПлановогоСрока КАК МаксДатыЗаписиПлановогоСрока)) КАК мДатыДоговоровСрезПоследних
        |		ПО МаксДатыЗаписиПлановогоСрока.ЭтапДоговора = мДатыДоговоровСрезПоследних.ЭтапДоговора
        |			И МаксДатыЗаписиПлановогоСрока.Период = мДатыДоговоровСрезПоследних.Период
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	ЭтапыШаблоны.Договор,
        |	ЕСТЬNULL(ПлановыеСроки.ПлановыйСрок, &ПустаяДата) КАК ПлановыйСрок
        |ПОМЕСТИТЬ ПлановыеСрокиЭтапов
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |		ЛЕВОЕ СОЕДИНЕНИЕ ПлановыеСроки КАК ПлановыеСроки
        |		ПО ЭтапыШаблоны.ЭтапДоговора = ПлановыеСроки.ЭтапДоговора
        |ГДЕ
        |	ЕСТЬNULL(ПлановыеСроки.ПлановыйСрок, &ПустаяДата) > &ПустаяДата
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ МаксДатыЗаписиПлановогоСрока
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ ПлановыеСроки
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЭтапыШаблоны.Договор,
        |	ЭтапыШаблоны.ЭтапДоговора,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыПредоставленияРешения
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыРешений.ЭтапДоговора
        |				ИЗ
        |					ДатыРешений КАК ДатыРешений)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаРешения,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыПодписания
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыПодписания.ЭтапДоговора
        |				ИЗ
        |					ДатыПодписания КАК ДатыПодписания)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаПодписания,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыВыполненияПредыдущегоЭтапа
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыВыполненияПредЭтапа.ЭтапДоговора
        |				ИЗ
        |					ДатыВыполненияПредЭтапа КАК ДатыВыполненияПредЭтапа)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаВыполненияПредЭтапа,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыПредоплаты
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					Оплачены.ЭтапДоговора
        |				ИЗ
        |					Оплачены КАК Оплачены)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаПредоплаты,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыПредоставленияДокументов
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыПредоставленияДокументов.ЭтапДоговора
        |				ИЗ
        |					ДатыПредоставленияДокументов КАК ДатыПредоставленияДокументов)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаПредоставленияДокументов,
        |	ВЫБОР
        |		КОГДА НЕ ЭтапыШаблоны.УчетДатыВозвратаДоговора
        |			ТОГДА ИСТИНА
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыВозвратаДоговора.ЭтапДоговора
        |				ИЗ
        |					ДатыВозвратаДоговора КАК ДатыВозвратаДоговора)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаВозвратаДоговора,
        |	ВЫБОР
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					ДатыПродления.ЭтапДоговора
        |				ИЗ
        |					ДатыПродления КАК ДатыПродления)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеДатаПродления,
        |	ВЫБОР
        |		КОГДА ЭтапыШаблоны.ЭтапДоговора В
        |				(ВЫБРАТЬ
        |					Остановлены.ЭтапДоговора
        |				ИЗ
        |					Остановлены КАК Остановлены)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыполнениеРаботыОстановлены
        |ПОМЕСТИТЬ ВыполнениеЭлемУсловий
        |ИЗ
        |	ЭтапыШаблоны КАК ЭтапыШаблоны
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВыполнениеЭлемУсловий.Договор,
        |	ВыполнениеЭлемУсловий.ЭтапДоговора КАК ЭтапДоговора,
        |	ВЫБОР
        |		КОГДА ВыполнениеЭлемУсловий.ВыполнениеДатаРешения
        |				И ВыполнениеЭлемУсловий.ВыполнениеДатаПодписания
        |				И ВыполнениеЭлемУсловий.ВыполнениеДатаВыполненияПредЭтапа
        |				И ВыполнениеЭлемУсловий.ВыполнениеДатаПредоплаты
        |				И ВыполнениеЭлемУсловий.ВыполнениеДатаПредоставленияДокументов
        |				И ВыполнениеЭлемУсловий.ВыполнениеДатаВозвратаДоговора
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ВыпУсловий,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(ПлановыеСрокиЭтапов.ПлановыйСрок, ЛОЖЬ) = ЛОЖЬ
        |			ТОГДА ЛОЖЬ
        |		ИНАЧЕ ИСТИНА
        |	КОНЕЦ КАК Срок,
        |	ВыполнениеЭлемУсловий.ЭтапДоговора.ШаблонУсловийДоговора КАК Условие,
        |	ВыполнениеЭлемУсловий.ВыполнениеДатаПродления КАК Продление,
        |	ВыполнениеЭлемУсловий.ВыполнениеРаботыОстановлены КАК Стоп
        |ИЗ
        |	ВыполнениеЭлемУсловий КАК ВыполнениеЭлемУсловий
        |		ЛЕВОЕ СОЕДИНЕНИЕ ПлановыеСрокиЭтапов КАК ПлановыеСрокиЭтапов
        |		ПО ВыполнениеЭлемУсловий.ЭтапДоговора = ПлановыеСрокиЭтапов.ЭтапДоговора
        |ГДЕ
        |	ВЫБОР
        |			КОГДА ВыполнениеЭлемУсловий.ВыполнениеРаботыОстановлены
        |					И НЕ ВЫБОР
        |							КОГДА ЕСТЬNULL(ПлановыеСрокиЭтапов.ПлановыйСрок, ЛОЖЬ) = ЛОЖЬ
        |								ТОГДА ЛОЖЬ
        |							ИНАЧЕ ИСТИНА
        |						КОНЕЦ
        |				ТОГДА ЛОЖЬ
        |			КОГДА ВыполнениеЭлемУсловий.ВыполнениеДатаПродления
        |					И ВЫБОР
        |						КОГДА ЕСТЬNULL(ПлановыеСрокиЭтапов.ПлановыйСрок, ЛОЖЬ) = ЛОЖЬ
        |							ТОГДА ЛОЖЬ
        |						ИНАЧЕ ИСТИНА
        |					КОНЕЦ
        |				ТОГДА ЛОЖЬ
        |			ИНАЧЕ ВЫБОР
        |					КОГДА ВыполнениеЭлемУсловий.ВыполнениеДатаРешения
        |							И ВыполнениеЭлемУсловий.ВыполнениеДатаПодписания
        |							И ВыполнениеЭлемУсловий.ВыполнениеДатаВыполненияПредЭтапа
        |							И ВыполнениеЭлемУсловий.ВыполнениеДатаПредоплаты
        |							И ВыполнениеЭлемУсловий.ВыполнениеДатаПредоставленияДокументов
        |							И ВыполнениеЭлемУсловий.ВыполнениеДатаВозвратаДоговора
        |						ТОГДА ИСТИНА
        |					ИНАЧЕ ЛОЖЬ
        |				КОНЕЦ <> ВЫБОР
        |					КОГДА ЕСТЬNULL(ПлановыеСрокиЭтапов.ПлановыйСрок, ЛОЖЬ) = ЛОЖЬ
        |						ТОГДА ЛОЖЬ
        |					ИНАЧЕ ИСТИНА
        |				КОНЕЦ
        |		КОНЕЦ
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВыполнениеЭлемУсловий.Договор.ЧисловойНомер,
        |	ЭтапДоговора";
        
    Запрос.УстановитьПараметр("ПустаяДата", '00010101000000');
    Запрос.УстановитьПараметр("Дата1", Период.ДатаНачала);
    Запрос.УстановитьПараметр("Дата2", Период.ДатаОкончания);
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    
    Проблемы.Очистить();
    Пока Выборка.Следующий() Цикл
        Строка = Проблемы.Добавить();
        ЗаполнитьЗначенияСвойств(Строка, Выборка);
    КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Поиск(Команда)
    ПоискНаСервере();
    Найдено = "Найдено " + Проблемы.Количество() + " этапов";
КонецПроцедуры
