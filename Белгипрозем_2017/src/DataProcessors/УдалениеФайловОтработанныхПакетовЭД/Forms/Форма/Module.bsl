////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗаполнитьТаблицуФайловОтработанныхПакетовЭД()
	
	ОтработанныеПакеты.Очистить();
	
	ЗапросПакетов = Новый Запрос;
	УдаляемыеСтатусыПакетов = Новый СписокЗначений;
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Доставлен);
	УдаляемыеСтатусыПакетов.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	ЗапросПакетов.УстановитьПараметр("СтатусПакета", УдаляемыеСтатусыПакетов);
	
	ЗапросПакетов.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	СообщенияЭДО.Ссылка КАК Документ,
		|	СообщенияЭДО.Документ.СтатусПакета КАК Статус,
		|	ЛОЖЬ КАК Выбран,
		|	СообщенияЭДО.ДатаСоздания КАК ДатаПолучения,
		|	СообщенияЭДО.Документ.Организация КАК Организация,
		|	СообщенияЭДО.Документ.Контрагент КАК Контрагент,
		|	СообщенияЭДО.Документ.Направление КАК Направление
		|ИЗ
		|	Справочник.СообщенияЭДО КАК СообщенияЭДО
		|ГДЕ
		|	СообщенияЭДО.Документ ССЫЛКА Документ.ПакетЭД
		|	И СообщенияЭДО.ПометкаУдаления = ЛОЖЬ
		|	И ВЫРАЗИТЬ(СообщенияЭДО.Документ КАК Документ.ПакетЭД).СтатусПакета В (&СтатусПакета)";
	
	ЗначениеВРеквизитФормы(ЗапросПакетов.Выполнить().Выгрузить(), "ОтработанныеПакеты");
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДанные()
	
	Для Каждого СтрокаУдаления Из ОтработанныеПакеты Цикл
		Если СтрокаУдаления.Выбран Тогда
			ОбъектУдаления = СтрокаУдаления.Документ.ПолучитьОбъект();
			ОбъектУдаления.ПометкаУдаления = Истина;
			ОбъектУдаления.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияФлажков(ПараметрУстановки)
	
	Для Каждого Строка Из ОтработанныеПакеты Цикл
		Строка.Выбран = ПараметрУстановки;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	УстановитьЗначенияФлажков(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	УстановитьЗначенияФлажков(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОтмеченныеПЭД(Команда)
	
	УдалитьДанные();
	ОбновитьДанныеПоТаблицам(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоТаблицам(Команда)
	
	ЗаполнитьТаблицуФайловОтработанныхПакетовЭД();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ОтработанныеПакетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьЭДДляПросмотра(Элементы.ОтработанныеПакеты.ТекущиеДанные.Документ);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуФайловОтработанныхПакетовЭД();
	
КонецПроцедуры
