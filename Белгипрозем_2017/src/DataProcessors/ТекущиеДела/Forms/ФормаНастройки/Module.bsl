////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;	
	
	Если Параметры.Свойство("СписокПоказателейСВидимостью") Тогда
		СписокПоказателей = Параметры.СписокПоказателейСВидимостью;
	КонецЕсли;	
	
	Если Параметры.Свойство("НастройкиАвтообновления") Тогда
		НастройкиАвтообновления = Параметры.НастройкиАвтообновления;
		Если ТипЗнч(НастройкиАвтообновления) = Тип("Структура") Тогда
			Автообновление = НастройкиАвтообновления.Автообновление;
			ПериодАвтообновления = НастройкиАвтообновления.ПериодАвтообновления;
		КонецЕсли;
	КонецЕсли;    		
	
	Элементы.ПериодАвтообновления.Доступность = Автообновление;
	               	
	// Дерево папок  	
	СохраненныеЗначенияРеквизитов = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы + "/ТекущиеДанные");
	
	Если СохраненныеЗначенияРеквизитов <> Неопределено Тогда		
		ОтображатьУдаленные = СохраненныеЗначенияРеквизитов.Получить("ОтображатьУдаленные");		
		ОтображатьТолькоМоиПапки = СохраненныеЗначенияРеквизитов.Получить("ОтображатьТолькоМоиПапки");		
	КонецЕсли;		
	
	Если Параметры.Свойство("ВыбранныеПапкиПисем") Тогда
		Если Параметры.ВыбранныеПапкиПисем <> Неопределено Тогда
			ДобавитьПапкиВСписокВыбранныхСервер(Параметры.ВыбранныеПапкиПисем);
		КонецЕсли;
	Иначе // Показатели почты недоступны, скрыть закладку настройки	почты
		Элементы.СтраницаПочта.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьДеревоПапок();
	
	СостояниеДереваПапок = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ЭтаФорма.ИмяФормы, 
		"СостояниеДерева", 
		Неопределено);	
	
	Элементы.ДеревоПапокКонтекстноеМенюТолькоМоиПапки.Пометка = ОтображатьТолькоМоиПапки;
	Элементы.ДеревоПапокКонтекстноеМенюОтображатьУдаленные.Пометка = ОтображатьУдаленные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСтроковоеПредставлениеВыбранныхПапок();
	
	Если СостояниеДереваПапок <> Неопределено Тогда
		ВосстановитьСостояниеДереваПапок(СостояниеДереваПапок);
	КонецЕсли;
	
	ИзмененоСостояниеДерева = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИспользоватьАвтообновлениеПриИзменении(Элемент)
	
	Элементы.ПериодАвтообновления.Доступность = Автообновление;
	Если ПериодАвтообновления < 60 Тогда
		ПериодАвтообновления = 300; // Значение по умолчанию если период еще не задан
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтообновленияПриИзменении(Элемент)
	
	Если ПериодАвтообновления < 60 Тогда
		ПериодАвтообновления = 60; // Не меньше 60 секунд
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СписокПоказателей

&НаКлиенте
Процедура СписокПоказателейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПоказателейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПоказателейПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокПоказателей.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
	Если ТекущиеДанные.Значение = "Контроль" И ТекущиеДанные.Пометка Тогда 
		ВидимостьПоказателей("Форум");
    ИначеЕсли ТекущиеДанные.Значение = "Форум" И ТекущиеДанные.Пометка Тогда 
		ВидимостьПоказателей("Контроль");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоПапок

&НаКлиенте
Процедура ДеревоПапокВыбранаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоПапок.ТекущиеДанные;
	
	ТекущаяСтрока = Элементы.ДеревоПапок.ТекущаяСтрока;
	ЭлементДерева = ДеревоПапок.НайтиПоИдентификатору(ТекущаяСтрока);
	ПометитьЭлементДереваВключаяДочерние(ЭлементДерева, ТекущиеДанные.Выбрана);
	
	ОбновитьСтроковоеПредставлениеВыбранныхПапок();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПапокПередРазворачиванием(Элемент, Строка, Отказ)
	
	ИзмененоСостояниеДерева = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПапокПередСворачиванием(Элемент, Строка, Отказ)
	
	ИзмененоСостояниеДерева = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура;
	
	Результат.Вставить("СписокПоказателейСВидимостью", СписокПоказателей);
	
	НастройкиАвтообновления = Новый Структура();
	НастройкиАвтообновления.Вставить("Автообновление", Автообновление);
	НастройкиАвтообновления.Вставить("ПериодАвтообновления", ПериодАвтообновления);
	
	Результат.Вставить("НастройкиАвтообновления", НастройкиАвтообновления);
	
	// Дерево папок
	Если ИзмененоСостояниеДерева Тогда
		СостояниеДерева = ЗапомнитьСостояниеДереваПапок();
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ЭтаФорма.ИмяФормы, "СостояниеДерева", СостояниеДерева);
	КонецЕсли;
	
	СписокВыбранныхПапок = Новый Массив;
	Для Каждого СтрокаВыбраннойПапки из ВыбранныеПапки Цикл
		СписокВыбранныхПапок.Добавить(СтрокаВыбраннойПапки.Ссылка);
	КонецЦикла;
	
	Результат.Вставить("ВыбранныеПапкиПисем", СписокВыбранныхПапок);
			
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоМоиПапки(Команда)
	
	ОтображатьТолькоМоиПапки = Не ОтображатьТолькоМоиПапки;
	Элементы.ДеревоПапокКонтекстноеМенюТолькоМоиПапки.Пометка = ОтображатьТолькоМоиПапки;
	ЗаполнитьДеревоПапок();
	
	ВосстановитьСостояниеДереваПапок(СостояниеДереваПапок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьУдаленные(Команда)
	
	ОтображатьУдаленные = Не ОтображатьУдаленные;
	Элементы.ДеревоПапокКонтекстноеМенюОтображатьУдаленные.Пометка = ОтображатьУдаленные;
	ЗаполнитьДеревоПапок();
	
	ВосстановитьСостояниеДереваПапок(СостояниеДереваПапок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Работа с деревом папок почты

&НаСервере
Процедура ЗаполнитьДеревоПапок()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиПисем.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.Ссылка В (&СписокВыбранныхПапок)
		|			ТОГДА Истина
		|		ИНАЧЕ Ложь
		|	КОНЕЦ КАК Выбрана,
		|	ПапкиПисем.ПометкаУдаления КАК ПометкаУдаления,
		|	ПапкиПисем.Представление КАК Представление,
		|	ПапкиПисем.ВидПапки КАК ВидПапки,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.ПометкаУдаления
		|			ТОГДА 1
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Черновики)
		|			ТОГДА 2
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Корзина)
		|			ТОГДА 4
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НомерКартинки,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Входящие)
		|			ТОГДА 1
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Исходящие)
		|			ТОГДА 2
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Отправленные)
		|			ТОГДА 3
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Корзина)
		|			ТОГДА 5
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Черновики)
		|			ТОГДА 6
		|		ИНАЧЕ 7
		|	КОНЕЦ КАК Порядок,
		|	ПапкиПисем.ВариантОтображенияКоличестваПисем,");
		
	Если ОтображатьТолькоМоиПапки Тогда
		Запрос.Текст = Запрос.Текст +
			"
			|	ЛОЖЬ КАК ПапкаБыстрогоДоступа";
	Иначе
		Запрос.Текст = Запрос.Текст +
			"
			|ВЫБОР
			|	КОГДА ПапкиПисемБыстрогоДоступа.Папка ЕСТЬ NULL 
			|		ТОГДА ЛОЖЬ
			|	ИНАЧЕ ИСТИНА
			|КОНЕЦ КАК ПапкаБыстрогоДоступа"
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +
		"
		|ИЗ
		|	Справочник.ПапкиПисем КАК ПапкиПисем
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
		|	ПО (ПапкиПисемБыстрогоДоступа.Папка = ПапкиПисем.Ссылка И ПапкиПисемБыстрогоДоступа.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	((НЕ ПапкиПисем.ПометкаУдаления)
		|			ИЛИ &ОтображатьУдаленные)";
		
	Если ОтображатьТолькоМоиПапки Тогда
		Запрос.Текст = Запрос.Текст +
			"
			|	И ПапкиПисем.Ссылка В ИЕРАРХИИ
			|		(ВЫБРАТЬ
			|			ПапкиПисемБыстрогоДоступа.Папка
			|		ИЗ
			|			РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
			|		ГДЕ
			|			ПапкиПисемБыстрогоДоступа.Пользователь = &ТекущийПользователь)";
	КонецЕсли;
		
	Запрос.Текст = Запрос.Текст +
		"
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка ИЕРАРХИЯ";
		
	Запрос.УстановитьПараметр("ОтображатьУдаленные", ОтображатьУдаленные);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	СписокВыбранныхПапок = Новый Массив;
	Для Каждого СтрокаВыбраннойПапки из ВыбранныеПапки Цикл
		СписокВыбранныхПапок.Добавить(СтрокаВыбраннойПапки.Ссылка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("СписокВыбранныхПапок", СписокВыбранныхПапок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ТекУчетнаяЗапись = Неопределено;
	СтрокаУчетнаяЗапись = Неопределено;
	
	ДеревоПапокОбъект = РеквизитФормыВЗначение("ДеревоПапок");
	ДеревоПапокОбъект.Строки.Очистить();
	ДобавитьПапкиВДерево(ДеревоПапокОбъект.Строки, Дерево.Строки);
	СортироватьИерархически(ДеревоПапокОбъект.Строки, "Порядок, Наименование");
	
	ЗначениеВДанныеФормы(ДеревоПапокОбъект, ДеревоПапок);
	
КонецПроцедуры

&НаСервере
Процедура СортироватьИерархически(СтрокиДерева, Знач Колонки)
	
	СтрокиДерева.Сортировать(Колонки);
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			СортироватьИерархически(СтрокаДерева.Строки, Колонки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПапкиВДерево(ДеревоСтроки, ИсточникСтроки)
	
	МенеджерПапокПисем = Справочники.ПапкиПисем;
	
	Для каждого ПапкаИнфо Из ИсточникСтроки Цикл
		СтрокаПапка = ДеревоСтроки.Добавить();
		СтрокаПапка.Ссылка = ПапкаИнфо.Ссылка;
		СтрокаПапка.Выбрана = ПапкаИнфо.Выбрана;
		СтрокаПапка.Наименование = ПапкаИнфо.Представление;
		СтрокаПапка.ПолныйПуть = МенеджерПапокПисем.ПолучитьПолныйПутьПапки(ПапкаИнфо.Ссылка);
		СтрокаПапка.ВидПапки = ПапкаИнфо.ВидПапки;
		СтрокаПапка.Порядок = ПапкаИнфо.Порядок;
		СтрокаПапка.НомерКартинки = ПапкаИнфо.НомерКартинки;
		СтрокаПапка.ВариантОтображенияКоличестваПисем = ПапкаИнфо.ВариантОтображенияКоличестваПисем;
		СтрокаПапка.ПапкаБыстрогоДоступа = ПапкаИнфо.ПапкаБыстрогоДоступа;
		СтрокаПапка.ПометкаУдаления = ПапкаИнфо.ПометкаУдаления;
		ДобавитьПапкиВДерево(СтрокаПапка.Строки, ПапкаИнфо.Строки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьЭлементДереваВключаяДочерние(ЭлементДерева, ПометкаВыбора)
	
	ЭлементДерева.Выбрана = ПометкаВыбора;
	
	ПараметрыОтбора = Новый Структура("Ссылка", ЭлементДерева.Ссылка);
	Если ПометкаВыбора Тогда
		Если ВыбранныеПапки.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
			НоваяСтрока = ВыбранныеПапки.Добавить();
			ЗаполнитьЗначенияСвойств(
				НоваяСтрока, 
				ДеревоПапок.НайтиПоИдентификатору(ЭлементДерева.ПолучитьИдентификатор()));	
		КонецЕсли;		
	Иначе			
		НайденныеСтроки = ВыбранныеПапки.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ВыбранныеПапки.Удалить(НайденныеСтроки[0]);
		КонецЕсли;
	КонецЕсли;		
	
	ПодчиненныеЭлементыДерева = ЭлементДерева.ПолучитьЭлементы();
	Для Каждого ПодчиненныйЭлементДерева Из ПодчиненныеЭлементыДерева Цикл		
		ПометитьЭлементДереваВключаяДочерние(ПодчиненныйЭлементДерева, ПометкаВыбора);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСостояниеДереваПапок(Состояние)
	
	Контекст = Новый Структура;
	Контекст.Вставить("Дерево", ДеревоПапок);
	Контекст.Вставить("ФормаДерево", Элементы.ДеревоПапок);
	Контекст.Вставить("Состояние", Состояние.Развернут);
	Контекст.Вставить("ТекСсылка", Состояние.ТекСсылка);
	ОбойтиДерево(ДеревоПапок.ПолучитьЭлементы(), Контекст, "УстановитьСостояниеРазвернут");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбойтиДерево(ДеревоЭлементы, Контекст, ИмяПроцедуры)
	
	Для каждого Элемент Из ДеревоЭлементы Цикл
		// Рекурсивный вызов
		ОбойтиДерево(Элемент.ПолучитьЭлементы(), Контекст, ИмяПроцедуры);
		Результат = Вычислить(ИмяПроцедуры + "(Элемент, Контекст)");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПапкиВСписокВыбранныхСервер(Папки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиПисем.Ссылка КАК Ссылка,
		|	ПапкиПисем.ПометкаУдаления КАК ПометкаУдаления,
		|	ВЫБОР
		|		КОГДА ПапкиПисем.ПометкаУдаления
		|			ТОГДА 1
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Черновики)
		|			ТОГДА 2
		|		КОГДА ПапкиПисем.ВидПапки = ЗНАЧЕНИЕ(Перечисление.ВидыПапокПисем.Корзина)
		|			ТОГДА 4
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НомерКартинки,
		|	ВЫБОР
		|		КОГДА ПапкиПисемБыстрогоДоступа.Папка ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ПапкаБыстрогоДоступа
		|ИЗ
		|	Справочник.ПапкиПисем КАК ПапкиПисем
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиПисемБыстрогоДоступа КАК ПапкиПисемБыстрогоДоступа
		|		ПО (ПапкиПисемБыстрогоДоступа.Папка = ПапкиПисем.Ссылка)
		|			И (ПапкиПисемБыстрогоДоступа.Пользователь = &ТекущийПользователь)
		|ГДЕ
		|	(ПапкиПисем.Ссылка В (&Папки))";
		
	Запрос.УстановитьПараметр("ТекущийПользователь", ПользователиКлиентСервер.ТекущийПользователь());
	Запрос.УстановитьПараметр("Папки", Папки);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ВыбранныеПапки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.ПолныйПуть = Справочники.ПапкиПисем.ПолучитьПолныйПутьПапки(Выборка.Ссылка);
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтроковоеПредставлениеВыбранныхПапок()
	
	ВыбранныеПапкиСтрока = "";
	
	Для каждого СтрокаПапки Из ВыбранныеПапки Цикл
		Если ЗначениеЗаполнено(ВыбранныеПапкиСтрока) Тогда
			ВыбранныеПапкиСтрока = ВыбранныеПапкиСтрока + "; ";
		КонецЕсли;
		ВыбранныеПапкиСтрока = ВыбранныеПапкиСтрока + СтрокаПапки.ПолныйПуть;		
	КонецЦикла; 
		
КонецПроцедуры

&НаКлиенте
Функция УстановитьСостояниеРазвернут(Элемент, Контекст)
	
	ИдентификаторСтроки = Элемент.ПолучитьИдентификатор();
	ТекДанные = Контекст.Дерево.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если Контекст.Состояние.Получить(ТекДанные.Ссылка) = Истина Тогда
		Контекст.ФормаДерево.Развернуть(ИдентификаторСтроки);
	Иначе
		Контекст.ФормаДерево.Свернуть(ИдентификаторСтроки);
	КонецЕсли;
	Если ТекДанные.Ссылка = Контекст.ТекСсылка Тогда
		Контекст.ФормаДерево.ТекущаяСтрока = ИдентификаторСтроки;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ЗапомнитьСостояниеДереваПапок()
	
	Состояние = Новый Структура;
	Состояние.Вставить("ТекСсылка", Неопределено);
	Если Элементы.ДеревоПапок.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Элементы.ДеревоПапок.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
			Состояние.ТекСсылка = ТекущиеДанные.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Дерево", ДеревоПапок);
	Контекст.Вставить("ФормаДерево", Элементы.ДеревоПапок);
	Контекст.Вставить("Состояние", Новый Соответствие);
	ОбойтиДерево(ДеревоПапок.ПолучитьЭлементы(), Контекст, "ЗапомнитьСостояниеРазвернут");
	Состояние.Вставить("Развернут", Контекст.Состояние);
	
	Возврат Состояние;
	
КонецФункции

&НаКлиенте
Функция ЗапомнитьСостояниеРазвернут(Элемент, Контекст)
	
	ИдентификаторСтроки = Элемент.ПолучитьИдентификатор();
	ТекДанные = Контекст.Дерево.НайтиПоИдентификатору(ИдентификаторСтроки);
	Контекст.Состояние.Вставить(ТекДанные.Ссылка, Контекст.ФормаДерево.Развернут(ИдентификаторСтроки));
	
КонецФункции

&НаКлиенте
Процедура ВидимостьПоказателей(ИмяПоказателя)
	
	КоличествоВидимыхВСтроке2 = 0;
	Для Каждого СтрокаСписка Из СписокПоказателей Цикл			
		Если СтрокаСписка.Пометка
			И (СтрокаСписка.Значение = "Форум" Или СтрокаСписка.Значение = "Контроль" 
			Или СтрокаСписка.Значение = "Мероприятия" Или СтрокаСписка.Значение = "ЗадачиПодчиненных") Тогда
			КоличествоВидимыхВСтроке2 = КоличествоВидимыхВСтроке2 + 1;
		КонецЕсли;	
	КонецЦикла;
	
	//В строке вывода может быть максимум 3 показателя, поэтому один уберем
	Если КоличествоВидимыхВСтроке2 = 4 Тогда  
		Для Каждого СтрокаСписка Из СписокПоказателей Цикл			
			Если СтрокаСписка.Значение = ИмяПоказателя Тогда 
				СтрокаСписка.Пометка = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла; 	
	КонецЕсли;	

КонецПроцедуры	
