&НаСервере
Процедура ОбновитьСписокДоговоров()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мОбменЕРИПСрезПоследних.Договор
		|ПОМЕСТИТЬ ЕРИП
		|ИЗ
		|	РегистрСведений.мОбменЕРИП.СрезПоследних КАК мОбменЕРИПСрезПоследних
		|ГДЕ
		|	мОбменЕРИПСрезПоследних.Договор.ДатаРегистрации МЕЖДУ &Дата1 И &Дата2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВнутренниеДокументы.Ссылка КАК Договор,
		|	ВнутренниеДокументы.Сумма,
		|	ВЫБОР
		|		КОГДА ВнутренниеДокументы.Сумма = 0
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВнутренниеДокументы.ВидДокумента = &ВидДокумента
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ
		|	КОНЕЦ КАК Использовать,
		|	ОбщиеРеквизитыДокументов.КорреспондентыДляСписков КАК Заказчик,
		|	ВнутренниеДокументы.Подготовил
		|ИЗ
		|	Справочник.ВнутренниеДокументы КАК ВнутренниеДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбщиеРеквизитыДокументов КАК ОбщиеРеквизитыДокументов
		|		ПО (ОбщиеРеквизитыДокументов.Документ = ВнутренниеДокументы.Ссылка)
		|ГДЕ
		|	ВнутренниеДокументы.ДатаРегистрации МЕЖДУ &Дата1 И &Дата2
		|	И НЕ ВнутренниеДокументы.Ссылка В
		|				(ВЫБРАТЬ
		|					ЕРИП.Договор
		|				ИЗ
		|					ЕРИП КАК ЕРИП)
		|	И (ВнутренниеДокументы.ВидДокумента = &ВидДокумента ИЛИ ВнутренниеДокументы.ВидДокумента = &ВидДокумента2)
		|	И ВнутренниеДокументы.Подготовил = &Подготовил";
	
	Если ЗначениеЗаполнено(Подготовил) Тогда
		Запрос.УстановитьПараметр("Подготовил", Подготовил);
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ВнутренниеДокументы.Подготовил = &Подготовил", "")
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("Дата1", Период.ДатаНачала);
	Запрос.УстановитьПараметр("Дата2", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ВидДокумента", Справочники.ВидыВнутреннихДокументов.НайтиПоНаименованию("Договор с физ. лицами"));
	Запрос.УстановитьПараметр("ВидДокумента2", Справочники.ВидыВнутреннихДокументов.НайтиПоНаименованию("Договор с юр. лицами"));
	
	Тз = РеквизитФормыВЗначение("Договоры", Тип("ТаблицаЗначений"));
	Тз = Запрос.Выполнить().Выгрузить();
	
	ЗначениеВРеквизитФормы(Тз, "Договоры");
КонецПроцедуры
 
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьСписокДоговоров();
КонецПроцедуры

&НаСервере
Функция ЗарегистрироватьНаСервере()
	ОплатитьДо = мОбменЕРИПСервер.ОплатитьЕРИПДо(ТекущаяДата());
	
	НаборЗаписей = РегистрыСведений.мОбменЕРИП.СоздатьНаборЗаписей();
	
	Для каждого Стр Из Договоры Цикл
		Если Стр.Использовать Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Период = ТекущаяДата();
			Запись.Договор = Стр.Договор;
			Запись.Сумма = Стр.Сумма;
			Запись.Состояние = Перечисления.мСостояниеОбменЕРИП.ДляОтправки;
			Запись.ОплатитьДо = ОплатитьДо;
		КонецЕсли; 
	КонецЦикла; 
	Колво = НаборЗаписей.Количество();
	Если Колво > 0 Тогда
		НаборЗаписей.Записать(Ложь);
	КонецЕсли; 
	Возврат Колво;
КонецФункции

&НаКлиенте
Процедура Зарегистрировать(Команда)
	Колво = ЗарегистрироватьНаСервере();
	Если Колво > 0 Тогда
		ПоказатьОповещениеПользователя("Зарегистрировано "+Колво+" договоров"); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодготовилПриИзменении(Элемент)
	ОбновитьСписокДоговоров();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокДоговоров();
КонецПроцедуры
