
#Область ПрограммныйИнтерфейс

// Записывает настройку отложенного старта для бизнес-процесса
//
// Параметры:
//   - БизнесПроцесс - БизнесПроцессСсылка - процесс для которогото происходит запись.
//   - ДатаОтложенногоСтарта - ДатаИВремя
//   - Состояние - ПеречислениеССылка.СостояниеОтложенногоСтарта
//
Процедура ЗаписатьНастройку(
	БизнесПроцесс, ДатаОтложенногоСтарта, Состояние, ПричинаОтменыОтложенногоСтарта = "") Экспорт
	
	Настройка = СоздатьМенеджерЗаписи();
	Настройка.БизнесПроцесс = БизнесПроцесс;
	Настройка.Прочитать();
	Настройка.БизнесПроцесс = БизнесПроцесс;
	Настройка.ДатаОтложенногоСтарта = ДатаОтложенногоСтарта;
	Настройка.Состояние = Состояние;
	Настройка.ПричинаОтменыОтложенногоСтарта = ПричинаОтменыОтложенногоСтарта;
	
	Настройка.Записать();
	
КонецПроцедуры

// Удаляет настройку отложенного старта для бизнес-процесса
//
// Параметры:
//   - БизнесПроцесс - БизнесПроцессСсылка - процесс для которогото происходит удаление настройки.
//
Процедура УдалитьНастроку(БизнесПроцесс) Экспорт
	
	Настройка = СоздатьМенеджерЗаписи();
	Настройка.БизнесПроцесс = БизнесПроцесс;
	Настройка.Удалить();
	
КонецПроцедуры

// Возвращает настройку отложенного старта для бизнес-процесса
//
// Параметры:
//   - БизнесПроцесс - БизнесПроцессСсылка - процесс для которогото происходит поиск настройки.
//
// Возвращаемое значение:
//   - Неопределено - Возвращается если не найдена настройка.
//   - Структура - если настройка найдена.
//       - БизнесПроцесс - БизнесПроцессСсылка
//       - ДатаОтложенногоСтарта - ДатаИВремя
//       - Состояние - ПеречислениеССылка.СостояниеОтложенногоСтарта
Функция ПолучитьНастройкуПроцесса(БизнесПроцесс) Экспорт
	
	Результат = Неопределено;
	
	Если Не ЗначениеЗаполнено(БизнесПроцесс) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкаОтложенногоСтартаБизнесПроцессов.БизнесПроцесс,
		|	НастройкаОтложенногоСтартаБизнесПроцессов.ДатаОтложенногоСтарта,
		|	НастройкаОтложенногоСтартаБизнесПроцессов.Состояние,
		|	НастройкаОтложенногоСтартаБизнесПроцессов.ПричинаОтменыОтложенногоСтарта
		|ИЗ
		|	РегистрСведений.НастройкаОтложенногоСтартаБизнесПроцессов КАК НастройкаОтложенногоСтартаБизнесПроцессов
		|ГДЕ
		|	НастройкаОтложенногоСтартаБизнесПроцессов.БизнесПроцесс = &БизнесПроцесс";
	Запрос.УстановитьПараметр("БизнесПроцесс", БизнесПроцесс);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Новый Структура;
		Результат.Вставить("БизнесПроцесс", Выборка.БизнесПроцесс);
		Результат.Вставить("ДатаОтложенногоСтарта", Выборка.ДатаОтложенногоСтарта);
		Результат.Вставить("Состояние", Выборка.Состояние);
		Результат.Вставить("ПричинаОтменыОтложенногоСтарта", Выборка.ПричинаОтменыОтложенногоСтарта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//Код процедур и функций
#КонецОбласти