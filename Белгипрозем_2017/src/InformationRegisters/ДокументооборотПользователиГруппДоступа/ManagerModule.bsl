
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбновитьВсе() Экспорт
	
	// Удаление всех записей регистра
	НаборЗаписей = РегистрыСведений.ДокументооборотПользователиГруппДоступа.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыДоступа.Ссылка
		|ИЗ
		|	Справочник.ГруппыДоступа КАК ГруппыДоступа";

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбновитьПоГруппеДоступа(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьПоГруппеДоступа(ГруппаДоступа) Экспорт
	
	НаборЗаписей = РегистрыСведений.ДокументооборотПользователиГруппДоступа.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаДоступа.Установить(ГруппаДоступа);
	НаборЗаписей.Записать();
	
	Для каждого Эл Из ГруппаДоступа.Пользователи Цикл
		
		Если ТипЗнч(Эл.Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
			
			НаборЗаписей = РегистрыСведений.ДокументооборотПользователиГруппДоступа.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ГруппаДоступа.Установить(ГруппаДоступа);
			НаборЗаписей.Отбор.Пользователь.Установить(Эл.Пользователь);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = Эл.Пользователь;
			НоваяЗапись.ГруппаДоступа = ГруппаДоступа;
			
			НаборЗаписей.Записать();
			
		ИначеЕсли ТипЗнч(Эл.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда	
			
			ПользователиГруппы = ДокументооборотПраваДоступаПовтИсп.ПолучитьСоставГруппыПользователей(Эл.Пользователь);
			
			Для каждого ПользовательГруппы Из ПользователиГруппы Цикл
				
				НаборЗаписей = РегистрыСведений.ДокументооборотПользователиГруппДоступа.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ГруппаДоступа.Установить(ГруппаДоступа);
				НаборЗаписей.Отбор.Пользователь.Установить(ПользовательГруппы.Пользователь);
			
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Пользователь = ПользовательГруппы.Пользователь;
				НоваяЗапись.ГруппаДоступа = ГруппаДоступа;
			
				НаборЗаписей.Записать();
				
			КонецЦикла;	
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
