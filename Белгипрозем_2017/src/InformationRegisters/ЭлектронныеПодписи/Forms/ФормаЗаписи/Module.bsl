////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = Неопределено;
	
	Если Не Параметры.Ключ.Пустой()Тогда
		
		МенеджерЗаписи = РаботаСЭП.ПолучитьЭлектроннуюПодпись(Запись.Объект, Запись.УстановившийПодпись,
			Запись.ДатаПодписи);
			
	ИначеЕсли Параметры.Свойство("ДатаПодписи") И Параметры.Свойство("Объект") 
		И Параметры.Свойство("УстановившийПодпись")
		И ТипЗнч(Параметры.УстановившийПодпись) = Тип("СправочникСсылка.Пользователи") Тогда
		
		МенеджерЗаписи = РаботаСЭП.ПолучитьЭлектроннуюПодпись(Параметры.Объект, Параметры.УстановившийПодпись,
			Параметры.ДатаПодписи);
			
	КонецЕсли;	
	
	Если МенеджерЗаписи = Неопределено Тогда
		ВызватьИсключение("Электронная подпись не найдена");
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");

	Если ТипЗнч(Запись.Объект) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		ПодписанныйОбъект = Запись.Объект.Владелец;
	Иначе
		ПодписанныйОбъект = Запись.Объект;
	КонецЕсли;
	
	ОбщийСтатусПроверки = РаботаСЭПКлиентСервер.ПолучитьОбщийСтатусПроверкиПодписи(Запись.ПодписьВерна,
		Запись.СертификатДействителен, Запись.ДатаПроверкиПодписи);
		
	СтатусПроверкиПодписи = РаботаСЭПКлиентСервер.ПолучитьСтатусПроверкиПодписи(Запись.ДатаПроверкиПодписи,
		Запись.ПодписьВерна, Запись.ТекстОшибкиПроверкиПодписи);
	СтатусПроверкиСертификата = РаботаСЭПКлиентСервер.ПолучитьСтатусПроверкиСертификата(Запись.ДатаПроверкиПодписи,
		Запись.СертификатДействителен, Запись.ТекстОшибкиПроверкиСертификата);
		
	Если Параметры.Свойство("АдресПодписи") Тогда
		АдресПодписи = Параметры.АдресПодписи;
	Иначе
		ДвоичныеДанные = МенеджерЗаписи.Подпись.Получить();
		Если ДвоичныеДанные <> Неопределено Тогда 
			АдресПодписи = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресСертификата") Тогда
		АдресСертификата = Параметры.АдресСертификата;
	Иначе
		ДвоичныеДанные = МенеджерЗаписи.Сертификат.Получить();
		Если ДвоичныеДанные <> Неопределено Тогда 
			АдресСертификата = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КомуВыданОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектроннаяПодписьКлиент.ОткрытьСертификатПоОтпечаткуИАдресу(Запись.Отпечаток, АдресСертификата);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ЭлектроннаяПодписьКлиент.СохранитьПодпись(АдресПодписи);
	
КонецПроцедуры
