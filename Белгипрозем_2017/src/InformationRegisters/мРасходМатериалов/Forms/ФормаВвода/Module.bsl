
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дело = Неопределено;
	Если Параметры.Свойство("АктПроверки") Тогда
		Если ЗначениеЗаполнено(Параметры.АктПроверки) Тогда
			Дело = Параметры.АктПроверки.Владелец;
		КонецЕсли; 
	КонецЕсли; 	
	
	Если Параметры.Свойство("Факт") Тогда
		Элементы.План.Доступность = Ложь;
		Элементы.Факт.Доступность = Истина;
	КонецЕсли; 
	
	ЗаполнитьТаблицуРасхода(Дело);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуРасхода(Дело)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мРасходМатериалов.Дело,
		|	мРасходМатериалов.Материал,
		|	мРасходМатериалов.План,
		|	мРасходМатериалов.Факт
		|ИЗ
		|	РегистрСведений.мРасходМатериалов КАК мРасходМатериалов
		|ГДЕ
		|	мРасходМатериалов.Дело = &Дело";
	
	Запрос.УстановитьПараметр("Дело", Дело);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаРасхода.Очистить();
	
	Пока Выборка.Следующий() Цикл
		Стр = ТаблицаРасхода.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка);
	КонецЦикла;
	
	// Дополнить недостающими материалами
	МассивМатериалов = мРаботаСоСметами.ПолучитьМассивРасходуемыхМатериалов();
	
	Для каждого Материал Из МассивМатериалов Цикл
		Отбор = Новый Структура("Материал", Материал); 
		ИскСтрока = ТаблицаРасхода.НайтиСтроки(Отбор);
		Если ИскСтрока.Количество() = 0 Тогда
			НовСтрока = ТаблицаРасхода.Добавить();
			НовСтрока.Материал = Материал;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьТаблицуРасхода(Дело)

&НаСервере
Процедура ЗаписатьЗакрытьНаСервере()
	НаборЗаписей = РегистрыСведений.мРасходМатериалов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Дело.Установить(Дело);
	
	Записывать = Ложь;
	
	Для каждого Стр Из ТаблицаРасхода Цикл
		Если Стр.Факт <> 0 Тогда
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Дело = Дело;
			ЗаписьНабора.Материал = Стр.Материал;
			ЗаписьНабора.План = Стр.План;
			ЗаписьНабора.Факт = Стр.Факт;
			
			Записывать = Истина;
		КонецЕсли; 
	КонецЦикла;
	Если Записывать Тогда
		НаборЗаписей.Записать();
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	ЗаписатьЗакрытьНаСервере();
	Закрыть();
КонецПроцедуры
 
 