// Возвращает норму времени
Функция НормаВремениЗИС(ЮрФизЛицо, ИспользуетГеопортал, ДатаСреза = Неопределено) Экспорт
	Если ДатаСреза = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мНормыВремениЗИССрезПоследних.НормаВремени
		|ИЗ
		|	РегистрСведений.мНормыВремениЗИС.СрезПоследних(
		|			&ДатаСреза,
		|			ЮрФизЛицо = &ЮрФизЛицо
		|				И ИспользуетГеопортал = &ИспользуетГеопортал) КАК мНормыВремениЗИССрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("ИспользуетГеопортал", ИспользуетГеопортал);
	Запрос.УстановитьПараметр("ЮрФизЛицо", ЮрФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.НормаВремени;
	КонецЦикла;
	
	Возврат 0;
КонецФункции 

// Возвращает Таблицу значений норм времени для массива договоров
Функция НормыВремениЗИСДляДоговоров(МассивДоговоров, ДатаСреза = Неопределено) Экспорт
	Если ДатаСреза = Неопределено Тогда
		ДатаСреза = ТекущаяДата();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мДоговорыЗИС.ИспользуетГеопортал,
		|	мДоговорыЗИС.Договор
		|ПОМЕСТИТЬ Геопортал
		|ИЗ
		|	РегистрСведений.мДоговорыЗИС КАК мДоговорыЗИС
		|ГДЕ
		|	мДоговорыЗИС.Договор В(&МассивДоговоров)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА мНормыВремениЗИССрезПоследних.ЮрФизЛицо = &ФизЛицо
		|				ТОГДА мНормыВремениЗИССрезПоследних.НормаВремени
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НормаВремениФЛ,
		|	Геопортал.Договор,
		|	СУММА(ВЫБОР
		|			КОГДА мНормыВремениЗИССрезПоследних.ЮрФизЛицо <> &ФизЛицо
		|				ТОГДА мНормыВремениЗИССрезПоследних.НормаВремени
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НормаВремениЮЛ
		|ИЗ
		|	Геопортал КАК Геопортал
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.мНормыВремениЗИС.СрезПоследних(
		|				&ДатаСреза,
		|				ИспользуетГеопортал В
		|					(ВЫБРАТЬ
		|						Геопортал.ИспользуетГеопортал
		|					ИЗ
		|						Геопортал КАК Геопортал)) КАК мНормыВремениЗИССрезПоследних
		|		ПО Геопортал.ИспользуетГеопортал = мНормыВремениЗИССрезПоследних.ИспользуетГеопортал
		|
		|СГРУППИРОВАТЬ ПО
		|	Геопортал.Договор";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("МассивДоговоров", МассивДоговоров);
	Запрос.УстановитьПараметр("ФизЛицо", Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции 
 