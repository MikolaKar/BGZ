
// Находит существующий дескриптор для объекта
Функция НайтиДескрипторДляОбъекта(ОбъектДоступа) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаДляОбъектов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДоступаДляОбъектов КАК ДескрипторыДоступаДляОбъектов
		|ГДЕ
		|	ДескрипторыДоступаДляОбъектов.Объект = &ОбъектДоступа";

	Запрос.УстановитьПараметр("ОбъектДоступа", ОбъектДоступа);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Дескриптор;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Заново определяет новый дескриптор для объекта
// При этом выполняется вычисление прав и привязка дескриптора к объекту
Функция ОпределитьДескрипторДляОбъекта(ОбъектДоступа) Экспорт
	
	ВнешняяТранзакция = ТранзакцияАктивна();
	Если Не ВнешняяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;	
	
	// Определение дескриптора
	Дескриптор = ДокументооборотПраваДоступа.ОпределитьДескрипторДоступаОбъекта(ОбъектДоступа);
	
	// Сохранение соответствия объекта доступа и его дескриптор
	Сохранить(Дескриптор, ОбъектДоступа);
	
	Если Не ВнешняяТранзакция Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;	
	
	Возврат Дескриптор;
	
КонецФункции

// Сохраняет соответствие объекта доступа и его дескриптора
Процедура Сохранить(Дескриптор, ОбъектДоступа, ОпределятьЗависимыеПрава = Истина) Экспорт
	
	СтарыйДескриптор = НайтиДескрипторДляОбъекта(ОбъектДоступа);
	
	Если СтарыйДескриптор <> Дескриптор Тогда
	
		НаборЗаписей = РегистрыСведений.ДескрипторыДоступаДляОбъектов.СоздатьНаборЗаписей();
	    НаборЗаписей.Отбор.Объект.Установить(ОбъектДоступа);
		
		Строка = НаборЗаписей.Добавить();
		Строка.Объект = ОбъектДоступа;
		Строка.Дескриптор = Дескриптор;
		Строка.ОбъектМетаданных = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Дескриптор, "ИдентификаторОбъектаМетаданных");
		
		Если Не ОпределятьЗависимыеПрава Тогда
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОпределениеЗависимыхПрав", Истина);
		КонецЕсли;
		
	    НаборЗаписей.Записать();
		
	КонецЕсли;	
	
КонецПроцедуры
