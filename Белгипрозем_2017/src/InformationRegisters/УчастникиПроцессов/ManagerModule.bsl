
Функция ПолучитьУчастников(Процесс) Экспорт
	
	Набор = РегистрыСведений.УчастникиПроцессов.СоздатьНаборЗаписей();
	Набор.Отбор.Процесс.Установить(Процесс);
	Набор.Прочитать();
	Возврат Набор.Выгрузить();
	
КонецФункции

Процедура ЗаписатьНаборПоПроцессу(Процесс, ТаблицаИсточник) Экспорт
	
	Если Не ЗначениеЗаполнено(Процесс) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.УчастникиПроцессов.СоздатьНаборЗаписей();
	Набор.Отбор.Процесс.Установить(Процесс);
	
	Для каждого ИсточникСтрока Из ТаблицаИсточник Цикл
		
		Если Не ЗначениеЗаполнено(ИсточникСтрока.Участник) Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = Набор.Добавить();
		Запись.Процесс = Процесс;
		Запись.Участник = ИсточникСтрока.Участник;
		Запись.ОсновнойОбъектАдресации = ИсточникСтрока.ОсновнойОбъектАдресации;
		Запись.ДополнительныйОбъектАдресации = ИсточникСтрока.ДополнительныйОбъектАдресации;
		
	КонецЦикла;
	
	Набор.Записать(Истина);
	
КонецПроцедуры

Процедура ОбновитьВсеДанные() Экспорт
	
	Набор = РегистрыСведений.УчастникиПроцессов.СоздатьНаборЗаписей();
	Набор.Записать();
	
	ШаблонТекстаЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТаблицаПроцесса.Ссылка
		|ИЗ
		|	БизнесПроцесс.%ИмяПроцесса% КАК ТаблицаПроцесса
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиПроцессов КАК УчастникиПроцессов
		|		ПО ТаблицаПроцесса.Ссылка = УчастникиПроцессов.Процесс
		|ГДЕ
		|	УчастникиПроцессов.Процесс ЕСТЬ NULL
		|	И НЕ ТаблицаПроцесса.Ссылка В (&МассивИсключений)";
		
	МассивИсключений = Новый Массив;
	ТаблицаНабора = РегистрыСведений.УчастникиПроцессов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		
	Для Каждого МетаданныеПроцесса Из Метаданные.БизнесПроцессы Цикл
		
		РезультатПустой = Ложь;
		Пока Не РезультатПустой Цикл
			
			Запрос = Новый Запрос(СтрЗаменить(ШаблонТекстаЗапроса, "%ИмяПроцесса%", МетаданныеПроцесса.Имя));
			Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
			Результат = Запрос.Выполнить();
			РезультатПустой = Результат.Пустой();
			
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ПроцессСсылка = Выборка.Ссылка;
				ПроцессОбъект = ПроцессСсылка.ПолучитьОбъект();
				
				ТаблицаНабора.Очистить();
				
				ЕстьУчастники = Ложь;
				Участники = ПраваДоступаНаБизнесПроцессыВызовСервера.ПолучитьТаблицуУчастниковПроцесса(ПроцессОбъект);
				Для Каждого Участник Из Участники Цикл
					ЕстьУчастники = ЗначениеЗаполнено(Участник.Участник);
					Запись = ТаблицаНабора.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, Участник);
					Запись.Процесс = ПроцессСсылка;
				КонецЦикла;
				
				СоставРГ = РегистрыСведений.РабочиеГруппы.ПолучитьУчастниковПоОбъекту(ПроцессСсылка);
				Для Каждого СтрокаРГ Из СоставРГ Цикл
					ЕстьУчастники = ЗначениеЗаполнено(СтрокаРГ.Участник);
					Запись = ТаблицаНабора.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, СтрокаРГ);
					Запись.Процесс = ПроцессСсылка;
				КонецЦикла;
				
				ТаблицаНабора.Свернуть("Процесс, Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
				РегистрыСведений.УчастникиПроцессов.ЗаписатьНаборПоПроцессу(ПроцессСсылка, ТаблицаНабора);
				
				Если Не ЕстьУчастники Тогда
					МассивИсключений.Добавить(ПроцессСсылка);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
