
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Устанавливает признак отображения графика нагрузки пользователя.
//
Процедура УстановитьПризнакОтображенияГрафикаНагрузки(ПоказыватьГрафикНагрузкиПользователя, Знач Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запись = РегистрыСведений.НастройкиТекущихДел.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Прочитать();
	
	Если ПоказыватьГрафикНагрузкиПользователя 
		И Не Запись.Выбран() Тогда
		// Если установка признака отображения (Истина) происходит в первый раз, 
		// то сразу рассчитать показатели для пользователя
		ПользователиДляРасчета = Новый Массив();
		ПользователиДляРасчета.Добавить(Пользователь);	
		ТекущиеДелаСервер.РассчитатьПоказателиНагрузкиДляПользователей(ПользователиДляРасчета);
	КонецЕсли;
	
	Запись.Пользователь = Пользователь;
	Запись.ПоказыватьГрафикНагрузкиПользователя = ПоказыватьГрафикНагрузкиПользователя;	
	Запись.Записать(Истина);
	
КонецПроцедуры

// Возвращает признак отображения графика нагрузки пользователя.
//
Функция ПолучитьПризнакОтображенияГрафикаНагрузки(Знач Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запись = РегистрыСведений.НастройкиТекущихДел.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Запись.ПоказыватьГрафикНагрузкиПользователя;
	
КонецФункции

// Возвращает массив пользователей с установленным признаком отображения графика нагрузки
//
Функция ПользователиСПризнакомОтображенияГрафикаНагрузки() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиТекущихДел.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.НастройкиТекущихДел КАК НастройкиТекущихДел
		|ГДЕ
		|	НастройкиТекущихДел.ПоказыватьГрафикНагрузкиПользователя = ИСТИНА
		|	И НастройкиТекущихДел.Пользователь.ПометкаУдаления = ЛОЖЬ
		|	И НастройкиТекущихДел.Пользователь.Недействителен = ЛОЖЬ";

	Результат = Запрос.Выполнить();

	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Результат.Выгрузить().ВыгрузитьКолонку("Пользователь");
	КонецЕсли;	
    	
КонецФункции

#КонецЕсли
