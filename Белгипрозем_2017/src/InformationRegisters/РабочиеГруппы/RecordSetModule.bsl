
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		
		ОбработанныеОбъекты = Новый Соответствие;
		ОписаниеТиповВходящихВМеханизмПрав = 
		Метаданные.РегистрыСведений.ДескрипторыДоступаДляОбъектов.Измерения.Объект.Тип;
		
		Для Каждого Запись Из ЭтотОбъект Цикл
			
			ТекущийОбъект = Запись.Объект;
			Если ОбработанныеОбъекты.Получить(ТекущийОбъект) = Неопределено Тогда
				ТипОбъекта = ТипЗнч(ТекущийОбъект);
				Если ОписаниеТиповВходящихВМеханизмПрав.СодержитТип(ТипОбъекта)
					И ТипОбъекта <> Тип("ЗадачаСсылка.ЗадачаИсполнителя")
					И Не ОбщегоНазначения.ЭтоБизнесПроцесс(ТекущийОбъект.Метаданные()) Тогда
					
					Если РегистрыСведений.ДескрипторыДоступаДляОбъектов.НайтиДескрипторДляОбъекта(ТекущийОбъект) <> Неопределено Тогда
						ДокументооборотПраваДоступа.ОпределитьДескрипторДоступаОбъекта(ТекущийОбъект);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			ОбработанныеОбъекты.Вставить(ТекущийОбъект);
			
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	ОбъектОтбора = ЭтотОбъект.Отбор.Объект.Значение;
	Если ЗначениеЗаполнено(ОбъектОтбора) И ОбщегоНазначения.ЭтоБизнесПроцесс(ОбъектОтбора.Метаданные()) Тогда
		
		// Запись в РС УчастникиПроцессов
		НаборИзменен = Ложь;
		ТаблицаНабора = РегистрыСведений.УчастникиПроцессов.ПолучитьУчастников(ОбъектОтбора);
		СтруктураПоиска = Новый Структура("Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
		
		Для Каждого СтрокаРГ Из ЭтотОбъект Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаРГ);
			НайденныеСтроки = ТаблицаНабора.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НаборИзменен = Истина;
				ЗаполнитьЗначенияСвойств(ТаблицаНабора.Добавить(), СтрокаРГ);
			КонецЕсли;
		КонецЦикла;
		
		Если НаборИзменен Тогда
			РегистрыСведений.УчастникиПроцессов.ЗаписатьНаборПоПроцессу(ОбъектОтбора, ТаблицаНабора);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
