// Записывает набор записей в регистр сведений с отбором по объекту
//
Процедура ЗаписатьНаборПоОбъекту(Объект, ТаблицаИсточник, ОбновитьПраваДоступа) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.РабочиеГруппы.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	
	Для каждого ИсточникСтрока Из ТаблицаИсточник Цикл
		
		Если Не ЗначениеЗаполнено(ИсточникСтрока.Участник) Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = Набор.Добавить();
		Запись.Объект = Объект;
		Запись.Участник = ИсточникСтрока.Участник;
		Запись.ОсновнойОбъектАдресации = ИсточникСтрока.ОсновнойОбъектАдресации;
		Запись.ДополнительныйОбъектАдресации = ИсточникСтрока.ДополнительныйОбъектАдресации;
	КонецЦикла;
	
	Набор.Записать(Истина);
	
	// Обновление прав доступа
	Если ОбновитьПраваДоступа Тогда
		ДокументооборотПраваДоступа.ОпределитьДескрипторДоступаОбъекта(Объект);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает таблицу участников рабочей группы по объекту.
//
Функция ПолучитьУчастниковПоОбъекту(Объект) Экспорт
	
	Набор = РегистрыСведений.РабочиеГруппы.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	Набор.Прочитать();
	Возврат Набор.Выгрузить();
	
КонецФункции

// Возвращает пустую таблицу участников рабочей группы.
//
Функция ПолучитьПустуюТаблицуУчастников() Экспорт
	
	Возврат РегистрыСведений.РабочиеГруппы.СоздатьНаборЗаписей().ВыгрузитьКолонки(
		"Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации");
	
КонецФункции

// Возвращает таблицу участников рабочей группы.
//
Функция ПолучитьТаблицуУчастниковПоОбъекту(Объект) Экспорт
	
	Набор = РегистрыСведений.РабочиеГруппы.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	Набор.Прочитать();
	
	Результат = Новый Массив;
	
	Для каждого Запись Из Набор Цикл
		
		Результат.Добавить( 
			Новый Структура("Участник, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации",
				Запись.Участник, Запись.ОсновнойОбъектАдресации, Запись.ДополнительныйОбъектАдресации));
				
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции