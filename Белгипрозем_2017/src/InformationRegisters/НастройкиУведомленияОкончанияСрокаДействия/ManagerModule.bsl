#Область ПрограммныйИнтерфейс

// Возвращает настройку пользователя уведомления о сроке задачи
Функция ПолучитьНастройку(Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиУведомленияОкончанияСрокаДействия.СрокУведомления,
	|	НастройкиУведомленияОкончанияСрокаДействия.ЧастотаУведомления
	|ИЗ
	|	РегистрСведений.НастройкиУведомленияОкончанияСрокаДействия КАК НастройкиУведомленияОкончанияСрокаДействия
	|ГДЕ
	|	НастройкиУведомленияОкончанияСрокаДействия.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		НастройкаУведомлений = 
			Новый Структура("СрокУведомления, ЧастотаУведомления",
			Выборка.СрокУведомления,
			Выборка.ЧастотаУведомления);
		
		Возврат НастройкаУведомлений;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Записывает настройку уведомления о сроке задачи
Процедура СохранитьНастройку(Пользователь, СрокУведомления, ЧастотаУведомления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись = РегистрыСведений.НастройкиУведомленияОкончанияСрокаДействия.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Прочитать();
	
	Запись.Пользователь = Пользователь;
	Запись.СрокУведомления = СрокУведомления;
	Запись.ЧастотаУведомления = ЧастотаУведомления;
	Запись.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
