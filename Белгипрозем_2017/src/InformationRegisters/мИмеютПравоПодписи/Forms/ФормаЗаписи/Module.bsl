
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    _Организация = Неопределено;
	Параметры.Свойство("Организация", _Организация);
    Если ЗначениеЗаполнено(_Организация) Тогда
        Запись.Организация = _Организация; 
    КонецЕсли; 
   
    _ВидДокумента = Неопределено;
	Параметры.Свойство("ВидДокумента", _ВидДокумента);
    Если ЗначениеЗаполнено(_ВидДокумента) Тогда
        Запись.ВидДокумента = _ВидДокумента;
    КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура УбратьЛишнихПоУмолчанию(ПараметрыЗаписи)
	//  не должно быть двух записей по одной организации и виду дока и с флагом по умолчанию
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	мИмеютПравоПодписи.Организация,
		|	мИмеютПравоПодписи.ВидДокумента,
		|	мИмеютПравоПодписи.Пользователь,
		|	мИмеютПравоПодписи.ПоУмолчанию
		|ИЗ
		|	РегистрСведений.мИмеютПравоПодписи КАК мИмеютПравоПодписи
		|ГДЕ
		|	мИмеютПравоПодписи.ПоУмолчанию = &ПоУмолчанию
		|	И мИмеютПравоПодписи.Организация = &Организация
		|	И мИмеютПравоПодписи.ВидДокумента = &ВидДокумента
		|	И мИмеютПравоПодписи.Пользователь <> &Пользователь";
	
	Запрос.УстановитьПараметр("ВидДокумента", ПараметрыЗаписи.ВидДокумента);
	Запрос.УстановитьПараметр("Организация", ПараметрыЗаписи.Организация);
	Запрос.УстановитьПараметр("Пользователь", ПараметрыЗаписи.Пользователь);
	Запрос.УстановитьПараметр("ПоУмолчанию", Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		МенеджерЗаписи = РегистрыСведений.мИмеютПравоПодписи.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ВидДокумента	= Выборка.ВидДокумента;
		МенеджерЗаписи.Организация 	= Выборка.Организация;
		МенеджерЗаписи.Пользователь	= Выборка.Пользователь;
		МенеджерЗаписи.ПоУмолчанию	= Выборка.ПоУмолчанию;
		МенеджерЗаписи.Прочитать();
		
		МенеджерЗаписи.ВидДокумента	= Выборка.ВидДокумента;
		МенеджерЗаписи.Организация 	= Выборка.Организация;
		МенеджерЗаписи.Пользователь	= Выборка.Пользователь;
		МенеджерЗаписи.ПоУмолчанию	= Ложь;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// если этот объект с флагом по умолчанию, то
	//  не должно быть других по этой организации и виду дока с флагом по умолчанию
	Если ТекущийОбъект.ПоУмолчанию Тогда
		ПараметрыЗаписи.Вставить("Организация", ТекущийОбъект.Организация);
		ПараметрыЗаписи.Вставить("ВидДокумента", ТекущийОбъект.ВидДокумента);
		ПараметрыЗаписи.Вставить("Пользователь", ТекущийОбъект.Пользователь);
		ПараметрыЗаписи.Вставить("ПоУмолчанию", ТекущийОбъект.ПоУмолчанию);
		УбратьЛишнихПоУмолчанию(ПараметрыЗаписи);	
	КонецЕсли;
КонецПроцедуры
 