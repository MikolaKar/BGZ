
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Добавляет указанные права указанного пользователя или роли к набору записей
// Производит автоматическое разыменование роли до конкретных пользователей
Процедура ДобавитьПользователя(
	Пользователь, 
	ОсновнойОбъектАдресации, 
	ДополнительныйОбъектАдресации,
	Чтение,
	Добавление,
	Изменение,
	Удаление,
	УправлениеПравами,
	ПроверятьДубли = Истина) Экспорт
	
	// Если пользователь не указан, то запись не добавляется
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;	
		
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Ст = Новый Структура;
		Ст.Вставить("Пользователь", Пользователь);
		Ст.Вставить("Чтение", Чтение);
		Ст.Вставить("Добавление", Добавление);
		Ст.Вставить("Изменение", Изменение);
		Ст.Вставить("Удаление", Удаление);
		Ст.Вставить("УправлениеПравами", УправлениеПравами);
		
		ДобавитьПраваВНаборПрав(Ст, ПроверятьДубли);
		
	ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
		
		ИсполнителиРоли = РегистрыСведений.ИсполнителиЗадач.ПолучитьИсполнителейРоли(
			Пользователь,
			ОсновнойОбъектАдресации, 
			ДополнительныйОбъектАдресации);
			
		Ст = Новый Структура;
		Ст.Вставить("Пользователь", Неопределено);
		Ст.Вставить("Чтение", Чтение);
		Ст.Вставить("Добавление", Добавление);
		Ст.Вставить("Изменение", Изменение);
		Ст.Вставить("Удаление", Удаление);
		Ст.Вставить("УправлениеПравами", УправлениеПравами);
		
		Для каждого Эл Из ИсполнителиРоли Цикл
			
			Ст.Пользователь = Эл.Исполнитель;
			ДобавитьПраваВНаборПрав(Ст, ПроверятьДубли);
			
		КонецЦикла;				
		
	ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда	
		
		ПользователиГруппы = ДокументооборотПраваДоступаПовтИсп.ПолучитьСоставГруппыПользователей(Пользователь);
		
		Ст = Новый Структура;
		Ст.Вставить("Пользователь", Неопределено);
		Ст.Вставить("Чтение", Чтение);
		Ст.Вставить("Добавление", Добавление);
		Ст.Вставить("Изменение", Изменение);
		Ст.Вставить("Удаление", Удаление);
		Ст.Вставить("УправлениеПравами", УправлениеПравами);
		
		Для каждого Эл Из ПользователиГруппы Цикл
			
			Ст.Пользователь = Эл.Пользователь;
			ДобавитьПраваВНаборПрав(Ст, ПроверятьДубли);
			
		КонецЦикла;
		
	Иначе
		
		ВызватьИсключение(НСтр("ru = 'Неизвестный тип пользователя.'"));
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ДобавитьПраваВНаборПрав(НоваяЗапись, ПроверятьДубли = Истина)
	
	// Поиск существующей записи для указанного пользователя
	Если ПроверятьДубли Тогда
		
		Для каждого Эл Из ЭтотОбъект Цикл
			
			Если Эл.Пользователь = НоваяЗапись.Пользователь Тогда
				
				Эл.Чтение = Эл.Чтение ИЛИ НоваяЗапись.Чтение;
				Эл.Добавление = Эл.Добавление ИЛИ НоваяЗапись.Добавление;
				Эл.Изменение = Эл.Изменение ИЛИ НоваяЗапись.Изменение;
				Эл.Удаление = Эл.Удаление ИЛИ НоваяЗапись.Удаление;
				Эл.УправлениеПравами = Эл.УправлениеПравами ИЛИ НоваяЗапись.УправлениеПравами;
				
				Возврат;
				
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЕсли;	
		
	// Добавление новой записи
	Запись = Добавить();
	Запись.Пользователь = НоваяЗапись.Пользователь;
	Запись.Чтение = НоваяЗапись.Чтение;
	Запись.Добавление = НоваяЗапись.Добавление;
	Запись.Изменение = НоваяЗапись.Изменение;
	Запись.Удаление = НоваяЗапись.Удаление;
	Запись.УправлениеПравами = НоваяЗапись.УправлениеПравами;
	
КонецПроцедуры

#КонецЕсли
