
// Находит дескриптор доступа для указанного файла
Функция НайтиДескрипторДляФайла(Файл) Экспорт
	
	ВладелецФайла = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Файл, "ВладелецФайла");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДескрипторыДоступаДляФайлов.Дескриптор
		|ИЗ
		|	РегистрСведений.ДескрипторыДоступаДляФайлов КАК ДескрипторыДоступаДляФайлов
		|ГДЕ
		|	ДескрипторыДоступаДляФайлов.ВладелецФайла = &ВладелецФайла";

	Запрос.УстановитьПараметр("ВладелецФайла", ВладелецФайла);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Дескриптор;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Сохраняет соответствие между владельцем файла и дескриптором доступа
Процедура Сохранить(Дескриптор, ВладелецФайла) Экспорт 
	
	НаборЗаписей = РегистрыСведений.ДескрипторыДоступаДляФайлов.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.ВладелецФайла.Установить(ВладелецФайла);
	
	Строка = НаборЗаписей.Добавить();
	Строка.ВладелецФайла = ВладелецФайла;
	Строка.Дескриптор = Дескриптор;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДескрипторыДоступаОбъектов.ИдентификаторОбъектаМетаданных
		|ИЗ
		|	Справочник.ДескрипторыДоступаФайлов КАК ДескрипторыДоступаФайлов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДескрипторыДоступаОбъектов КАК ДескрипторыДоступаОбъектов
		|		ПО ДескрипторыДоступаФайлов.ДескрипторВладельца = ДескрипторыДоступаОбъектов.Ссылка
		|ГДЕ
		|	ДескрипторыДоступаФайлов.Ссылка = &ДескрипторФайла");
		
	Запрос.УстановитьПараметр("ДескрипторФайла", Дескриптор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбъектМетаданных = Неопределено;
	Если Выборка.Следующий() Тогда
		ОбъектМетаданных = Выборка.ИдентификаторОбъектаМетаданных;
	Иначе
		ТипВладельца = ТипЗнч(ВладелецФайла);
		ОбъектМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипВладельца);
	КонецЕсли;
	
	Строка.ОбъектМетаданных = ОбъектМетаданных;
	
    НаборЗаписей.Записать();
	
КонецПроцедуры	
