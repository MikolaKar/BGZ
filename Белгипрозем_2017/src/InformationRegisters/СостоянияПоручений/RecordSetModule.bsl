
Процедура ПриЗаписи(Отказ, Замещение)
	
	Для НомерЗаписи = 0 По Количество() - 1 Цикл
		
		Документ = Получить(НомерЗаписи).Документ;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СостоянияПорученийСрезПоследних.Состояние,
			|	СостоянияПорученийСрезПоследних.Установил
			|ИЗ
			|	РегистрСведений.СостоянияПоручений.СрезПоследних(, Документ = &Документ) КАК СостоянияПорученийСрезПоследних";
			
		Запрос.УстановитьПараметр("Документ", Документ);	
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Таблица = РезультатЗапроса.Выгрузить();
			
			// Запишем в регистр ТекущиеСостоянияДокументов
			ЗаписатьВРегистрТекущиеСостоянияПоручений(Документ, Таблица[0].Состояние, Таблица[0].Установил);
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьВРегистрТекущиеСостоянияПоручений(Документ, Состояние, Установил)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Запишем в регистр ТекущиеСостоянияПоручений
	МенеджерЗаписи = РегистрыСведений.ТекущиеСостоянияПоручений.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = Документ;
	МенеджерЗаписи.Состояние = Состояние;
	МенеджерЗаписи.Установил = Установил;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры
