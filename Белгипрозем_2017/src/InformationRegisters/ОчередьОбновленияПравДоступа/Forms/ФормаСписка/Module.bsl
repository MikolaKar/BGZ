&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОчереди = "Оперативная";
	ОбновитьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСервер()
	
	Список.Отбор.Элементы.Очистить();
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приоритет");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ПравоеЗначение = ТекущийПриоритет();
	
	Элементы.Список.Обновить();
	ОбновитьКоличество();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличество()
	
	Заголовок = НСтр("ru = 'Очередь обновления доступа (%1)'");
	Заголовок = СтрЗаменить(Заголовок, "%1", Строка(РазмерОчереди()));
		
КонецПроцедуры	

&НаСервере
Функция РазмерОчереди()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК ЧислоЗаписей
		|ИЗ
		|	РегистрСведений.ОчередьОбновленияПравДоступа КАК ОчередьОбновленияПравДоступа
		|ГДЕ
		|	ОчередьОбновленияПравДоступа.Приоритет = &Приоритет";

	Запрос.УстановитьПараметр("Приоритет", ТекущийПриоритет());	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Возврат ВыборкаДетальныеЗаписи.ЧислоЗаписей;
		
	КонецЕсли;
	
	Возврат 0;

КонецФункции

&НаКлиенте
Процедура Очистить(Команда)
	
	ТекстВопроса = НСтр("ru = 'Внимание! 
		|Очистка приведет к тому, что стоящие в очереди права не будут обновлены. 
		|Очистить очередь?'");
		
	ОписаниеОповещения = 
		Новый ОписаниеОповещения("ОчиститьПродолжение", ЭтотОбъект);
		
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПродолжение(Ответ, Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСервер()
	
	Набор = РегистрыСведений.ОчередьОбновленияПравДоступа.СоздатьНаборЗаписей();
	Набор.Отбор.Приоритет.Установить(ТекущийПриоритет());
	Набор.Записать();
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПорцию(Команда)
	
	ОбновитьПорциюСервер();
	
КонецПроцедуры

&НаСервере
Функция ОбновитьПорциюСервер(РазмерПорции = 10)
	
	ПриоритетСеанса = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
	Осталось = 0;
	
	Попытка
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритет();
		Осталось = РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьПорцию(РазмерПорции);
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ПриоритетСеанса;
		
	Исключение	
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ПриоритетСеанса;
		ВызватьИсключение;
		
	КонецПопытки;
	
	ОбновитьСервер();
	
	Возврат Осталось;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьВыбранные(Команда)
	
	ОбновитьВыбранныеСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВыбранныеСервер()
	
	ПриоритетСеанса = ПараметрыСеанса.ПриоритетОчередиОбновленияПрав;
	
	Попытка
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ТекущийПриоритет();
		
		Для каждого Эл Из Элементы.Список.ВыделенныеСтроки Цикл
			
			РегистрыСведений.ОчередьОбновленияПравДоступа.ОбработатьЭлементОчереди(
				Эл.Объект, Эл.ОбновлениеЗависимыхПрав);
			
		КонецЦикла;	
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ПриоритетСеанса;
		
	Исключение	
		
		ПараметрыСеанса.ПриоритетОчередиОбновленияПрав = ПриоритетСеанса;
		ВызватьИсключение;
		
	КонецПопытки;
	
	ОбновитьСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элементы.Список.ТекущиеДанные.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВсе(Команда)
	
	Состояние(НСтр("ru = 'Выполняется обработка очереди обновления прав.
		|Пожалуйста подождите ...'"));
		
	ДатаНачала = ТекущаяДата();
	
	ОбщееЧисло = РазмерОчереди();
	Обработано = 0;
	РазмерПорции = 100;
	
	Осталось = ОбновитьПорциюСервер(РазмерПорции);
	Пока Осталось <> 0 Цикл
		
		Обработано = Обработано + РазмерПорции;
		Если Обработано > ОбщееЧисло Тогда
			Обработано = 0;
			ОбщееЧисло = РазмерОчереди();
		КонецЕсли;	
		
		ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
 			НСтр("ru = 'Обработано: %1 из %2'"),
			Обработано, ОбщееЧисло);
			
		Если ОбщееЧисло <> 0 Тогда	
			Процент = (Обработано*100)/ОбщееЧисло;
		Иначе	
			Процент = 100;
		КонецЕсли;
		
		Состояние(
			НСтр("ru = 'Выполняется обработка очереди обновления прав...'"),
			Процент,
			ТекстПояснения,
			БиблиотекаКартинок.Информация32);
		
		Осталось = ОбновитьПорциюСервер(РазмерПорции);
		
	КонецЦикла;	
	
	ДатаОкончания = ТекущаяДата();
	
	Состояние("");
	
	ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
 		НСтр("ru = 'Обработка очереди обновления прав завершена (%1 сек).'"),
		ДатаОкончания - ДатаНачала);
		
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОчередиПриИзменении(Элемент)
	
	ОбновитьСервер();
	
КонецПроцедуры

Функция ТекущийПриоритет()
	
	Приоритет = 1;
	Если ВидОчереди <> "Оперативная" Тогда
		Приоритет = 2;
	КонецЕсли;
	
	Возврат Приоритет;
	
КонецФункции	


