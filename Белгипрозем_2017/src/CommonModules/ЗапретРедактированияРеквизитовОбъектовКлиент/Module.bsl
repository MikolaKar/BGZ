////////////////////////////////////////////////////////////////////////////////
// Подсистема "Запрет редактирования реквизитов объектов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Разрешает редактирование заблокированных элементов формы, связанных с заданными реквизитами.
//
// Параметры:
//  Форма        - УправляемаяФорма - форма, в которой требуется разрешить
//                 редактирование элементов формы, заданных реквизитов.
//
//  ОбработкаПродолжения - Неопределено - никаких действий после выполнения процедуры.
//                         ОписаниеОповещения, которое вызывается после выполнения процедуры,
//                         в обработку передается параметр Успех типа Булево:
//                         Истина - ссылок не обнаружено или решение пользователя,
//                         Ложь   - видимых заблокированных реквизитов нет или
//                                  ссылки обнаружены и пользователь отказался от продолжения.
//
Процедура РазрешитьРедактированиеРеквизитовОбъекта(Знач Форма, ОбработкаПродолжения = Неопределено) Экспорт
	
	ЗаблокированныеРеквизиты = Реквизиты(Форма);
	
	Если ЗаблокированныеРеквизиты.Количество() = 0 Тогда
		ПоказатьПредупреждениеВсеВидимыеРеквизитыРазблокированы(
			Новый ОписаниеОповещения("РазрешитьРедактированиеРеквизитовОбъектаПослеПредупреждения",
				ЭтотОбъект, ОбработкаПродолжения));
		Возврат;
	КонецЕсли;
	
	СинонимыРеквизитов = Новый Массив;
	
	Для Каждого ОписаниеРеквизита Из Форма.ПараметрыЗапретаРедактированияРеквизитов Цикл
		Если ЗаблокированныеРеквизиты.Найти(ОписаниеРеквизита.ИмяРеквизита) <> Неопределено Тогда
			СинонимыРеквизитов.Добавить(ОписаниеРеквизита.Представление);
		КонецЕсли;
	КонецЦикла;
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Форма.Объект.Ссылка);
	
	Параметры = Новый Структура;
	Параметры.Вставить("Форма", Форма);
	Параметры.Вставить("ЗаблокированныеРеквизиты", ЗаблокированныеРеквизиты);
	Параметры.Вставить("ОбработкаПродолжения", ОбработкаПродолжения);
	
	ПроверитьСсылкиНаОбъект(
		Новый ОписаниеОповещения(
			"РазрешитьРедактированиеРеквизитовОбъектаПослеПроверкиСсылок", ЭтотОбъект, Параметры),
		МассивСсылок,
		СинонимыРеквизитов);
	
КонецПроцедуры

// Устанавливает доступность элементов формы, связанных с заданными реквизитами,
// для которых установлено разрешение изменения. Если передать массив реквизитов,
// тогда сначала будет дополнен состав реквизитов разрешенных для изменения.
//   Если разблокировка элементов формы, связанных с заданными реквизитами
// снята для всех реквизитов, тогда кнопка разрешения редактирования блокируется.
//
// Параметры:
//  Форма        - УправляемаяФорма - форма, в которой требуется разрешить
//                 редактирование элементов формы, заданных реквизитов.
//  
//  Реквизиты    - Массив       - имена реквизитов, для которых нужно установить разрешенность изменения.
//                                Используется, когда функция РазрешитьРедактированиеРеквизитовОбъекта
//                                не используется.
//                 Неопределено - состав реквизитов доступных для редактирования не изменяется,
//                                а для элементов формы, связанных с реквизитами, изменение которых
//                                разрешено устанавливается доступность.
//
Процедура УстановитьДоступностьЭлементовФормы(Знач Форма, Знач Реквизиты = Неопределено) Экспорт
	
	УстановитьРазрешенностьРедактированияРеквизитов(Форма, Реквизиты);
	
	Для Каждого ОписаниеБлокируемогоРеквизита Из Форма.ПараметрыЗапретаРедактированияРеквизитов Цикл
		Если ОписаниеБлокируемогоРеквизита.РедактированиеРазрешено Тогда
			Для Каждого БлокируемыйЭлементФормы Из ОписаниеБлокируемогоРеквизита.БлокируемыеЭлементы Цикл
				ЭлементФормы = Форма.Элементы.Найти(БлокируемыйЭлементФормы.Значение);
				Если ЭлементФормы <> Неопределено Тогда
					Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы")
					 ИЛИ ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
						ЭлементФормы.ТолькоПросмотр = Ложь;
					Иначе
						ЭлементФормы.Доступность = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Спрашивает у пользователя подтверждение на включение редактирования реквизитов
// и проверяет, есть ли ссылки на объект в информационной базе.
//
// Параметры:
//  ОбработкаПродолжения - ОписаниеОповещения, которое вызывается после проверки,
//                         в обработку передается параметр:
//                         Истина - ссылок не обнаружено или решение пользователя,
//                         Ложь   - ссылки обнаружены и пользователь отказался от продолжения.
//  МассивСсылок         - Массив - массив значений типа Ссылка, которые нужно найти в различных объектах.
//  СинонимыРеквизитов   - Массив - массив значений типа Строка, которые показываются пользователю.
//
Процедура ПроверитьСсылкиНаОбъект(Знач ОбработкаПродолжения, Знач МассивСсылок, Знач СинонимыРеквизитов) Экспорт
	
	ЗаголовокДиалога = НСтр("ru = 'Разрешение редактирования реквизитов'");
	
	РеквизитыПредставление = "";
	Для Каждого СинонимРеквизита Из СинонимыРеквизитов Цикл
		РеквизитыПредставление = РеквизитыПредставление + СинонимРеквизита + "," + Символы.ПС;
	КонецЦикла;
	РеквизитыПредставление = Лев(РеквизитыПредставление, СтрДлина(РеквизитыПредставление) - 2);
	
	Если СинонимыРеквизитов.Количество() > 1 Тогда
		ТекстВопроса = НСтр("ru = 'Для того чтобы не допустить рассогласования данных в программе, следующие реквизиты не доступны для редактирования:
			|%1.
			|
			|Перед тем, как разрешить их редактирование, рекомендуется оценить последствия,
			|проверив все места использования этого элемента в программе.
			|Поиск мест использования может занять длительное время.'");
								  
	Иначе
		ТекстВопроса = НСтр("ru = 'Для того чтобы не допустить рассогласования данных в программе, реквизит %1 не доступен для редактирования.
			|
			|Перед тем, как разрешить его редактирование, рекомендуется оценить последствия,
			|проверив все места использования %2 в программе.
			|Поиск мест использования может занять длительное время.'");
	КонецЕсли;
	
	Если МассивСсылок.Количество() = 1 Тогда
		ОбъектыПредставление = МассивСсылок[0];
	Иначе
		ОбъектыПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
			НСтр("ru = 'выбранных элементов (%1)'"), МассивСсылок.Количество());
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, РеквизитыПредставление, ОбъектыПредставление);
	
	Параметры = Новый Структура;
	Параметры.Вставить("МассивСсылок", МассивСсылок);
	Параметры.Вставить("СинонимыРеквизитов", СинонимыРеквизитов);
	Параметры.Вставить("ЗаголовокДиалога", ЗаголовокДиалога);
	Параметры.Вставить("ОбработкаПродолжения", ОбработкаПродолжения);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Проверить и разрешить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПроверитьСсылкиНаОбъектПослеПодтвержденияПроверки", ЭтотОбъект, Параметры),
		ТекстВопроса, Кнопки, , КодВозвратаДиалога.Да, ЗаголовокДиалога);
	
КонецПроцедуры

// Устанавливает разрешенность редактирования для тех реквизитов, описание которых подготовлено в форме.
//  Используется, когда доступность элементов формы изменяется самостоятельно, без
// использования функции УстановитьДоступностьЭлементовФормы.
//
// Параметры:
//  Форма        - УправляемаяФорма - форма, в которой требуется разрешить
//                 редактирование реквизитов объекта.
//  
//  Реквизиты    - Массив - реквизиты, которые нужно пометить, как разрешенные для изменения.
//  
//  РедактированиеРазрешено - Булево, начальное значение Истина - значение разрешенности редактирования реквизитов,
//                 которое нужно установить. Значение не будет установлено Истина, если нет права редактирования реквизита.
//                 Неопределено - не изменять разрешенность редактирования реквизитов.
//                 Ложь, Истина - установить указанное значение разрешенности редактирования реквизитов.
// 
//  ПравоРедактирования - Булево, начальное значение Неопределено - позволяет переопределить или доопределить
//                 возможность разблокировки реквизитов, которая вычисляется автоматически с помощью метода ПравоДоступа.
//                 Неопределено - не изменять ПравоРедактирования
//                 Ложь, Истина - установить указанное значение ПраваРедактирования для указанных реквизитов.
// 
Процедура УстановитьРазрешенностьРедактированияРеквизитов(Знач Форма, Знач Реквизиты, Знач РедактированиеРазрешено = Истина, Знач ПравоРедактирования = Неопределено) Экспорт
	
	Если ТипЗнч(Реквизиты) = Тип("Массив") Тогда
		
		Для Каждого Реквизит Из Реквизиты Цикл
			ОписаниеРеквизита = Форма.ПараметрыЗапретаРедактированияРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизита", Реквизит))[0];
			Если ТипЗнч(ПравоРедактирования) = Тип("Булево") Тогда
				ОписаниеРеквизита.ПравоРедактирования = ПравоРедактирования;
			КонецЕсли;
			Если ТипЗнч(РедактированиеРазрешено) = Тип("Булево") Тогда
				ОписаниеРеквизита.РедактированиеРазрешено = ОписаниеРеквизита.ПравоРедактирования И РедактированиеРазрешено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Обновление доступности команды РазрешитьРедактированиеРеквизитовОбъекта
	ВсеРеквизитыРазблокированы = Истина;
	
	Для каждого ОписаниеБлокируемогоРеквизита Из Форма.ПараметрыЗапретаРедактированияРеквизитов Цикл
		Если ОписаниеБлокируемогоРеквизита.ПравоРедактирования
		И НЕ ОписаниеБлокируемогоРеквизита.РедактированиеРазрешено Тогда
			ВсеРеквизитыРазблокированы = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВсеРеквизитыРазблокированы Тогда
		Форма.Элементы.РазрешитьРедактированиеРеквизитовОбъекта.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив имен реквизитов, заданных в свойстве формы
// ПараметрыЗапретаРедактированияРеквизитов на основе имен реквизитов
// указанных в модуле мененджера объекта, исключая реквизиты,
// у которых ПравоРедактирования = Ложь.
//
// Параметры:
//  Форма         - Форма объекта с обязательным стандартным реквизитом Объект
//  ТолькоЗаблокированные - Булево, начальное значение Истина,
//                  для вспомогательных целей можно задать Ложь, чтобы
//                  получить список всех видимых реквизитов, которые могут разблокироваться.
//  ТолькоВидимые - Булево, начальное значение Истина,
//                  чтобы получить и разблокировать все реквизиты объекта, нужно указать Ложь.
//
// Возвращаемое значение:
//  Массив - имена реквизитов
//
Функция Реквизиты(Знач Форма, Знач ТолькоЗаблокированные = Истина, ТолькоВидимые = Истина) Экспорт
	
	Реквизиты = Новый Массив;
	
	Для Каждого ОписаниеБлокируемогоРеквизита Из Форма.ПараметрыЗапретаРедактированияРеквизитов Цикл
		
		Если ОписаниеБлокируемогоРеквизита.ПравоРедактирования
		   И (    ОписаниеБлокируемогоРеквизита.РедактированиеРазрешено = Ложь
		      ИЛИ ТолькоЗаблокированные = Ложь) Тогда
			
			ДобавитьРеквизит = Ложь;
			Для Каждого БлокируемыйЭлементФормы Из ОписаниеБлокируемогоРеквизита.БлокируемыеЭлементы Цикл
				ЭлементФормы = Форма.Элементы.Найти(БлокируемыйЭлементФормы.Значение);
				Если ЭлементФормы <> Неопределено И (ЭлементФормы.Видимость Или НЕ ТолькоВидимые) Тогда
					ДобавитьРеквизит = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ДобавитьРеквизит Тогда
				Реквизиты.Добавить(ОписаниеБлокируемогоРеквизита.ИмяРеквизита);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Выводит предупреждение о том, что все видимые реквизиты разблокированы.
// Необходимость в предупреждении возникает, когда команда разблокировки
// остается включенной из-за наличия невидимых неразблокированных реквизитов.
//
// Параметры:
//  ОбработкаПродолжения - Неопределено - никаких действий после выполнения процедуры.
//                         ОписаниеОповещения, которое вызывается после выполнения процедуры.
//
Процедура ПоказатьПредупреждениеВсеВидимыеРеквизитыРазблокированы(ОбработкаПродолжения = Неопределено) Экспорт
	
	ПоказатьПредупреждение(ОбработкаПродолжения,
		НСтр("ru = 'Редактирование всех видимых реквизитов объекта уже разрешено.'"));
	
КонецПроцедуры

// Устарела. Следует использовать функцию Реквизиты.
Функция РеквизитыКромеНевидимых(Знач Форма, Знач ТолькоЗаблокированные = Истина) Экспорт
	
	Возврат Реквизиты(Форма, ТолькоЗаблокированные);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РазрешитьРедактированиеРеквизитовОбъектаПослеПредупреждения(ОбработкаПродолжения) Экспорт
	
	Если ОбработкаПродолжения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОбработкаПродолжения, Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура РазрешитьРедактированиеРеквизитовОбъектаПослеПроверкиСсылок(Результат, Параметры) Экспорт
	
	Если Результат Тогда
		УстановитьРазрешенностьРедактированияРеквизитов(
			Параметры.Форма, Параметры.ЗаблокированныеРеквизиты);
		
		УстановитьДоступностьЭлементовФормы(Параметры.Форма);
	КонецЕсли;
	
	Если Параметры.ОбработкаПродолжения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСсылкиНаОбъектПослеПодтвержденияПроверки(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ложь);
		Возврат;
	КонецЕсли;
		
	Если Параметры.МассивСсылок.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Истина);
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияВызовСервера.ЕстьСсылкиНаОбъект(Параметры.МассивСсылок) Тогда
		
		Если Параметры.МассивСсылок.Количество() = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Элемент ""%1"" уже используется в других местах в программе.
				|Не рекомендуется разрешать редактирование из-за риска рассогласования данных.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Параметры.МассивСсылок[0]);
		Иначе
			ТекстСообщения = НСтр("ru = 'Выбранные элементы (%1) уже используются в других местах в программе.
				|Не рекомендуется разрешать редактирование из-за риска рассогласования данных.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Параметры.МассивСсылок.Количество());
		КонецЕсли;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Разрешить редактирование'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена'"));
		ПоказатьВопрос(
			Новый ОписаниеОповещения(
				"ПроверитьСсылкиНаОбъектПослеПодтвержденияРедактирования", ЭтотОбъект, Параметры),
			ТекстСообщения, Кнопки, , КодВозвратаДиалога.Нет, Параметры.ЗаголовокДиалога);
	Иначе
		Если Параметры.МассивСсылок.Количество() = 1 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Редактирование реквизитов разрешено'"),
				ПолучитьНавигационнуюСсылку(Параметры.МассивСсылок[0]), Параметры.МассивСсылок[0]);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Разрешено редактирование реквизитов объектов (%1)'"), Параметры.МассивСсылок.Количество());
			ПоказатьОповещениеПользователя(НСтр("ru = 'Редактирование реквизитов разрешено'"),, ТекстСообщения);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСсылкиНаОбъектПослеПодтвержденияРедактирования(Ответ, Параметры) Экспорт
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Ответ = КодВозвратаДиалога.Да);
	
КонецПроцедуры

Процедура ПроверитьСсылкиНаОбъектПослеПредупрежденияРедактирования(Параметры) Экспорт
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения, Истина);
	
КонецПроцедуры

#КонецОбласти
