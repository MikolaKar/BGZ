
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интернет-поддержка пользователей"
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет необходимость запуска механизма при старте приложения
// в соответстивии с пользовательскими настройками и реализацией
// переопределяемых модулей.
// 
// Возвращаемое значение:
//	Булево - Истина, если запускать при старте, Ложь - в противном случае.
//
Функция ЗапускатьМеханизмПриСтарте() Экспорт
	
	НеобходимоЗапускатьПриСтарте = Ложь;
	ИнтернетПоддержкаПользователейСерверПереопределяемый.ЗапускатьИнтернетПоддержкуПриСтартеПрограммы(
		НеобходимоЗапускатьПриСтарте);
	
	Если НеобходимоЗапускатьПриСтарте <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВсегдаПоказыватьПриСтартеПрограммы = ХранилищеОбщихНастроек.Загрузить(
		"ИнтернетПоддержкаПользователей",
		"ВсегдаПоказыватьПриСтартеПрограммы");
	
	ВсегдаПоказыватьПриСтартеПрограммы = ?(
		ВсегдаПоказыватьПриСтартеПрограммы = Неопределено,
		Истина,
		ВсегдаПоказыватьПриСтартеПрограммы);
	
	Возврат ВсегдаПоказыватьПриСтартеПрограммы
		И ТекущаяДатаСеанса() >= ИнтернетПоддержкаПользователейВызовСервера.ЗначениеНастройкиНеНапоминатьОбАвторизацииДо();
	
КонецФункции

// Определяет, является ли текущий пользователь полноправным
// пользователем информационной базы.
//
// Возвращаемое значение:
// Булево - Истина - текущий пользователь является полноправным пользователем
//	 системы, Ложь - в противном случае;
//
Функция ЭтоПолноправныйПользователь() Экспорт
	
	ЭтоПолноправныйПользователь = Ложь;
	ИнтернетПоддержкаПользователейСерверПереопределяемый.ПриОпределенииЭтоПолноправныйПользователь(ЭтоПолноправныйПользователь);
	Возврат (ЭтоПолноправныйПользователь = Истина);
	
КонецФункции

// Возвращает логин и пароль пользователя Интернет-поддержки,
// сохраненные в информационной базе.
//
// Возвращаемое значение:
//	Структура - структура, содержащая логин и пароль пользователя
//		Интернет-поддержки:
//		* Логин - Строка - логин пользователя Интернет-поддержки;
//		* Пароль - Строка - пароль пользователя Интернет-поддержки, отсутствует,
//			если пользователь не установить флаг "Запомнить меня".
//	Неопределено - при отсутствии сохраненных данных авторизации.
//
Функция ДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт
	
	ЗапросПараметров = Новый Запрос(
	"ВЫБРАТЬ
	|	ПараметрыИнтернетПоддержкиПользователей.Имя КАК ИмяПараметра,
	|	ПараметрыИнтернетПоддержкиПользователей.Значение КАК ЗначениеПараметра
	|ИЗ
	|	РегистрСведений.ПараметрыИнтернетПоддержкиПользователей КАК ПараметрыИнтернетПоддержкиПользователей
	|ГДЕ
	|	ПараметрыИнтернетПоддержкиПользователей.Имя В (""login"", ""password"")
	|	И ПараметрыИнтернетПоддержкиПользователей.Пользователь = &ПустойИдентификатор");
	
	ЗапросПараметров.УстановитьПараметр("ПустойИдентификатор",
		Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	
	ЛогинПользователя  = Неопределено;
	ПарольПользователя = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаПараметров = ЗапросПараметров.Выполнить().Выбрать();
	Пока ВыборкаПараметров.Следующий() Цикл
		
		// В запросе регистр символов не учитывается
		ИмяПараметраНРег = НРег(ВыборкаПараметров.ИмяПараметра);
		Если ИмяПараметраНРег = "login" Тогда
			ЛогинПользователя = ВыборкаПараметров.ЗначениеПараметра;
			
		ИначеЕсли ИмяПараметраНРег = "password" Тогда
			ПарольПользователя = ВыборкаПараметров.ЗначениеПараметра;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЛогинПользователя <> Неопределено И ПарольПользователя <> Неопределено Тогда
		Возврат Новый Структура("Логин, Пароль", ЛогинПользователя, ПарольПользователя);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Для конфигураций с внедренной библиотекой "Библиотека стандартных подсистем"
// (БСП). Добавляет необходимые параметры работы клиента при запуске.
//
// Параметры:
//	Параметры - Структура - заполняемые параметры;
//
Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	// При запуске программы зачитываются необходимые параметры, формируется
	// контекст взаимодействия с сервисом ИПП
	
	МестоЗапускаПриСтартеПрограммы = "systemStartNew";
	ПараметрыИПП = ИнтернетПоддержкаПользователейВызовСервера.ПараметрыИнтернетПоддержки(
		МестоЗапускаПриСтартеПрограммы);
	
	УправляющаяСтруктураЗапуска = ИнтернетПоддержкаПользователейКлиентСервер.ОпределитьВозможностьЗапускаПоМестуИПараметрам(
		МестоЗапускаПриСтартеПрограммы,
		ПараметрыИПП);
	
	ВозвращаемыеПараметры = Новый Структура;
	
	Если УправляющаяСтруктураЗапуска <> Неопределено Тогда
		// Если запуск запрещен
		ВозвращаемыеПараметры.Вставить("УправляющаяСтруктураЗапуска", УправляющаяСтруктураЗапуска);
	Иначе
		КонтекстВзаимодействия = ИнтернетПоддержкаПользователейВызовСервера.НовыйКонтекстВзаимодействия(
			МестоЗапускаПриСтартеПрограммы,
			ПараметрыИПП,
			Ложь,
			Неопределено);
		ВозвращаемыеПараметры.Вставить("ПараметрыКонтекстаВзаимодействия", КонтекстВзаимодействия);
	КонецЕсли;
	
	Параметры.Вставить("ИнтернетПоддержкаПользователей", ВозвращаемыеПараметры);
	
КонецПроцедуры

// Для конфигураций с внедренной библиотекой "Библиотека стандартных подсистем"
// (БСП) версии 2.2.2 и старше.
// Используется для заполнения разрешений работы в безопасном режиме
// (см. одноименную процедуру модуля РаботаВБезопасномРежимеПереопределяемый)
//
// Параметры:
//	ЗапросыРазрешений - заполняемые разрешения;
//
// Пример использования в переопределяемой процедуре
// РаботаВБезопасномРежимеПереопределяемый.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам():
// 
//	// ИнтернетПоддержкаПользователей
//	ИнтернетПоддержкаПользователей.ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(Разрешения)
//	// Конец ИнтернетПоддержкаПользователей
//
Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	Если ИспользованиеИнтернетПоддержкиРазрешеноВТекущемРежимеРаботы() Тогда
		
		Если Метаданные.ОбщиеМодули.Найти("РаботаВБезопасномРежиме") = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МодульРаботаВБезопасномРежиме = Вычислить("РаботаВБезопасномРежиме");
		
		Если ТипЗнч(МодульРаботаВБезопасномРежиме) <> Тип("ОбщийМодуль") Тогда
			Возврат;
		КонецЕсли;
		
		ОписаниеРесурса = ИнтернетПоддержкаПользователейКлиентСервер.ОписаниеРесурсаИнтернетПоддержки();
		Разрешение = МодульРаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
			ОписаниеРесурса.Протокол,
			ОписаниеРесурса.Адрес,
			ОписаниеРесурса.Порт,
			ОписаниеРесурса.Описание);
		
		ЗапросыРазрешений.Добавить(Разрешение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет, разрешено ли использование интернет-поддержки в текущем режиме
// работы информационной базы в соответствии с реализацией процедуры
// ИнтернетПоддержкаПользователейСерверПереопределяемый.ИспользоватьИнтернетПоддержку()
//
Функция ИспользованиеИнтернетПоддержкиРазрешеноВТекущемРежимеРаботы()
	
	Отказ = Ложь;
	ИнтернетПоддержкаПользователейСерверПереопределяемый.ИспользоватьИнтернетПоддержку(Отказ);
	
	Возврат (Отказ = Ложь);
	
КонецФункции

// Определяет, разрешено ли использование интернет-поддержки текущему
// пользователю в зависимости от его прав доступа.
//
// Возвращаемое значение:
//	Булево - Истина, если использование разрешено, Ложь - в противном случае.
//
Функция ИспользованиеИнтернетПоддержкиРазрешеноДляТекущегоПользователя() Экспорт
	
	Результат = (РольДоступна("ИспользованиеИПП") ИЛИ ЭтоПолноправныйПользователь());
	Если НЕ Результат Тогда
		ИнтернетПоддержкаПользователейСерверПереопределяемый.ПроверитьВозможностьЗапуска(Результат);
	КонецЕсли;
	
	Возврат (Результат = Истина);
	
КонецФункции

// Возвращает код основного языка конфигурации. Используется для записи событий
// в журнал регистрации. Для определения кода основного языка используется
// процедура
// ИнтернетПоддержкаПользователейСерверПереопределяемый.ПриОпределенииКодаОсновногоЯзыкаКонфигурации()
// Если переопределяемой процедурой возвращен некорректный код языка, тогда в
// качестве кода принимается значение Метаданные.ОсновнойЯзык.КодЯзыка.
//
// Возвращаемое значение:
//	Строка - код основного языка конфигурации.
//
Функция КодОсновногоЯзыкаКонфигурации() Экспорт
	
	КодЯзыка = Неопределено;
	ИнтернетПоддержкаПользователейСерверПереопределяемый.ПриОпределенииКодаОсновногоЯзыкаКонфигурации(КодЯзыка);
	
	Если ТипЗнч(КодЯзыка) <> Тип("Строка") Тогда
		КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	КонецЕсли;
	
	Возврат КодЯзыка;
	
КонецФункции

#КонецОбласти
