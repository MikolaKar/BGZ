
////////////////////////////////////////////////////////////////////////////////
// ОтложенныйСтартБизнесПроцессовКлиент: в модуле содержатся процедуры для работы
// с механизмом отложенного старта бизнес-процессов на клиенте.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура открывает форму для настройки старта бизнес процесса.
//
// Параметры:
//   - БизнесПроцесс - БизнесПроцессСсылка - процесс для которого следует открыть форму настройки
//                     отложенного старта.
//   - СрокИсполненияПроцесса - Дата и время - срок исполнения процесса, который используется
//                              для контроля даты отложенного старта. Если срок исполнения будет
//                              меньше даты отложенного старта, то пользователю будет задан вопрос.
//
Процедура НастроитьОтложенныйСтарт(БизнесПроцесс, СрокИсполненияПроцесса = '00010101') Экспорт
	
	Если БизнесПроцесс.Пустая() Тогда
		ТекстПредупреждения = 
			НСтр("ru = 'Невозможно настроить отложенный старт для незаписанного процесса.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("БизнесПроцесс", БизнесПроцесс);
	ПараметрыФормы.Вставить("СрокИсполненияПроцесса", СрокИсполненияПроцесса);
	
	ОткрытьФорму(
		"РегистрСведений.НастройкаОтложенногоСтартаБизнесПроцессов.Форма.ФормаНастройкиРасписания",
		ПараметрыФормы,,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПоказатьПричинуОтменыОтложенногоСтарта(Форма) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Ошибка'"));
	ПараметрыФормы.Вставить("ТекстСообщения", Форма.Элементы.ДекорацияСтартОтложенТекст.Заголовок);
	ОткрытьФорму("ОбщаяФорма.Сообщение", ПараметрыФормы, Форма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоответствиеСрокаИсполненияПроцессаИДатыОтложенногоСтарта(
	Отказ, Форма, НастройкаОтложенногоСтарта, ПараметрыЗаписи, СрокИсполненияПроцесса = Неопределено) Экспорт
	
	Если ПараметрыЗаписи.
		Свойство("ЗаписатьБезСоответствияСрокаИсполненияПроцессаИДатыОтложенногоСтарта") Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Если СрокИсполненияПроцесса = Неопределено Тогда
		СрокИсполненияПроцесса = Объект.СрокИсполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокИсполненияПроцесса)
			И СрокИсполненияПроцесса < НастройкаОтложенногоСтарта.ДатаОтложенногоСтарта Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
		ПараметрыОповещения.Вставить("ИдентификаторФормыПроцесса", Форма.УникальныйИдентификатор);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ЗавершениеПроверкиСоответствияСрокаИсполненияПроцессаИДатыОтложенногоСтарта",
			ЭтотОбъект,
			ПараметрыОповещения);
		
		ТекстВопроса = НСтр("ru = 'Срок исполнения меньше даты отложенного старта процесса.
				|Сохранить изменения?'");
		
		КнопкиВопроса = Новый СписокЗначений;
		КнопкиВопроса.Добавить(Истина, "Сохранить");
		КнопкиВопроса.Добавить(Ложь, "Отмена");
			
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиВопроса);
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗавершениеПроверкиСоответствияСрокаИсполненияПроцессаИДатыОтложенногоСтарта(
	Результат, Параметры) Экспорт
	
	Если Результат = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ПараметрыЗаписи.Вставить("ЗаписатьБезСоответствияСрокаИсполненияПроцессаИДатыОтложенногоСтарта", Истина);
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ИдентификаторФормы", Параметры.ИдентификаторФормыПроцесса);
	ПараметрыОповещения.Вставить("ПараметрыЗаписиПроцесса", Параметры.ПараметрыЗаписи);
	Оповестить("СтартПроцессаПослеВопроса", ПараметрыОповещения);
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПроцессаДляОтложенногоСтарта(
	Отказ, ПараметрыЗаписи, Форма, ОткрытьФормуНастройкиОтложенногоСтарта = Ложь) Экспорт
	
	РаботаСБизнесПроцессамиКлиент.ПередСтартомБизнесПроцесса(
		Форма.Объект,
		Отказ,
		Форма.УникальныйИдентификатор,
		ПараметрыЗаписи);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МультипредметностьКлиент.ПроверитьЗаполнениеПредметовПроцесса(Форма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = НЕ Форма.ПроверитьЗаполнение();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОткрытьФормуНастройкиОтложенногоСтарта Тогда
		
		Если Форма.Модифицированность ИЛИ Форма.Объект.Ссылка.Пустая() Тогда
			Форма.Записать(ПараметрыЗаписи);
		КонецЕсли;
		
		СрокИсполнения = '00010101';
		ПараметрыЗаписи.Свойство("СрокИсполнения", СрокИсполнения);
		
		ПараметрыФормыНастройкиОтложенногоСтарта = Новый Структура;
		ПараметрыФормыНастройкиОтложенногоСтарта.Вставить("Процесс", Форма.Объект.Ссылка);
		ПараметрыФормыНастройкиОтложенногоСтарта.Вставить("СрокИсполнения", СрокИсполнения);
		
		ПодключитьОбработчикОжидания("ОткрытьФормуНастройкиОтложенногоСтарта", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуНастройки() Экспорт
	
	ОтложенныйСтартБизнесПроцессовКлиент.НастроитьОтложенныйСтарт(
			ПараметрыФормыНастройкиОтложенногоСтарта.Процесс,
			ПараметрыФормыНастройкиОтложенногоСтарта.СрокИсполнения);
	
КонецПроцедуры

#КонецОбласти