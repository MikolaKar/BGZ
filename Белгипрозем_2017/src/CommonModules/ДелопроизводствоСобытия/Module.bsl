// Обработчик подписки на событие ЗаписатьОбщиеРеквизитыДокументов.
//
Процедура ЗаписатьОбщиеРеквизитыДокументов(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда 
        Возврат;  
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Истина);
	
	КорреспондентыДляСписков = "";
	Если ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
		
		КорреспондентыДляСписков = Делопроизводство.ПолучитьКорреспондентовДляСписков(
			Источник.Получатели.Выгрузить(), "Получатель");
		
	ИначеЕсли ТипЗнч(Источник.Ссылка) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда 
		
//1С-Минск +
		//КорреспондентыДляСписков = Делопроизводство.ПолучитьКорреспондентовДляСписков(
		//	Источник.Корреспонденты.Выгрузить());
		ДобавитьКонтактныхЛицИПодписантов(Источник, КорреспондентыДляСписков);
//1С-Минск - 	
	КонецЕсли;	
	
	МенеджерЗаписи = РегистрыСведений.ОбщиеРеквизитыДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = Источник.Ссылка;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Документ 				= Источник.Ссылка;
	МенеджерЗаписи.РегистрационныйНомер 	= Источник.РегистрационныйНомер;
	МенеджерЗаписи.ДатаРегистрации 			= Источник.ДатаРегистрации;
	МенеджерЗаписи.Сумма 					= Источник.Сумма;
	МенеджерЗаписи.Валюта 					= Источник.Валюта;
	МенеджерЗаписи.СрокИсполнения 			= Источник.СрокИсполнения;
    МенеджерЗаписи.Заголовок 				= Источник.Заголовок;
	МенеджерЗаписи.КорреспондентыДляСписков = КорреспондентыДляСписков;
	МенеджерЗаписи.Записать();

КонецПроцедуры

Процедура ДобавитьКонтактныхЛицИПодписантов(Источник, КорреспондентыДляСписков) 
	// Добавление Контактных лиц, подписантов, Корреспондентов и Инвесторов
	СписокДобавленных = новый СписокЗначений;
	
	ДобавитьКонтактныхЛицИПодписантовИзТаблицы(Источник.Корреспонденты, КорреспондентыДляСписков, СписокДобавленных);
	ДобавитьКонтактныхЛицИПодписантовИзТаблицы(Источник.Инвесторы, КорреспондентыДляСписков, СписокДобавленных);
	
	Если Источник.ВидДокумента = Справочники.ВидыВнутреннихДокументов.ДелоУслуги Тогда
		ЭтоФизЛицо = мРаботаСДоговорами.ЭтоФизЛицо(Источник.мЗаказчик);
		ДобавитьКорреспондента(Источник.мЗаказчик, ЭтоФизЛицо, КорреспондентыДляСписков, СписокДобавленных);
	КонецЕсли; 
КонецПроцедуры

Процедура ДобавитьКонтактныхЛицИПодписантовИзТаблицы(ТаблицаИсточник, КорреспондентыДляСписков, СписокДобавленных) 
	Для каждого Стр Из ТаблицаИсточник Цикл
		ЭтоФизЛицо = мРаботаСДоговорами.ЭтоФизЛицо(Стр.Корреспондент);
		ДобавитьКорреспондента(Стр.Корреспондент, ЭтоФизЛицо, КорреспондентыДляСписков, СписокДобавленных);
		
		Если ЗначениеЗаполнено(Стр.КонтактноеЛицо) Тогда
			ДобавитьФизЛицо(Стр.КонтактноеЛицо, КорреспондентыДляСписков, СписокДобавленных);
		КонецЕсли; 
		Если ЗначениеЗаполнено(Стр.ПодписалОтКорреспондента) Тогда
			ДобавитьФизЛицо(Стр.ПодписалОтКорреспондента, КорреспондентыДляСписков, СписокДобавленных);
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры
 
Процедура ДобавитьКорреспондента(Корреспондент, ЭтоФизЛицо, КорреспондентыДляСписков, СписокДобавленных) 
	Если ЭтоФизЛицо Тогда
		ИскЗначение = СписокДобавленных.НайтиПоЗначению(Корреспондент.ФизЛицо);
		Если ИскЗначение = Неопределено Тогда
			СписокДобавленных.Добавить(Корреспондент.ФизЛицо);
			КорреспондентыДляСписков = КорреспондентыДляСписков	+ ?(КорреспондентыДляСписков = "", "", ", ")
			+ СокрЛП(Корреспондент.ПолноеНаименование);
		КонецЕсли; 
	Иначе	
		ИскЗначение = СписокДобавленных.НайтиПоЗначению(Корреспондент);
		Если ИскЗначение = Неопределено Тогда
			СписокДобавленных.Добавить(Корреспондент);
			КорреспондентыДляСписков = КорреспондентыДляСписков	+ ?(КорреспондентыДляСписков = "", "", ", ")
			+ СокрЛП(Корреспондент.ПолноеНаименование);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

Процедура ДобавитьФизЛицо(Контакт, КорреспондентыДляСписков, СписокДобавленных)
	Если ЗначениеЗаполнено(Контакт.ФизЛицо) Тогда
		ИскЗначение = СписокДобавленных.НайтиПоЗначению(Контакт.ФизЛицо);
		Если ИскЗначение = Неопределено Тогда
			СписокДобавленных.Добавить(Контакт.ФизЛицо);
			КорреспондентыДляСписков = КорреспондентыДляСписков	+ ?(КорреспондентыДляСписков = "", "", ", ")
			+ СокрЛП(Контакт.Наименование);
		КонецЕсли; 
	Иначе	
		ИскЗначение = СписокДобавленных.НайтиПоЗначению(Контакт.Наименование);
		Если ИскЗначение = Неопределено Тогда
			СписокДобавленных.Добавить(Контакт.Наименование);
			КорреспондентыДляСписков = КорреспондентыДляСписков	+ ?(КорреспондентыДляСписков = "", "", ", ")
			+ СокрЛП(Контакт.Наименование);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры
 