
#Область ПрограммныйИнтерфейс

// Получает список папок писем, для которых включена синхронизация с указанным мобильным клиентом
Функция ПолучитьСинхронизируемыеПапкиПисем(МобильныйКлиент) Экспорт

	Возврат РегистрыСведений.СинхронизацияПапокПисемСМобильнымКлиентом.ПолучитьПапкиДляСинхронизации(
		МобильныйКлиент.Пользователь, Истина);

КонецФункции

// Получает представление и контакт адресата для передачи на мобильный клиент
Функция ПолучитьПредставлениеИКонтактАдресата(АдресатСсылка) Экспорт

	Возврат ВстроеннаяПочтаСервер.ПолучитьПредставлениеИКонтактАдресата(АдресатСсылка);

КонецФункции

// Возвращает массив не помеченных на удаление узлов плана обмена Мобильный
Функция ПолучитьВсеУзлы() Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ПланыОбмена.Мобильный.ЭтотУзел());
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Мобильный.Ссылка
		|ИЗ
		|	ПланОбмена.Мобильный КАК Мобильный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОМобильныхКлиентах КАК СведенияОМобильныхКлиентах
		|		ПО Мобильный.Ссылка = СведенияОМобильныхКлиентах.МобильныйКлиент
		|ГДЕ
		|	Мобильный.ПометкаУдаления = ЛОЖЬ
		|	И Мобильный.Ссылка <> &Ссылка";

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);

КонецФункции

// Получает массив ссылок на узла плана обмена Мобильный, принадлежащие указанному пользователю
// Параметры:
//	Пользователь - ссылка на пользователя
Функция ПолучитьУзлыОбменаПоВладельцу(Пользователь) Экспорт

	ЗапросПолучениеУзлов = Новый Запрос;
	ЗапросПолучениеУзлов.УстановитьПараметр("Пользователь", Пользователь);
	ЗапросПолучениеУзлов.УстановитьПараметр("Ссылка"      , ПланыОбмена.Мобильный.ЭтотУзел());
	ЗапросПолучениеУзлов.Текст = 
		"ВЫБРАТЬ
		|	Мобильный.Ссылка
		|ИЗ
		|	ПланОбмена.Мобильный КАК Мобильный
		|ГДЕ
		|	Мобильный.Пользователь = &Пользователь
		|	И Мобильный.ПометкаУдаления = ЛОЖЬ
		|	И Мобильный.Ссылка <> &Ссылка";

	Возврат ЗапросПолучениеУзлов.Выполнить().Выгрузить().ВыгрузитьКолонку(0);

КонецФункции

// Получает центральный узел регистрации всех изменений данных
Функция ПолучитьУзелДляРегистрацииДанных() Экспорт

	УзелОбменаСсылка = ПланыОбмена.МобильныйРегистрацияВсехДанных.НайтиПоКоду("1");

	Если Не ЗначениеЗаполнено(УзелОбменаСсылка) Тогда

		УзелОбменаОбъект = ПланыОбмена.МобильныйРегистрацияВсехДанных.СоздатьУзел();

		УзелОбменаОбъект.Код          = "1";
		УзелОбменаОбъект.Наименование = НСтр("ru = 'Единый узел для регистрации всех данных для мобильных клиентов'");

		УзелОбменаОбъект.Записать();

		Возврат УзелОбменаОбъект.Ссылка;

	Иначе

		Возврат УзелОбменаСсылка;

	КонецЕсли;

КонецФункции

#КонецОбласти