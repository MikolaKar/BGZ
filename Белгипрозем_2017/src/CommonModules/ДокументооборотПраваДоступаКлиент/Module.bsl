
// Выполняет проверку настроенных прав.
//
// Параметры:
//  ТаблицаПрав - Таблица формы - проверяемая таблица прав.
//
// Возвращаемое значение:
//  Булево - Истина, если права настроены без ошибок.
//
Функция ПроверитьТаблицуПравПапки(ТаблицаПрав) Экспорт
	
	ОбщегоНазначенияДокументооборотКлиент.УдалитьПустыеСтрокиТаблицы(ТаблицаПрав, "Пользователь");
	
	ЕстьОшибки = Ложь;
	ИндексСтроки = -1;
	
	Для каждого Эл Из ТаблицаПрав Цикл
		
		ИндексСтроки = ИндексСтроки + 1;
		Для Каждого Эл2 Из ТаблицаПрав Цикл
			
			Если Эл2 = Эл Тогда
				Прервать;
			КонецЕсли;
			
			Если Эл2.Пользователь = Эл.Пользователь И Эл2.Унаследовано = Эл.Унаследовано Тогда
				
				ЕстьОшибки = Истина;
				// Выдаем сообщение об ошибке
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Права для ""%1"" уже указаны выше.'"), 
					Эл.Пользователь);
					
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,,
					"ТаблицаПрав[" + Формат(ИндексСтроки, "ЧГ=0") + "].Пользователь");
					
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат Не ЕстьОшибки;
	
КонецФункции

// Обрабатывает добавление строки в таблицу прав папок
//
// Параметры:
//  ФормаНастроек - Форма - форма настроек прав папки
//
Процедура ДобавитьСтрокуВТаблицуПрав(ФормаНастроек, Копирование) Экспорт
	
	ФормаНастроек.Модифицированность = Истина;
	
	Элементы = ФормаНастроек.Элементы;
	ЭлементТаблицаПрав = Элементы.ТаблицаПрав;
	
	НоваяСтрокаПрав = ФормаНастроек.ТаблицаПрав.Добавить();
	НоваяСтрокаПрав.Пользователь = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	НоваяСтрокаПрав.ПравоЧтения = 1;
	НоваяСтрокаПрав.ПравоЧтенияСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоЧтения);
	НоваяСтрокаПрав.ПравоДобавленияСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоДобавления);
	НоваяСтрокаПрав.ПравоИзмененияСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоИзменения);
	НоваяСтрокаПрав.ПравоИзмененияПапокСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоИзмененияПапок);
	НоваяСтрокаПрав.ПравоУдаленияДанныхСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоУдаленияДанных);
	НоваяСтрокаПрав.ПравоУправленияПравамиСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПравоУправленияПравами);
	
	НоваяСтрокаПрав.ПередаватьДочерним = 1;
	НоваяСтрокаПрав.ПередаватьДочернимСтрокой = СтрокаПрава(НоваяСтрокаПрав.ПередаватьДочерним);
	
	Если Копирование Тогда
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПрав, ЭлементТаблицаПрав.ТекущиеДанные,, "Унаследовано");
	КонецЕсли;
	
	ЭлементТаблицаПрав.ТекущаяСтрока = НоваяСтрокаПрав.ПолучитьИдентификатор();
	ЭлементТаблицаПрав.ТекущийЭлемент = Элементы.ТаблицаПравПользователь;
	ЭлементТаблицаПрав.ИзменитьСтроку();
	
КонецПроцедуры

// Возвращает текстовое представление значения права
Функция СтрокаПрава(Право)
	
	Если Право = 0 Тогда
		Возврат НСтр("ru = ' '");
	ИначеЕсли Право = 1 Тогда
		Возврат НСтр("ru = 'Да'");
	ИначеЕсли Право = 2 Тогда
		Возврат НСтр("ru = 'Нет'");
	КонецЕсли;
	
КонецФункции
