////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ДЛЯ РАБОТЫ С РЕЗОЛЮЦИЯМИ (КЛИЕНТ)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ДобавитьРезолюцию(ЭтаФорма) Экспорт
	
	ДокументСсылка = ЭтаФорма.Объект.Ссылка;
	
	Если ДокументСсылка.Пустая() Тогда 
		Если Не ЭтаФорма.Записать() Тогда 
			Возврат;
		КонецЕсли;
		
		ДокументСсылка = ЭтаФорма.Объект.Ссылка;
		
		ПоказатьОповещениеПользователя(
			"Создание:", 
			ПолучитьНавигационнуюСсылку(ДокументСсылка),
			Строка(ДокументСсылка),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", ДокументСсылка);
	ОткрытьФорму("Справочник.Резолюции.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

Процедура ИзменитьРезолюцию(ЭтаФорма) Экспорт
	
	Резолюция = ПолучитьТекущуюРезолюцию(ЭтаФорма);
	
	Если Резолюция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Документ = ЭтаФорма.Объект;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Резолюция.Ссылка);
	ПараметрыФормы.Вставить("ОбъектСсылка", Документ.Ссылка);
	
	Если Не РаботаСРезолюциями.ЭтоАвторРезолюции(Резолюция.Ссылка) Тогда
		ПараметрыФормы.Вставить("ТекстПредупреждения",
			НСтр("ru = 'Редактировать резолюцию может только автор или пользователь, который ее внес.'"));
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Резолюция.Источник) Тогда 
		ПараметрыФормы.Вставить("ТекстПредупреждения",
			НСтр("ru = 'Нельзя редактировать резолюцию, добавленную при выполнении процесса ""Рассмотрение"".'"));
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);	
	КонецЕсли;	
	
	ОткрытьФорму("Справочник.Резолюции.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

Процедура УдалитьРезолюцию(ЭтаФорма, ОписаниеОповещения) Экспорт
	
	Резолюция = ПолучитьТекущуюРезолюцию(ЭтаФорма);
	
	Если Резолюция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Не РаботаСРезолюциями.ЭтоАвторРезолюции(Резолюция.Ссылка) Тогда
		ТекстПредупреждения = НСтр("ru = 'Удалить резолюцию может только автор или пользователь, который ее внес.'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Резолюция.Источник) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Нельзя удалить резолюцию, добавленную при выполнении процесса ""Рассмотрение"".'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
    	Возврат;
	КонецЕсли;
	
	Документ = ЭтаФорма.Объект;
	ТекущаяПометкаУдаления = Резолюция.ПометкаУдаления;
	
	Если ТекущаяПометкаУдаления Тогда
		ТекстВопроса = НСтр("ru = 'Снять с резолюции пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить резолюцию на удаление?'");
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ПараметрыОбработчика.Вставить("Резолюция", Резолюция);
	ПараметрыОбработчика.Вставить("ТекущаяПометкаУдаления", ТекущаяПометкаУдаления);
	ПараметрыОбработчика.Вставить("Документ", Документ);
	ОписаниеОповещенияОтветаНаВопрос = Новый ОписаниеОповещения(
		"УдалитьРезолюциюПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчика);
	ПоказатьВопрос(ОписаниеОповещенияОтветаНаВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	Возврат;
	
КонецПроцедуры

Процедура УдалитьРезолюциюПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПометкаИзменена = РаботаСРезолюциями.УстановитьПометкуНаУдаление(
		Параметры.Резолюция.Ссылка,
		НЕ Параметры.ТекущаяПометкаУдаления);
	
	Если ПометкаИзменена Тогда
		Если Параметры.ТекущаяПометкаУдаления Тогда
			Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка на удаление с резолюции к документу ""%1"" успешно снята.'"),
				Параметры.Документ.Наименование));
		Иначе
			Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка на удаление успешно установлена для резолюции к документу ""%1"".'"),
				Параметры.Документ.Наименование));
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения);	
	Иначе
		Если Параметры.ТекущаяПометкаУдаления Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось снять пометку на удаление с резолюции к документу ""%1"".'"),
				Параметры.Документ.Наименование);
		Иначе
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось установить пометку на удаление для резолюции к документу ""%1"".'"),
				Параметры.Документ.Наименование);
		КонецЕсли;
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииСтатусПроверкиЭП(ЭтаФорма) Экспорт
	
	Резолюция = ПолучитьТекущуюРезолюцию(ЭтаФорма);
	РаботаСЭПКлиент.ОткрытьПодпись(Резолюция, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекущуюРезолюцию(ЭтаФорма)
	
	Если ЭтаФорма.Резолюции.Количество() = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли ЭтаФорма.Резолюции.Количество() = 1 Тогда
		Возврат ЭтаФорма.Резолюции[0];
	Иначе
		Возврат ЭтаФорма.Элементы.Резолюции.ТекущиеДанные;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
