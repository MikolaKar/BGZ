
////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Возвращает дерево - ветви - структура предприятия, листья - пользователи
Функция ПолучитьПользователейВПодразделениях() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникСтруктураПредприятия.Ссылка КАК ГруппаСсылка,
		|	СправочникСтруктураПредприятия.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	СправочникСтруктураПредприятия.Наименование КАК ГруппаНаименование,
		|	СведенияОПользователях.Пользователь КАК ПользователиСсылка,
		|	СведенияОПользователях.Пользователь.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СведенияОПользователях.Пользователь.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	ВЫБОР
		|		КОГДА СведенияОПользователях.Пользователь = СведенияОПользователях.Подразделение.Руководитель
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СправочникСтруктураПредприятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|		ПО (СведенияОПользователях.Подразделение = СправочникСтруктураПредприятия.Ссылка)
		|			И (СведенияОПользователях.Пользователь.Недействителен = ЛОЖЬ)
		|			И (СведенияОПользователях.Пользователь.ПометкаУдаления = ЛОЖЬ)
		|ГДЕ
		|	СправочникСтруктураПредприятия.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиЗадач.РольИсполнителя,
		|	ИсполнителиЗадач.РольИсполнителя.Наименование,
		|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
		|		ПО ВрТаблица.ПользователиСсылка = ИсполнителиЗадач.Исполнитель
		|			И (ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает дерево - ветви - группы пользователей, листья - пользователи
Функция ПолучитьПользователейВГруппах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ГруппыПользователей.Ссылка КАК ГруппаСсылка,
		|	ГруппыПользователей.ПометкаУдаления КАК ГруппаПометкаУдаления,
		|	ГруппыПользователей.Наименование КАК ГруппаНаименование,
		|	СправочникПользователи.Ссылка КАК ПользователиСсылка,
		|	СправочникПользователи.ПометкаУдаления КАК ПользователиПометкаУдаления,
		|	СправочникПользователи.ПредставлениеВПерепискеСРангом КАК ПользователиНаименование,
		|	ВЫБОР
		|		КОГДА СправочникПользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРуководитель
		|ПОМЕСТИТЬ ВрТаблица
		|ИЗ
		|	Справочник.ГруппыПользователей КАК ГруппыПользователей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		|		ПО ГруппыПользователей.Ссылка = ГруппыПользователейСостав.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК СправочникПользователи
		|		ПО (СправочникПользователи.Ссылка = ГруппыПользователейСостав.Пользователь
		|				ИЛИ ГруппыПользователей.Ссылка = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи))
		|			И (СправочникПользователи.Недействителен = ЛОЖЬ)
		|			И (СправочникПользователи.ПометкаУдаления = ЛОЖЬ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|		ПО (СправочникПользователи.Ссылка = СведенияОПользователях.Пользователь)
		|ГДЕ
		|	ГруппыПользователей.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ИсполнителиЗадач.РольИсполнителя,
		|	ИсполнителиЗадач.РольИсполнителя.Наименование,
		|	ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование
		|ПОМЕСТИТЬ ВрТаблицаРоли
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
		|		ПО ВрТаблица.ПользователиСсылка = ИсполнителиЗадач.Исполнитель
		|			И (ИсполнителиЗадач.РольИсполнителя.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВрТаблица.ГруппаСсылка,
		|	ВрТаблица.ГруппаПометкаУдаления,
		|	ВрТаблица.ГруппаНаименование,
		|	ВрТаблица.ПользователиСсылка,
		|	ВрТаблица.ПользователиПометкаУдаления,
		|	ВрТаблица.ПользователиНаименование,
		|	ВрТаблица.ЭтоРуководитель
		|ИЗ
		|	ВрТаблица КАК ВрТаблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВрТаблицаРоли.ГруппаСсылка,
		|	ВрТаблицаРоли.ГруппаПометкаУдаления,
		|	ВрТаблицаРоли.ГруппаНаименование,
		|	ВрТаблицаРоли.РольИсполнителя,
		|	ВрТаблицаРоли.РольИсполнителяПометкаУдаления,
		|	ВрТаблицаРоли.РольИсполнителяНаименование,
		|	ЛОЖЬ
		|ИЗ
		|	ВрТаблицаРоли КАК ВрТаблицаРоли
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВрТаблица.ГруппаСсылка ИЕРАРХИЯ,
		|	ВрТаблица.ПользователиНаименование";
		
	ВыборкаДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Возврат ВыборкаДерево;

КонецФункции	

// Возвращает таблицу - все пользователи
Функция ПолучитьПользователейСписком() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка,
		|	Пользователи.ПометкаУдаления,
		|	Пользователи.ПредставлениеВПерепискеСРангом КАК Наименование,
		|	ВЫБОР
		|		КОГДА Пользователи.Ссылка = СведенияОПользователях.Подразделение.Руководитель
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРуководитель
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|		ПО Пользователи.Ссылка = СведенияОПользователях.Пользователь
		|ГДЕ
		|	Пользователи.Недействителен = ЛОЖЬ
		|	И Пользователи.ПометкаУдаления = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РолиИсполнителей.Ссылка,
		|	РолиИсполнителей.ПометкаУдаления,
		|	РолиИсполнителей.Наименование,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|ГДЕ
		|	РолиИсполнителей.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	ВыборкаТаблица = Запрос.Выполнить().Выгрузить();
	
	Возврат ВыборкаТаблица;

КонецФункции	

// Функция ЭтоПолноправныйПользовательИБ проверяет,
// является ли полноправным текущий пользователь ИБ или
// пользователь ИБ заданного пользователя (обычного или внешнего).
//
//  Полноправными считается:
// а) пользователь ИБ при пустом списке пользователей ИБ,
//    если основная роль не задана или ПолныеПрава,
// б) пользователь ИБ с ролью ПолныеПрава.
//
//
// Параметры:
//  Пользователь - Неопределено (проверяется текущий пользователь ИБ),
//                 Справочник.Пользователи, Справочник.ВнешниеПользователи
//                 (осуществляется поиск пользователя ИБ по уникальному
//                  идентификатору, заданному в реквизите ИдентификаторПользователяИБ,
//                  если пользователь ИБ не найден, возвращается Ложь).
//
// Возвращаемое значение:
//  Булево.
//
Функция ЭтоПолноправныйПользовательИБ(Пользователь = Неопределено) Экспорт
	
	Возврат Пользователи.ЭтоПолноправныйПользователь(Пользователь,, Ложь);
	
КонецФункции	

// Функция ВсеРоли() возвращает таблицу значений имен всех ролей конфигурации.
//
// Параметры:
// 
// Возвращаемое значение:
//  ТаблицаЗначений
//      Имя           - Строка
//      Синоним       - Строка
//      Идентификатор - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//
Функция ВсеРоли() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя",           Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Синоним",       Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	
	Для каждого Роль Из Метаданные.Роли Цикл
		Строка = Таблица.Добавить();
		Строка.Имя           = Роль.Имя;
		Строка.Синоним       = ?(ЗначениеЗаполнено(Роль.Синоним), Роль.Синоним, Роль.Имя);
		Строка.Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Роль);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции
