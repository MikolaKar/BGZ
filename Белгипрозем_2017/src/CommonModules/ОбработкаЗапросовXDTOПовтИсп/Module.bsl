////////////////////////////////////////////////////////////////////////////////
// Обработка запросов XDTO, повторно используемые значения
// Предоставляет веб-сервису DMService часто используемые данные
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
	
// Возвращает узел интегрированной системы по идентификатору
//
// Параметры:
//   ИдентификаторУзла - Строка - идентификатор узла ИС
//
// Возвращаемое значение:
//   ПланОбменаСсылка.ИнтегрированныеСистемы - ссылка на найденный узел
//
Функция УзелИнтегрированнойСистемы(ИдентификаторУзла) Экспорт
	
	Если ИдентификаторУзла = "" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЭтотУзел = ПланыОбмена.ИнтегрированныеСистемы.ЭтотУзел();
	ИмяЭтогоУзла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотУзел, "Наименование");
	ИмяПоУмолчанию = НСтр("ru='1С:Документооборот'");
	
	Если Не ЗначениеЗаполнено(ИмяЭтогоУзла) Тогда
		ЭтотУзелОбъект = ЭтотУзел.ПолучитьОбъект();
		ЭтотУзелОбъект.Наименование = ИмяПоУмолчанию;
		ЭтотУзелОбъект.УстановитьНовыйКод();
		ЭтотУзелОбъект.Записать();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИнтегрированныеСистемы.Ссылка
		|ИЗ
		|	ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
		|ГДЕ
		|	ИнтегрированныеСистемы.Идентификатор = &ИдентификаторУзла";
	Запрос.УстановитьПараметр("ИдентификаторУзла", ИдентификаторУзла);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		УзелОбъект = ПланыОбмена.ИнтегрированныеСистемы.СоздатьУзел();
		УзелОбъект.УстановитьНовыйКод();
		УзелОбъект.Наименование = ИдентификаторУзла;
		УзелОбъект.Идентификатор = ИдентификаторУзла;
		УзелОбъект.Записать();
		УзелСсылка = УзелОбъект.Ссылка;
	Иначе
		УзелСсылка = Результат.Выгрузить()[0].Ссылка;
	КонецЕсли;
	
	Возврат УзелСсылка;
	
КонецФункции

// Возвращает узлы интегрированных систем для регистрации данных
//
// Возвращаемое значение:
//   Массив - содержит элементы типа
//     * ПланОбменаСсылка.ИнтегрированныеСистемы
//
Функция ПолучитьУзлыОбменаИнтегрированныхСистем() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнтегрированныеСистемы.Ссылка
		|ИЗ
		|	ПланОбмена.ИнтегрированныеСистемы КАК ИнтегрированныеСистемы
		|ГДЕ
		|	НЕ ИнтегрированныеСистемы.ПометкаУдаления
		|	И ИнтегрированныеСистемы.Ссылка <> &ЭтотУзел";
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ИнтегрированныеСистемы.ЭтотУзел());
		
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Узлы = Новый Массив;
	Иначе
		Узлы = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат Узлы;
	
КонецФункции

#КонецОбласти