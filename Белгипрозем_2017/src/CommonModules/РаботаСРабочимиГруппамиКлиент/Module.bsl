// При попытке удаления последнего участника рабочей группы документа, для
// которого установлено правило обязательного заполнения рабочей группы
// выводится предупреждение и отменяется удаление.
//
Процедура РабочаяГруппаТаблицаПередУдалением(Форма, Отказ, ОписаниеОповещения) Экспорт
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	ВыделенныеСтроки = Форма.Элементы.РабочаяГруппаТаблица.ВыделенныеСтроки;
	КоличествоВыделенныхСтрок = ВыделенныеСтроки.Количество();
	КоличествоСтрокВсего = Форма.РабочаяГруппаТаблица.Количество();
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидДокумента) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для документов этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоМероприятие(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидМероприятия) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для мероприятий этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДелопроизводствоКлиентСервер.ЭтоПроект(Форма.Объект.Ссылка) Тогда
		Если КоличествоСтрокВсего = КоличествоВыделенныхСтрок Тогда
			Если РаботаСРабочимиГруппами.ОбязательноеЗаполнениеРабочихГруппДокументов(Форма.Объект.ВидПроекта) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для проектов этого вида рабочая группа должна быть обязательно заполнена хотя бы одним участником!'"));
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ПараметрыОбработчика.Вставить("Форма", Форма);
	ОписаниеОповещенияВопроса = Новый ОписаниеОповещения(
		"РабочаяГруппаТаблицаПередУдалениемПродолжение",
		ЭтотОбъект,
		ПараметрыОбработчика);
		
	Если Форма.РабочаяГруппаТаблица.Количество() > ВыделенныеСтроки.Количество() Тогда
		Для Каждого Индекс Из ВыделенныеСтроки Цикл
			Если Форма.Элементы.РабочаяГруппаТаблица.ДанныеСтроки(Индекс).Участник = ТекущийПользователь Тогда
				ТекстВопроса = НСтр("ru = 'Вы пытаетесь удалить себя из рабочей группы. Вы можете потерять доступ к документу. Продолжить?'");
				ПоказатьВопрос(ОписаниеОповещенияВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);		
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияВопроса, КодВозвратаДиалога.Да);
	
КонецПроцедуры

Процедура РабочаяГруппаТаблицаПередУдалениемПродолжение(Результат, Параметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Форма.КоличествоУчастниковРабочейГруппы = Параметры.Форма.РабочаяГруппаТаблица.Количество() 
		- Параметры.Форма.Элементы.РабочаяГруппаТаблица.ВыделенныеСтроки.Количество();
	Параметры.Форма.Модифицированность = Истина;
	ВыполнитьОбработкуОповещения(Параметры.ОписаниеОповещения);
	
КонецПроцедуры

// Обработчик табличной части формы шаблона документа РабочаяГруппаПриНачалеРедактирования
//
Процедура РабочаяГруппаПриНачалеРедактирования(РабочаяГруппаЭлемент, НоваяСтрока) Экспорт
	
	Если НоваяСтрока Тогда
		РабочаяГруппаЭлемент.ТекущиеДанные.Участник = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		УстановитьРеквизитыУсловногоОформления(РабочаяГруппаЭлемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик окончания редактирования таблицы РабочаяГруппаТаблица
//
Процедура РабочаяГруппаПриОкончанииРедактирования(
	Форма,
	Элемент,
	ОтменаРедактирования) Экспорт
	
	КоличествоУчастниковРабочейГруппы = Форма.РабочаяГруппаТаблица.Количество();
	Форма.КоличествоУчастниковРабочейГруппы = КоличествоУчастниковРабочейГруппы;
	Если КоличествоУчастниковРабочейГруппы > 0 Тогда
		Форма.РабочаяГруппаТаблица.Сортировать("Иконка, ЭтоРоль Убыв, Участник");
	КонецЕсли;
	Если Не ОтменаРедактирования Тогда
		Форма.Модифицированность = Истина;
		УстановитьРеквизитыУсловногоОформления(Элемент.ТекущиеДанные);
		Форма.Элементы.РабочаяГруппаТаблица.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ШаблонРабочаяГруппаПриОкончанииРедактирования(
	Форма,
	РабочаяГруппаТаблица,
	РабочаяГруппаЭлемент,
	ОтменаРедактирования) Экспорт
	
	КоличествоУчастниковРабочейГруппы = РабочаяГруппаТаблица.Количество();
	Если КоличествоУчастниковРабочейГруппы > 0 Тогда
		РабочаяГруппаТаблица.Сортировать("Иконка, ЭтоРоль Убыв, Участник");
	КонецЕсли;
	Если Не ОтменаРедактирования Тогда
		Форма.Модифицированность = Истина;
		УстановитьРеквизитыУсловногоОформления(РабочаяГруппаЭлемент.ТекущиеДанные);
		Форма.Элементы.РабочаяГруппаДокумента.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик автоподбора для реквизита Участник табличной части формы шаблона документа РабочаяГруппа
//
Процедура ШаблонРабочаяГруппаУчастникАвтоПодбор(
	Элемент,
	Текст,
	ДанныеВыбора,
	Ожидание,
	СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСРабочимиГруппами.СформироватьДанныеВыбораУчастника(Текст, Истина);
	КонецЕсли;	
	
КонецПроцедуры

// Обработчик автоподбора для реквизита Участник табличной части формы шаблона документа РабочаяГруппа
//
Процедура ДокументРабочаяГруппаУчастникАвтоПодбор(
	Элемент,
	Текст,
	ДанныеВыбора,
	Ожидание,
	СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = РаботаСРабочимиГруппами.СформироватьДанныеВыбораУчастника(Текст, Ложь);
	КонецЕсли;	
	
КонецПроцедуры

// Обработчик события табличной части РабочаяГруппа формы проекта
//
Процедура ПроектРабочаяГруппаУчастникНачалоВыбора(
	Форма,
	Элемент,
	ДанныеВыбора,
	СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Выбор участника рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбиратьГруппуПользователей", Ложь);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Ложь);
	ПараметрыФормы.Вставить("ПоказыватьФункцииДокумента", Ложь);
	ПараметрыФормы.Вставить("Исполнитель", Форма.Элементы.РабочаяГруппаТаблица.ТекущиеДанные.Участник);
	ОткрытьФорму("ОбщаяФорма.ВыборИсполнителя", ПараметрыФормы, Форма.Элементы.РабочаяГруппаТаблица);
	
КонецПроцедуры

// Обработчик события табличной части РабочаяГруппа формы шаблона документа
//
Процедура ШаблонРабочаяГруппаУчастникНачалоВыбора(
	Форма,
	Элемент,
	ДанныеВыбора,
	СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Выбор участника рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбиратьГруппуПользователей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункцииДокумента", Истина);
	ПараметрыФормы.Вставить("Исполнитель", Форма.Элементы.РабочаяГруппаДокумента.ТекущиеДанные.Участник);
	ОткрытьФорму("ОбщаяФорма.ВыборИсполнителя", ПараметрыФормы, Форма.Элементы.РабочаяГруппаДокумента);
	
КонецПроцедуры

// Обработчик события табличной части РабочаяГруппа формы шаблона документа
//
Процедура ДокументРабочаяГруппаУчастникНачалоВыбора(
	Форма,
	Элемент,
	ДанныеВыбора,
	СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Выбор участника рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбиратьГруппуПользователей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Ложь);
	ПараметрыФормы.Вставить("Исполнитель", Форма.Элементы.РабочаяГруппаТаблица.ТекущиеДанные.Участник);
	ОткрытьФорму("ОбщаяФорма.ВыборИсполнителя", ПараметрыФормы, Форма.Элементы.РабочаяГруппаТаблица);
	
КонецПроцедуры

// Обработчик команды формы шаблона документа ПодобратьУчастниковРабочейГруппы
//
Процедура ШаблонПодобратьУчастниковРабочейГруппы(Форма, РабочаяГруппаТаблица, РабочаяГруппаЭлемент) Экспорт
	
	РабочаяГруппа = Новый Массив;
	Для каждого РабочаяГруппаСтрока Из РабочаяГруппаТаблица Цикл
		Участник = Новый Структура;
		Участник.Вставить("Исполнитель", РабочаяГруппаСтрока.Участник);
		Участник.Вставить("ОсновнойОбъектАдресации", РабочаяГруппаСтрока.ОсновнойОбъектАдресации);
		Участник.Вставить("ДополнительныйОбъектАдресации", РабочаяГруппаСтрока.ДополнительныйОбъектАдресации);
		РабочаяГруппа.Добавить(Участник);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Подбор участников рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбранныеЗаголовок", НСтр("ru = 'Выбранные участники'"));
	ПараметрыФормы.Вставить("Выбранные", РабочаяГруппа);
	ПараметрыФормы.Вставить("ПодбиратьГруппыПользователей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьРоли", Истина);
	ПараметрыФормы.Вставить("ТолькоПростыеРоли", Ложь);
	ПараметрыФормы.Вставить("БезВнешнихРолей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункцииДокумента", Истина);
	ОткрытьФорму("ОбщаяФорма.ПодборИсполнителей", ПараметрыФормы, РабочаяГруппаЭлемент);
	
КонецПроцедуры

// Обработчик команды формы документа ПодобратьУчастниковРабочейГруппы
//
Процедура ДокументПодобратьУчастниковРабочейГруппы(Форма) Экспорт
	
	РабочаяГруппа = Новый Массив;
	Для каждого РабочаяГруппаТаблицаСтрока Из Форма.РабочаяГруппаТаблица Цикл
		Участник = Новый Структура;
		Участник.Вставить("Исполнитель", РабочаяГруппаТаблицаСтрока.Участник);
		Участник.Вставить("ОсновнойОбъектАдресации", РабочаяГруппаТаблицаСтрока.ОсновнойОбъектАдресации);
		Участник.Вставить("ДополнительныйОбъектАдресации", РабочаяГруппаТаблицаСтрока.ДополнительныйОбъектАдресации);
		РабочаяГруппа.Добавить(Участник);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Подбор участников рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбранныеЗаголовок", НСтр("ru = 'Выбранные участники'"));
	ПараметрыФормы.Вставить("Выбранные", РабочаяГруппа);
	ПараметрыФормы.Вставить("ПодбиратьГруппыПользователей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьРоли", Истина);
	ПараметрыФормы.Вставить("ТолькоПростыеРоли", Ложь);
	ПараметрыФормы.Вставить("БезВнешнихРолей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Ложь);
	ОткрытьФорму("ОбщаяФорма.ПодборИсполнителей", ПараметрыФормы, Форма.Элементы.РабочаяГруппаТаблица);
	
КонецПроцедуры

// Обработчик команды формы проекта ПодобратьУчастниковРабочейГруппы
//
Процедура ПроектПодобратьУчастниковРабочейГруппы(Форма) Экспорт
	
	РабочаяГруппа = Новый Массив;
	Для каждого РабочаяГруппаТаблицаСтрока Из Форма.РабочаяГруппаТаблица Цикл
		Участник = Новый Структура;
		Участник.Вставить("Исполнитель", РабочаяГруппаТаблицаСтрока.Участник);
		Участник.Вставить("ОсновнойОбъектАдресации", РабочаяГруппаТаблицаСтрока.ОсновнойОбъектАдресации);
		Участник.Вставить("ДополнительныйОбъектАдресации", РабочаяГруппаТаблицаСтрока.ДополнительныйОбъектАдресации);
		РабочаяГруппа.Добавить(Участник);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Подбор участников рабочей группы'"));
	ПараметрыФормы.Вставить("ВыбранныеЗаголовок", НСтр("ru = 'Выбранные участники'"));
	ПараметрыФормы.Вставить("Выбранные", РабочаяГруппа);
	ПараметрыФормы.Вставить("ПодбиратьГруппыПользователей", Ложь);
	ПараметрыФормы.Вставить("ПоказыватьРоли", Истина);
	ПараметрыФормы.Вставить("ТолькоПростыеРоли", Ложь);
	ПараметрыФормы.Вставить("БезВнешнихРолей", Истина);
	ПараметрыФормы.Вставить("ПоказыватьФункции", Ложь);
	ОткрытьФорму("ОбщаяФорма.ПодборИсполнителей", ПараметрыФормы, Форма.Элементы.РабочаяГруппаТаблица);
	
КонецПроцедуры

// Устанавливает в строке реквизиты, необходимые для условного оформления
//
Процедура УстановитьРеквизитыУсловногоОформления(ТекущиеДанные) Экспорт
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные.ЭтоРоль = Ложь;
	Если ЗначениеЗаполнено(ТекущиеДанные.Участник) Тогда
		Если ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			ТекущиеДанные.Иконка = 1;
			ТекущиеДанные.ЭтоРоль = Истина;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
			ТекущиеДанные.Иконка = 2;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("СправочникСсылка.Пользователи") Тогда
			ТекущиеДанные.Иконка = 3;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Участник) = Тип("Строка") Тогда
			ТекущиеДанные.Иконка = 4;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
