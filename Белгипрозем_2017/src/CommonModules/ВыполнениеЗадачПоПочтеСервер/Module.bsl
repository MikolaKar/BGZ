////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции для работы механизма выполнения задач по почте
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Обработчик регламентного задания ВыполнениеЗадачПоПочте.
// Предназначен для обработки писем пришедших в качестве ответов на уведомление
// о новой задаче на адрес системной учетной записи.
// 
Процедура ВыполнитьЗадачиПоПочте() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ОчиститьРегистрОтУстаревшихЗаписей();
	
	Попытка
		ПараметрыЗагрузкиПочтовыхСообщений = Неопределено;
		Сообщения = ПолучитьВходящиеСообщенияСистемнойУчетнойЗаписи(
						ПараметрыЗагрузкиПочтовыхСообщений);
		
		Если ТипЗнч(Сообщения) <> Тип("Массив") Тогда
			Возврат;
		КонецЕсли;
		
		МассивСообщенийКУдалению = Новый Массив;
		
		Для каждого Сообщение Из Сообщения Цикл
			Если ОбработатьСообщениеОВыполненииЗадачи(Сообщение) Тогда
				МассивСообщенийКУдалению.Добавить(Сообщение.Идентификатор);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивСообщенийКУдалению.Количество() > 0 Тогда
			УдалитьСообщенияОВыполненииЗадач(
				МассивСообщенийКУдалению, ПараметрыЗагрузкиПочтовыхСообщений);
		КонецЕсли;
		
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			НСтр("ru='ВыполнениеЗадачПоПочте'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			СообщениеОбОшибке);
			
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Формирует тему уведомления по задаче.
//
Функция СформироватьТемуУведомленияПоЗадачеСВозможностьюВыполненияПоПочте(
		ЗадачаСсылка) Экспорт
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"%1: %2", НСтр("ru = 'Задача'"), ЗадачаСсылка);
	
	Возврат Результат;
	
КонецФункции

// Формирует текст уведомления по задаче, текст включает всю необходимую информацию
// для возможности выполнения задачи при ответе на уведомление. В параметр файлы возвращается
// массив ссылок на файлы, которые необходимо приложить к уведомлению.
//
Функция СформироватьТекстУведомленияПоЗадачеСВозможностьюВыполненияПоПочте(
			ЗадачаСсылка,
			ПолучательУведомления,
			Файлы) Экспорт
	
	// Получение файлов предметов задачи
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивПредметовОбъекта(
					ЗадачаСсылка,,
					Истина);
	
	ДоступныеПредметы = Новый Массив;
	
	Для каждого Предмет Из Предметы Цикл
		Если ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(
				Предмет, ПолучательУведомления).Чтение Тогда
				
			ДоступныеПредметы.Добавить(Предмет);
		КонецЕсли;
	КонецЦикла;
	
	ФайлыНеПопавшиеВУведомления = " ";
	
	Если ЗначениеЗаполнено(ДоступныеПредметы) Тогда
		МаксимальныйРазмерВложений = 
			Константы.МаксимальныйРазмерВложенийВУведомленииНовойЗадачи.Получить() * 1024 * 1024;
			
		ТекущийРазмерВложений = 0;
		
		Файлы = Новый Массив;
		
		ТаблицаФайлов = ПолучитьФайлыПредметов(ДоступныеПредметы);
		
		Если ЗначениеЗаполнено(ТаблицаФайлов) Тогда
			Для каждого СтрокаФайла из ТаблицаФайлов Цикл
				ТекущийРазмерВложений = ТекущийРазмерВложений + СтрокаФайла.ТекущаяВерсияРазмер;
			
				Если ТекущийРазмерВложений > МаксимальныйРазмерВложений Тогда
					Если ЗначениеЗаполнено(ФайлыНеПопавшиеВУведомления) Тогда
						ФайлыНеПопавшиеВУведомления = ФайлыНеПопавшиеВУведомления + Символы.ПС;
					КонецЕсли;
					ФайлыНеПопавшиеВУведомления = ФайлыНеПопавшиеВУведомления + " - " 
						+ СтрокаФайла.ИмяФайла;
				Иначе
					СтруктураФайла = Новый Структура("Ссылка, ИмяФайла");
					ЗаполнитьЗначенияСвойств(СтруктураФайла, СтрокаФайла);
					Файлы.Добавить(СтруктураФайла);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФайлыНеПопавшиеВУведомления) Тогда
			ФайлыНеПопавшиеВУведомления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '
					|Общий размер вложений превысил допустимый максимум.
					|Список файлов, которые не попали в уведомление:
					|%1
					|'"),
				ФайлыНеПопавшиеВУведомления);
		КонецЕсли;
	КонецЕсли;
	
	СсылкиОтветов = ПолучитьОтветыВВидеСсылокMailto(ЗадачаСсылка);
	
	Если СсылкиОтветов.КоличествоОтветов > 1 Тогда
		Комментарий =
			НСтр("ru = 'Не отвечайте на это письмо. Для выполнения задачи нажмите на одну из ссылок.'");
	ИначеЕсли СсылкиОтветов.КоличествоОтветов = 1 Тогда
		Комментарий = НСтр("ru = 'Не отвечайте на это письмо. Для выполнения задачи нажмите на ссылку.'");
	КонецЕсли;
	
	ПредставлениеЗадачи = РаботаСУведомлениями.СформироватьПредставлениеЗадачи(
		ЗадачаСсылка, Справочники.ВидыБизнесСобытий.СозданиеЗадачи, "");
		
	ПерваяСтрокаПредставления = СтрПолучитьСтроку(ПредставлениеЗадачи,1);
	
	ОстальнаяЧастьПредставления = СтрЗаменить(
		ПредставлениеЗадачи,
		ПерваяСтрокаПредставления + "
			|",
		"");
	
	ОстальнаяЧастьПредставления = СтрЗаменить(ОстальнаяЧастьПредставления, Символы.ПС, "<BR>");
	
	Результат = "
		|<html>
		|<body>
		|%1
		|<BR>
		|%2
		|<BR>
		|<BR>
		|%3
		|<BR>
		|<BR>
		|%4
		|<BR>
		|<BR>
		|<B>%5</B>
		|</body>
		|</html>";
		
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Результат,
		ПерваяСтрокаПредставления,
		ОстальнаяЧастьПредставления,
		ФайлыНеПопавшиеВУведомления,
		Комментарий,
		СсылкиОтветов.Текст);
		
	Возврат Результат;
		
КонецФункции

// Возвращает структуру с настройками по умолчанию механизма выполнения задач по почте
//
// Возвращаемое значение:
//  Структура:
//   МаксимальныйРазмерВложенийВУведомленииНовойЗадачи - Число
//   СрокХраненияПротоколовВыполненияЗадачПоПочте - Число
//
Функция ПолучитьНастройкиПоУмолчанию() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("МаксимальныйРазмерВложенийВУведомленииНовойЗадачи", 10);
	Результат.Вставить("СрокХраненияПротоколовВыполненияЗадачПоПочте", 7);
	
	Возврат Результат;
	
КонецФункции

// Формирует уведомление и помещает его в очередь на отправку по задаче
Функция СформироватьУведомлениеПоЗадаче(Задача) Экспорт
	
	Результат = Ложь;
	
	Если ПроверитьДоступностьФормированияУведомлений(Задача) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		РаботаСУведомлениями.ДобавитьУведомленияИсполнителейЗадачи(
			Задача,
			ПредопределенноеЗначение("Справочник.ВидыБизнесСобытий.СозданиеЗадачи"),
			Задача);
			
		Результат = Истина;
			
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет доступность исполнения процедуры СформироватьУведомлениеПоЗадаче
Функция ПроверитьДоступностьФормированияУведомлений(Задача) Экспорт
	
	Если ЗначениеЗаполнено(Задача) Тогда
	
		Результат = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Задача,
			"Выполнена, ТекущийИсполнитель, ТекущийОсновнойОбъектАдресации, ТекущийДополнительныйОбъектАдресации");
		
		Если НЕ ЗначениеЗаполнено(РеквизитыЗадачи.ТекущийИсполнитель) Тогда
			Результат = Ложь;
		КонецЕсли;
		
		ТипИсполнителя = ТипЗнч(РеквизитыЗадачи.ТекущийИсполнитель);
		
		ИсполнителиЗадачи = Новый Массив;
		ВключеноВыполнениеЗадачПоПочте = Ложь;
		Если ТипИсполнителя = Тип("СправочникСсылка.Пользователи") Тогда
			ИсполнителиЗадачи.Добавить(РеквизитыЗадачи.ТекущийИсполнитель);
		ИначеЕсли ТипИсполнителя = Тип("СправочникСсылка.РолиИсполнителей") Тогда
			ИсполнителиЗадачи = РегистрыСведений.ИсполнителиЗадач.ПолучитьИсполнителейРоли(
				РеквизитыЗадачи.ТекущийИсполнитель,
				РеквизитыЗадачи.ТекущийОсновнойОбъектАдресации,
				РеквизитыЗадачи.ТекущийДополнительныйОбъектАдресации,
				Истина);
				
			ИсполнителиЗадачи = ИсполнителиЗадачи.ВыгрузитьКолонку("Исполнитель");
		КонецЕсли;
		
		Для Каждого ИсполнительЗадачи Из ИсполнителиЗадачи Цикл
			ВключеноВыполнениеЗадачПоПочте = РаботаСУведомлениями.ПолучитьНастройкуПользователя(
				ИсполнительЗадачи,
				Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте, 
				Справочники.ВидыБизнесСобытий.СозданиеЗадачи);
			Если ВключеноВыполнениеЗадачПоПочте Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ВключеноВыполнениеЗадачПоПочте Тогда
			
			Результат = Ложь;
			
		КонецЕсли;
		
		Если РеквизитыЗадачи.Выполнена Тогда
			Результат = Ложь;
		КонецЕсли;
		
	Иначе
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Формирует текст ссылок MailTo для ответов на задачу.
//
Функция ПолучитьОтветыВВидеСсылокMailto(ЗадачаСсылка)
	
	Результат = Новый Структура;
	Результат.Вставить("Текст", "");
	Результат.Вставить("КоличествоОтветов", 0);
	
	ВариантыОтветовДляВыполненияЗадачи = ПолучитьСписокВариантовВыполненияЗадачи(ЗадачаСсылка);
	
	ВозможныеВариантыВыполнения = ВариантыОтветовДляВыполненияЗадачи.СписокВариантовОтветов;
	
	Результат.КоличествоОтветов = ВозможныеВариантыВыполнения.Количество();
		
	АдресЭлектроннойПочты =
		ОбщегоНазначения.ПолучитьЗначениеРеквизита(
			Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты,
			"АдресЭлектроннойПочты");
			
	УИЗадачи = Строка(ЗадачаСсылка.УникальныйИдентификатор());
	
	Разделитель = "";
	
	Для Каждого ВариантОтвета Из ВозможныеВариантыВыполнения Цикл
		
		РезультатОтвета = ПолучитьЧисловойИдентификаторВариантаОтвета(ВариантОтвета.Значение);
		
		КомментарийКИсполнению = "";
		
		//МиСофт+
		Если ВариантыОтветовДляВыполненияЗадачи.ИспользоватьКомментарий Тогда
			КомментарийКИсполнению = 
			НСтр("ru = '
			|1. Приложите файлы, если нужно.
			|2. Впишите ваш комментарий по выполнению задачи.
			|
			|Комментарий:'");
		КонецЕсли;
		//МиСофт-
		
		ТекстОтвета = НСтр("ru = '%1
			|
			|________________________________________________
			|Это служебная информация, необходимая для выполнения задачи.
			|Не удаляйте и не изменяйте ее!
			|
			|*Задача:%2
			|*Ответ:%3
			|
			|'");
			
		ТекстОтвета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОтвета,
			КомментарийКИсполнению,
			УИЗадачи,
			РезультатОтвета);
			
		СсылкаНаОтвет = ВстроеннаяПочтаКлиентСервер.СформироватьСсылкуMailto(
			АдресЭлектроннойПочты,,,
			ВариантОтвета.Представление + ": " + Строка(ЗадачаСсылка),
			ТекстОтвета,
			ПредопределенноеЗначение("Перечисление.ТипыТекстовПочтовыхСообщений.HTML"),
			ВариантОтвета.Представление + "...");
			
		Результат.Текст = Результат.Текст + Разделитель + СсылкаНаОтвет;
		
		Разделитель = " / ";
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив входящих сообщений пришедших на адрес системной учетной записи.
//
Функция ПолучитьВходящиеСообщенияСистемнойУчетнойЗаписи(ПараметрыЗагрузкиПочтовыхСообщений)
	
	ПараметрыЗагрузкиПочтовыхСообщений = 
		ЛегкаяПочтаСервер.ПолучитьПараметрыЗагрузкиПочтовыхСообщений();
		
	ПараметрыЗагрузкиПочтовыхСообщений.Вставить(
		"НепрочитанныеСообщения", Истина);
	ПараметрыЗагрузкиПочтовыхСообщений.Вставить(
		"УникальныйИдентификатор", Новый УникальныйИдентификатор);
	
	ДоступныеПрофили = 
		ПараметрыЗагрузкиПочтовыхСообщений.НастройкиПрофилейДляЗагрузки.ДоступныеПрофили;
		
	ПараметрыЗагрузкиПочтовыхСообщений.НастройкиПрофилейДляЗагрузки.Профиль = Неопределено;
	
	СистемнаяУчетнаяЗапись = 
		Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты;
	
	Для каждого СтруктураПрофиля Из ДоступныеПрофили Цикл
		Если СтруктураПрофиля.Профиль = СистемнаяУчетнаяЗапись Тогда
			
			ПараметрыЗагрузкиПочтовыхСообщений.НастройкиПрофилейДляЗагрузки.Профиль = 
				СтруктураПрофиля;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыЗагрузкиПочтовыхСообщений.НастройкиПрофилейДляЗагрузки.Профиль = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЛегкаяПочтаСервер.ПолучитьИнтернетПочта(ПараметрыЗагрузкиПочтовыхСообщений);
	КонецЕсли;
	
КонецФункции

// Удаляет сообщение из почтового ящика системной учетной записи
//
Процедура УдалитьСообщенияОВыполненииЗадач(
			СерверныеИдентификаторыСообщений, ПараметрыЗагрузкиПочтовыхСообщений)
	
	Пароль = ПараметрыЗагрузкиПочтовыхСообщений.НастройкиПрофилейДляЗагрузки.Профиль.Пароль;
	
	Результат = ЛегкаяПочтаСервер.УдалитьСообщенияИнтернетПочта(
		Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты,
		Пароль,
		СерверныеИдентификаторыСообщений);
	
КонецПроцедуры

// Обрабатывает сообщение о выполнении задачи.
// При успешной обработке возвращает истину. В случае возникновения ошибки
// во время обработки возвращает ложь. 
// В тексте сообщения производит поиск ответа для выполнения задачи.
// При обнаружении ответа, выполняет задачу с указанным ответом.
// Результат обработки записывает в регистр сведений ПротоколВыполненияЗадачПоПочте.
//
Функция ОбработатьСообщениеОВыполненииЗадачи(Сообщение)
	
	НачатьТранзакцию();
	
	ЗадачаСсылка = ПредопределенноеЗначение("Задача.ЗадачаИсполнителя.ПустаяСсылка");
	
	Попытка
		
		ИдентификаторЗадачи = ПолучитьСтрокуСообщенияПоЗаголовкуПоля(
			Сообщение,
			НСтр( "ru = '*Задача:'"),
			36);
			
		ИдентификаторОтвета = ПолучитьСтрокуСообщенияПоЗаголовкуПоля(
			Сообщение,
			НСтр( "ru = '*Ответ:'"),
			1);
		
		Если НЕ ЗначениеЗаполнено(ИдентификаторЗадачи) Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ВСообщенииНеУказанИдентификаторЗадачи,,
				ЗадачаСсылка);
				
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ВСообщенииНеУказанИдентификаторЗадачи,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
		
		ЗадачаПоИдентификатору = Задачи.ЗадачаИсполнителя.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ИдентификаторЗадачи));
			
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗадачаИсполнителя.Ссылка
			|ИЗ
			|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
			|ГДЕ
			|	ЗадачаИсполнителя.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ЗадачаПоИдентификатору);
		
		Если Запрос.Выполнить().Пустой() Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение, Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачаНеНайдена,,
				ЗадачаСсылка);
				
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачаНеНайдена,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
		
		ЗадачаСсылка = ЗадачаПоИдентификатору;
			
		Если НЕ ЗначениеЗаполнено(ИдентификаторОтвета) Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ВСообщенииНеУказанИдентификаторОтвета,,
				ЗадачаСсылка);
			
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ВСообщенииНеУказанИдентификаторОтвета,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
		
		ВариантВыполненияЗадачи =
			ПолучитьВариантВыполнениеЗадачиПоПоЧисловомуИдентификатору(ИдентификаторОтвета);
		
		Если ВариантВыполненияЗадачи = Неопределено Тогда
					
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение, Перечисления.РезультатВыполненияЗадачиПоПочте.ОтветНеНайден,,
				ЗадачаСсылка);
				
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ОтветНеНайден,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
			
		Если НЕ ПроверитьСоответсвиеРезультатВыполненияИЗадачи(
			ЗадачаСсылка,
			ВариантВыполненияЗадачи) Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачеНеСоответствуетЗначениеОтвета,,
				ЗадачаСсылка);
			
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачеНеСоответствуетЗначениеОтвета,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
		
		ПараметрыОтправителя = РаботаСоСтроками.РазложитьПредставлениеАдресаЭлектроннойПочты(Сообщение.Отправитель);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПользователиКонтактнаяИнформация.Ссылка КАК Пользователь
			|ПОМЕСТИТЬ НайденныеПользователиПоАдресу
			|ИЗ
			|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
			|ГДЕ
			|	ПользователиКонтактнаяИнформация.АдресЭП ПОДОБНО ""%"" + &АдресЭП + ""%""
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СпособыУведомленияПользователей.Пользователь
			|ИЗ
			|	РегистрСведений.СпособыУведомленияПользователей КАК СпособыУведомленияПользователей
			|ГДЕ
			|	СпособыУведомленияПользователей.ДанныеСпособа ПОДОБНО ""%"" + &АдресЭП + ""%""
			|	И СпособыУведомленияПользователей.СпособУведомления = ЗНАЧЕНИЕ(Перечисление.СпособыУведомления.ПоПочте)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НайденныеПользователиПоАдресу.Пользователь
			|ИЗ
			|	НайденныеПользователиПоАдресу КАК НайденныеПользователиПоАдресу
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО НайденныеПользователиПоАдресу.Пользователь = Пользователи.Ссылка
			|ГДЕ
			|	Пользователи.ПометкаУдаления = ЛОЖЬ
			|	И Пользователи.Служебный = ЛОЖЬ
			|	И Пользователи.Недействителен = ЛОЖЬ";
			
		Запрос.УстановитьПараметр("АдресЭП", ПараметрыОтправителя.Адрес);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 1 Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНайденоНесколькоИсполнителей,,
				ЗадачаСсылка);
			
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНайденоНесколькоИсполнителей,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		ИначеЕсли Выборка.Количество() = 0 Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНеНайденИсполнительЗадачи,,
				ЗадачаСсылка);
			
			ОтправитьУведомлениеОбОшибке(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНеНайденИсполнительЗадачи,
				ЗадачаСсылка);
			
			ЗафиксироватьТранзакцию();
			Возврат Истина;
			
		КонецЕсли;
		
		Выборка.Следующий();
		ИсполнительЗадачиПоЭлАдресу = Выборка.Пользователь;
		
		РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаСсылка,
			"Исполнитель,
			|РольИсполнителя,
			|ОсновнойОбъектАдресации,
			|ДополнительныйОбъектАдресации,
			|Выполнена,
			|СостояниеБизнесПроцесса");
			
		ИсполнительСсылка = РеквизитыЗадачи.Исполнитель;
			
		Если ЗначениеЗаполнено(РеквизитыЗадачи.Исполнитель) Тогда // Ролевая задача принята к исполнению
			
			Если РеквизитыЗадачи.Исполнитель <> ИсполнительЗадачиПоЭлАдресу Тогда
			
				РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
					Сообщение,
					Перечисления.РезультатВыполненияЗадачиПоПочте.ДляЗадачиОпределенДругойИсполнитель,,
					ЗадачаСсылка);
				
				ОтправитьУведомлениеОбОшибке(
					Сообщение,
					Перечисления.РезультатВыполненияЗадачиПоПочте.ДляЗадачиОпределенДругойИсполнитель,
					ЗадачаСсылка);
				
				ЗафиксироватьТранзакцию();
				Возврат Истина;
				
			КонецЕсли;
			
		Иначе // Ролевая задача не принятая к исполнению
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ИсполнителиЗадач.Исполнитель
				|ИЗ
				|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
				|ГДЕ
				|	ИсполнителиЗадач.РольИсполнителя = &РольИсполнителя
				|	И ИсполнителиЗадач.ОсновнойОбъектАдресации = &ОсновнойОбъектАдресации
				|	И ИсполнителиЗадач.ДополнительныйОбъектАдресации = &ДополнительныйОбъектАдресации
				|	И ИсполнителиЗадач.Исполнитель = &Исполнитель";
				
			Запрос.УстановитьПараметр("РольИсполнителя", РеквизитыЗадачи.РольИсполнителя);
			Запрос.УстановитьПараметр("ОсновнойОбъектАдресации", РеквизитыЗадачи.ОсновнойОбъектАдресации);
			Запрос.УстановитьПараметр("ДополнительныйОбъектАдресации", РеквизитыЗадачи.ДополнительныйОбъектАдресации);
			Запрос.УстановитьПараметр("Исполнитель", ИсполнительЗадачиПоЭлАдресу);
			
			Если Запрос.Выполнить().Пустой() Тогда // Если пользователь не является исполнителем текущей задачи
				
				РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
					Сообщение,
					Перечисления.РезультатВыполненияЗадачиПоПочте.ДляЗадачиОпределенДругойИсполнитель,,
					ЗадачаСсылка);
				
				ОтправитьУведомлениеОбОшибке(
					Сообщение,
					Перечисления.РезультатВыполненияЗадачиПоПочте.ДляЗадачиОпределенДругойИсполнитель,
					ЗадачаСсылка);
				
				ЗафиксироватьТранзакцию();
				Возврат Истина;
				
			КонецЕсли;
			
			ИсполнительСсылка = ИсполнительЗадачиПоЭлАдресу;
			
		КонецЕсли;
		
		Если РеквизитыЗадачи.Выполнена = Истина
			ИЛИ РеквизитыЗадачи.СостояниеБизнесПроцесса <> Перечисления.СостоянияБизнесПроцессов.Активен Тогда
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение,
				Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачаНеАктивна,,
				ЗадачаСсылка);
			
		Иначе
			
			ТекстОкончанияКомментария = НСтр("ru = '________________________________________________'");
			НачальнаяПозицияКомментария = 1;
			КонечнаяПозицияКомментария = Найти(Сообщение.Текст, ТекстОкончанияКомментария);
			КоличествоСимволов = КонечнаяПозицияКомментария - НачальнаяПозицияКомментария;
			КомментарийВСообщении = Сред(Сообщение.Текст, НачальнаяПозицияКомментария, КоличествоСимволов);
			
			КомментарийВСообщении = СокрЛП(КомментарийВСообщении);
			
			КомментарийВСообщении = КомментарийВСообщении + ?(ЗначениеЗаполнено(КомментарийВСообщении), "
				|", "") + НСтр("ru = 'Задача исполнена по почте.'");
			
			Параметры = Новый Структура;
			Параметры.Вставить("ВариантВыполнения", ВариантВыполненияЗадачи);
			Параметры.Вставить("Комментарий", КомментарийВСообщении);
			Параметры.Вставить("Исполнитель", ИсполнительСсылка);
			
			БизнесПроцессыИЗадачиВызовСервера.ВыполнитьЗадачуПоПочте(ЗадачаСсылка, Параметры);
			
			ОбработатьВложенияВСообщении(ЗадачаСсылка, Сообщение);
			
			РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
				Сообщение, 
				Перечисления.РезультатВыполненияЗадачиПоПочте.Выполнена,
				Строка(ВариантВыполненияЗадачи) + ". " + КомментарийВСообщении,
				ЗадачаСсылка);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		Возврат Истина;
		
	Исключение
		ОтменитьТранзакцию();
		РегистрыСведений.ПротоколВыполненияЗадачПоПочте.ЗаписатьРезультатВыполненияПоСообщению(
			Сообщение, 
			Перечисления.РезультатВыполненияЗадачиПоПочте.Ошибка,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			ЗадачаСсылка);
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ПолучитьВариантВыполнениеЗадачиПоПоЧисловомуИдентификатору(НомерРезультата)
	
	Если НомерРезультата = "1" Тогда
		ВариантВыполненияЗадачи =
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно;
	ИначеЕсли НомерРезультата = "2" Тогда
		ВариантВыполненияЗадачи =
			Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно;
	ИначеЕсли НомерРезультата = "3" Тогда
		ВариантВыполненияЗадачи =
			Перечисления.ВариантыВыполненияПроцессовИЗадач.ПоложительноСЗамечаниями;
	Иначе
		ВариантВыполненияЗадачи = Неопределено;
	КонецЕсли;
	
	Возврат ВариантВыполненияЗадачи;
	
КонецФункции

Функция ПолучитьЧисловойИдентификаторВариантаОтвета(ВариантОтвета)
	
	РезультатОтвета = "0";
	
	Если ВариантОтвета =
		Перечисления.ВариантыВыполненияПроцессовИЗадач.Положительно Тогда
		
		РезультатОтвета = "1";
		
	ИначеЕсли ВариантОтвета =
		Перечисления.ВариантыВыполненияПроцессовИЗадач.Отрицательно Тогда
		
		РезультатОтвета = "2";
		
	ИначеЕсли ВариантОтвета =
		Перечисления.ВариантыВыполненияПроцессовИЗадач.ПоложительноСЗамечаниями Тогда
		
		РезультатОтвета = "3";
		
	КонецЕсли;
	
	Возврат РезультатОтвета;
	
КонецФункции

Функция ПроверитьСоответсвиеРезультатВыполненияИЗадачи(ЗадачаСсылка, РезультатОтвета)
	
	Результат = Истина;
	
	ВозможныеВариантыВыполнения = ПолучитьСписокВариантовВыполненияЗадачи(ЗадачаСсылка);
	
	Если ВозможныеВариантыВыполнения.СписокВариантовОтветов.НайтиПоЗначению(
			РезультатОтвета) = Неопределено Тогда
			
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСписокВариантовВыполненияЗадачи(ЗадачаСсылка)
	
	СтруктураРеквизитов = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
		ЗадачаСсылка, "БизнесПроцесс, ТочкаМаршрута");
		
	МенеджерПроцесса = ОбщегоНазначения.МенеджерОбъектаПоСсылке(
		СтруктураРеквизитов.БизнесПроцесс);
	
	ВозможныеВариантыВыполнения =
		МенеджерПроцесса.ВариантыОтветовДляВыполненияЗадачиПоПочте(
			ЗадачаСсылка,
			СтруктураРеквизитов.БизнесПроцесс,
			СтруктураРеквизитов.ТочкаМаршрута);
		
	Возврат ВозможныеВариантыВыполнения;
	
КонецФункции

// Возвращает строку в тексте сообщения после строки ЗаголовокПоля.
// 
Функция ПолучитьСтрокуСообщенияПоЗаголовкуПоля(Сообщение, ЗаголовокПоля, ДлинаСтроки)
	
	Результат = "";
	
	ТекстСообщения = Сообщение.Текст;
	
	Позиция = Найти(ТекстСообщения, ЗаголовокПоля);
	
	Если Позиция = 0 Тогда
		Результат = "";
	Иначе
		Результат = Сред(ТекстСообщения, Позиция + СтрДлина(ЗаголовокПоля), ДлинаСтроки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает вложения в сообщении.
// Если вложения в сообщении отличаются от файлов предмета задачи, 
// то прикрепляет вложения к бизнес-процессу задачи как вспомогательные 
// предметы.
//
Процедура ОбработатьВложенияВСообщении(ЗадачаСсылка, Сообщение)
	
	Если НЕ ЗначениеЗаполнено(Сообщение.Вложения) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыЗадачи = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(
	ЗадачаСсылка, "БизнесПроцесс");
	
	Для каждого Вложение Из Сообщение.Вложения Цикл
		ИмяФайлаИнфо = РаботаСоСтроками.РазложитьИмяФайла(Вложение.ИмяФайла);
		
		ВремяИзменения = ТекущаяДатаСеанса();
		ВремяИзмененияУниверсальное = РаботаСФайламиКлиентСервер.ПолучитьУниверсальноеВремя(
		ВремяИзменения);
		
		Комментарий = "Файл получен при исполнении задачи по почте.
			|Задача: " + ЗадачаСсылка + "
			|Адрес отправителя: " + Сообщение.Отправитель;
		
		ФайлСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(
		РеквизитыЗадачи.БизнесПроцесс, // ВладелецФайла
		ИмяФайлаИнфо.Имя, // ИмяБезРасширения,
		ИмяФайлаИнфо.Расширение, // РасширениеБезТочки
		ВремяИзменения,//ВремяИзменения
		ВремяИзмененияУниверсальное,//ВремяИзмененияУниверсальное
		Вложение.Размер,
		Вложение.Адрес, //АдресВременногоХранилищаФайла
		Неопределено, //АдресВременногоХранилищаТекста
		Ложь,, // ЭтоВебКлиент
		Комментарий //Комментарий
		);
		
		ТекущиеИменаПредметов =
		МультипредметностьВызовСервера.ПолучитьМассивИменПредметовПоСсылкеНаПроцесс(
		РеквизитыЗадачи.БизнесПроцесс);
		ИмяПредмета = МультипредметностьВызовСервера.ПолучитьСсылкуНаИмяПредметаПоСсылкеНаПредмет(
		ФайлСсылка, ТекущиеИменаПредметов);
		
		Мультипредметность.ЗаписатьПредметБизнесПроцесса(
		РеквизитыЗадачи.БизнесПроцесс, 
		ИмяПредмета,
		ФайлСсылка);
		
	КонецЦикла;

КонецПроцедуры

Процедура ОтправитьУведомлениеОбОшибке(Сообщение, РезультатВыполненияЗадачиПоПочте, Задача)
	
	Если ТипЗнч(РезультатВыполненияЗадачиПоПочте) <>
		Тип("ПеречислениеСсылка.РезультатВыполненияЗадачиПоПочте") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(Сообщение) <> Тип("Структура")
		ИЛИ НЕ Сообщение.Свойство("Отправитель") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(Задача) <> Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Возврат;
	КонецЕсли;
	
	ТемаУведомления = НСтр("ru = 'Ошибка исполнения задачи по почте: '");
	
	Если ЗначениеЗаполнено(Задача) Тогда
		ТемаУведомления = ТемаУведомления + Строка(Задача);
	КонецЕсли;
	
	Если РезультатВыполненияЗадачиПоПочте =
		Перечисления.РезультатВыполненияЗадачиПоПочте.ЗадачаНеНайдена Тогда
		
		ТекстУведомления = 
			НСтр("ru = 'При обработке письма об исполнении задачи возникла ошибка.
			|Не найдена задача в базе данных.
			|Обратитесь к автору задачи или администратору.'");
			
	ИначеЕсли РезультатВыполненияЗадачиПоПочте =
			Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНайденоНесколькоИсполнителей
		ИЛИ РезультатВыполненияЗадачиПоПочте =
			Перечисления.РезультатВыполненияЗадачиПоПочте.ПоАдресуОтправителяНеНайденИсполнительЗадачи Тогда
		
		ТекстУведомления = 
			НСтр("ru = 'При обработке письма об исполнении задачи возникла ошибка.
			|По вашему адресу не удалось определить исполнителя задачи.
			|Обратитесь к администратору.'");
			
	ИначеЕсли РезультатВыполненияЗадачиПоПочте =
			Перечисления.РезультатВыполненияЗадачиПоПочте.ДляЗадачиОпределенДругойИсполнитель Тогда
			
		ТекстУведомления = 
			НСтр("ru = 'При обработке письма об исполнении задачи возникла ошибка.
			|Для задачи определен другой исполнитель.
			|Обратитесь к автору задачи.'");
			
	Иначе
		
		//МиСофт+
		ТекстУведомления = 
			НСтр("ru = 'При обработке письма об исполнении задачи возникла ошибка.
			|Возможно в вашем письме (ответе) отсутствовал текст служебной информации.
			|Попробуйте исполнить задачу еще раз.'");
		//МиСофт-
		
	КонецЕсли;
	
	ПоляЗаголовка = Новый Массив;
	
	Для Каждого Заголовок ИЗ Сообщение.ПоляЗаголовка Цикл
		Если Заголовок.Имя = "Message-Id" Тогда
			ЗаголовокInReplyTo = Почта.СформироватьСтруктуруПоляЗаголовка(
				"In-Reply-To",
				Заголовок.Значение,
				СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования);
			ПоляЗаголовка.Добавить(ЗаголовокInReplyTo);
		КонецЕсли;
	КонецЦикла;
	
	ОтправитьУведомление(
		ТемаУведомления,
		ТекстУведомления,
		Сообщение.Отправитель,
		Новый Массив,
		ПоляЗаголовка);
	
	
КонецПроцедуры

Процедура ОтправитьУведомление(ТемаУведомления,
	ТекстУведомления,
	Получатель,
	МассивВложений,
	ПоляЗаголовка)
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Тема", ТемаУведомления);
	ПараметрыПисьма.Вставить("Текст", ТекстУведомления);
	ПараметрыПисьма.Вставить("Кому", Получатель);
	ПараметрыПисьма.Вставить("Вложения", МассивВложений);
	ПараметрыПисьма.Вставить("ТипТекста", Перечисления.ТипыТекстовПочтовыхСообщений.ПростойТекст);
	ПараметрыПисьма.Вставить("ПоляЗаголовка", ПоляЗаголовка);
	
	ЛегкаяПочтаСервер.ОтправитьИнтернетПочта(ПараметрыПисьма);
	
КонецПроцедуры

// Возвращает таблицу значений с файлами указанных предметов
//
Функция ПолучитьФайлыПредметов(Предметы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение КАК ИмяФайла,
		|	Файлы.ТекущаяВерсияРазмер,
		|	Файлы.Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.ВладелецФайла В(&ВладелецФайла)
		|	И НЕ Файлы.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение,
		|	Файлы.ТекущаяВерсияРазмер,
		|	Файлы.Ссылка
		|ИЗ
		|	Справочник.Файлы КАК Файлы
		|ГДЕ
		|	Файлы.Ссылка В(&ВладелецФайла)
		|	И НЕ Файлы.ПометкаУдаления";

	Запрос.УстановитьПараметр("ВладелецФайла", Предметы);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат Результат.Выгрузить();
	КонецЕсли;
	
КонецФункции