
// Выполняет непосредственную проверку прихода новых писем.
Процедура ПроверкаПоступленияНовыхПисем() Экспорт
	
	ВозвращаемыеПараметры = Неопределено;
	ВстроеннаяПочтаСервер.ПроверитьНаличиеНовыхПисем(ВозвращаемыеПараметры);
	
	Если Не ВозвращаемыеПараметры.НужноОбновитьСписок Тогда
		Возврат;
	КонецЕсли;	
	
	#Если Не ВебКлиент Тогда 
		Если ВозвращаемыеПараметры.ЧислоНовыхПисем <> 0 Тогда
			
			СправочникСсылка = ПредопределенноеЗначение("Справочник.СправочникДляОткрытияСпискаПисем.Умолчательный");
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(СправочникСсылка);
			
			ТекстОповещения = "";
			
			Если ВозвращаемыеПараметры.ЧислоНовыхПисем = 1 Тогда
				ТекстОповещения = НСтр("ru='Получено новое письмо.'");
			Иначе	
				ТекстОповещения = НСтр("ru='Получены новые письма.'");
			КонецЕсли;	
			
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"ВстроеннаяПочта/ОповещениеПользователя",
				"ЧислоНовыхПисем",
				ВозвращаемыеПараметры.ЧислоНовыхПисем);
				
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"ВстроеннаяПочта/ОповещениеПользователя",
				"ПоследнееПисьмо",
				ВозвращаемыеПараметры.ПоследнееПисьмо);
				
			ПоказатьОповещениеПользователя(
				ТекстОповещения, 
				НавигационнаяСсылка, 
				ВозвращаемыеПараметры.Описание, 
				БиблиотекаКартинок.ОповещениеОНовыхПисьмах);
			
			КонецЕсли;
	#КонецЕсли
	
	ПараметрыОповещения = Новый Структура("МожноОбновитьСписокПисем", Истина);	
	Оповестить("ПришлиНовыеПисьма_ПроверкаВозможностиОбновленияСписка", ПараметрыОповещения);
	
	Если ПараметрыОповещения.МожноОбновитьСписокПисем Тогда
		Оповестить("ПисьмаИзменены"); // чтобы обновить список писем
	КонецЕсли;	
	
КонецПроцедуры
