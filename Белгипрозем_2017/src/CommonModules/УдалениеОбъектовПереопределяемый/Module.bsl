
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает массив всех возможных типов связанных объектов
//
Функция ПолучитьТипыСвязанныхОбъектов() Экспорт
	
	МетаданныеСвязанныхОбъектов = Новый Массив;
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ВизыСогласования);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.Резолюции);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.ПроизошедшиеБизнесСобытия);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Документы.ВходящееСообщениеСВД);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Документы.ИсходящееСообщениеСВД);
	МетаданныеСвязанныхОбъектов.Добавить(Метаданные.Справочники.УведомленияПрограммы);
	
	Возврат МетаданныеСвязанныхОбъектов;
	
КонецФункции
	
// Находит объекты, которые согласно прикладной логике должны быть удалены 
// вместе с выбранными удаляемыми объектами
Функция ПолучитьТаблицуСвязанныхОбъектов(УдаляемыеОбъекты) Экспорт
	
	ТаблицаСвязанных = Новый ТаблицаЗначений;
	ТаблицаСвязанных.Колонки.Добавить("УдаляемыйОбъект");
	ТаблицаСвязанных.Колонки.Добавить("СвязанныйОбъект");
	ТаблицаСвязанных.Колонки.Добавить("ПометкаУдаленияСвязанного");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УдаляемыеОбъекты", УдаляемыеОбъекты);
		
	ДобавитьСвязанныеБизнесСобытия(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеВизыСогласования(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеРезолюции(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеСообщенияСВД(Запрос, ТаблицаСвязанных);
	ДобавитьСвязанныеУведомленияПрограммы(Запрос, ТаблицаСвязанных);
	
	ТаблицаСвязанных.Свернуть("УдаляемыйОбъект, СвязанныйОбъект, ПометкаУдаленияСвязанного");
	
	Возврат ТаблицаСвязанных;
	
КонецФункции

// Удаляет записи регистров, имеющие ссылки на удаляемые объекты
// Параметры:
//  УдаляемыеОбъекты - массив удаляемых объектов
//
Процедура УдалитьДанныеСвязанныхРегистров(УдаляемыеОбъекты) Экспорт
	
	// ФактическиеТрудозатраты
	ИмяРегистра = "ФактическиеТрудозатраты";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ЕжедневныйОтчет");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
	// СвязанныеЗаписиКалендаря
	ИмяРегистра = "СвязанныеЗаписиКалендаря";
	ПоляПоиска = Новый Массив;
	ПоляПоиска.Добавить("ЗаписьКалендаря");
	УдалениеОбъектов.УдалитьДанныеСвязанногоРегистра(ИмяРегистра, ПоляПоиска, УдаляемыеОбъекты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Бизнес-события по источнику и виду
Процедура ДобавитьСвязанныеБизнесСобытия(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроизошедшиеБизнесСобытия.Источник КАК УдаляемыйОбъект,
		|	ПроизошедшиеБизнесСобытия.Ссылка КАК СвязанныйОбъект,
		|	ПроизошедшиеБизнесСобытия.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ПроизошедшиеБизнесСобытия КАК ПроизошедшиеБизнесСобытия
		|ГДЕ
		|	ПроизошедшиеБизнесСобытия.Источник В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроизошедшиеБизнесСобытия.ВидСобытия,
		|	ПроизошедшиеБизнесСобытия.Ссылка,
		|	ПроизошедшиеБизнесСобытия.ПометкаУдаления
		|ИЗ
		|	Справочник.ПроизошедшиеБизнесСобытия КАК ПроизошедшиеБизнесСобытия
		|ГДЕ
		|	ПроизошедшиеБизнесСобытия.ВидСобытия В(&УдаляемыеОбъекты)";
		
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
		
КонецПроцедуры

// Визы согласования по источнику и документу
Процедура ДобавитьСвязанныеВизыСогласования(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВизыСогласования.Источник КАК УдаляемыйОбъект,
		|	ВизыСогласования.Ссылка КАК СвязанныйОбъект,
		|	ВизыСогласования.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Источник В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВизыСогласования.Документ,
		|	ВизыСогласования.Ссылка,
		|	ВизыСогласования.ПометкаУдаления
		|ИЗ
		|	Справочник.ВизыСогласования КАК ВизыСогласования
		|ГДЕ
		|	ВизыСогласования.Документ В(&УдаляемыеОбъекты)";
	
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры	

// Резолюции по источнику и документу
Процедура ДобавитьСвязанныеРезолюции(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Резолюции.Источник КАК УдаляемыйОбъект,
		|	Резолюции.Ссылка КАК СвязанныйОбъект,
		|	Резолюции.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции
		|ГДЕ
		|	Резолюции.Источник В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Резолюции.Документ,
		|	Резолюции.Ссылка,
		|	Резолюции.ПометкаУдаления
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции
		|ГДЕ
		|	Резолюции.Документ В(&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры	

// Сообщения СВД по документу
Процедура ДобавитьСвязанныеСообщенияСВД(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВходящееСообщениеСВД.Документ КАК УдаляемыйОбъект,
		|	ВходящееСообщениеСВД.Ссылка КАК СвязанныйОбъект,
		|	ВходящееСообщениеСВД.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции,
		|	Документ.ВходящееСообщениеСВД КАК ВходящееСообщениеСВД
		|ГДЕ
		|	ВходящееСообщениеСВД.Документ В(&УдаляемыеОбъекты)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИсходящееСообщениеСВД.Документ,
		|	ИсходящееСообщениеСВД.Ссылка,
		|	ИсходящееСообщениеСВД.ПометкаУдаления
		|ИЗ
		|	Справочник.Резолюции КАК Резолюции,
		|	Документ.ИсходящееСообщениеСВД КАК ИсходящееСообщениеСВД
		|ГДЕ
		|	ИсходящееСообщениеСВД.Документ В(&УдаляемыеОбъекты)";
	
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры	

// Уведомления программы по объектам
Процедура ДобавитьСвязанныеУведомленияПрограммы(Запрос, ТаблицаСвязанных)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомленияПрограммы.Объект КАК УдаляемыйОбъект,
		|	УведомленияПрограммы.Ссылка КАК СвязанныйОбъект,
		|	УведомленияПрограммы.ПометкаУдаления КАК ПометкаУдаленияСвязанного
		|ИЗ
		|	Справочник.УведомленияПрограммы КАК УведомленияПрограммы
		|ГДЕ
		|	УведомленияПрограммы.Объект В(&УдаляемыеОбъекты)";
		
		
	ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных);
	
КонецПроцедуры

// Добавляет строки результата запроса к переданной таблице связанных объектов
Процедура ДобавитьРезультатЗапросаВТаблицуСвязанных(Запрос, ТаблицаСвязанных)
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаСвязанных.Добавить(), Выборка);
	КонецЦикла;	
		
КонецПроцедуры

#КонецОбласти
