
// Выводит картинки состояний в карточке документа
Процедура ВывестиСтрокуСостояний(Форма) Экспорт 
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	КешИнформации = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	КешИнформации.Объект = Объект.Ссылка;
	КешИнформации.Прочитать();
	
	// Картинка Взаимодействие
	Элементы.КартинкаВзаимодействие.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи");
	Если КешИнформации.ЕстьЗадачи Тогда 
		Форма.СтатусВзаимодействие = 1;
		Элементы.КартинкаВзаимодействие.Подсказка = НСтр("ru = 'Есть выполняющиеся задачи'");
	Иначе	
		Форма.СтатусВзаимодействие = 0;
		Элементы.КартинкаВзаимодействие.Подсказка = НСтр("ru = 'Нет выполняющихся задач'");
	КонецЕсли;	
	
	// Картинка Категории
	Элементы.КартинкаКатегории.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьКатегорииДанных");
	Если Форма.СписокКатегорийДанных.Количество() > 0 Тогда 
		Форма.СтатусКатегории = 1;
		Элементы.КартинкаКатегории.Подсказка = НСтр("ru = 'Есть категории'");
	Иначе
		Форма.СтатусКатегории = 0;
		Элементы.КартинкаКатегории.Подсказка = НСтр("ru = 'Нет категорий'");
	КонецЕсли;	
	
	// Картинка СВД 
	Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда 
		Элементы.КартинкаСВД.Видимость = РаботаССВД.ДоступнаРаботаСЭДО();
		Если КешИнформации.ПолученПоСВД = Истина Тогда 
			Форма.СтатусСВД = 1;
			Элементы.КартинкаСВД.Подсказка = НСтр("ru = 'Поступил по СВД'");
		Иначе
			Форма.СтатусСВД = 0;
			Элементы.КартинкаСВД.Подсказка = НСтр("ru = 'Поступил не по СВД'");
		КонецЕсли;	
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда 
		Элементы.СостояниеПроцессаВСВД.Видимость = РаботаССВД.ДоступнаРаботаСЭДО();
		Форма.СостояниеПроцессаВСВД = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КешИнформации.СостояниеСВД, "НомерКартинкиВКоллекции");
			
		Если Форма.СостояниеПроцессаВСВД = 1 Тогда 
			Элементы.СостояниеПроцессаВСВД.Подсказка = НСтр("ru = 'В очереди на отправку по СВД'");
		ИначеЕсли Форма.СостояниеПроцессаВСВД = 2 Тогда 
			Элементы.СостояниеПроцессаВСВД.Подсказка = НСтр("ru = 'Отправлен по СВД'");
		ИначеЕсли Форма.СостояниеПроцессаВСВД = 3 Тогда 
			Элементы.СостояниеПроцессаВСВД.Подсказка = НСтр("ru = 'Доставлен по СВД'");
		ИначеЕсли Форма.СостояниеПроцессаВСВД = 4 Тогда 
			Элементы.СостояниеПроцессаВСВД.Подсказка = НСтр("ru = 'Зарегистрирован у получателя по СВД'");
		ИначеЕсли Форма.СостояниеПроцессаВСВД = 6 Тогда 
			Элементы.СостояниеПроцессаВСВД.Подсказка = НСтр("ru = 'Ошибка отправки по СВД'");			
		КонецЕсли;	
	
	КонецЕсли;	
	
	// Картинка ЭП
	Элементы.КартинкаЭП.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	ВывестиКартинкуЭП(Форма);
	
	// Картинка контроль
	Элементы.КартинкаКонтроль.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольОбъектов");
	ВывестиКартинкуКонтроля(Форма);
	
КонецПроцедуры	

Процедура ВывестиКартинкуКонтроля(Форма) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольОбъектов") Тогда 
		Возврат;
	КонецЕсли;	
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	КешИнформации = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	КешИнформации.Объект = Объект.Ссылка;
	КешИнформации.Прочитать();
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
	
	Если Не ЗначениеЗаполнено(КешИнформации.СостояниеКонтроля) Тогда 
		
		Форма.СтатусКонтроля = 5; 
		Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'Не стоит на контроле'");
		
	ИначеЕсли КешИнформации.СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтроле Тогда 
		
		Если Не ЗначениеЗаполнено(КешИнформации.СрокИсполнения) Или КешИнформации.СрокИсполнения >= ТекущаяДата Тогда 
			Форма.СтатусКонтроля = 1;
			Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'На контроле'");
		Иначе	
			Форма.СтатусКонтроля = 3;
			Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'На контроле'");
		КонецЕсли;	
		
	ИначеЕсли КешИнформации.СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроля Тогда 	
		
		Форма.СтатусКонтроля = 4;
		Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'Снят с контроля'");
		
	ИначеЕсли КешИнформации.СостояниеКонтроля = Перечисления.СостоянияКонтроля.НаКонтролеНесколько Тогда 
		
		 Если Не ЗначениеЗаполнено(КешИнформации.СрокИсполнения) Или КешИнформации.СрокИсполнения >= ТекущаяДата Тогда 
			Форма.СтатусКонтроля = 6;
		ИначеЕсли Не ЗначениеЗаполнено(КешИнформации.СрокИсполненияОбщий) Или КешИнформации.СрокИсполненияОбщий >= ТекущаяДата Тогда 
			Форма.СтатусКонтроля = 9;
		Иначе
			Форма.СтатусКонтроля = 7;
		КонецЕсли;	
		Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'На контроле (несколько)'");
		
	ИначеЕсли КешИнформации.СостояниеКонтроля = Перечисления.СостоянияКонтроля.СнятСКонтроляНесколько Тогда
		
		Форма.СтатусКонтроля = 8;
		Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'Снят с контроля (несколько)'");
		
	ИначеЕсли КешИнформации.СостояниеКонтроля = Перечисления.СостоянияКонтроля.СмешанноНесколько Тогда	
		
		Если Не ЗначениеЗаполнено(КешИнформации.СрокИсполнения) Или КешИнформации.СрокИсполнения >= ТекущаяДата Тогда 
			Форма.СтатусКонтроля = 10;
		Иначе
			Форма.СтатусКонтроля = 11;
		КонецЕсли;
		Элементы.КартинкаКонтроль.Подсказка = НСтр("ru = 'На контроле и снят с контроля'");
		
	КонецЕсли;	
	
КонецПроцедуры	

// Выводит картинку состояния ЭП
Процедура ВывестиКартинкуЭП(Форма) Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи") Тогда 
		Возврат;
	КонецЕсли;	
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	КешИнформации = РегистрыСведений.КешИнформацииОбОбъектах.СоздатьМенеджерЗаписи();
	КешИнформации.Объект = Объект.Ссылка;
	КешИнформации.Прочитать();
	
	Если КешИнформации.СтатусЭП = Перечисления.СтатусПроверкиЭП.ПодписиНет Тогда 
		Форма.СтатусЭП = 0;
		Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Не подписан электронной подписью'");
	ИначеЕсли  КешИнформации.СтатусЭП = Перечисления.СтатусПроверкиЭП.ПодписьНеПроверена Тогда
		Форма.СтатусЭП = 1;
		Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Подписан электронной подписью. Подпись не проверена.'");
	ИначеЕсли  КешИнформации.СтатусЭП = Перечисления.СтатусПроверкиЭП.ПодписьДействительна Тогда 
		Форма.СтатусЭП = 2;
		Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Подписан электронной подписью. Подпись действительна.'");
	ИначеЕсли  КешИнформации.СтатусЭП = Перечисления.СтатусПроверкиЭП.ПодписьНедействительна Тогда 
		Форма.СтатусЭП = 3;
		Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Подписан электронной подписью. Подпись недействительна.'");
	Иначе 
		Если Объект.ПодписанЭП Тогда
			Форма.СтатусЭП = 1;
			Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Подписан электронной подписью. Подпись не проверена.'");
		Иначе 
			Форма.СтатусЭП = 0;
			Элементы.КартинкаЭП.Подсказка = НСтр("ru = 'Не подписан электронной подписью'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры	

// Возвращает текст HTML для показа обзора внутреннего документа
Функция ПолучитьОбзорВнутреннегоДокумента(Форма) Экспорт 
	
	HTMLТекст = ОбзорДокументаПереопределяемый.ПолучитьОбзорВнутреннегоДокумента(Форма);
	Если ЗначениеЗаполнено(HTMLТекст) Тогда 
		Возврат HTMLТекст;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Если Объект.ВидДокумента.ВестиУчетПоКорреспондентам Тогда
		Возврат ПолучитьОбзорВнутреннегоДокументаСКорреспондентом(Форма)
	Иначе	
		Возврат ПолучитьОбзорВнутреннегоДокументаБезКорреспондента(Форма);
	КонецЕсли;	
	
КонецФункции	

// Возвращает текст HTML для показа обзора исходящего документа
Функция ПолучитьОбзорИсходящегоДокумента(Форма) Экспорт 
	
	HTMLТекст = ОбзорДокументаПереопределяемый.ПолучитьОбзорИсходящегоДокумента(Форма);
	Если ЗначениеЗаполнено(HTMLТекст) Тогда 
		Возврат HTMLТекст;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Организация = Объект.Организация;
	Иначе
		Организация = Константы.НаименованиеПредприятия.Получить();
	КонецЕсли;

	// Формируем основное представление документа	
	HTMLТекст = "<html>
	|<head>
	|<style type=""text/css"">
	|	body {
	|		overflow:    auto;
	|		margin-top:  12px; 		 
	|		margin-left: 20px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	table {
	|		width:       450px;
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	td {vertical-align: top;}
	| 	a:link {
	|		color: #006699; text-decoration: none;}
	|	a:visited {
	|		color: #006699; text-decoration: none;}
	|	a:hover {
	|		color: #006699; text-decoration: underline;}
	|	p {
	|		margin-top: 7px;}
	|</style>
	|<body>";
	
	HTMLТекст = HTMLТекст + "<table>";
	HTMLТекст = HTMLТекст + "<tr>";
	
	HTMLТекст = HTMLТекст + "<td width=""40%"" align=center >";
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'От кого:'"), "");
	HTMLТекст = HTMLТекст + "<p>";
	ДобавитьРеквизит(HTMLТекст, "", Организация);
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A>",
		ПолучитьНавигационнуюСсылку(Объект.ВидДокумента),
		ВРег((Строка(Объект.ВидДокумента))));
    HTMLТекст = HTMLТекст + "<p>";
	
	Если ЗначениеЗаполнено(Объект.ДатаРегистрации) И ЗначениеЗаполнено(Объект.РегистрационныйНомер) Тогда 
		ДобавитьРеквизит(HTMLТекст, "", Формат(Объект.ДатаРегистрации, "ДЛФ=D") 
		+ " № " +  Объект.РегистрационныйНомер);
	Иначе
		ДобавитьРеквизит(HTMLТекст, "__________ №__________", "");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "<p>";
	Если ЗначениеЗаполнено(Форма.ВОтветНаСтрока) И Лев(Форма.ВОтветНаСтрока, 1) = "№" Тогда 
		ДобавитьРеквизит(HTMLТекст, "", НСтр("ru = 'На'") + " " + Форма.ВОтветНаСтрока);
	Иначе
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'На №__________ от __________'"), "");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "</td>";	
	
	HTMLТекст = HTMLТекст + "<td>";
	HTMLТекст = HTMLТекст + "</td>";
	
	HTMLТекст = HTMLТекст + "<td width=""40%"">";
	Если Форма.НесколькоПолучателей Тогда
		ДобавитьРеквизит(HTMLТекст, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Кому (%1):'"), Объект.Получатели.Количество()), "");
		
		Для Каждого Получателя Из Объект.Получатели Цикл
			HTMLТекст = HTMLТекст + "<p>";
			ДобавитьРеквизит(HTMLТекст, "", Получателя.Получатель);
			
			Если ЗначениеЗаполнено(Получателя.Адресат) Тогда
				ДобавитьРеквизит(HTMLТекст, "", Получателя.Адресат);
				КонтактнаяИнформацияАдресат = ПолучитьКонтактнуюИнформацияОбъекта(Получателя.Адресат);
				
				Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Телефон) Тогда
					ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформацияАдресат.Телефон, "696969");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Емайл) Тогда
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<FONT color=#696969>%1</FONT>",
						НСтр("ru = 'E-mail:'") + " ");
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
						"message"+КонтактнаяИнформацияАдресат.Емайл,
						КонтактнаяИнформацияАдресат.Емайл);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
	Иначе
	    Если ЗначениеЗаполнено(Форма.Получатель) Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Кому:'"), "");
			HTMLТекст = HTMLТекст + "<p>";
			
			ДобавитьРеквизит(HTMLТекст, "", Форма.Получатель);
			
			Если ЗначениеЗаполнено(Форма.Адресат) Тогда
				ДобавитьРеквизит(HTMLТекст, "", Форма.Адресат);
				КонтактнаяИнформацияАдресат = ПолучитьКонтактнуюИнформацияОбъекта(Форма.Адресат);
				
				Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Телефон) Тогда
					ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформацияАдресат.Телефон, "696969");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Емайл) Тогда
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<FONT color=#696969>%1</FONT>",
						НСтр("ru = 'E-mail:'") + " ");
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
						"message"+КонтактнаяИнформацияАдресат.Емайл,
						КонтактнаяИнформацияАдресат.Емайл);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<B>%1</B>",
		Объект.Заголовок);
	HTMLТекст = HTMLТекст + "<p>";	
		
	Если ЗначениеЗаполнено(Объект.Содержание) Тогда
		ДобавитьРеквизит(HTMLТекст, "", СтрЗаменить(Объект.Содержание, Символы.ПС, "<br>"));
		
		Если Объект.ВидДокумента.УчитыватьСуммуДокумента Или
			(Форма.Свойства_ИспользоватьСвойства И Форма.Свойства_ОписаниеДополнительныхРеквизитов.Количество() > 0) Тогда 
			HTMLТекст = HTMLТекст + "<p>";
		КонецЕсли;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.СрокИсполнения) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Срок ответа:'"), Формат(Объект.СрокИсполнения, "ДЛФ=D"));
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьСуммуДокумента Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Сумма:'"), 	Формат(Объект.Сумма, "ЧДЦ=2; ЧН=0") + " " + Объект.Валюта.Наименование);
	КонецЕсли;
	
	Если Форма.Свойства_ИспользоватьСвойства Тогда 
		Для каждого Строка Из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
			Значение = Форма[Строка.ИмяРеквизитаЗначение];
			Если Не Строка.Удалено И ЗначениеЗаполнено(Значение) Тогда
				ДобавитьДопРеквизит(HTMLТекст, Значение, Строка);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
		
	HTMLТекст = HTMLТекст + "</td></tr>";	
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	Если ЗначениеЗаполнено(Объект.Подписал)
	 Или ЗначениеЗаполнено(Объект.Подготовил)
	Тогда 
		Если ЗначениеЗаполнено(Объект.Содержание) 
			Или Объект.ВидДокумента.УчитыватьСуммуДокумента 
			Или (Форма.Свойства_ИспользоватьСвойства И Форма.Свойства_ОписаниеДополнительныхРеквизитов.Количество() > 0)  Тогда 
				HTMLТекст = HTMLТекст + "<p>";
		КонецЕсли;	
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Подписал) Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), Объект.Подписал);
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Подготовил) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Подготовил:'"));
		ДобавитьЗначение(HTMLТекст, Объект.Подготовил, "");
		ДобавитьЗначение(HTMLТекст, " (" + Формат(Объект.ДатаСоздания, "ДЛФ=D") + ")", "");
		HTMLТекст = HTMLТекст + "<br>";
		
		КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(Объект.Подготовил);
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
			ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<FONT color=#696969>%1</FONT>",
					НСтр("ru = 'E-mail:'") + " ");
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
				"message"+КонтактнаяИнформация.Емайл,
				КонтактнаяИнформация.Емайл);
		КонецЕсли;
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	ДелоТекст = Форма.ДелоТекст;
	КомуПереданДокумент = Форма.КомуПереданДокумент;
	ВестиУчетПоНоменклатуреДел = Объект.ВидДокумента.ВестиУчетПоНоменклатуреДел;
	
	Если (ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел))
	 Или ЗначениеЗаполнено(ДелоТекст)
	 Или ЗначениеЗаполнено(КомуПереданДокумент) 
	Тогда 
		HTMLТекст = HTMLТекст + "<p>";
	КонецЕсли;
	
	Если ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел) Тогда			
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Номенклатура дел:'"), Объект.НоменклатураДел);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДелоТекст) Тогда
		ДобавитьПодпись(HTMLТекст,  НСтр("ru = 'Помещен в дело:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"dossier",
			ДелоТекст);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(КомуПереданДокумент) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Передан:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"holder",
			КомуПереданДокумент);
	КонецЕсли;	
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")
	 Или ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности")
	 Или ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов")
	Тогда 	
		HTMLТекст = HTMLТекст + "<p>";
	КонецЕсли;	
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Гриф:'"),  Объект.ГрифДоступа);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вопрос:'"),  Объект.ВопросДеятельности);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") И ЗначениеЗаполнено(Объект.Проект) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Проект:'"),  Объект.Проект);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов") Тогда 
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Состояние:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"status",
			Форма.СостояниеТекст);
    КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВидДокумента) Тогда 
		ИспользоватьЭтапыОбработкиДокумента = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ВидДокумента,
			"ИспользоватьЭтапыОбработкиДокумента");
		
		Если ИспользоватьЭтапыОбработкиДокумента Тогда 
			ПоследнийЭтап = Неопределено;
			ДатаПоследнегоЭтапа = '00010101';
			Для Каждого Строка Из Форма.ЭтапыОбработкиДокументов Цикл
				Если Строка.Пройден И Строка.ДатаПрохождения >= ДатаПоследнегоЭтапа Тогда 
					ПоследнийЭтап = Строка.Этап;
					ДатаПоследнегоЭтапа = Строка.ДатаПрохождения;
				КонецЕсли;	
			КонецЦикла;
			Если ЗначениеЗаполнено(ПоследнийЭтап) Тогда 
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Последний этап:'"),  Строка(ПоследнийЭтап));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Ответственный:'"),  Объект.Ответственный);
	КонецЕсли;

	HTMLТекст = HTMLТекст + "</td></tr>";	
		
	HTMLТекст = HTMLТекст + "</table>";
	HTMLТекст = HTMLТекст + "</body></html>";
	
	УдалитьТег(HTMLТекст, "script");
	
	Возврат HTMLТекст;
	
КонецФункции	

// Возвращает текст HTML для показа обзора входящего документа
Функция ПолучитьОбзорВходящегоДокумента(Форма) Экспорт 
	
	HTMLТекст = ОбзорДокументаПереопределяемый.ПолучитьОбзорВходящегоДокумента(Форма);
	Если ЗначениеЗаполнено(HTMLТекст) Тогда 
		Возврат HTMLТекст;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Организация = Объект.Организация;
	Иначе
		Организация = Константы.НаименованиеПредприятия.Получить();
	КонецЕсли;

	// Формируем основное представление документа	
	HTMLТекст = "<html>
	|<head>
	|<style type=""text/css"">
	|	body {
	|		overflow:    auto;
	|		margin-top:  12px; 		 
	|		margin-left: 20px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	table {
	|		width:       450px;  
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	td {vertical-align: top;}
	| 	a:link {
	|		color: #006699; text-decoration: none;}
	|	a:visited {
	|		color: #006699; text-decoration: none;}
	|	a:hover {
	|		color: #006699; text-decoration: underline;}
	|	p {
	|		margin-top: 7px;}
	|</style>
	|<body>";
	
	HTMLТекст = HTMLТекст + "<table>";
	HTMLТекст = HTMLТекст + "<tr>";
	
	HTMLТекст = HTMLТекст + "<td width=""40%"" align=center >";
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'От кого:'"), "");
	HTMLТекст = HTMLТекст + "<p>";
	ДобавитьРеквизит(HTMLТекст, "", Объект.Отправитель);
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A>",
		ПолучитьНавигационнуюСсылку(Объект.ВидДокумента),
		ВРег((Строка(Объект.ВидДокумента))));
    HTMLТекст = HTMLТекст + "<p>";
	
	Если ЗначениеЗаполнено(Объект.ИсходящаяДата) И ЗначениеЗаполнено(Объект.ИсходящийНомер) Тогда 
		ДобавитьРеквизит(HTMLТекст, "", Формат(Объект.ИсходящаяДата, "ДЛФ=D") 
		+ " № " +  Объект.ИсходящийНомер);
	Иначе
		ДобавитьРеквизит(HTMLТекст, "__________ №__________", "");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "<p>";
	Если ЗначениеЗаполнено(Форма.ВОтветНаСтрока) И Лев(Форма.ВОтветНаСтрока, 1) = "№" Тогда 
		ДобавитьРеквизит(HTMLТекст, "", НСтр("ru = 'На'") + " " + Форма.ВОтветНаСтрока);
	Иначе
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'На №__________ от __________'"), "");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "</td>";	
	
	HTMLТекст = HTMLТекст + "<td>";
	HTMLТекст = HTMLТекст + "</td>";
	
	HTMLТекст = HTMLТекст + "<td width=""40%"">";
	
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Кому:'"), "");
	HTMLТекст = HTMLТекст + "<p>";
	
	ДобавитьРеквизит(HTMLТекст, "", Организация);
	
	Если ЗначениеЗаполнено(Объект.Адресат) Тогда
		ДобавитьРеквизит(HTMLТекст, "", Объект.Адресат);
		КонтактнаяИнформацияАдресат = ПолучитьКонтактнуюИнформацияОбъекта(Объект.Адресат);
		
		Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Телефон) Тогда
			ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформацияАдресат.Телефон, "696969");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактнаяИнформацияАдресат.Емайл) Тогда
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<FONT color=#696969>%1</FONT>",
				НСтр("ru = 'E-mail:'") + " ");
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
				"message"+КонтактнаяИнформацияАдресат.Емайл,
				КонтактнаяИнформацияАдресат.Емайл);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаРегистрации) И ЗначениеЗаполнено(Объект.РегистрационныйНомер) Тогда 
		HTMLТекст = HTMLТекст + "<p>";
		ДобавитьРеквизит(HTMLТекст, "", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Рег. № %1 от %2'"),
			Объект.РегистрационныйНомер,
			Формат(Объект.ДатаРегистрации, "ДЛФ=D")));
	КонецЕсли;	
	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<B>%1</B>",
		Объект.Заголовок);
		
	Если ЗначениеЗаполнено(Объект.Содержание)
		Или ЗначениеЗаполнено(Объект.СрокИсполнения)  
		Или  Объект.ВидДокумента.УчитыватьСуммуДокумента 
		Или (Форма.Свойства_ИспользоватьСвойства И Форма.Свойства_ОписаниеДополнительныхРеквизитов.Количество() > 0) Тогда 
		HTMLТекст = HTMLТекст + "<p>";	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Объект.Содержание) Тогда
		ДобавитьРеквизит(HTMLТекст, "", СтрЗаменить(Объект.Содержание, Символы.ПС, "<br>"));
		Если Объект.ВидДокумента.УчитыватьСуммуДокумента Или
			(Форма.Свойства_ИспользоватьСвойства И Форма.Свойства_ОписаниеДополнительныхРеквизитов.Количество() > 0) Тогда 
			HTMLТекст = HTMLТекст + "<p>";
		КонецЕсли;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.СрокИсполнения) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Срок исполнения:'"), Формат(Объект.СрокИсполнения, "ДЛФ=D"));
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьСуммуДокумента Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Сумма:'"), 	Формат(Объект.Сумма, "ЧДЦ=2; ЧН=0") + " " + Объект.Валюта.Наименование);
	КонецЕсли;
	
//1С-Минск +
	Если Объект.ВидДокумента = Справочники.ВидыВходящихДокументов.ПоручениеНаВыполнениеРабот Тогда
		СостояниеПоручения = мРаботаСДоговорами.СостояниеПоручения(Объект.Ссылка);
		Если СостояниеПоручения = ПредопределенноеЗначение("Перечисление.мСостоянияПоручения.Возвращено") Тогда
			ПричинаВозврата = мРаботаСДоговорами.ПричинаВозвратаПоручения(Объект.Ссылка);
			ТекстСостояния = "" + СостояниеПоручения + " ("+ПричинаВозврата+")";
		Иначе	
			ТекстСостояния = "" + СостояниеПоручения;
		КонецЕсли; 
		ДобавитьРеквизит(HTMLТекст, "Состояние поручения:", ТекстСостояния);
	КонецЕсли; 
//1С-Минск -
	
	Если Форма.Свойства_ИспользоватьСвойства Тогда 
		Для каждого Строка Из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
			Значение = Форма[Строка.ИмяРеквизитаЗначение];
			Если Не Строка.Удалено И ЗначениеЗаполнено(Значение) Тогда
				ДобавитьДопРеквизит(HTMLТекст, Значение, Строка);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
		
	HTMLТекст = HTMLТекст + "</td></tr>";	
	
	Если ЗначениеЗаполнено(Объект.Подписал) Тогда 
		
		HTMLТекст = HTMLТекст + "<tr>";
		HTMLТекст = HTMLТекст + "<td colspan=""3"">";
		
		Если ЗначениеЗаполнено(Объект.Содержание) 
			Или Объект.ВидДокумента.УчитыватьСуммуДокумента 
			Или ЗначениеЗаполнено(Объект.СрокИсполнения)
			Или (Форма.Свойства_ИспользоватьСвойства И Форма.Свойства_ОписаниеДополнительныхРеквизитов.Количество() > 0)  Тогда 
			HTMLТекст = HTMLТекст + "<p>";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.Подписал) Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), Объект.Подписал);
			
			КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(Объект.Подписал);
			
			Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
				ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<FONT color=#696969>%1</FONT>",
					НСтр("ru = 'E-mail:'") + " ");
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
					"message"+КонтактнаяИнформация.Емайл,
					КонтактнаяИнформация.Емайл);
			КонецЕсли;
		КонецЕсли;	
		
		HTMLТекст = HTMLТекст + "</td></tr>";
		
	КонецЕсли;	
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	ДелоТекст = Форма.ДелоТекст;
	КомуПереданДокумент = Форма.КомуПереданДокумент;
	ВестиУчетПоНоменклатуреДел = Объект.ВидДокумента.ВестиУчетПоНоменклатуреДел;
	
	Если (ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел))
		Или ЗначениеЗаполнено(ДелоТекст)
		Или ЗначениеЗаполнено(КомуПереданДокумент) 
		Тогда 
		HTMLТекст = HTMLТекст + "<p>";
		
		Если ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел) Тогда			
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Номенклатура дел:'"), Объект.НоменклатураДел);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДелоТекст) Тогда
			ДобавитьПодпись(HTMLТекст,  НСтр("ru = 'Помещен в дело:'"));
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"dossier",
			ДелоТекст);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(КомуПереданДокумент) Тогда
			ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Передан:'"));
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"holder",
			КомуПереданДокумент);
		КонецЕсли;	
		
		HTMLТекст = HTMLТекст + "</td></tr>";
		
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""3"">";
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа")
	 Или ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности")
	 Или ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов")
	 Или (ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") И ЗначениеЗаполнено(Объект.Проект))
	Тогда 	
		HTMLТекст = HTMLТекст + "<p>";
	КонецЕсли;	
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Гриф:'"),  Объект.ГрифДоступа);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вопрос:'"),  Объект.ВопросДеятельности);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") И ЗначениеЗаполнено(Объект.Проект) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Проект:'"),  Объект.Проект);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов") Тогда 
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Состояние:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"status",
			Форма.СостояниеТекст);
    КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВидДокумента) Тогда 
		ИспользоватьЭтапыОбработкиДокумента = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ВидДокумента,
			"ИспользоватьЭтапыОбработкиДокумента");
		
		Если ИспользоватьЭтапыОбработкиДокумента Тогда 
			ПоследнийЭтап = Неопределено;
			ДатаПоследнегоЭтапа = '00010101';
			Для Каждого Строка Из Форма.ЭтапыОбработкиДокументов Цикл
				Если Строка.Пройден И Строка.ДатаПрохождения >= ДатаПоследнегоЭтапа Тогда 
					ПоследнийЭтап = Строка.Этап;
					ДатаПоследнегоЭтапа = Строка.ДатаПрохождения;
				КонецЕсли;	
			КонецЦикла;
			Если ЗначениеЗаполнено(ПоследнийЭтап) Тогда 
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Последний этап:'"),  Строка(ПоследнийЭтап));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Ответственный:'"),  Объект.Ответственный);
	КонецЕсли;

	HTMLТекст = HTMLТекст + "</td></tr>";	
		
	HTMLТекст = HTMLТекст + "</table>";
	HTMLТекст = HTMLТекст + "</body></html>";
	
	УдалитьТег(HTMLТекст, "script");
	
	Возврат HTMLТекст;
	
КонецФункции	

Функция ПолучитьОбзорВнутреннегоДокументаБезКорреспондента(Форма)

	Объект = Форма.Объект;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Организация = Объект.Организация;
	Иначе
		Организация = Константы.НаименованиеПредприятия.Получить();
	КонецЕсли;
	
	// Формируем основное представление документа	
	HTMLТекст = "<html>
	|<head>
	|<style type=""text/css"">
	|	body {
	|		overflow:    auto;
	|		margin-top:  12px; 		 
	|		margin-left: 20px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	table {
	|		width:       450px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	td {vertical-align: top;}
	| 	a:link {
	|		color: #006699; text-decoration: none;}
	|	a:visited {
	|		color: #006699; text-decoration: none;}
	|	a:hover {
	|		color: #006699; text-decoration: underline;}
	|	p {
	|		margin-top: 7px;}
	|</style>
	|<body>";
	
	HTMLТекст = HTMLТекст + "<table>";
	
	HTMLТекст = HTMLТекст + "<tr>";
	HTMLТекст = HTMLТекст + "<td colspan=""2"" align=""center"">";
	ДобавитьРеквизит(HTMLТекст, "", Организация);
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"" align=""center"">";
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A>",
		ПолучитьНавигационнуюСсылку(Объект.ВидДокумента),
		ВРег((Строка(Объект.ВидДокумента))));
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td width=""80%"">";
	Если ЗначениеЗаполнено(Объект.ДатаРегистрации) Тогда 
		ДобавитьРеквизит(HTMLТекст, "", Формат(Объект.ДатаРегистрации, "ДЛФ=D"));
	Иначе
		ДобавитьРеквизит(HTMLТекст, "____________", "");
	КонецЕсли;
	HTMLТекст = HTMLТекст + "</td>";
	
    HTMLТекст = HTMLТекст + "<td>";
	Если ЗначениеЗаполнено(Объект.РегистрационныйНомер) Тогда 
		ДобавитьРеквизит(HTMLТекст, "№ ", Объект.РегистрационныйНомер);
	Иначе
		ДобавитьРеквизит(HTMLТекст, "№___________", "");
	КонецЕсли;	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<B>%1</B>",
		Объект.Заголовок);
	HTMLТекст = HTMLТекст + "<p>";
		
	HTMLТекст = HTMLТекст + "<p>";	
	Если ЗначениеЗаполнено(Объект.Содержание) Тогда
		ДобавитьРеквизит(HTMLТекст, "", СтрЗаменить(Объект.Содержание, Символы.ПС, "<br>"));
		HTMLТекст = HTMLТекст + "<p>";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СрокИсполнения) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Срок исполнения:'"), Формат(Объект.СрокИсполнения, "ДЛФ=D"));
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьСуммуДокумента Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Сумма:'"), 
			Формат(Объект.Сумма, "ЧДЦ=2; ЧН=0") + " " + Объект.Валюта.Наименование);
		Если Объект.ВидДокумента.ВестиУчетПоСтатьямДДС Тогда
			СтатьиДДС = Форма.СтатьиДвиженияДенежныхСредств;
			КоличествоСтатей = СтатьиДДС.Количество();
			Если КоличествоСтатей = 1 Тогда
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Статья ДДС:'"), 
					СтатьиДДС[0].СтатьяДвиженияДенежныхСредств);
			ИначеЕсли КоличествоСтатей > 1 Тогда
				ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Статьи ДДС:'"));
				Разделитель = "";
				Для каждого Строка из СтатьиДДС Цикл
					HTMLТекст = HTMLТекст + Разделитель;
					ДобавитьЗначение(HTMLТекст, Строка.СтатьяДвиженияДенежныхСредств, "");
					Разделитель = "; ";
				КонецЦикла;
				HTMLТекст = HTMLТекст + "<br>";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьСрокДействия Тогда			
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок действия:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"period",
			Форма.СрокДействияСтрока);
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьНедействующиеДокументы Тогда			
		Если Объект.НеДействует Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Не действует:'"), НСтр("ru = 'Да'"));
		КонецЕсли;	
	КонецЕсли;
		
	Если Форма.Свойства_ИспользоватьСвойства Тогда 
		Для каждого Строка Из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
			Значение = Форма[Строка.ИмяРеквизитаЗначение];
			Если Не Строка.Удалено И ЗначениеЗаполнено(Значение) Тогда
				ДобавитьДопРеквизит(HTMLТекст, Значение, Строка);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
		
	HTMLТекст = HTMLТекст + "</td></tr>";	
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td>";
	
	HTMLТекст = HTMLТекст + "<p>";
	Если ЗначениеЗаполнено(Объект.Утвердил) Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), Объект.Утвердил);
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Подготовил) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Подготовил:'"));
		ДобавитьЗначение(HTMLТекст, Объект.Подготовил, "");
		ДобавитьЗначение(HTMLТекст, " (" + Формат(Объект.ДатаСоздания, "ДЛФ=D") + ")", "");
		HTMLТекст = HTMLТекст + "<br>";
		
		КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(Объект.Подготовил);
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
			ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<FONT color=#696969>%1</FONT>",
				НСтр("ru = 'E-mail:'") + " ");
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
				"message"+КонтактнаяИнформация.Емайл,
				КонтактнаяИнформация.Емайл);
		КонецЕсли;
	КонецЕсли;
		
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	Если Объект.ВидДокумента.ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел) Тогда			
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Номенклатура дел:'"), Объект.НоменклатураДел);
	КонецЕсли;
	
	ДелоТекст = Форма.ДелоТекст;
	Если ЗначениеЗаполнено(ДелоТекст) Тогда
		ДобавитьПодпись(HTMLТекст,  НСтр("ru = 'Помещен в дело:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"dossier",
			ДелоТекст);
	КонецЕсли;	
	
	КомуПереданДокумент = Форма.КомуПереданДокумент;
	Если ЗначениеЗаполнено(КомуПереданДокумент) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Передан:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"holder",
			КомуПереданДокумент);
	КонецЕсли;	
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Папка:'"), 	Объект.Папка);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Гриф:'"),  Объект.ГрифДоступа);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вопрос:'"),  Объект.ВопросДеятельности);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") И ЗначениеЗаполнено(Объект.Проект) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Проект:'"),  Объект.Проект);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов") Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Состояние:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"status",
			Форма.СостояниеТекст);
	КонецЕсли;		
		
	Если ЗначениеЗаполнено(Объект.ВидДокумента) Тогда 
		ИспользоватьЭтапыОбработкиДокумента = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ВидДокумента,
			"ИспользоватьЭтапыОбработкиДокумента");
		
		Если ИспользоватьЭтапыОбработкиДокумента Тогда 
			ПоследнийЭтап = Неопределено;
			ДатаПоследнегоЭтапа = '00010101';
			Для Каждого Строка Из Форма.ЭтапыОбработкиДокументов Цикл
				Если Строка.Пройден И Строка.ДатаПрохождения >= ДатаПоследнегоЭтапа Тогда 
					ПоследнийЭтап = Строка.Этап;
					ДатаПоследнегоЭтапа = Строка.ДатаПрохождения;
				КонецЕсли;	
			КонецЦикла;
			Если ЗначениеЗаполнено(ПоследнийЭтап) Тогда 
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Последний этап:'"),  Строка(ПоследнийЭтап));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Ответственный:'"),  Объект.Ответственный);
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "</table>";
	HTMLТекст = HTMLТекст + "</body></html>";
	
	УдалитьТег(HTMLТекст, "script");
	
	Возврат HTMLТекст;
	
КонецФункции	

Функция ПолучитьОбзорВнутреннегоДокументаСКорреспондентом(Форма)

	Объект = Форма.Объект;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям") Тогда
		Организация = Объект.Организация;
	Иначе
		Организация = Константы.НаименованиеПредприятия.Получить();
	КонецЕсли;
	
	// Формируем основное представление документа	
	HTMLТекст = "<html>
	|<head>
	|<style type=""text/css"">
	|	body {
	|		overflow:    auto;
	|		margin-top:  12px; 		 
	|		margin-left: 20px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	table {
	|		width:       450px; 
	|		font-family: MS Shell Dlg, Microsoft Sans Serif, sans-serif; 
	|		font-size:   8pt;}
	|	td {vertical-align: top;}
	| 	a:link {
	|		color: #006699; text-decoration: none;}
	|	a:visited {
	|		color: #006699; text-decoration: none;}
	|	a:hover {
	|		color: #006699; text-decoration: underline;}
	|	p {
	|		margin-top: 7px;}
	|</style>
	|<body>";
	
	HTMLТекст = HTMLТекст + "<table>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"" align=""center"">";
	
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<A href=v8doc:%1>%2</A> № %3 от %4",
		ПолучитьНавигационнуюСсылку(Объект.ВидДокумента),
		ВРег((Строка(Объект.ВидДокумента))),
		?(ЗначениеЗаполнено(Объект.РегистрационныйНомер), Объект.РегистрационныйНомер, "_____"),
		?(ЗначениеЗаполнено(Объект.ДатаРегистрации), Формат(Объект.ДатаРегистрации, "ДЛФ=D"), "_____"));
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"" align=""center"" >";
	
	HTMLТекст = HTMLТекст + "<p>";
	HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<B>%1</B>",
		Объект.Заголовок);
	HTMLТекст = HTMLТекст + "</td></tr>";	
		
		
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"" >";	
	
	HTMLТекст = HTMLТекст + "<p>";	
	Если ЗначениеЗаполнено(Объект.Содержание) Тогда
		ДобавитьРеквизит(HTMLТекст, "", СтрЗаменить(Объект.Содержание, Символы.ПС, "<br>"));
		HTMLТекст = HTMLТекст + "<p>";
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.СрокИсполнения) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Срок исполнения:'"), Формат(Объект.СрокИсполнения, "ДЛФ=D"));
	КонецЕсли;
	
	// Сумма и статьи ДДС.
	Если Объект.ВидДокумента.УчитыватьСуммуДокумента Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Сумма:'"), 
			Формат(Объект.Сумма, "ЧДЦ=2; ЧН=0") + " " + Объект.Валюта.Наименование);
		Если Объект.ВидДокумента.ВестиУчетПоСтатьямДДС Тогда
			СтатьиДДС = Форма.СтатьиДвиженияДенежныхСредств;
			КоличествоСтатей = СтатьиДДС.Количество();
			Если КоличествоСтатей = 1 Тогда
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Статья ДДС:'"), 
					СтатьиДДС[0].СтатьяДвиженияДенежныхСредств);
			ИначеЕсли КоличествоСтатей > 1 Тогда
				ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Статьи ДДС:'"));
				Разделитель = "";
				Для каждого Строка из СтатьиДДС Цикл
					HTMLТекст = HTMLТекст + Разделитель;
					ДобавитьЗначение(HTMLТекст, Строка.СтатьяДвиженияДенежныхСредств, "");
					Разделитель = "; ";
				КонецЦикла;
				HTMLТекст = HTMLТекст + "<br>";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьСрокДействия Тогда			
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Срок действия:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"period",
			Форма.СрокДействияСтрока);
	КонецЕсли;
	
	Если Объект.ВидДокумента.УчитыватьНедействующиеДокументы Тогда			
		Если Объект.НеДействует Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Расторгнут:'"), НСтр("ru = 'Да'"));
		КонецЕсли;	
	КонецЕсли;
		
	Если Форма.Свойства_ИспользоватьСвойства Тогда 
		Для каждого Строка Из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
			Значение = Форма[Строка.ИмяРеквизитаЗначение];
			Если Не Строка.Удалено И ЗначениеЗаполнено(Значение) Тогда
				ДобавитьДопРеквизит(HTMLТекст, Значение, Строка);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	HTMLТекст = HTMLТекст + "</td></tr>";	
	
	
	
	HTMLТекст = HTMLТекст + "<tr>";

	HTMLТекст = HTMLТекст + "<td width=""70%"">";
	HTMLТекст = HTMLТекст + "<p>";	
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Наша организация:'"), "");
	HTMLТекст = HTMLТекст + "<p>";
	ДобавитьРеквизит(HTMLТекст, "", Организация);
	
	Если ЗначениеЗаполнено(Объект.Утвердил) Тогда
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), Объект.Утвердил);
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(Объект.Подготовил) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Подготовил:'"));
		ДобавитьЗначение(HTMLТекст, Объект.Подготовил, "");
		ДобавитьЗначение(HTMLТекст, " (" + Формат(Объект.ДатаСоздания, "ДЛФ=D") + ")", "");
		HTMLТекст = HTMLТекст + "<br>";
		
		КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(Объект.Подготовил);
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
			ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<FONT color=#696969>%1</FONT>",
				НСтр("ru = 'E-mail:'") + " ");
			HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
				"message"+КонтактнаяИнформация.Емайл,
				КонтактнаяИнформация.Емайл);
		КонецЕсли;
	КонецЕсли;

	
	HTMLТекст = HTMLТекст + "</td>";
	
	Если Не Форма.НесколькоКонтрагентов Тогда
		HTMLТекст = HTMLТекст + "<td>";
		HTMLТекст = HTMLТекст + "<p>";	
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Корреспондент:'"), "");
		HTMLТекст = HTMLТекст + "<p>";
		
		Если ЗначениеЗаполнено(Объект.Корреспондент) Тогда
			ДобавитьРеквизит(HTMLТекст, "", Объект.Корреспондент);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ПодписалОтКорреспондента) Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), Объект.ПодписалОтКорреспондента);
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
			ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Контакт:'"), Объект.КонтактноеЛицо);
			КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(Объект.КонтактноеЛицо);
		
			Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
				ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<FONT color=#696969>%1</FONT>",
					НСтр("ru = 'E-mail:'") + " ");
				HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
					"message"+КонтактнаяИнформация.Емайл,
					КонтактнаяИнформация.Емайл);
			КонецЕсли;
		КонецЕсли;		
	Иначе		
		
		HTMLТекст = HTMLТекст + "<td>";
		HTMLТекст = HTMLТекст + "<p>";	
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Корреспонденты:'"), "");
		HTMLТекст = HTMLТекст + "<p>";
		
		Для Каждого СтрокаКорреспондент Из Объект.Корреспонденты Цикл
			Если ЗначениеЗаполнено(СтрокаКорреспондент.Корреспондент) Тогда
				ДобавитьРеквизит(HTMLТекст, "", СтрокаКорреспондент.Корреспондент);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаКорреспондент.ПодписалОтКорреспондента) Тогда
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Подписал:'"), СтрокаКорреспондент.ПодписалОтКорреспондента);
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(СтрокаКорреспондент.КонтактноеЛицо) Тогда
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Контакт:'"), СтрокаКорреспондент.КонтактноеЛицо);
				КонтактнаяИнформация = ПолучитьКонтактнуюИнформацияОбъекта(СтрокаКорреспондент.КонтактноеЛицо);
		
				Если ЗначениеЗаполнено(КонтактнаяИнформация.Телефон) Тогда
					ДобавитьРеквизит(HTMLТекст, "", КонтактнаяИнформация.Телефон, "696969");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(КонтактнаяИнформация.Емайл) Тогда
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<FONT color=#696969>%1</FONT>",
						НСтр("ru = 'E-mail:'") + " ");
					HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"<A color=#696969 href=v8doc:%1><FONT color=#696969>%2</FONT></A><BR>",
						"message"+КонтактнаяИнформация.Емайл,
						КонтактнаяИнформация.Емайл);
				КонецЕсли;
			КонецЕсли;
			
			HTMLТекст = HTMLТекст + "<p>";	
		КонецЦикла;	

		// Удаление лишнего отступа
		HTMLТекст = Лев(HTMLТекст, СтрДлина(HTMLТекст)-3);
		
	КонецЕсли;	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
    HTMLТекст = HTMLТекст + "<td colspan=""2"">";
	
	HTMLТекст = HTMLТекст + "<p>";	
	Если Объект.ВидДокумента.ВестиУчетПоНоменклатуреДел И ЗначениеЗаполнено(Объект.НоменклатураДел) Тогда			
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Номенклатура дел:'"), Объект.НоменклатураДел);
	КонецЕсли;
	
	ДелоТекст = Форма.ДелоТекст;
	Если ЗначениеЗаполнено(ДелоТекст) Тогда
		ДобавитьПодпись(HTMLТекст,  НСтр("ru = 'Помещен в дело:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"dossier",
			ДелоТекст);
	КонецЕсли;	
	
	КомуПереданДокумент = Форма.КомуПереданДокумент;
	Если ЗначениеЗаполнено(КомуПереданДокумент) Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Передан:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"holder",
			КомуПереданДокумент);
	КонецЕсли;	
	
	HTMLТекст = HTMLТекст + "</td></tr>";
	
	HTMLТекст = HTMLТекст + "<tr>";
	HTMLТекст = HTMLТекст + "<td colspan=""2"">";
	
	HTMLТекст = HTMLТекст + "<p>";
	ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Папка:'"), 	Объект.Папка);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрифыДоступа") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Гриф:'"),  Объект.ГрифДоступа);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВопросыДеятельности") Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Вопрос:'"),  Объект.ВопросДеятельности);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") И ЗначениеЗаполнено(Объект.Проект) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Проект:'"),  Объект.Проект);
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов") Тогда
		ДобавитьПодпись(HTMLТекст, НСтр("ru = 'Состояние:'"));
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A><BR>",
			"status",
			Форма.СостояниеТекст);
	КонецЕсли; 		
	
	Если ЗначениеЗаполнено(Объект.ВидДокумента) Тогда 
		ИспользоватьЭтапыОбработкиДокумента = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.ВидДокумента,
			"ИспользоватьЭтапыОбработкиДокумента");
		
		Если ИспользоватьЭтапыОбработкиДокумента Тогда 
			ПоследнийЭтап = Неопределено;
			ДатаПоследнегоЭтапа = '00010101';
			Для Каждого Строка Из Форма.ЭтапыОбработкиДокументов Цикл
				Если Строка.Пройден И Строка.ДатаПрохождения >= ДатаПоследнегоЭтапа Тогда 
					ПоследнийЭтап = Строка.Этап;
					ДатаПоследнегоЭтапа = Строка.ДатаПрохождения;
				КонецЕсли;	
			КонецЦикла;
			Если ЗначениеЗаполнено(ПоследнийЭтап) Тогда 
				ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Последний этап:'"),  Строка(ПоследнийЭтап));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ответственный) Тогда 
		ДобавитьРеквизит(HTMLТекст, НСтр("ru = 'Ответственный:'"),  Объект.Ответственный);
	КонецЕсли;
	
	HTMLТекст = HTMLТекст + "</td></tr>";	
	
	HTMLТекст = HTMLТекст + "</table>";
	HTMLТекст = HTMLТекст + "</body></html>";
	
	УдалитьТег(HTMLТекст, "script");
	
	Возврат HTMLТекст;
	
КонецФункции	

Процедура ДобавитьПодпись(HTMLТекст, Подпись)
	
	Если ЗначениеЗаполнено(Подпись) Тогда 
		HTMLТекст = HTMLТекст + "<FONT color=#413003>";
		HTMLТекст = HTMLТекст + Подпись + " ";
		HTMLТекст = HTMLТекст + "</FONT>";
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗначение(HTMLТекст, Значение, Цвет)
	
	Если ТипЗнч(Значение) = Тип("Строка") 
	 Или ТипЗнч(Значение) = Тип("Дата")	
	 Или ТипЗнч(Значение) = Тип("Число")
	 Или ТипЗнч(Значение) = Тип("Булево") Тогда 
		Если ЗначениеЗаполнено(Цвет) Тогда 
			HTMLТекст = HTMLТекст + "<FONT color=#"+Цвет+">";
			HTMLТекст = HTMLТекст + Значение;
			HTMLТекст = HTMLТекст + "</FONT>";
		Иначе	
			HTMLТекст = HTMLТекст + Значение;
		КонецЕсли;	
	Иначе
		HTMLТекст = HTMLТекст + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<A href=v8doc:%1>%2</A>",
			ПолучитьНавигационнуюСсылку(Значение),
			Строка(Значение));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРеквизит(HTMLТекст, Подпись, Значение, Цвет = "")
	
	ДобавитьПодпись(HTMLТекст, Подпись);
	ДобавитьЗначение(HTMLТекст, Значение, Цвет); 
	HTMLТекст = HTMLТекст + "<br>";
	
КонецПроцедуры

Процедура ДобавитьДопРеквизит(HTMLТекст, Значение, Строка)
	
	ФорматСвойства = Строка.Свойство.ФорматСвойства;
	
	Если Не ЗначениеЗаполнено(ФорматСвойства) Тогда 
		ТипЗначения = Строка.Свойство.ТипЗначения;
		
		Если ТипЗначения.СодержитТип(Тип("Дата")) Тогда 
			Если ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время Тогда 
				ФорматСвойства = ФорматСвойства + "ДЛФ = 'T';";
			ИначеЕсли ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата Тогда 
				ФорматСвойства = ФорматСвойства + "ДЛФ = 'D';";
			ИначеЕсли ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.ДатаВремя Тогда 	
				ФорматСвойства = ФорматСвойства + "ДЛФ = 'DT';";
			КонецЕсли;	
		КонецЕсли;	
		
		Если ТипЗначения.СодержитТип(Тип("Число")) Тогда 	
			Разрядность = ТипЗначения.КвалификаторыЧисла.Разрядность;
			РазрядностьДробнойЧасти = ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
			
			ФорматСвойства = ФорматСвойства + "ЧЦ = '" + Разрядность + "';";
			ФорматСвойства = ФорматСвойства + "ЧДЦ = '" + РазрядностьДробнойЧасти + "';";
		КонецЕсли;	
	КонецЕсли;		
	
	Если ЗначениеЗаполнено(ФорматСвойства) Тогда 
		ДобавитьРеквизит(HTMLТекст, Строка(Строка.Наименование) + ":", Формат(Значение, ФорматСвойства));
	Иначе
		ДобавитьРеквизит(HTMLТекст, Строка(Строка.Наименование) + ":", Значение);
	КонецЕсли;	
	
КонецПроцедуры	

Функция ПолучитьКонтактнуюИнформацияОбъекта(Пользователь)
	
	КонтактнаяИнформация = Новый Структура;
	Емайл = "";Телефон = "";
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		Емайл = УправлениеКонтактнойИнформацией.ПолучитьКонтактнуюИнформацияОбъекта(Пользователь, 
			Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		Телефон = УправлениеКонтактнойИнформацией.ПолучитьКонтактнуюИнформацияОбъекта(Пользователь, 
			Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
	ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Емайл = УправлениеКонтактнойИнформацией.ПолучитьКонтактнуюИнформацияОбъекта(Пользователь, 
			Справочники.ВидыКонтактнойИнформации.EmailКонтактногоЛица);
		Телефон = УправлениеКонтактнойИнформацией.ПолучитьКонтактнуюИнформацияОбъекта(Пользователь, 
			Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица);
	КонецЕсли; 
	
	Если 	ЗначениеЗаполнено(Телефон) Тогда 
		КонтактнаяИнформация.Вставить("Телефон", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Телефон: %1'"), Телефон));	
	Иначе
		КонтактнаяИнформация.Вставить("Телефон", Телефон);
	КонецЕсли;			

	КонтактнаяИнформация.Вставить("Емайл", Емайл);

	Возврат КонтактнаяИнформация;
	
КонецФункции	

Процедура УдалитьТег(ТекстHTML, ИмяТега)
	
	НРегТекстHTML = НРег(ТекстHTML);
	
	Пока Найти(НРегТекстHTML, "<" + ИмяТега) <> 0 Цикл
		
		ПозицияНачалаТегаHTML = Найти(НРегТекстHTML, "<" + ИмяТега);
		ПозицияОкончанияТегаHTML = 0;
		Если ПозицияНачалаТегаHTML > 0 Тогда
			ПозицияОкончанияТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаТегаHTML);
		КонецЕсли;
		
		Если ПозицияНачалаТегаHTML <> 0 И ПозицияОкончанияТегаHTML <> 0 Тогда
			
			ПозицияНачалаЗакрывающегоТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, "</" + ИмяТега, ПозицияОкончанияТегаHTML);
			ПозицияОкончанияЗакрывающегоТегаHTML = 0;
			Если ПозицияНачалаЗакрывающегоТегаHTML > 0 Тогда
				ПозицияОкончанияЗакрывающегоТегаHTML = РаботаСоСтроками.НайтиПосле(НРегТекстHTML, ">", ПозицияНачалаЗакрывающегоТегаHTML);
			КонецЕсли;
			
			Если ПозицияНачалаЗакрывающегоТегаHTML <> 0 И ПозицияОкончанияЗакрывающегоТегаHTML <> 0 Тогда
				
				ТекстHTML = Лев(ТекстHTML, ПозицияНачалаТегаHTML - 1) 
					+ Сред(ТекстHTML, ПозицияОкончанияЗакрывающегоТегаHTML + 1);
				НРегТекстHTML = НРег(ТекстHTML);
				
			Иначе
				Возврат;	
			КонецЕсли;		
				
		Иначе
			Возврат;	
		КонецЕсли;		
		
	КонецЦикла;	
	
КонецПроцедуры	

