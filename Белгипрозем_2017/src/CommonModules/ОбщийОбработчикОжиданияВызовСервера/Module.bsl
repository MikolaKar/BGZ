
////////////////////////////////////////////////////////////////////////////////
// ОбщийОбработчикОжиданияСервер: модуль содержащий серверные процедуры
// работы механизма Общий обработчик ожидания.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет действия обработчика ожидания на клиенте.
// Процедура предназначена для вызова из ОбщийОбработчикОжиданияГлобальный.ОбщийОбработчикОжидания
// 
// Параметры:
//    - Обработчики - Структура - Содержит обработчики, готовые к выполнению.
//
// Возвращаемое значение:
//    - Структура - содержит данные обработчиков для последующей обработки на клиенте.
//
Функция ВыполнитьДействияОбработчикаОжидания(Обработчики) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("РезультатыПроверкиПочты",Неопределено);
	Результат.Вставить("ЕстьУведомленияПрограммы", Ложь);
	Результат.Вставить("РезультатыСохраненияЗамеров",Неопределено);
	
	Если Обработчики.Свойство("ПроверкаПочты")
		И Обработчики.ПроверкаПочты Тогда
		
		РезультатыПроверкиПочты = Неопределено;
		ВстроеннаяПочтаСервер.ПроверитьНаличиеНовыхПисем(РезультатыПроверкиПочты);
		
		Если РезультатыПроверкиПочты.НужноОбновитьСписок Тогда
			Результат.РезультатыПроверкиПочты = РезультатыПроверкиПочты;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Обработчики.Свойство("ПроверкаУведомленийПрограммы")
		И Обработчики.ПроверкаУведомленийПрограммы Тогда
		
		Результат.ЕстьУведомленияПрограммы = 
			РаботаСУведомлениямиПрограммыСервер.ЕстьУведомленияДляТекущегоПользователя();
		
	КонецЕсли;
	
	Если Обработчики.Свойство("СохранениеЗамеровПроизводительности")
		И Обработчики.СохранениеЗамеровПроизводительности
		И Обработчики.Свойство("СохранениеЗамеровПроизводительностиЗамеры") Тогда
		
		Замеры = Обработчики.СохранениеЗамеровПроизводительностиЗамеры;
		Результат.РезультатыСохраненияЗамеров = 
			ОценкаПроизводительностиВызовСервераПолныеПрава.ЗаписатьРезультатыЗамеров(Замеры);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти