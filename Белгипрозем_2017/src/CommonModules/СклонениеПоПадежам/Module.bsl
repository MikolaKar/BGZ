//+Функционал склонения по падежам

//Основная функция склонения
Функция ПросклонятьСтрокуФИО(Строка,ПолСсылка,Падеж,Вид="ФИО") Экспорт
	
	Если ПолСсылка=Перечисления.ПолФизическогоЛица.Мужской Тогда
		Пол = "М";	
	иначеЕсли ПолСсылка=Перечисления.ПолФизическогоЛица.Женский тогда 
		Пол = "Ж";
	иначе
		//определяем пол самостоятельно
		ПолНов = ПолучитьПол(Строка);
		Если ПолНов=Перечисления.ПолФизическогоЛица.Женский Тогда
			Пол = "Ж";	
		иначе
			Пол = "М";
		КонецЕсли;
	КонецЕсли;
	//ищем вариант с инициалами И.О.
	Если Найти(Строка,".")>0 Тогда
		Вид="ФИО";
	КонецЕсли;
	
	С=обСлужРазбитьНаФИО(Строка);
	НовоеФамилия=обСклонениеСтроки(С.Фамилия, Падеж, "Фамилия", Пол);
	НовоеИмя=обСклонениеСтроки(С.Имя, Падеж, "Имя", Пол);
	НовоеОтчество=обСклонениеСтроки(С.Отчество, Падеж, "Отчество", Пол);
	Если Вид="ФИО" Тогда
	    НовоеФИО=НовоеФамилия+?(НовоеИмя="",""," ")+НовоеИмя+?(НовоеОтчество="",""," ")+НовоеОтчество;
	иначеЕсли Вид="ИО" тогда
		НовоеФИО = НовоеИмя+?(НовоеОтчество="",""," ")+НовоеОтчество;
	иначеЕсли Вид="Ф_И.О." тогда	
	    НовоеФИО=НовоеФамилия+?(НовоеИмя="",""," ")+Лев(НовоеИмя,1)+"."+Лев(НовоеОтчество,1)+".";			
	иначеЕсли Вид="И.О._Ф" тогда	
	    НовоеФИО=?(НовоеИмя="",""," ")+Лев(НовоеИмя,1)+"."+Лев(НовоеОтчество,1)+". "+НовоеФамилия;			
	КонецЕсли;
	
	Возврат НовоеФИО;
КонецФункции // ПросклонятьСтроку()

Функция обСлужПроверитьДляСклонения(П, Строка)
	Перем ТекСтрока;
	ТекСтрока="";
	Поз=1;
	Всего=СтрДлина(Строка);
	Пока Поз<=Всего Цикл
		ТекСимвол=Сред(Строка,Поз, 1);
		Если ТекСимвол<>" " Тогда
			ТекСтрока=ТекСтрока+ТекСимвол;
		КонецЕсли;
		Если ТекСимвол=" " ИЛИ Поз=Всего Тогда
			Если СтрДлина(ТекСтрока)=1 Тогда
				Если П.ПравыеБуквы1=ТекСтрока Тогда
					Возврат истина;
				КонецЕсли;
			ИначеЕсли СтрДлина(ТекСтрока)=2 Тогда
				Если П.ПравыеБуквы2=ТекСтрока Тогда
					Возврат истина;
				КонецЕсли;
			ИначеЕсли СтрДлина(ТекСтрока)=3 Тогда
				Если П.ПравыеБуквы3=ТекСтрока Тогда
					Возврат истина;
				КонецЕсли;
			ИначеЕсли СтрДлина(ТекСтрока)=4 Тогда
				Если П.ПравыеБуквы4=ТекСтрока Тогда
					Возврат истина;
				КонецЕсли;
			КонецЕсли;		
			ТекСтрока="";
		КонецЕсли;
		Поз=Поз+1;
	КонецЦикла;
	Возврат ложь;
КонецФункции

Функция обСлужАнализСловаДляСклонения(П, Строка)
	П.Вставить("Врег", ВРЕГ(СокрЛП(Строка)));
	П.Вставить("ПравыеБуквы1", ВРЕГ(Прав(СокрЛП(Строка),1)));
	П.Вставить("ПравыеБуквы2", ВРЕГ(Прав(СокрЛП(Строка), 2)));
	П.Вставить("ПравыеБуквы3", ВРЕГ(Прав(СокрЛП(Строка), 3)));
	П.Вставить("ПравыеБуквы4", ВРЕГ(Прав(СокрЛП(Строка), 4)));
	П.Вставить("ЗаканчиваетсяНаСогласную", Найти("БВГДЖЗКЛМНПРСТФХЦЧШЩ", П.ПравыеБуквы1)<>0);
	П.Вставить("ЗаканчиваетсяНаГласную", НЕ П.ЗаканчиваетсяНаСогласную);
КонецФункции

//Считывает из строки таблицу исключений склонения
Функция обСлужПрочитатьТаблицуИсключенийСклонения(Строка)
	Перем ТЗ;
	ТЗ=Новый ТаблицаЗначений();
	ТЗ.Колонки.Добавить("Значение");
	ТЗ.Колонки.Добавить("Падеж");
	ТЗ.Колонки.Добавить("Вид");
	ТЗ.Колонки.Добавить("Пол");
	ТЗ.Колонки.Добавить("Язык");
	ТЗ.Колонки.Добавить("Результат");
	
	ТекСтр=Неопределено;
	ТекКол=Неопределено;
	Ч=Новый ЧтениеXML();
	Ч.УстановитьСтроку(Строка);
	Пока Ч.Прочитать() Цикл
		Если Ч.ТипУзла=ТипУзлаXML.НачалоЭлемента Тогда
			Если Ч.Имя="строка" Тогда
				ТекСтр=ТЗ.Добавить();
			КонецЕсли;
			Если Найти("значение;падеж;вид;пол;язык;результат;", Ч.Имя+";")<>0 Тогда
				ТекКол=Ч.Имя;
			Иначе
				ТекКол=Неопределено;
			КонецЕсли;
		ИначеЕсли Ч.ТипУзла=ТипУзлаXML.Текст Тогда
			Если ТекСтр<>Неопределено И ТекКол<>Неопределено Тогда
				ТекСтр[ТекКол]=Ч.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат ТЗ;
КонецФункции

Функция обСлужРазбитьНаФИО(ИсхС) Экспорт
	С = СокрЛП(ИсхС);
	Фамилия = "";
	Имя = "";
	Отчество = "";
	Поз = Найти(С, " ");
	Если Поз=0 Тогда
		Фамилия=С;
	Иначе
		Фамилия=Лев(С, Поз-1);
		С=СокрЛП(Сред(С, Поз+1));
		Поз = Найти(С, " ");
		Если Поз=0 Тогда
			Имя=С;
		Иначе
			Имя=Лев(С, Поз-1);
			С=СокрЛП(Сред(С, Поз+1));
			Поз = Найти(С, " ");
			Если Поз=0 Тогда
				Отчество=С;
			Иначе
				Отчество=Лев(С, Поз-1);
				С=СокрЛП(Сред(С, Поз+1));
				Поз = Найти(С, " ");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Фамилия, Имя, Отчество", Фамилия, Имя, Отчество);
КонецФункции

Функция обСлужРазбитьНаСловаДляСклонения(ИсхС, Разделитель=" ")
	С = СокрЛП(ИсхС);
	Слова=Новый Массив();
	
	Пока Истина Цикл
		Поз = Найти(С, Разделитель);
		Если Поз = 0 Тогда
			Если С<>"" Тогда
				Слова.Добавить(С);
			КонецЕсли;
			Прервать;
		Иначе
			Слова.Добавить(Лев(С, Поз-1));
			С = СокрЛП(Сред(С, Поз+1));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Слова;
КонецФункции

//обСклонениеСтроки
//Строка - склоняемая строка
//Вид - Фамилия, Имя, Отчество, Должность
//Пол - М или Ж
//Падеж (число или строка): 
//	И - именительный (кто, что)
//	Р - родительный (кого, чего)
//	Д - дательный  (кому, чему)
//	В - винительный (кого, что)
//	Т - творительный (кем, чем)
//	П - предложный (о ком, о чем)
// 	1 - именительный  ( нет кого?    ) Железнякова Юрия Юрьевича     
// 	2 - родительный  ( нет кого?    ) Железнякова Юрия Юрьевича     
// 	3 - дательный    ( кому?        ) Железнякову Юрию Юрьевичу 
// 	4 - винительный  ( вижу кого?   ) Железнякова Юрия Юрьевича  
// 	5 - творительный ( кем?         ) Железняковым Юрием Юрьевичем    
// 	6 - предложный   ( о ком?       ) Железнякове Юрии Юрьевиче 
//	Благодарю за начальный код krysa, SQL.ru
//П - структура с дополнительными свойствами.
//	В нее добавляются автоматом: Строка, Падеж, Вид, Пол, Язык
//Использование хукера:
//	Если есть общая функция обХукерСклонения(П), то она вызывается с доступными параметрами П
//	Можно указать в параметре П.ИмяХукера имя другой функции, тогда будет вызываться она
//
Функция обСклонениеСтроки(Строка, _Падеж, Вид, _Пол=Неопределено, _Язык=Неопределено, П=Неопределено) Экспорт
	Перем Падеж, Пол, Язык, ТаблицаИсключений;
	
	Если П=Неопределено Тогда
		П=Новый Структура();
	КонецЕсли;
	
	Если Строка="" Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(_Падеж)=Тип("Число") Тогда
		Если _Падеж=1 Тогда
			Падеж="И";
		ИначеЕсли _Падеж=2 Тогда
			Падеж="Р";
		ИначеЕсли _Падеж=3 Тогда
			Падеж="Д";
		ИначеЕсли _Падеж=4 Тогда
			Падеж="В";
		ИначеЕсли _Падеж=5 Тогда
			Падеж="Т";
		ИначеЕсли _Падеж=6 Тогда
			Падеж="П";
		КонецЕсли;
	Иначе
		Падеж=_Падеж;
	КонецЕсли;
	
	Если _Язык=Неопределено Тогда
		Язык="RU";
	Иначе
		Язык=_Язык;
	КонецЕсли;
	
	Если ТипЗнч(_Пол)=Тип("Число") Тогда
		Если _Пол=1 Тогда
			Пол="М";
		ИначеЕсли _Пол=2 Тогда
			Пол="Ж";
		КонецЕсли;
	Иначе
		Пол=_Пол;
	КонецЕсли;
	
	
	П.Вставить("Строка", Строка);
	П.Вставить("Падеж", Падеж);
	П.Вставить("Вид", Вид);
	П.Вставить("Пол", Пол);
	П.Вставить("Язык", Язык);
	
	
	обСлужАнализСловаДляСклонения(П, Строка);
	
	////ХУКЕР - перед склонением (своя обработка склонения)
	//ИмяХукера="обХукерСклонения";
	//Если П.Свойство("ИмяХукера") Тогда
	//	ИмяХукера=П.ИмяХукера;
	//КонецЕсли;
	//
	//Если ИмяХукера<>Неопределено Тогда
	//	Р=Неопределено;
	//	Попытка 
	//		Р=Вычислить(ИмяХукера+"(П)");
	//	Исключение
	//	КонецПопытки;
	//	Если Р<>Неопределено Тогда
	//		Возврат Р;
	//	КонецЕсли;
	//КонецЕсли;
	
	//Проверяем, есть ли константа с исключениями...
	Если НЕ П.Свойство("ТаблицаИсключений") Тогда
		Если Метаданные.Константы.Найти("КонстантаГения1СДляИсключенийСклонения")=Неопределено Тогда
			П.Вставить("ТаблицаИсключений", Неопределено);
			//Проверить, есть ли права доступа
			//Получаем таблицу исключений
		Иначе
			ЗначениеКонстантыИсключений=СокрЛП(Константы.КонстантаГения1СДляИсключенийСклонения.Получить());
			Если ЗначениеКонстантыИсключений="" Тогда
				П.Вставить("ТаблицаИсключений", Неопределено);
			Иначе
				П.Вставить("ТаблицаИсключений", обСлужПрочитатьТаблицуИсключенийСклонения(ЗначениеКонстантыИсключений));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Проверяем, может мы получили исключение
	Если П.ТаблицаИсключений<>Неопределено Тогда
		Строки = П.ТаблицаИсключений.НайтиСтроки(Новый Структура("Значение,Падеж,Вид,Пол,Язык", Строка,Падеж,Вид,Пол,Язык));
		Если Строки.Количество()>0 Тогда
			Возврат Строки[0].Результат;
		КонецЕсли;
	КонецЕсли;
	
	НовыеПравые1 = Неопределено;
	НовыеПравые2 = Неопределено;
	НовыеПравые3 = Неопределено;
	НовыеПравыеДобавить = Неопределено;
	
	Если Язык="RU" Тогда
		//CКЛОНЕНИЕ ФАМИЛИИ
		Если Вид = "Фамилия" Тогда 
			//Составная фамилия через дефис
			Слова=обСлужРазбитьНаСловаДляСклонения(Строка, "-");
			Если Слова.Количество()>1 Тогда
				Р="";
				Для Каждого Слово ИЗ Слова Цикл
					ТекР=обСклонениеСтроки(Слово, Падеж, "Фамилия", Пол, Язык, П);
					Р=Р+?(Р="","","-")+ТекР;
				КонецЦикла;
				Возврат Р;
			КонецЕсли;
			
			Если обСлужПроверитьДляСклонения(П, "ЕН ЦКО НКО ОВО АГО ЫХ ЮДО АГО ОТО") Тогда
				//Не склоняем некоторые фамилии
			ИначеЕсли Падеж = "П" Тогда
				//Предложный (о ком о чем)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ИЧ ЙН ИН ОН ЫН АН АК ЯН ЯК УК ЕВ ОВ ОР ИР ЕР НГ") Тогда
					НовыеПравыеДобавить = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИНА ЫНА ЕВА ОВА")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ИЙ ОЙ") Тогда 
					НовыеПравые2 = "ом";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИЙ") Тогда 
					НовыеПравые2 = "ой";
				КонецЕсли;
				
			ИначеЕсли Падеж = "В" Тогда
				//Винительный (кого, что)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИНА ЫНА ЕВА ОВА")  Тогда
					НовыеПравые1 = "у";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ИЧ ЙН ИН ЫН ОН ЯН АН АК УК ЯК ЕВ ОВ ОР ИР ЕР НГ")  Тогда
					НовыеПравыеДобавить = "а";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ИЙ ОЙ")  Тогда
					НовыеПравые2 = "ого";
				ИначеЕсли обСлужПроверитьДляСклонения(П,  "АЯ")  Тогда
					НовыеПравые2 = "ую";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Т" Тогда
				//Творительный (Кем, Чем)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П,  "ИН ЫН ЕВ ОВ")  Тогда
					НовыеПравыеДобавить = "ым";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЙН УК АК ЯК АН ОН ЯН  ИР ОР ЕР НГ")  Тогда
					НовыеПравыеДобавить = "ом";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ИЧ")  Тогда
					НовыеПравыеДобавить = "ем";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые1 = "м";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ОЙ")  Тогда
					НовыеПравые2 = "ым";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИНА ЫНА ЕВА ОВА")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Р" Тогда
				//Родительный (Кого, Чего)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьва";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П,  "ИЧ ЙН ИН ЫН ОН АН АК ЯН УК ЯК ЕВ ОВ ОР ИР ЕР НГ")  Тогда
					НовыеПравыеДобавить = "а";                              
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П,  "ИЙ ОЙ")  Тогда
					НовыеПравые2 = "ого";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П,  "ИНА ЫНА ЕВА ОВА")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П,  "АЯ")  Тогда
					НовыеПравые2 = "ой";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Д" Тогда
				//Дательный (Кому, Чему)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П,  "ИЧ ЙН ИН ЫН ОН АК АН ЯН ЯК ЕВ ОВ УК ИР ОР ЕР НГ")  Тогда
					НовыеПравыеДобавить = "у";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П,  "ИЙ ОЙ")  Тогда
					НовыеПравые2 = "ому";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П,  "ИНА ЫНА ЕВА ОВА")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П,  "АЯ")  Тогда
					НовыеПравые2 = "ой";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли Вид="Имя" Тогда
			
			//СКЛОНЕНИЕ ИМЕНИ 
			
			Если Падеж = "Т" Тогда
				
				//Творительный (Кем, Чем)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьвом";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЕЛ")  Тогда
					НовыеПравые2 = "лом";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Й")  Тогда
					НовыеПравые1 = "ем";
				ИначеЕсли Пол="М" И П.ЗаканчиваетсяНаСогласную Тогда
					НовыеПравыеДобавить = "ом";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Я А")  Тогда
					НовыеПравые1 = "ей";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "ем";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "ю";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "ей";
				КонецЕсли;
				
			ИначеЕсли Падеж = "П" Тогда
				
				//Предложный (О ком о чем)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьве";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЕЛ")  Тогда
					НовыеПравые2 = "ле";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые1 = "и";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Й")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="М" И П.ЗаканчиваетсяНаСогласную Тогда
					НовыеПравыеДобавить = "е";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Я А")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые2 = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИЯ")  Тогда
					НовыеПравые2 = "ии";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А Я")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИЯ")  Тогда
					НовыеПравые1 = "и";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "и";
				КонецЕсли;
				
			ИначеЕсли Падеж = "В" Тогда
				
				//Винительный (кого, что)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьва";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЕЛ")  Тогда
					НовыеПравые2 = "ла";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Й")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли Пол="М" И П.ЗаканчиваетсяНаСогласную Тогда
					НовыеПравыеДобавить = "а";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "у";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "ю";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "у";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "ю";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Р" Тогда
				
				//Родительный (Кого, Чего)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьва";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Й")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЕЛ")  Тогда
					НовыеПравые2 = "ла";
				ИначеЕсли Пол="М" И П.ЗаканчиваетсяНаСогласную Тогда
					НовыеПравыеДобавить = "а";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "А Я")  Тогда
					НовыеПравые1 = "и";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ы";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "и";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Д" Тогда
				
				//Дательный (Кому, Чему)
				Если ЛОЖЬ Тогда
				ИначеЕсли Пол="М"  И обСлужПроверитьДляСклонения(П, "ЛЕВ") Тогда 
					НовыеПравые2 = "ьву";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "ЕЛ")  Тогда
					НовыеПравые2 = "лу";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "Й")  Тогда
					НовыеПравые1 = "ю";
				ИначеЕсли Пол="М" И П.ЗаканчиваетсяНаСогласную Тогда
					НовыеПравыеДобавить = "у";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="М" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "ю";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "ИЯ")  Тогда
					НовыеПравые1 = "и";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Я")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "и";
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Вид="Отчество" Тогда
			
			//СКЛОНЕНИЕ ОТЧЕСТВА 
			
			Если Падеж = "Т" Тогда
				//Творительный (Кем, Чем)
				Если Пол="М" И обСлужПроверитьДляСклонения(П, "Ч")  Тогда
					НовыеПравыеДобавить = "ем";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ой";
				КонецЕсли;
				
			ИначеЕсли Падеж = "П" Тогда
				
				//Предложный (О ком о чем)
				Если Пол="М" И обСлужПроверитьДляСклонения(П, "Ч")  Тогда
					НовыеПравыеДобавить = "е";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "е";
				КонецЕсли;
				
			ИначеЕсли Падеж = "В" Тогда
				
				//Винительный (кого, что)
				Если Пол="М" И обСлужПроверитьДляСклонения(П, "Ч")  Тогда
					НовыеПравыеДобавить = "а";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "у";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Р" Тогда
				
				//Родительный (Кого, Чего)  НЕТ ФИО
				Если Пол="М" И обСлужПроверитьДляСклонения(П, "Ч")  Тогда
					НовыеПравыеДобавить = "а";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ы";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Д" Тогда
				
				//Дательный (Кому, Чему)
				Если Пол="М" И обСлужПроверитьДляСклонения(П, "Ч")  Тогда
					НовыеПравыеДобавить = "у";
				ИначеЕсли Пол="Ж" И обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "е";
				КонецЕсли;
				
			КонецЕсли;
			
			
		ИначеЕсли Вид = "Должность" Тогда 
			
			//Разбиваем по словам должность
			Слова=обСлужРазбитьНаСловаДляСклонения(Строка);
			Р="";
			БольшеНеСклонять=ложь;
			Для Каждого Слово ИЗ Слова Цикл
				обСлужАнализСловаДляСклонения(П, Слово);
				//Руководитель ансамбля ...  
				//инструктор по ...
				//транспортного цеха 
				// Заместитель директора
				// Директор филиала
				Если обСлужПроверитьДляСклонения(П, "БЛЯ ОГО ОРА АЛА") ИЛИ П.Врег="ПО" Тогда 
					БольшеНеСклонять=истина;
				КонецЕсли;
				Если БольшеНеСклонять Тогда
					ТекР=Слово;
				Иначе
					ТекР=обСклонениеСтроки(Слово, Падеж, "СловоДолжности", Пол, Язык, П);
				КонецЕсли;
				Р=Р+?(Р="",""," ")+ТекР;
			КонецЦикла;
			
			Возврат Р;
			
		ИначеЕсли Вид = "СловоДолжности" Тогда 
			
			//СКЛОНЯЕМ ДОЛЖНОСТЬ
			//Заканчивается на согласную: Директор, программист
			//Заканчивается на гласную: Уборщица
			//Заканчивается на ь: Водитель, Руководитель
			//Заканчивается на й: Генеральный, 
			//Прилагательное: Ответственная, заведущая (на ая)
			
			Если обСлужПроверитьДляСклонения(П, "НИ") Тогда
				//Не склоняем некоторые части должности
			ИначеЕсли Падеж = "Т" Тогда
				
				//Творительный (Кем, Чем)
				Если обСлужПроверитьДляСклонения(П, "ЫЙ ОЙ")  Тогда
					НовыеПравые2 = "ым";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые2 = "им";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ЩАЯ")  Тогда
					НовыеПравые2 = "ей";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ЦА ЧА ША")  Тогда
					НовыеПравые1 = "ей";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ой";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "ем";
				ИначеЕсли П.ЗаканчиваетсяНаСогласную  Тогда 
					НовыеПравыеДобавить = "ом";
				КонецЕсли;
				
			ИначеЕсли Падеж = "П" Тогда
				
				//Предложный (О ком о чем)
				Если обСлужПроверитьДляСклонения(П, "ЫЙ ОЙ")  Тогда
					НовыеПравые2 = "ом";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые2 = "ем";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ЩАЯ")  Тогда
					НовыеПравые2 = "ей";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "А Ь")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли П.ЗаканчиваетсяНаСогласную  Тогда
					НовыеПравыеДобавить = "е";
				КонецЕсли;
				
			ИначеЕсли Падеж = "В" Тогда
				
				//Винительный (кого, что)
				Если обСлужПроверитьДляСклонения(П, "ЫЙ ОЙ")  Тогда
					НовыеПравые2 = "ого";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые2 = "его";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ую";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "у";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли П.ЗаканчиваетсяНаСогласную  Тогда
					НовыеПравыеДобавить = "а";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Р" Тогда
				
				//Родительный (Кого, Чего)
				Если обСлужПроверитьДляСклонения(П, "ЫЙ ОЙ")  Тогда
					НовыеПравые2 = "ого";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ЩАЯ")  Тогда
					НовыеПравые3 = "щей";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые2 = "его";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "ы";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "я";
				ИначеЕсли П.ЗаканчиваетсяНаСогласную  Тогда
					НовыеПравыеДобавить = "а";
				КонецЕсли;
				
			ИначеЕсли Падеж = "Д" Тогда
				
				//Дательный (Кому, Чему)
				Если обСлужПроверитьДляСклонения(П, "ЫЙ ОЙ")  Тогда
					НовыеПравые2 = "ому";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ЩАЯ")  Тогда
					НовыеПравые2 = "ей";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "АЯ")  Тогда
					НовыеПравые2 = "ой";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "ИЙ")  Тогда
					НовыеПравые2 = "ему";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "А")  Тогда
					НовыеПравые1 = "е";
				ИначеЕсли обСлужПроверитьДляСклонения(П, "Ь")  Тогда
					НовыеПравые1 = "ю";
				ИначеЕсли П.ЗаканчиваетсяНаСогласную  Тогда
					НовыеПравыеДобавить = "у";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	//Проверяем
	Если НовыеПравые1 <> Неопределено Тогда
		Р = Сред(Строка, 1, СтрДлина(Строка)-1) + НовыеПравые1;
	ИначеЕсли НовыеПравые2 <> Неопределено Тогда
		Р = Сред(Строка, 1, СтрДлина(Строка)-2) + НовыеПравые2;
	ИначеЕсли НовыеПравые3 <> Неопределено Тогда
		Р = Сред(Строка, 1, СтрДлина(Строка)-3) + НовыеПравые3;
	ИначеЕсли НовыеПравыеДобавить <> Неопределено Тогда
		Р = Строка + НовыеПравыеДобавить;
		НовыеПравыеДобавить = Неопределено;
	Иначе
		Р=Строка;
	КонецЕсли;
	
	Возврат Р;
КонецФункции 

// Функция определяет пол физлица по его отчеству
//
Функция ПолучитьПол(ФИОРаботника) Экспорт
	
	ФИО = ФИОРаботника;
	
	Если Найти(ФИО,".")>0 Тогда
		Возврат Перечисления.ПолФизическогоЛица.ПустаяСсылка();
	КонецЕсли;
	
	Фамилия		= ВыделитьСлово(ФИО);
	Имя			= ВыделитьСлово(ФИО);
	Отчество	= ВыделитьСлово(ФИО);
	
		
	Если ЗначениеЗаполнено(Отчество) тогда
		Если Прав(Отчество, 2) = "ич" Тогда
			Возврат Перечисления.ПолФизическогоЛица.Мужской;
			
		ИначеЕсли Прав(Отчество, 2) = "на" Тогда
			Возврат Перечисления.ПолФизическогоЛица.Женский;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Перечисления.ПолФизическогоЛица.ПустаяСсылка();
	
КонецФункции // ПолучитьПол()

// Функция выделяет первое слово в предложении
Функция ВыделитьСлово(ИсходнаяСтрока,Разделитель=" ") Экспорт
	
	Буфер = СокрЛ(ИсходнаяСтрока);
	ПозицияПослПробела = Найти(Буфер, Разделитель);

	Если ПозицияПослПробела = 0 Тогда
		ИсходнаяСтрока = "";
		Возврат Буфер;
	КонецЕсли;
	
	ВыделенноеСлово = СокрЛП(Лев(Буфер, ПозицияПослПробела));
	ИсходнаяСтрока = Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
	
	Возврат ВыделенноеСлово;
	
КонецФункции
//-Функция склонения
