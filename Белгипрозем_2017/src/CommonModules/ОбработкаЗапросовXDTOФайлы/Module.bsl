////////////////////////////////////////////////////////////////////////////////
// Обработка запросов XDTO, работа с файлами
// Реализует функционал веб-сервиса DMService в части работы с файлами
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает папку файлов документооборота для внешнего объекта
//
// Параметры:
//   ИДВнешнегоОбъекта - Строка - идентификатор внешнего объекта
//   ТипВнешнегоОбъекта - Строка - имя типа XDTO внешнего объекта
//
// Возвращаемое значение:
//   СправочникСсылка.ПапкиФайлов - папка для хранения файлов объекта, или
//   Неопределено - если папка не найдена
//
Функция ПолучитьПапкуФайловДОПоВнешнемуОбъекту(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗапросКРегистру = Новый Запрос;
	ЗапросКРегистру.Текст =
		"ВЫБРАТЬ
		|	СсылкаНаПапкуФайловДО
		|ИЗ
		|	РегистрСведений.ПапкиХраненияФайловОбъектовИнтегрированныхСистем
		|ГДЕ
		|	ИДВнешнегоОбъекта = &ИДВнешнегоОбъекта
		|	И ТипВнешнегоОбъекта = &ТипВнешнегоОбъекта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СсылкаНаПапкуФайловДО
		|ИЗ
		|	РегистрСведений.ПапкиХраненияФайловОбъектовИнтегрированныхСистем
		|ГДЕ
		|	ИДВнешнегоОбъекта = &ИДВнешнегоОбъекта
		|	И ТипВнешнегоОбъекта = """"";
	ЗапросКРегистру.УстановитьПараметр("ИДВнешнегоОбъекта",ИДВнешнегоОбъекта);
	ЗапросКРегистру.УстановитьПараметр("ТипВнешнегоОбъекта",ТипВнешнегоОбъекта);
	ВыборкаРегистра = ЗапросКРегистру.Выполнить().Выбрать();
	
	Если ВыборкаРегистра.Следующий() Тогда
		Возврат ВыборкаРегистра.СсылкаНаПапкуФайловДО;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Записывает в РС связь папки файлов со внешним объектом
//
// Параметры:
//   ИДВнешнегоОбъекта - Строка - идентификатор внешнего объекта
//   ТипВнешнегоОбъекта - Строка - имя типа XDTO внешнего объекта
//   СсылкаНаПапкуФайловДО - СправочникСсылка.ПапкиФайлов - папка для хранения файлов объекта
//
Процедура ЗаписатьПапкуФайлаДляВнешнегоОбъекта(ИДВнешнегоОбъекта, ТипВнешнегоОбъекта, СсылкаНаПапкуФайловДО) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Создать набор записей
	НаборЗаписей = РегистрыСведений.ПапкиХраненияФайловОбъектовИнтегрированныхСистем.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ИДВнешнегоОбъекта.Установить(ИДВнешнегоОбъекта);
	НаборЗаписей.Отбор.ТипВнешнегоОбъекта.Установить(ТипВнешнегоОбъекта);

	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.ИДВнешнегоОбъекта = ИДВнешнегоОбъекта;
	НоваяЗапись.ТипВнешнегоОбъекта = ТипВнешнегоОбъекта;
	НоваяЗапись.СсылкаНаПапкуФайловДО = СсылкаНаПапкуФайловДО;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Получает список файлов в папке-владельце (внешний объект)
//
// Параметры:
//   Сообщение - ОбъектXDTO типа DMGetFileListRequest
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMGetFileListResponse или DMError
//
Функция ПолучитьСписокФайлов(Сообщение) Экспорт
	
	Попытка 
		
		Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMGetFileListResponse");
		Узел = ОбработкаЗапросовXDTO.УзелИнтегрированнойСистемы(Сообщение);
		
		ИдентификаторВладельца = Сообщение.externalObjects[0].id;
		ТипВладельца = Сообщение.externalObjects[0].type;
		МассивОбъектовДО = ОбработкаЗапросовXDTO.ПолучитьОбъектыДОПоВнешнемуОбъекту(Узел, ИдентификаторВладельца, ТипВладельца);
		
		ТаблицаФайлов = Новый ТаблицаЗначений;
		ТаблицаФайлов.Колонки.Добавить("Имя");
		ТаблицаФайлов.Колонки.Добавить("Ссылка");
		
		Для Каждого Ссылка из МассивОбъектовДО Цикл
			НоваяСтрока = ТаблицаФайлов.Добавить();
			НоваяСтрока.Ссылка = Ссылка;
			НоваяСтрока.Имя = Строка(Ссылка);
		КонецЦикла;
		
		// Сортировка по имени
		ТаблицаФайлов.Сортировать("Имя Возр");  
		
		МассивОбъектовДО.Очистить();
		Для Каждого Строка из ТаблицаФайлов Цикл
			Если ТипЗнч(Строка.Ссылка) = Тип("СправочникСсылка.Файлы") Тогда
				МассивОбъектовДО.Добавить(Строка.Ссылка);
			КонецЕсли;
		КонецЦикла;
		
		МассивФайлов = ЗаполнитьСписокФайлов(МассивОбъектовДО, Сообщение.ColumnSet);
		ЗаполнитьСписокФайловИзМассива(Ответ, МассивФайлов, Сообщение.ColumnSet);
		
		Возврат Ответ;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при получении списка файлов'");
	    Инфо = ИнформацияОбОшибке();
	    Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции	

// Получает объект XDTO, соответствующий файлу, по переданному идентификатору
//
// Параметры:
//   ИдентификаторФайла - Строка - идентификатор файла
//   НаборКолонок - Массив - набор имен реквизитов файла к получению
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMFile
//
Функция ПолучитьКарточкуФайла(ИдентификаторФайла, НаборКолонок) Экспорт
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор(ИдентификаторФайла);
	ФайлСсылка = Справочники.Файлы.ПолучитьСсылку(УникальныйИдентификатор);
	// проверить что не пуста 
	Если НЕ ЗначениеЗаполнено(ФайлСсылка.ТекущаяВерсия) Тогда
		ВызватьИсключение "Файл не найден";
	КонецЕсли;	
	
	ОбъектDMFile = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFile");

	ФайлСтруктура = ЗаполнитьСтруктуруФайла(ФайлСсылка, НаборКолонок);
	ЗаполнитьXDTOФайл(ОбъектDMFile, ФайлСтруктура, НаборКолонок);
	
	Возврат ОбъектDMFile;
	
КонецФункции	

// Получает объект XDTO, соответствующий версии файла, по переданному идентификатору
//
// Параметры:
//   ИдентификаторВерсии - Строка - идентификатор версии файла
//   НаборКолонок - Массив - набор имен реквизитов версии файла к получению
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMFileVersion
//
Функция ПолучитьВерсиюФайла(ИдентификаторВерсии, НаборКолонок) Экспорт
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор(ИдентификаторВерсии);
	ВерсияСсылка = Справочники.ВерсииФайлов.ПолучитьСсылку(УникальныйИдентификатор);
	
	ОбъектDMFileVersion = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFileVersion");
	СтруктураВерсии = ЗаполнитьСтруктуруВерсииФайла(ВерсияСсылка, НаборКолонок);
	ЗаполнитьXDTOВерсииФайла(ОбъектDMFileVersion, СтруктураВерсии, НаборКолонок);
	
	Возврат ОбъектDMFileVersion;
	
КонецФункции	

// Заполняет список файлов из массива структур, содержащих их реквизиты
//
// Параметры:
//   Ответ - ОбъектXDTO типа DMGetFileListResponse, неявно возвращаемое значение
//   МассивФайлов - Массив - массива структур, содержащих реквизиты файлов
//   НаборКолонок - Массив - массив имен реквизитов к заполнению
//
Процедура ЗаполнитьСписокФайловИзМассива(Ответ, МассивФайлов, НаборКолонок) Экспорт
	
	Для Каждого ФайлСтруктура Из МассивФайлов Цикл
		
		ОбъектDMFile = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFile");
		ЗаполнитьXDTOФайл(ОбъектDMFile, ФайлСтруктура, НаборКолонок);
		
		Ответ.files.Добавить(ОбъектDMFile);
		
	КонецЦикла;	
	
	
КонецПроцедуры	

// Получает массив структур с реквизитами файлов по массиву ссылок на файлы
//
// Параметры:
//   МассивОбъектовДО - Массив - содержит элементы типа СправочникСсылка.Файлы
//   НаборКолонок - Массив - содержит имена реквизитов к получению
//
// Возвращаемое значение:
//   Массив - содержит элементы типа 
//     * Структура, ключи которой - имена реквизитов файла, а значения - значения этих реквизитов.
//
Функция ЗаполнитьСписокФайлов(МассивОбъектовДО, НаборКолонок) Экспорт
	
	МассивФайлов = Новый Массив;
	
	Для Каждого ФайлСсылка Из МассивОбъектовДО Цикл
		
		РеквизитыФайла = ЗаполнитьСтруктуруФайла(ФайлСсылка, НаборКолонок);	
		МассивФайлов.Добавить(РеквизитыФайла);	
		
	КонецЦикла;	
	
	Возврат МассивФайлов;
	
КонецФункции	

// Дополняет объект Документооборота сведениями о подписях из объекта XDTO
//
// Параметры:
//   signatures - СписокXDTO объектов типа DMSignature
//   СсылкаОбъектаД8 - СправочникСсылка.Файлы
//
// Возвращаемое значение:
//   Нет.
//
Функция ЗанестиИнформациюОПодписяхОбъекта(signatures, СсылкаОбъектаД8) Экспорт
	
	МассивДанныхДляЗанесенияВБазу = Новый Массив;
	
	Для Каждого ПодписьСтруктура Из signatures Цикл
	
		ДанныеПодписи = Новый Структура("ОбъектСсылка, НоваяПодписьДвоичныеДанные, Отпечаток, ДатаПодписи, Комментарий, ИмяФайлаПодписи, КомуВыданСертификат, АдресФайла, ДвоичныеДанныеСертификата, УстановившийПодпись");
		
		ДанныеПодписи.ОбъектСсылка = СсылкаОбъектаД8;
		
		ДанныеПодписи.КомуВыданСертификат = ПодписьСтруктура.author;
		ДанныеПодписи.ДвоичныеДанныеСертификата = ПодписьСтруктура.certificate;
		ДанныеПодписи.Комментарий = ПодписьСтруктура.comment;
		ДанныеПодписи.ДатаПодписи = ПодписьСтруктура.date;
		ДанныеПодписи.НоваяПодписьДвоичныеДанные = ПодписьСтруктура.signature;
		ДанныеПодписи.ИмяФайлаПодписи = ПодписьСтруктура.signatureFileName;
		ДанныеПодписи.Отпечаток = ПодписьСтруктура.thumbprint;
		
		Если ПодписьСтруктура.Установлено("signer") И ПодписьСтруктура.signer <> Неопределено Тогда
			ДанныеПодписи.УстановившийПодпись = ОбработкаЗапросовXDTO.ПолучитьСсылкуПоObjectID(ПодписьСтруктура.signer.objectId);
		Иначе
			ДанныеПодписи.УстановившийПодпись = Пользователи.ТекущийПользователь();
		КонецЕсли;	
		
		МассивДанныхДляЗанесенияВБазу.Добавить(ДанныеПодписи);
		
	КонецЦикла;
	
	МассивАдресов = Новый Массив;
	РаботаСЭП.ЗаменитьИнформациюОПодписяхОбъекта(МассивДанныхДляЗанесенияВБазу, СсылкаОбъектаД8)
		
КонецФункции	

// Создает файл Документооборота по файлу на диске
//
// Параметры:
//   Сообщение - ОбъектXDTO типа DMAddLinkedFileRequest
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMAddLinkedFileResponse или DMError
//
Функция ДобавитьФайлСДиска(Сообщение) Экспорт
	
	Попытка 
		
		Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMAddLinkedFileResponse");
		Узел = ОбработкаЗапросовXDTO.УзелИнтегрированнойСистемы(Сообщение);
		
		ПапкаСсылка = ПолучитьПапкуДляХраненияФайлов(Сообщение.rootFolderId,
			Сообщение.file.externalObject.type, Сообщение.file.externalObject.id, Сообщение.file.externalObject.name);
			
		АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(Сообщение.file.binaryData);
		АдресВременногоХранилищаТекста = "";
		
		Если Сообщение.file.Установлено("text") Тогда
			АдресВременногоХранилищаТекста = Новый ХранилищеЗначения(Сообщение.file.text);
		КонецЕсли;
		
		ВебКлиент = Истина;

		// Создадим карточку Файла в БД
		ФайлСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(
			ПапкаСсылка,
			Сообщение.file.name,
			Сообщение.file.extension,
			Сообщение.file.modificationDate,
			Сообщение.file.modificationDateUniversal,
			Сообщение.file.size,
			АдресВременногоХранилищаФайла,
			АдресВременногоХранилищаТекста,
			ВебКлиент
			);
			
		Если Сообщение.file.signatures.Количество() > 0 Тогда
			ЗанестиИнформациюОПодписяхОбъекта(Сообщение.file.signatures, ФайлСсылка);
		КонецЕсли;
			
		ОбработкаЗапросовXDTO.ДобавитьСвязьОбъектаДОИВнешнегоОбъекта(
			Узел, Сообщение.file.externalObject.id, Сообщение.file.externalObject.type, ФайлСсылка);
			
		Ответ.file = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObject");
		Ответ.file.name = Строка(ФайлСсылка); 
		Ответ.file.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
		Ответ.file.objectId.id = Строка(ФайлСсылка.УникальныйИдентификатор()); 
		Ответ.file.objectId.type = "DMFile";
		
		ЗаписатьПапкуФайлаДляВнешнегоОбъекта(Сообщение.file.externalObject.id, Сообщение.file.externalObject.type, ПапкаСсылка);
		
		Возврат Ответ;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при добавлении файла с диска'");
	    Инфо = ИнформацияОбОшибке();
	    Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции	

// Получает массив структур, описывающих подпапки указанной папки первого уровня
//
// Параметры:
//   ИдентификаторКорневойПапки - Строка - идентификатор родительской папки
//
// Возвращаемое значение:
//   Массив - содержит элементы типа Структура:
//     * Идентификатор - Строка - идентификатор подпапки
//     * Наименование - Строка - наименование подпапки
//
Функция ПолучитьСписокПапок(ИдентификаторКорневойПапки) Экспорт
	
	СсылкаПапки = Справочники.ПапкиФайлов.ПустаяСсылка();
	
	Если НЕ ПустаяСтрока(ИдентификаторКорневойПапки) Тогда
		УникальныйИдентификатор = Новый УникальныйИдентификатор(ИдентификаторКорневойПапки);
		СсылкаПапки = Справочники.ПапкиФайлов.ПолучитьСсылку(УникальныйИдентификатор);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПапкиФайлов.Ссылка КАК Ссылка,
		|	ПапкиФайлов.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПапкиФайлов КАК ПапкиФайлов
		|ГДЕ
		|	ПапкиФайлов.Родитель = &Ссылка
		|	И ПапкиФайлов.ПометкаУдаления = &ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	Запрос.УстановитьПараметр("Ссылка", СсылкаПапки);
	Запрос.УстановитьПараметр("ПометкаУдаления", Ложь);
	
	МассивПапок = Новый Массив;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураПапки = Новый Структура("УникальныйИдентификатор, Наименование", 
			Выборка.Ссылка.УникальныйИдентификатор(), Выборка.Наименование);
		МассивПапок.Добавить(СтруктураПапки);	
	КонецЦикла;
	
	Возврат МассивПапок;
	
КонецФункции	

// Вызывается при удалении папки файлов, чтобы очистить регистр ПапкиХраненияФайловОбъектовИнтегрированныхСистем
//
// Параметры:
//   Источник - СправочникОбъект.ПапкиФайлов - удаляемая папка
//   Отказ - Булево
//
Процедура ПриУдаленииПапкиФайловПередУдалением(Источник, Отказ) Экспорт
	
	Отбор = Новый Структура("СсылкаНаПапкуФайловДО", Источник.Ссылка);
	ВыборкаРегистра = РегистрыСведений.ПапкиХраненияФайловОбъектовИнтегрированныхСистем.Выбрать(Отбор);
	
	ИДВнешнегоОбъекта = Неопределено;
	ТипВнешнегоОбъекта = Неопределено;
	Если ВыборкаРегистра.Следующий() Тогда
		ИДВнешнегоОбъекта = ВыборкаРегистра.ИДВнешнегоОбъекта;
		ТипВнешнегоОбъекта = ВыборкаРегистра.ТипВнешнегоОбъекта;
	Иначе
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Создать набор записей
	НаборЗаписей = РегистрыСведений.ПапкиХраненияФайловОбъектовИнтегрированныхСистем.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИДВнешнегоОбъекта.Установить(ИДВнешнегоОбъекта);
	НаборЗаписей.Отбор.ТипВнешнегоОбъекта.Установить(ТипВнешнегоОбъекта);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Обновляет файл Документооборота двоичными или текстовыми данными из объекта XDTO
//
// Параметры:
//   ФайлДО - СправочникФайлы.Ссылка - обновляемый файл
//   ФайлXDTO - ОбъектXDTO типа DMFile - источник данных заполнения
//
Процедура ОбновитьДвоичныеДанныеФайла(ФайлДО, ФайлXDTO) Экспорт
	
	АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ФайлXDTO.binaryData);
	АдресВременногоХранилищаТекста = "";
	
	ТекстНеИзвлеченНаКлиенте = Истина;
	
	Если ФайлXDTO.Установлено("text") Тогда
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ТекстовыйФайл = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаТекста.UTF8);
		ТекстовыйФайл.Записать(ФайлXDTO.text);
		ТекстовыйФайл.Закрыть();
		
		ДвоичныеДанныеТекста = Новый ДвоичныеДанные(ИмяВременногоФайла);
		АдресВременногоХранилищаТекста = ПоместитьВоВременноеХранилище(ДвоичныеДанныеТекста);
		ТекстНеИзвлеченНаКлиенте = Ложь;
		
		УдалитьФайлы(ИмяВременногоФайла);
		
	КонецЕсли;
	
	ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайла(ФайлДО);
	
	Если ДанныеФайла.РедактируетТекущийПользователь Тогда
		
		РаботаСФайламиВызовСервера.ОпубликоватьФайл(
			ФайлДО,
			Истина,
			АдресВременногоХранилищаФайла,
			"", // комментарии к версиям не поддерживаются
			ФайлXDTO.modificationDate,
			ФайлXDTO.modificationDateUniversal,
			ФайлXDTO.size,
			Строка(ФайлДО), // имя без расширения
			ФайлXDTO.extension,
			"", // относительный путь к файлу
			"", // полный путь к файлу
			АдресВременногоХранилищаТекста,
			Истина,
			ТекстНеИзвлеченНаКлиенте,
			Ложь, // в рабочем каталоге владельца
			Истина // не менять запись в РС ФайлыВРабочемКаталоге
			);
			
	Иначе // попытаемся захватить файл, затем опубликовать и освободить его
	
		СтрокаОшибки = "";
		Если Не РаботаСФайламиКлиентСервер.МожноЛиЗанятьФайл(ДанныеФайла, СтрокаОшибки) Тогда
			ВызватьИсключение СтрокаОшибки;
		КонецЕсли;
		
		СтрокаОшибки = "";
		ДатаЗаема = ТекущаяДатаСеанса();
		Если Не РаботаСФайламиВызовСервера.ЗанятьФайл(ДанныеФайла, СтрокаОшибки, ДатаЗаема) Тогда 
			ВызватьИсключение СтрокаОшибки;
		КонецЕсли;
		
		РаботаСФайламиВызовСервера.ПолучитьДанныеФайлаИОпубликоватьИОсвободитьФайл(
			ФайлДО,
			ДанныеФайла,
			Истина,
			АдресВременногоХранилищаФайла,
			"", // Комментарии к версиям не поддерживаются
			ФайлXDTO.modificationDate,
			ФайлXDTO.modificationDateUniversal,
			ФайлXDTO.size,
			Строка(ФайлДО), // имя без расширения
			ФайлXDTO.extension,
			"", // полный путь к файлу,
			АдресВременногоХранилищаТекста,
			Истина, // веб-клиент: изменений в ФайлыВРабочемКаталоге вноситься не будет
			ТекстНеИзвлеченНаКлиенте);
			
	КонецЕсли;
	
КонецПроцедуры

// Заполняет и записывает файл Документооборота по объекту XDTO
//
// Параметры:
//   Объект - ОбъектXDTO типа DMFile
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMFile, заполненный сведениями о записанном файле, или DMError
//
Функция ИзменитьФайл(Узел, Объект) Экспорт 
	
	Попытка 
		
		Ссылка = ОбработкаЗапросовXDTO.ПолучитьСсылкуПоObjectID(Объект.objectId);
		
		ЧислоПодписейДо = ЭлектроннаяПодпись.КоличествоПодписей(Ссылка.ТекущаяВерсия);
		ЧислоПодписейПосле = Объект.signatures.Количество();
		
		Если Объект.Установлено("binaryData") Тогда
			ОбновитьДвоичныеДанныеФайла(Ссылка, Объект);
		Иначе	
			
			Если ЧислоПодписейДо > ЧислоПодписейПосле Тогда // удаляем подписи перед изменением карточки файла
				ЗанестиИнформациюОПодписяхОбъекта(Объект.signatures, Ссылка);
			КонецЕсли;	
			
			НаименованиеДоЗаписи = Ссылка.Наименование;
			ОписаниеДоЗаписи 	 = Ссылка.Описание;
			
			Наименование = "";
			Описание = "";
			ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, Наименование, 	Объект, "name");
			ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, Описание, 		Объект, "description");
			
			Если НаименованиеДоЗаписи <> Наименование ИЛИ ОписаниеДоЗаписи <> Описание
				ИЛИ ОбработкаЗапросовXDTO.ДополнительныеРеквизитыИзменены(Узел, Ссылка, Объект)
				ИЛИ Объект.Установлено("activeVersion") Тогда
				
				ЗаблокироватьДанныеДляРедактирования(Ссылка);
				ФайлОбъект = Ссылка.ПолучитьОбъект();
				
				Если ЗначениеЗаполнено(Наименование) Тогда
					
					ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, ФайлОбъект.ПолноеНаименование, 	Объект, "name");
					ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, ФайлОбъект.Наименование, 		Объект, "name");
				
					ТипХраненияФайла = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ФайлОбъект.ТекущаяВерсия, "ТипХраненияФайла");
					
					// Возможно, требуется изменить имя файла в томе не диске.
					Если НаименованиеДоЗаписи <> ФайлОбъект.Наименование Тогда
						Если ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
							РаботаСФайламиВызовСервера.ПереименоватьФайлВерсииНаДиске(ФайлОбъект.ТекущаяВерсия, НаименованиеДоЗаписи, ФайлОбъект.Наименование);
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
				ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, ФайлОбъект.Описание, 	Объект, "description");
				
				// Возможно, необходима смена текущей версии.
				Если Объект.Установлено("activeVersion") Тогда
					Если Не ФайлОбъект.Редактирует.Пустая() Тогда
						Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
						Ошибка.subject = НСтр("ru = 'Ошибка при записи файла'");
						Ошибка.description = НСтр("ru = 'Смена активной версии разрешена только для не занятых файлов!'");
						РазблокироватьДанныеДляРедактирования(Ссылка);
						Возврат Ошибка;
					КонецЕсли;
					ОбработкаЗапросовXDTO.ЗаполнитьРеквизитИзСвойстваXDTO(Узел, ФайлОбъект.ТекущаяВерсия, 	Объект, "activeVersion");
				КонецЕсли;
				
				ТекстСообщения = "";
				Если Не ОбработкаЗапросовXDTO.ПроверитьЗаполнение(ФайлОбъект, ТекстСообщения) Тогда 
					Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
					Ошибка.subject = НСтр("ru = 'Ошибка при записи файла'");
					Ошибка.description = ТекстСообщения;
					РазблокироватьДанныеДляРедактирования(Ссылка);
					Возврат Ошибка;
				КонецЕсли;	
				
				ОбработкаЗапросовXDTO.ЗаписатьДополнительныеРеквизиты(Узел, ФайлОбъект, Объект);
				
				ФайлОбъект.Записать();
				РазблокироватьДанныеДляРедактирования(Ссылка);
				
				// Смена активной версии требует записи самой версии, чтобы отработали все подписки.
				Если Объект.Установлено("activeVersion") Тогда
					ВерсияОбъект = ФайлОбъект.ТекущаяВерсия.ПолучитьОбъект();
					ЗаблокироватьДанныеДляРедактирования(ФайлОбъект.ТекущаяВерсия);
					ВерсияОбъект.Записать();
					РазблокироватьДанныеДляРедактирования(ФайлОбъект.ТекущаяВерсия);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЧислоПодписейДо <= ЧислоПодписейПосле Тогда // добавляем подписи после изменения карточки
				ЗанестиИнформациюОПодписяхОбъекта(Объект.signatures, Ссылка);
			КонецЕсли;	
			
		КонецЕсли;
		
		Если Объект.Установлено("scannedOriginal") Тогда
			Делопроизводство.СохранитьСведенияОбОригиналеФайла(Ссылка,,Объект.scannedOriginal);
		КонецЕсли;
		
		НаборКолонок = Новый Массив;
		
		НаборКолонок.Добавить("objectId");
		НаборКолонок.Добавить("signed");
		НаборКолонок.Добавить("name");
		НаборКолонок.Добавить("size");
		НаборКолонок.Добавить("creationDate");
		НаборКолонок.Добавить("modificationDateUniversal");
		НаборКолонок.Добавить("author");
		НаборКолонок.Добавить("extension");
		НаборКолонок.Добавить("description");
		НаборКолонок.Добавить("editing");
		НаборКолонок.Добавить("encrypted");
		НаборКолонок.Добавить("scannedOriginal");
		НаборКолонок.Добавить("activeVersion");
		
		Возврат ПолучитьКарточкуФайла(Объект.objectId.id, НаборКолонок);
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при записи файла'");
		Инфо = ИнформацияОбОшибке();
		Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;	
	
КонецФункции	

// Добавляет в Документооборот файл с двоичными или текстовыми данными из объекта XDTO
//
// Параметры:
//   Сообщение - ОбъектXDTO типа DMAddFileRequest
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMAddFileResponse или DMError
//
Функция ДобавитьФайлВДокументСДиска(Сообщение) Экспорт
	
	Попытка 
		
		Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMAddFileResponse");
		
		ВладелецСсылка = ОбработкаЗапросовXDTO.ПолучитьСсылкуПоObjectID(Сообщение.owner.objectId);
			
		АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(Сообщение.file.binaryData);
		АдресВременногоХранилищаТекста = "";
		
		Если Сообщение.file.Установлено("text") Тогда
			АдресВременногоХранилищаТекста = Новый ХранилищеЗначения(Сообщение.file.Text);
		КонецЕсли;
		
		ВебКлиент = Истина; // чтобы в регистр файлов в локальном кеше не вносить изменения
		
		// В некотором контексте - например, при интерактивном добавлении печатной формы - может понадобиться 
		// найти существующий файл по имени и создать новую версию вместо безусловного создания нового файла.
		ФайлСсылка = Неопределено;
		Если Сообщение.Установлено("tryToUpdate") Тогда
			Если Сообщение.tryToUpdate Тогда
				Запрос = Новый Запрос(
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 
					|	Ссылка
					|ИЗ
					|	Справочник.Файлы
					|ГДЕ
					|	ВладелецФайла = &ВладелецФайла
					|	И НЕ ПометкаУдаления
					|	И Наименование = &Наименование
					|	И ХранитьВерсии
					|");
				Запрос.УстановитьПараметр("ВладелецФайла", ВладелецСсылка);
				Запрос.УстановитьПараметр("Наименование", Сообщение.file.name);
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					ФайлСсылка = Выборка.Ссылка;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ФайлСсылка = Неопределено Тогда // создадим новый файл
			ФайлСсылка = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(
				ВладелецСсылка,
				Сообщение.file.name,
				Сообщение.file.extension,
				Сообщение.file.modificationDate,
				Сообщение.file.modificationDateUniversal,
				Сообщение.file.size,
				АдресВременногоХранилищаФайла,
				АдресВременногоХранилищаТекста,
				ВебКлиент
				);
		Иначе // обновим существующий
			РаботаСФайламиВызовСервера.СоздатьВерсиюИОбновитьВерсиюВФайле(
				Сообщение.file.modificationDate,
				Сообщение.file.modificationDateUniversal,
				ФайлСсылка,
				Сообщение.file.name,
				Сообщение.file.size,
				Сообщение.file.extension,
				АдресВременногоХранилищаФайла,
				АдресВременногоХранилищаТекста,
				ВебКлиент
				);
		КонецЕсли;
			
		Если Сообщение.file.signatures.Количество() > 0 Тогда
			ЗанестиИнформациюОПодписяхОбъекта(Сообщение.file.signatures, ФайлСсылка);
		КонецЕсли;
		Если Сообщение.file.Установлено("scannedOriginal") Тогда
			Делопроизводство.СохранитьСведенияОбОригиналеФайла(ФайлСсылка,,Сообщение.file.scannedOriginal);
		КонецЕсли;
			
		Ответ.file = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObject");
		Ответ.file.name = Строка(ФайлСсылка); 
		Ответ.file.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
		Ответ.file.objectId.id = Строка(ФайлСсылка.УникальныйИдентификатор()); 
		Ответ.file.objectId.type = "DMFile";
		
		Возврат Ответ;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при добавлении файла с диска'");
	    Инфо = ИнформацияОбОшибке();
	    Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции	

// Занимает указанный файл на редактирование и, если указано свойством columnSet, получает его данные.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект типа DMLockFileRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMLockFileResponse или DMError.
//
Функция ЗанятьФайл(Сообщение) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		ИдентификаторФайла = Сообщение.objectId.id;
		ФайлСсылка = Справочники.Файлы.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторФайла));
		ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайла(ФайлСсылка); // возможно исключение
		ТекстСообщенияОбОшибке = "";
		ДатаЗаема = Сообщение.lockDate; // дата должна быть передана: часовой пояс клиента неизвестен
		
		Если РаботаСФайламиВызовСервера.ЗанятьФайл(ДанныеФайла, ТекстСообщенияОбОшибке, ДатаЗаема) Тогда
			Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMLockFileResponse");
			Если Сообщение.Установлено("columnSet") Тогда
				НаборКолонок = Сообщение.columnSet;
				ОбъектDMFile = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFile");
				ФайлСтруктура = ЗаполнитьСтруктуруФайла(ФайлСсылка, НаборКолонок);
				ЗаполнитьXDTOФайл(ОбъектDMFile, ФайлСтруктура, НаборКолонок);
				Ответ.object = ОбъектDMFile;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			Возврат Ответ;
			
		Иначе
			Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
			Ошибка.subject = НСтр("ru = 'Ошибка при занятии файла'");
			Ошибка.description = ТекстСообщенияОбОшибке;
			
			ОтменитьТранзакцию();
			Возврат Ошибка;
			
		КонецЕсли;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при занятии файла'");
		Инфо = ИнформацияОбОшибке();
		Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		
		ОтменитьТранзакцию();
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции

// Освобождает занятый файл.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект типа DMUnlockFileRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMUnlockFileResponse или DMError.
//
Функция ОсвободитьФайл(Сообщение) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		ИдентификаторФайла = Сообщение.objectId.id;
		ФайлСсылка = Справочники.Файлы.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторФайла));
		ДанныеФайла = РаботаСФайламиВызовСервера.ПолучитьДанныеФайла(ФайлСсылка); // возможно исключение
		
		Если ДанныеФайла.РедактируетТекущийПользователь Тогда 
			МожноОсвободитьФайл = Истина;
			
		ИначеЕсли ДанныеФайла.Редактирует.Пустая() Тогда
			ТекстСообщенияОбОшибке = НСтр("ru = 'Нельзя освободить файл, так как он никем не занят.'");
			МожноОсвободитьФайл = Ложь;
			
		Иначе // занят кем-то другим
			Если Пользователи.ЭтоПолноправныйПользователь() Тогда
				МожноОсвободитьФайл = Истина;
			Иначе
				СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя освободить файл, так как он занят пользователем ""%1"".'"),
					Строка(ДанныеФайла.Редактирует));
				МожноОсвободитьФайл = Ложь;
			КонецЕсли;
			
		КонецЕсли;
	
		Если МожноОсвободитьФайл Тогда
			РаботаСФайламиВызовСервера.ОсвободитьФайл(ДанныеФайла);
			Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUnlockFileResponse");
			Если Сообщение.Установлено("columnSet") Тогда
				НаборКолонок = Сообщение.columnSet;
				ОбъектDMFile = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFile");
				ФайлСтруктура = ЗаполнитьСтруктуруФайла(ФайлСсылка, НаборКолонок);
				ЗаполнитьXDTOФайл(ОбъектDMFile, ФайлСтруктура, НаборКолонок);
				Ответ.object = ОбъектDMFile;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			Возврат Ответ;
			
		Иначе
			Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
			Ошибка.subject = НСтр("ru = 'Ошибка при освобождении файла'");
			Ошибка.description = ТекстСообщенияОбОшибке;
			
			ОтменитьТранзакцию();
			Возврат Ошибка;
			
		КонецЕсли;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при освобождении файла'");
		Инфо = ИнформацияОбОшибке();
		Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		
		ОтменитьТранзакцию();
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции

// Получает сведения о редактировании файла текущим пользователем.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект типа DMGetFileEditingInfoRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMGetFileEditingInfoResponse или DMError.
//
Функция ПолучитьСведенияОРедактированииФайла(Сообщение) Экспорт
	
	Попытка
		
		// Ради экономии вызовов запрашивать можно сведения и о версии, и о файле.
		// В последнем случае возвращаются сведения о текущей версии; она же указывается в ответе.
		Если Сообщение.objectId.type = "DMFile" Тогда
			Файл = Справочники.Файлы.ПолучитьСсылку(Новый УникальныйИдентификатор(
				Сообщение.objectId.id));
			Версия = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Файл, "ТекущаяВерсия");
			Если Не ЗначениеЗаполнено(Версия) Тогда
				Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
				Ошибка.subject = НСтр("ru = 'Ошибка при получении сведений о редактировании файла'");
				Инфо = НСтр("ru = 'Файл не найден'");
				Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
				Возврат Ошибка;
			КонецЕсли;
			
		ИначеЕсли Сообщение.objectId.type = "DMFileVersion" Тогда
			Версия = Справочники.ВерсииФайлов.ПолучитьСсылку(Новый УникальныйИдентификатор(
				Сообщение.objectId.id));
				
		Иначе // может быть передан объект произвольного типа: выдадим понятное исключение
			Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
			Ошибка.subject = НСтр("ru = 'Ошибка при получении сведений о редактировании файла'");
			Инфо = НСтр("ru = 'Сведения о редактировании можно получить только для файла или для версии файла'");
			Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
			Возврат Ошибка;
			
		КонецЕсли;
		
		Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMGetFileEditingInfoResponse");
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Сведения.Путь КАК Путь,
			|	Сведения.ДатаПомещенияВРабочийКаталог КАК ДатаПомещенияВРабочийКаталог,
			|	Сведения.НаЧтение КАК НаЧтение,
			|	Сведения.ВРабочемКаталогеВладельца КАК ВРабочемКаталогеВладельца,
			|	Версии.Ссылка КАК Версия,
			|	Версии.ДатаМодификацииУниверсальная КАК ДатаМодификацииУниверсальная,
			|	Версии.Размер КАК Размер,
			|	Версии.Владелец.ВладелецФайла КАК ВладелецФайла
			|ИЗ
			|	Справочник.ВерсииФайлов КАК Версии
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФайлыВРабочемКаталоге КАК Сведения
			|		ПО Версии.Ссылка = Сведения.Версия
			|			И (Сведения.Пользователь = &Пользователь)
			|ГДЕ
			|	Версии.Ссылка = &Версия");
		Запрос.УстановитьПараметр("Версия", Версия);
		Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			
			Сведения = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFileEditingInfo");
			
			// Получим рекомендуемый каталог для сохранения.
			Каталог = РаботаСФайламиВызовСервера.ПолучитьРабочийКаталог(Выборка.ВладелецФайла);
			Если ЗначениеЗаполнено(Каталог) Тогда
				Сведения.inOwnersFolder = Истина;
			Иначе
				Каталог = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
					"ЛокальныйКэшФайлов", "ПутьКЛокальномуКэшуФайлов", "");
				Сведения.inOwnersFolder = Ложь;
			КонецЕсли;
			Сведения.folder = Каталог;
			
			Если Выборка.Путь = NULL Тогда // файла нет на диске
				Сведения.saved = Ложь;
				
			Иначе // файл есть на диске
				Сведения.saved = Истина;
				Сведения.saveDate = Выборка.ДатаПомещенияВРабочийКаталог;
				Сведения.readOnly = Выборка.НаЧтение;
				Сведения.modificationDateUniversal = Выборка.ДатаМодификацииУниверсальная;
				Сведения.size = Выборка.Размер;
				Если Выборка.ВРабочемКаталогеВладельца Тогда
					Сведения.inOwnersFolder = Истина;
					Сведения.fullPath = Выборка.Путь;
				Иначе // добавим к пути имя каталога
					Сведения.inOwnersFolder = Ложь;
					Сведения.fullPath = Каталог + Выборка.Путь;
				КонецЕсли;
				
			КонецЕсли;
			
			Ответ.info = Сведения;
			Ответ.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
			Ответ.objectId.id = Строка(Выборка.Версия.УникальныйИдентификатор());
			Ответ.objectId.type = "DMFileVersion";
			
		Иначе // указанная версия не существует (например, удалена) или недоступна по RLS
			Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
			Ошибка.subject = НСтр("ru = 'Ошибка при получении сведений о редактировании файла'");
			Инфо = НСтр("ru = 'Версия не найдена'");
			Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
			Возврат Ошибка;
			
		КонецЕсли;
		
		Возврат Ответ;
		
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при получении сведений о редактировании файла'");
		Инфо = ИнформацияОбОшибке();
		Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции

// Записывает сведения сведения о редактировании файла текущим пользователем.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект типа DMUpdateFileEditingInfoRequest.
//
// Возвращаемое значение:
//   ОбъектXDTO типа DMUpdateFileEditingInfoResponse или DMError.
//
Функция ЗаписатьСведенияОРедактированииФайла(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
		
	Попытка
		
		Версия = Справочники.ВерсииФайлов.ПолучитьСсылку(Новый УникальныйИдентификатор(
			Сообщение.objectId.id));
		
		Ответ = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUpdateFileEditingInfoResponse");
		
		Запись = РегистрыСведений.ФайлыВРабочемКаталоге.СоздатьМенеджерЗаписи();
		
		Запись.Версия = Версия;
		Запись.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
		
		Если Сообщение.info.saved Тогда
			Запись.Размер = Сообщение.info.size;
			Запись.ДатаПомещенияВРабочийКаталог = Сообщение.info.saveDate;
			Запись.НаЧтение = Сообщение.info.readOnly;
			Если Сообщение.info.inOwnersFolder Тогда
				Запись.ВРабочемКаталогеВладельца = Истина;
				Запись.Путь = Сообщение.info.fullPath;
			Иначе
				Каталог = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
					"ЛокальныйКэшФайлов", "ПутьКЛокальномуКэшуФайлов", "");
				ПолныйПуть = Сообщение.info.fullPath;
				Если Лев(ПолныйПуть, СтрДлина(Каталог)) = Каталог Тогда
					Запись.Путь = Сред(ПолныйПуть, СтрДлина(Каталог) + 1);
				Иначе
					Запись.Путь = ПолныйПуть;
				КонецЕсли;
			КонецЕсли;
			Запись.Записать();
		Иначе
			Запись.Удалить();
		КонецЕсли;
		
		Возврат Ответ;
			
	Исключение
		
		Ошибка = ОбработкаЗапросовXDTO.СоздатьОбъект("DMError");
		Ошибка.subject = НСтр("ru = 'Ошибка при записи сведений о редактировании файла'");
		Инфо = ИнформацияОбОшибке();
		Ошибка.description = ОбработкаЗапросовXDTO.ПолучитьОписаниеОшибки(Инфо);
		Возврат Ошибка;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заменяет символы \ / : * ? "" < > |   на пробелы
//
Процедура ОчиститьСтрокуОтСимволовЗапрещенныхВФайловойСистеме(Текст)
	
	Текст = СтрЗаменить(Текст, "\", " ");
	Текст = СтрЗаменить(Текст, "/", " ");
	Текст = СтрЗаменить(Текст, ":", " ");
	Текст = СтрЗаменить(Текст, "*", " ");
	Текст = СтрЗаменить(Текст, "?", " ");
	Текст = СтрЗаменить(Текст, "|", " ");
	Текст = СтрЗаменить(Текст, """", " ");
	Текст = СтрЗаменить(Текст, "<", " ");
	Текст = СтрЗаменить(Текст, ">", " ");
	
КонецПроцедуры

// Получает существующую или создает новую папку файлов в подкаталоге вида 
// "КорневаяПапка"-"Документ._ДемоЗаказПокупателя"-"Заказ ААА (10e387b0-3a94-11e0-bc2f-e0cb4ed5f5c8)"
//
// Параметры:
//   ИдентификаторКорневойПапки - Строка - идентификатор корневой папки 
//   ТипВладельца - Строка - полное имя типа владельца и одновременно наименование папки типа
//   ИдентификаторВладельца - Строка - идентификатор владельца
//   ИмяВладельца - Строка - наименование владельца
//
// Возвращаемое значение:
//   СправочникСсылка.ПапкиФайлов - ссылка на найденную или созданную папку
//
Функция ПолучитьПапкуДляХраненияФайлов(ИдентификаторКорневойПапки, 
	ТипВладельца, ИдентификаторВладельца, ИмяВладельца) Экспорт
	
	ПапкаХранения = ПолучитьПапкуФайловДОПоВнешнемуОбъекту(ИдентификаторВладельца, ТипВладельца);
	Если ПапкаХранения <> Неопределено Тогда
		Возврат ПапкаХранения;
	КонецЕсли;	
	
	ИдентификаторПапки = Новый УникальныйИдентификатор(ИдентификаторКорневойПапки);
	РодительскаяПапка = Справочники.ПапкиФайлов.ПолучитьСсылку(ИдентификаторПапки);
	
	Если РодительскаяПапка = Неопределено ИЛИ РодительскаяПапка.Пустая() Тогда
		ВызватьИсключение "Родительская папка не найдена.";
	КонецЕсли;
	
	ИмяПапкиТипа = ТипВладельца;
	ОчиститьСтрокуОтСимволовЗапрещенныхВФайловойСистеме(ИмяПапкиТипа);
	ПапкаТипа = Справочники.ПапкиФайлов.НайтиПоНаименованию(ИмяПапкиТипа, , РодительскаяПапка);
	Если ПапкаТипа = Неопределено ИЛИ ПапкаТипа.Пустая() Тогда
		ПапкаОбъект = Справочники.ПапкиФайлов.СоздатьЭлемент();
		ПапкаОбъект.Наименование = ИмяПапкиТипа;
		ПапкаОбъект.Родитель = РодительскаяПапка;
		ПапкаОбъект.Записать();
		ПапкаТипа = ПапкаОбъект.Ссылка;
	КонецЕсли;
	
	МаксимальнаяДлинаИмениПапки = 150;
	ИмяПапкиВладельца = ИмяВладельца + " (" + ИдентификаторВладельца + ")";
	Если СтрДлина(ИмяПапкиВладельца) > МаксимальнаяДлинаИмениПапки Тогда
		ДлинаДляИмени = МаксимальнаяДлинаИмениПапки - СтрДлина(ИдентификаторВладельца) - 3;
		ИмяПапкиВладельца = Лев(ИмяВладельца, ДлинаДляИмени) + " (" + ИдентификаторВладельца + ")";
	КонецЕсли;	
	
	ОчиститьСтрокуОтСимволовЗапрещенныхВФайловойСистеме(ИмяПапкиВладельца);
	ПапкаВладельца = Справочники.ПапкиФайлов.НайтиПоНаименованию(ИмяПапкиВладельца, , ПапкаТипа);
	Если ПапкаВладельца = Неопределено ИЛИ ПапкаВладельца.Пустая() Тогда
		ПапкаОбъект = Справочники.ПапкиФайлов.СоздатьЭлемент();
		ПапкаОбъект.Наименование = ИмяПапкиВладельца;
		ПапкаОбъект.Родитель = ПапкаТипа;
		ПапкаОбъект.Записать();
		ПапкаВладельца = ПапкаОбъект.Ссылка;
	КонецЕсли;
	
	Возврат ПапкаВладельца;
	
КонецФункции	

// Получает объект XDTO, соответствующий файлу, по переданной структуре
//
// Параметры:
//   ОбъектDMFile - ОбъектXDTO типа DMFile - заполняемый объект, неявно возвращаемое значение
//   ФайлСтруктура - Структура - значения реквизитов, источник данных заполнения
//   НаборКолонок - Массив - набор имен реквизитов к заполнению
//
Процедура ЗаполнитьXDTOФайл(ОбъектDMFile, ФайлСтруктура, НаборКолонок)

	ОбработкаЗапросовXDTO.ПолучитьНаборДополнительныхРеквизитовОбъектаДО(ФайлСтруктура.Ссылка, ОбъектDMFile);
	
	//  Обязательное поле
	ОбъектDMFile.name = ФайлСтруктура.Наименование; 
	ОбъектDMFile.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
	ОбъектDMFile.objectId.id = Строка(ФайлСтруктура.Ссылка.УникальныйИдентификатор()); 
	ОбъектDMFile.objectId.type = "DMFile";
	
	ИменаПолейПодписей = Новый Массив;
	ПрефиксПодписи = "signatures.";
	
	Для Каждого ИмяКолонки Из НаборКолонок Цикл
		
		Если Найти(ИмяКолонки, ПрефиксПодписи) <> 0 Тогда
			ИмяРеквизитаПодписи = Сред(ИмяКолонки, СтрДлина(ПрефиксПодписи) + 1);
			ИменаПолейПодписей.Добавить(ИмяРеквизитаПодписи);
			Продолжить;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Для Каждого КлючИЗначение Из ФайлСтруктура Цикл
		
		Если КлючИЗначение.Ключ = "ПодписанЭП" Тогда
			ОбъектDMFile.signed = ФайлСтруктура.ПодписанЭП;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ТекущаяВерсияРасширение" Тогда
			ОбъектDMFile.extension  = ФайлСтруктура.ТекущаяВерсияРасширение;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Описание" Тогда
			ОбъектDMFile.description = ФайлСтруктура.Описание;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ТекущаяВерсияРазмер" Тогда
			ОбъектDMFile.size = ФайлСтруктура.ТекущаяВерсияРазмер;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДатаСоздания" Тогда
			ОбъектDMFile.creationDate = ФайлСтруктура.ДатаСоздания;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДатаМодификацииУниверсальная" Тогда
			ОбъектDMFile.modificationDateUniversal = ФайлСтруктура.ДатаМодификацииУниверсальная;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ТекущаяВерсияДатаМодификацииФайла" Тогда
			ОбъектDMFile.modificationDate = ФайлСтруктура.ТекущаяВерсияДатаМодификацииФайла;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Зашифрован" Тогда
			ОбъектDMFile.encrypted = ФайлСтруктура.Зашифрован;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Редактирует" Тогда
			Если ФайлСтруктура.Редактирует.Пустая() Тогда
				ОбъектDMFile.editing = Ложь;
			Иначе
				ОбъектDMFile.editing = Истина;
				ОбъектDMFile.editingUser = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUser");
				ОбъектDMFile.editingUser.name = Строка(ФайлСтруктура.Редактирует); 
				ОбъектDMFile.editingUser.objectId = ОбработкаЗапросовXDTO.
					ПолучитьObjectIDПоСсылке(ФайлСтруктура.Редактирует);
			КонецЕсли;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДатаЗаема" Тогда
			ОбъектDMFile.lockDate  = ФайлСтруктура.ДатаЗаема;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ВладелецФайла" Тогда
			ОбъектDMFile.owner = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObject");
			ОбъектDMFile.owner.name = Строка(ФайлСтруктура.ВладелецФайла); 
			ОбъектDMFile.owner.objectId = ОбработкаЗапросовXDTO.ПолучитьObjectIDПоСсылке(ФайлСтруктура.ВладелецФайла);
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = "Автор" Тогда
			ОбъектDMFile.author = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUser");
			ОбъектDMFile.author.name = Строка(ФайлСтруктура.Автор); 
			ОбъектDMFile.author.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
			ОбъектDMFile.author.objectId.id = Строка(ФайлСтруктура.Автор.УникальныйИдентификатор()); 
			ОбъектDMFile.author.objectId.type = "DMUser";
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ЯвляетсяОригиналом" Тогда
			ОбъектDMFile.scannedOriginal  = ФайлСтруктура.ЯвляетсяОригиналом;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ТекущаяВерсия" Тогда
			ОбъектDMFile.activeVersion = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFileVersion");
			ОбъектDMFile.activeVersion.name = Строка(ФайлСтруктура.ТекущаяВерсия); 
			ОбъектDMFile.activeVersion.objectId = ОбработкаЗапросовXDTO.
				ПолучитьObjectIDПоСсылке(ФайлСтруктура.ТекущаяВерсия);
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДвоичныеДанные" Тогда
			ОбъектDMFile.binaryData = ФайлСтруктура.ДвоичныеДанные;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Подписи" И ИменаПолейПодписей.Количество() <> 0 Тогда
			// тут цикл по подписям
			
			Для Каждого Подпись Из ФайлСтруктура.Подписи Цикл
				
				ПодписьXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("DMSignature");
				
				Для Каждого КлючИЗначениеПодписи Из Подпись Цикл
					
					Если КлючИЗначениеПодписи.Ключ = "КомуВыданСертификат" Тогда
						ПодписьXDTO.author = Подпись.КомуВыданСертификат;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "ДатаПодписи" Тогда
						ПодписьXDTO.date = Подпись.ДатаПодписи;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Комментарий" Тогда
						ПодписьXDTO.comment = Подпись.Комментарий;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Подпись" Тогда
						ПодписьXDTO.signature = Подпись.Подпись;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Отпечаток" Тогда
						ПодписьXDTO.thumbprint = Подпись.Отпечаток;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Сертификат" Тогда
						ПодписьXDTO.certificate = Подпись.Сертификат;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "ИмяФайлаПодписи" Тогда
						ПодписьXDTO.signatureFileName = Подпись.ИмяФайлаПодписи;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "УстановившийПодпись" Тогда
						ПодписьXDTO.signer = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUser");
						ПодписьXDTO.signer.name = Строка(Подпись.УстановившийПодпись); 
						ПодписьXDTO.signer.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
						ПодписьXDTO.signer.objectId.id = Строка(Подпись.УстановившийПодпись.УникальныйИдентификатор()); 
						ПодписьXDTO.signer.objectId.type = "DMUser";
					КонецЕсли;	
					
				КонецЦикла;	
				
				ОбъектDMFile.signatures.Добавить(ПодписьXDTO);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет структуру данными файла
//
// Параметры:
//   ФайлСсылка - СправочникСсылка.Файлы - файл, данные которого подлежат получению
//   НаборКолонок - Массив - массив имен реквизитов к получению
//
// Возвращаемое значение:
//   Структура, ключи которой - имена реквизитов файла, а значения - значения этих реквизитов
//
Функция ЗаполнитьСтруктуруФайла(ФайлСсылка, НаборКолонок)
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("editing", "Редактирует");
	СтруктураРеквизитов.Вставить("editingUser", "Редактирует");
	СтруктураРеквизитов.Вставить("modificationDate", "ТекущаяВерсияДатаМодификацииФайла");
	СтруктураРеквизитов.Вставить("signed", "ПодписанЭП");
	СтруктураРеквизитов.Вставить("size", "ТекущаяВерсияРазмер");
	СтруктураРеквизитов.Вставить("creationDate", "ДатаСоздания");
	СтруктураРеквизитов.Вставить("author", "Автор");
	СтруктураРеквизитов.Вставить("extension", "ТекущаяВерсияРасширение");
	СтруктураРеквизитов.Вставить("description", "Описание");
	СтруктураРеквизитов.Вставить("encrypted", "Зашифрован");
	СтруктураРеквизитов.Вставить("lockDate", "ДатаЗаема");
	СтруктураРеквизитов.Вставить("owner", "ВладелецФайла");
	СтруктураРеквизитов.Вставить("activeVersion", "ТекущаяВерсия");
	
	ИменаРеквизитов = "";
	Разделитель = "";
	ПолучатьДвоичныеДанные = Ложь;
	ПолучатьЯвляетсяОригиналом = Ложь;
	ПолучатьТекущуюВерсию = Ложь;
	ПолучатьРеквизитыВерсии = Ложь;
	
	Для каждого КлючИЗначение из СтруктураРеквизитов Цикл
		Для каждого ИмяКолонки из НаборКолонок Цикл
			ПолучатьДвоичныеДанные = ПолучатьДвоичныеДанные или (ИмяКолонки = "binaryData");
			ПолучатьЯвляетсяОригиналом = ПолучатьЯвляетсяОригиналом или (ИмяКолонки = "scannedOriginal");
			ПолучатьТекущуюВерсию = ПолучатьТекущуюВерсию или (ИмяКолонки = "activeVersion");
			ПолучатьРеквизитыВерсии = ПолучатьРеквизитыВерсии или (ИмяКолонки = "modificationDateUniversal");
			Если ИмяКолонки = КлючИЗначение.Ключ Тогда
				ИменаРеквизитов = ИменаРеквизитов + Разделитель + КлючИЗначение.Значение;
				Разделитель = ", ";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	СтруктураРеквизитовПодписи = Новый Структура;
	СтруктураРеквизитовПодписи.Вставить("author", "КомуВыданСертификат");
	СтруктураРеквизитовПодписи.Вставить("date", "ДатаПодписи");
	СтруктураРеквизитовПодписи.Вставить("comment", "Комментарий");
	СтруктураРеквизитовПодписи.Вставить("signature", "Подпись");
	СтруктураРеквизитовПодписи.Вставить("thumbprint", "Отпечаток");
	СтруктураРеквизитовПодписи.Вставить("signer", "УстановившийПодпись");
	СтруктураРеквизитовПодписи.Вставить("certificate", "Сертификат");
	СтруктураРеквизитовПодписи.Вставить("signatureFileName", "ИмяФайлаПодписи");
	
	РеквизитыПодписей = ""; Разделитель = "";
	Для каждого КлючИЗначение из СтруктураРеквизитовПодписи Цикл
		Для каждого ИмяКолонки из НаборКолонок Цикл
			Если ИмяКолонки = "signatures." + КлючИЗначение.Ключ Тогда
				РеквизитыПодписей = РеквизитыПодписей + Разделитель + КлючИЗначение.Значение;
				Разделитель = ", ";
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	РеквизитыФайла = Новый Структура;
	
	ИменаРеквизитов = ИменаРеквизитов + ?(ПустаяСтрока(ИменаРеквизитов),"",", ") + "Ссылка, Наименование"; // нужны всегда
	
	Если НЕ ПустаяСтрока(ИменаРеквизитов) Тогда
		
		// Некоторые свойства можно получить лишь из версии, но не из самого файла.
		Если (НЕ ПустаяСтрока(РеквизитыПодписей) ИЛИ ПолучатьДвоичныеДанные ИЛИ ПолучатьРеквизитыВерсии)
			И НЕ ПолучатьТекущуюВерсию Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", " + "ТекущаяВерсия";
		КонецЕсли;
		
		РеквизитыФайла = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ФайлСсылка, ИменаРеквизитов);
		
		Если ПолучатьРеквизитыВерсии Тогда
			РеквизитыВерсии = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(РеквизитыФайла.ТекущаяВерсия, "ДатаМодификацииУниверсальная");
			Для Каждого КлючИЗначение Из РеквизитыВерсии Цикл
				РеквизитыФайла.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;
		КонецЕсли;
		
		// Заполнение подписей.
		Если НЕ ПустаяСтрока(РеквизитыПодписей) Тогда
			
			Подписи = Новый Массив;
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	" + РеквизитыПодписей + "
			               |ИЗ
			               |	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
			               |ГДЕ
			               |	ЭлектронныеПодписи.Объект = &ОбъектСсылка";
			
			Запрос.Параметры.Вставить("ОбъектСсылка", РеквизитыФайла.ТекущаяВерсия);
			ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаЗапроса.Следующий() Цикл
				
				РеквизитыПодписи = Новый Структура(РеквизитыПодписей);
				ЗаполнитьЗначенияСвойств(РеквизитыПодписи, ВыборкаЗапроса);
				
				Если РеквизитыПодписи.Свойство("Подпись") Тогда
					РеквизитыПодписи.Подпись = ВыборкаЗапроса.Подпись.Получить(); // получаем двоичные данные
				КонецЕсли;	
				
				Если РеквизитыПодписи.Свойство("Сертификат") Тогда
					РеквизитыПодписи.Сертификат = ВыборкаЗапроса.Сертификат.Получить(); // получаем двоичные данные
				КонецЕсли;	
				
				Подписи.Добавить(РеквизитыПодписи);
				
			КонецЦикла;
			
			РеквизитыФайла.Вставить("Подписи", Подписи);
			
		КонецЕсли;
		
		// Получение сведений о файле.
		Если ПолучатьЯвляетсяОригиналом Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ЯвляетсяОригиналом
				|ИЗ 
				|	РегистрСведений.СведенияОФайлах
				|ГДЕ
				|	Файл = &Файл
				|");
			Запрос.УстановитьПараметр("Файл", ФайлСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				РеквизитыФайла.Вставить("ЯвляетсяОригиналом", Выборка.ЯвляетсяОригиналом);
			Иначе
				РеквизитыФайла.Вставить("ЯвляетсяОригиналом", Ложь);
			КонецЕсли;
			
		КонецЕсли;
		
		// Получение собственно файла.
		Если ПолучатьДвоичныеДанные Тогда
			
			Если РеквизитыФайла.ТекущаяВерсия.ТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				
				ХранилищеФайла = РаботаСФайламиВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(РеквизитыФайла.ТекущаяВерсия);
				ДвоичныеДанныеФайла = ХранилищеФайла.Получить();
				
				РеквизитыФайла.Вставить("ДвоичныеДанные", ДвоичныеДанныеФайла);
				
			Иначе
				
				ВерсияСсылка = РеквизитыФайла.ТекущаяВерсия;
				
				Если НЕ ВерсияСсылка.Том.Пустая() Тогда
					ПолныйПуть = ФайловыеФункции.ПолныйПутьТома(ВерсияСсылка.Том) + ВерсияСсылка.ПутьКФайлу; 
					Попытка
						РеквизитыФайла.Вставить("ДвоичныеДанные", Новый ДвоичныеДанные(ПолныйПуть));
					Исключение
						СообщениеОбОшибке = ФайловыеФункцииСлужебныйКлиентСервер.ОшибкаФайлНеНайденВХранилищеФайлов(
							ВерсияСсылка.ПолноеНаименование + "." + ВерсияСсылка.Расширение);
						
						ВызватьИсключение СообщениеОбОшибке;
					КонецПопытки;
				КонецЕсли;
				
			КонецЕсли;
			
			РаботаСФайламиВызовСервера.ЗаписатьОбращениеКВерсииФайла(РеквизитыФайла.ТекущаяВерсия);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РеквизитыФайла;
	
КонецФункции

// Получает объект XDTO, соответствующий файлу, по переданной структуре
//
// Параметры:
//   ОбъектDMFileVersion - ОбъектXDTO типа DMFileVersion - заполняемый объект, неявно возвращаемое значение
//   СтруктураВерсии - Структура - значения реквизитов, источник данных заполнения
//   НаборКолонок - Массив - набор имен реквизитов к заполнению
//
Процедура ЗаполнитьXDTOВерсииФайла(ОбъектDMFileVersion, СтруктураВерсии, НаборКолонок)

	//  Обязательное поле
	ОбъектDMFileVersion.name = СтруктураВерсии.Наименование; 
	ОбъектDMFileVersion.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
	ОбъектDMFileVersion.objectId.id = Строка(СтруктураВерсии.Ссылка.УникальныйИдентификатор()); 
	ОбъектDMFileVersion.objectId.type = "DMFileVersion";
	
	ИменаПолейПодписей = Новый Массив;
	ПрефиксПодписи = "signatures.";
	
	Для Каждого ИмяКолонки Из НаборКолонок Цикл
		
		Если Найти(ИмяКолонки, ПрефиксПодписи) <> 0 Тогда
			ИмяРеквизитаПодписи = Сред(ИмяКолонки, СтрДлина(ПрефиксПодписи) + 1);
			ИменаПолейПодписей.Добавить(ИмяРеквизитаПодписи);
			Продолжить;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Для Каждого КлючИЗначение Из СтруктураВерсии Цикл
		
		Если КлючИЗначение.Ключ = "ПодписанЭП" Тогда
			ОбъектDMFileVersion.signed = СтруктураВерсии.ПодписанЭП;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Расширение" Тогда
			ОбъектDMFileVersion.extension  = СтруктураВерсии.Расширение;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Размер" Тогда
			ОбъектDMFileVersion.size = СтруктураВерсии.Размер;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДатаСоздания" Тогда
			ОбъектDMFileVersion.creationDate = СтруктураВерсии.ДатаСоздания;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДатаМодификацииУниверсальная" Тогда
			ОбъектDMFileVersion.modificationDateUniversal = СтруктураВерсии.ДатаМодификацииУниверсальная;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Зашифрован" Тогда
			ОбъектDMFileVersion.encrypted = СтруктураВерсии.Зашифрован;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Владелец" Тогда
			ОбъектDMFileVersion.owner = ОбработкаЗапросовXDTO.СоздатьОбъект("DMFile");
			ОбъектDMFileVersion.owner.name = Строка(СтруктураВерсии.Владелец); 
			ОбъектDMFileVersion.owner.objectId = ОбработкаЗапросовXDTO.ПолучитьObjectIDПоСсылке(СтруктураВерсии.Владелец);
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = "Автор" Тогда
			ОбъектDMFileVersion.author = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUser");
			ОбъектDMFileVersion.author.name = Строка(СтруктураВерсии.Автор); 
			ОбъектDMFileVersion.author.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
			ОбъектDMFileVersion.author.objectId.id = Строка(СтруктураВерсии.Автор.УникальныйИдентификатор()); 
			ОбъектDMFileVersion.author.objectId.type = "DMUser";
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "ДвоичныеДанные" Тогда
			ОбъектDMFileVersion.binaryData = СтруктураВерсии.ДвоичныеДанные;
		КонецЕсли;	
		
		Если КлючИЗначение.Ключ = "Подписи" И ИменаПолейПодписей.Количество() <> 0 Тогда
			
			Для Каждого Подпись Из СтруктураВерсии.Подписи Цикл
				
				ПодписьXDTO = ОбработкаЗапросовXDTO.СоздатьОбъект("DMSignature");
				
				Для Каждого КлючИЗначениеПодписи Из Подпись Цикл
					
					Если КлючИЗначениеПодписи.Ключ = "КомуВыданСертификат" Тогда
						ПодписьXDTO.author = Подпись.КомуВыданСертификат;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "ДатаПодписи" Тогда
						ПодписьXDTO.date = Подпись.ДатаПодписи;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Комментарий" Тогда
						ПодписьXDTO.comment = Подпись.Комментарий;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Подпись" Тогда
						ПодписьXDTO.signature = Подпись.Подпись;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Отпечаток" Тогда
						ПодписьXDTO.thumbprint = Подпись.Отпечаток;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "Сертификат" Тогда
						ПодписьXDTO.certificate = Подпись.Сертификат;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "ИмяФайлаПодписи" Тогда
						ПодписьXDTO.signatureFileName = Подпись.ИмяФайлаПодписи;
					КонецЕсли;	
					
					Если КлючИЗначениеПодписи.Ключ = "УстановившийПодпись" Тогда
						ПодписьXDTO.signer = ОбработкаЗапросовXDTO.СоздатьОбъект("DMUser");
						ПодписьXDTO.signer.name = Строка(Подпись.УстановившийПодпись); 
						ПодписьXDTO.signer.objectId = ОбработкаЗапросовXDTO.СоздатьОбъект("DMObjectID");
						ПодписьXDTO.signer.objectId.id = Строка(Подпись.УстановившийПодпись.УникальныйИдентификатор()); 
						ПодписьXDTO.signer.objectId.type = "DMUser";
					КонецЕсли;	
					
				КонецЦикла;	
				
				ОбъектDMFileVersion.signatures.Добавить(ПодписьXDTO);
				
			КонецЦикла;	
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

// Заполняет структуру данными версии файла
//
// Параметры:
//   ВерсияСсылка - СправочникСсылка.ВерсииФайлов - версия, данные которой подлежат получению
//   НаборКолонок - Массив - массив имен реквизитов к получению
//
// Возвращаемое значение:
//   Структура, ключи которой - имена реквизитов версии файла, а значения - значения этих реквизитов
//
Функция ЗаполнитьСтруктуруВерсииФайла(ВерсияСсылка, НаборКолонок)
	
	ИменаРеквизитов = "";
	РеквизитыПодписей = "";
	ПрефиксПодписи = "signatures.";
	
	ПолучатьДвоичныеДанные = Ложь;
	
	Для Каждого ИмяКолонки Из НаборКолонок Цикл
		
		Если Найти(ИмяКолонки, ПрефиксПодписи) <> 0 Тогда
			
			ИмяРеквизитаПодписи = Сред(ИмяКолонки, СтрДлина(ПрефиксПодписи) + 1);
			ИмяРеквизитаПодписиДляЗапроса = "";
			
			Если ИмяРеквизитаПодписи = "author" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "КомуВыданСертификат";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "date" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "ДатаПодписи";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "comment" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "Комментарий";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "signature" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "Подпись";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "thumbprint" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "Отпечаток";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "signer" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "УстановившийПодпись";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "certificate" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "Сертификат";
			КонецЕсли;	
			
			Если ИмяРеквизитаПодписи = "signatureFileName" Тогда
				ИмяРеквизитаПодписиДляЗапроса = "ИмяФайлаПодписи";
			КонецЕсли;	
						
			Если НЕ ПустаяСтрока(ИмяРеквизитаПодписиДляЗапроса) Тогда
				
				Если НЕ ПустаяСтрока(РеквизитыПодписей) Тогда
					РеквизитыПодписей = РеквизитыПодписей + ", ";
				КонецЕсли;	
				
				РеквизитыПодписей = РеквизитыПодписей + ИмяРеквизитаПодписиДляЗапроса;
				
			КонецЕсли;	
			
			Продолжить;
			
		КонецЕсли;	
		
		Если ИмяКолонки = "binaryData" Тогда
			ПолучатьДвоичныеДанные = Истина;
			Продолжить;
		КонецЕсли;	
		
		Если НЕ ПустаяСтрока(ИменаРеквизитов) Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", ";
		КонецЕсли;	
		
		Если ИмяКолонки = "modificationDateUniversal" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "ДатаМодификацииУниверсальная";
		КонецЕсли;	
		
		Если ИмяКолонки = "signed" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "ПодписанЭП";
		КонецЕсли;	
		
		Если ИмяКолонки = "size" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "Размер";
		КонецЕсли;	
		
		Если ИмяКолонки = "creationDate" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "ДатаСоздания";
		КонецЕсли;	
		
		Если ИмяКолонки = "author" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "Автор";
		КонецЕсли;	
		
		Если ИмяКолонки = "extension" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "Расширение";
		КонецЕсли;	
		
		Если ИмяКолонки = "encrypted" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "Зашифрован";
		КонецЕсли;	
		
		Если ИмяКолонки = "owner" Тогда
			ИменаРеквизитов = ИменаРеквизитов + "Владелец";
		КонецЕсли;	
		
	КонецЦикла;	
	
	РеквизитыВерсии = Новый Структура;
	
	ИменаРеквизитов = ИменаРеквизитов + ?(ПустаяСтрока(ИменаРеквизитов),"",", ") + "Ссылка, Наименование"; // нужны всегда
	
	Если НЕ ПустаяСтрока(ИменаРеквизитов) Тогда
		
		Если ПолучатьДвоичныеДанные Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", " + "ТипХраненияФайла";
		КонецЕсли;	
		
		РеквизитыВерсии = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВерсияСсылка, ИменаРеквизитов);
		
		// заполнение подписей
		Если НЕ ПустаяСтрока(РеквизитыПодписей) Тогда
			
			Подписи = Новый Массив;
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	" + РеквизитыПодписей + "
			               |ИЗ
			               |	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
			               |ГДЕ
			               |	ЭлектронныеПодписи.Объект = &ОбъектСсылка";
						 
			Запрос.Параметры.Вставить("ОбъектСсылка", ВерсияСсылка);
			ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаЗапроса.Следующий() Цикл
				
				РеквизитыПодписи = Новый Структура(РеквизитыПодписей);
				ЗаполнитьЗначенияСвойств(РеквизитыПодписи, ВыборкаЗапроса);
				
				Если РеквизитыПодписи.Свойство("Подпись") Тогда
					РеквизитыПодписи.Подпись = ВыборкаЗапроса.Подпись.Получить(); // получаем двоичные данные
				КонецЕсли;	
				
				Если РеквизитыПодписи.Свойство("Сертификат") Тогда
					РеквизитыПодписи.Сертификат = ВыборкаЗапроса.Сертификат.Получить(); // получаем двоичные данные
				КонецЕсли;	
				
				Подписи.Добавить(РеквизитыПодписи);
				
			КонецЦикла;
			
			РеквизитыВерсии.Вставить("Подписи", Подписи);
			
		КонецЕсли;	
		
		
		// получение собственно файла
		Если ПолучатьДвоичныеДанные Тогда
			
			Если РеквизитыВерсии.ТипХраненияФайла <> Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
				
				ХранилищеФайла = РаботаСФайламиВызовСервера.ПолучитьХранилищеФайлаИзИнформационнойБазы(ВерсияСсылка);
				ДвоичныеДанныеФайла = ХранилищеФайла.Получить();
				
				РеквизитыВерсии.Вставить("ДвоичныеДанные", ДвоичныеДанныеФайла);
				
			Иначе
				
				Если НЕ ВерсияСсылка.Том.Пустая() Тогда
					ПолныйПуть = ФайловыеФункции.ПолныйПутьТома(ВерсияСсылка.Том) + ВерсияСсылка.ПутьКФайлу; 
					Попытка
						РеквизитыВерсии.Вставить("ДвоичныеДанные", Новый ДвоичныеДанные(ПолныйПуть));
					Исключение
						СообщениеОбОшибке = ФайловыеФункцииСлужебныйКлиентСервер.ОшибкаФайлНеНайденВХранилищеФайлов(
							ВерсияСсылка.ПолноеНаименование + "." + ВерсияСсылка.Расширение);
						
						ВызватьИсключение СообщениеОбОшибке;
					КонецПопытки;
				КонецЕсли;
				
			КонецЕсли;	
			
			РаботаСФайламиВызовСервера.ЗаписатьОбращениеКВерсииФайла(ВерсияСсылка);
			
		КонецЕсли;		
		
	КонецЕсли;	
	
	Возврат РеквизитыВерсии;
		
КонецФункции	

#КонецОбласти