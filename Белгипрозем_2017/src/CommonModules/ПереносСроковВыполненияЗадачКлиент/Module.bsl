// Открывает форму для задания параметров переноса срока процесса
// Параметры:
//		ФормаПроцесса - ссылка на форму процесса, с которой открывается форма переноса
Процедура ОткрытьФормуПереносаСрокаПроцесса(ФормаПроцесса) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуПереносаСрокаПроцессаПослеВопросаОЗаписи", ЭтотОбъект, ФормаПроцесса);
	
	Если ФормаПроцесса.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Процесс изменен. Сохранить изменения?'");
		Режим = Новый СписокЗначений;
		Режим.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Сохранить'"));
		Режим.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не сохранять'"));
		Режим.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Режим, , КодВозвратаДиалога.Да);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

Процедура ОткрытьФормуПереносаСрокаПроцессаПослеВопросаОЗаписи(Ответ, ФормаПроцесса) Экспорт
	
	Если ФормаПроцесса.Модифицированность
		И Ответ = КодВозвратаДиалога.Да Тогда
		ФормаПроцесса.Записать();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("БизнесПроцесс", ФормаПроцесса.Объект.Ссылка);
	ПараметрыФормы.Вставить("ЗаявкаНаПереносСрока", ФормаПроцесса.ЗаявкаНаПереносСрока);
	ПараметрыФормы.Вставить("Идентификатор", ФормаПроцесса.УникальныйИдентификатор);
	ОткрытьФорму(
		"БизнесПроцесс.РешениеВопросовВыполненияЗадач.Форма.ФормаПереносСрокаБизнесПроцесса", 
		ПараметрыФормы);
	
КонецПроцедуры